\section{Product Groups}

This is formalized in~\mml{group7}. The basic idea is to have some
indexing set $I$, then consider a family of magmas indexed by it $F_{i}$
for $i\in I$. The Mizar notation for $F_{i}$ is ``\lstinline{F.i}''.
This leans on the notion of a product of a family of sets, as given in
Definition~\mml[def 5]{card3}:

\begin{mizar}
func product f -> set means :: CARD_3:def 5
:Def5: for x being object holds x in it iff
  ex g st x = g & dom g = dom f &
          for y being object st y in dom f
          holds g.y in f.y;
\end{mizar}

The basic ``design pattern'' surrounding product groups as implemented
in Mizar:
\begin{enumerate}
\item ``\lstinline{multMagma-yielding}'' for a relation means that each
  value in the range is a ``\lstinline{non empty multMagma}''
  (\mml[def1]{group7})
\item ``\lstinline{multMagma-Family of I}'' (where ``I'' is some indexing
  set) is just a ``\lstinline{multMagma-yielding ManySortedSet of I}''
  (\mml{group7})
\item ``\lstinline{ManySortedSet of I}'' is a
  ``\lstinline{total I-defined Function}'', i.e., it's a family of sets
  indexed by ``I'' (\mml[def1]{pboole})
\item If ``I'' is an indexing set and ``F'' is a
  ``\lstinline{multMagma-Family of I}'', then we can construct
  ``\lstinline{product F}'' (\mml[def2]{group7}).
\end{enumerate}

\begin{mizar}
definition :: GROUP_7:def 2
  let I be set, F be multMagma-Family of I;
  func product F -> strict multMagma means
  :Def2:
  the carrier of it = product Carrier F &
  for f, g being Element of product Carrier F,
      i being set
  st i in I
  ex Fi being non empty multMagma,
     h being Function
  st Fi = F.i & h = (the multF of it).(f,g) &
     h.i = (the multF of Fi).(f.i,g.i);
  existence;
  uniqueness;
end;
\end{mizar}

We need to establish a ``\lstinline{multMagma-Family of I}'' is
``\lstinline{Group-like associative non empty}''. The is a notion of a
``\lstinline{Group-Family of I}'' defined in~\mml{group19}:

\begin{mizar}
definition :: GROUP_19
  let I be set ;
  mode Group-Family of I is Group-like associative multMagma-Family of I;
end;

theorem Th1: :: GROUP_19:1
  for I being set
  for F being Group-Family of I
  for i being object st i in I holds
  F . i is Group
proof end;

definition
  let I be non empty set ;
  let F be Group-Family of I;
  let i be Element of I;
  :: original: .
  redefine func F . i -> Group;
  coherence by Th1;
end; 
\end{mizar}

The claim that, if $G=\prod_{i\in I} F_{i}$ is a product group, then
$F_{i_{1}}$ is isomorphic to a subgroup of $G$, namely, (assuming $I$ is ordered),
\begin{equation}
F_{i_{1}}\cong\left(\prod_{i<i_{1}}\trivialSubgroup\right)\times F_{i_{1}}\times\left(\prod_{i>i_{1}}\trivialSubgroup\right).
\end{equation}
This subgroup is defined in Definition~\mml[def2]{group12}:
\begin{mizar}
definition
  let I be non empty set ;
  let F be Group-like associative multMagma-Family of I;
  let i be Element of I;
  func ProjSet (F,i) -> Subset of (product F) means
  :: GROUP_12:def 1
  for x being set holds
  (x in it iff
   ex g being Element of (F.i) st x = (1_(product F)) +* (i,g));
  existence proof end;
  uniqueness proof end;
end;
definition
  let I be non empty set ;
  let F be Group-like associative multMagma-Family of I;
  let i be Element of I;
  func ProjGroup (F,i) -> strict Subgroup of product F means
  :: GROUP_12:def 2
  the carrier of it = ProjSet (F,i);
  existence proof end;
  uniqueness by GROUP_2:59;
end;
\end{mizar}
The isomorphism is defined as ``\lstinline{1ProdHom (F,i)}'' in \mml[def3]{group12}.

The proof that $F_{i_{1}}$ is, in fact, isomorphic to a \emph{normal}
subgroup of $G$ is handled in a cluster registration in \mml{group12}.

The notion of a ``product map'' is defined in \mml{group19} as:
\begin{mizar}
definition
  let F, G be non-empty non empty Function;
  let h be non empty Function;
  assume A1: (dom F = dom G &
              dom G = dom h &
              (for i being object
               st i in dom h
               holds h.i is Function of (F.i),(G.i)));
  func ProductMap (F,G,h) -> Function of (product F),(product G) means
  :: GROUP_19:def 5
  for x being Element of product F
  for i being object st i in dom h holds
  ex hi being Function of (F.i),(G.i) st
  (hi = h.i & (it.x).i = hi.(x.i));
  existence proof end;
  uniqueness proof end;
end;
\end{mizar}
In Definition~\mml[def6]{group19}, the notion of a product map is
defined for product groups as a group morphism.

The projection mapping is found in \mml{card3}:
\begin{mizar}
definition
  let x be object;
  let X be set;
  defpred S2[object , object] means ex g being Function
                                    st ($1 = g & $2 = g.x);
  func pi (X,x) -> set means
  :: CARD_3:def 6
  for y being object
  holds (y in it iff
         ex f being Function st (f in X & y = f.x));
existence proof end;
uniqueness proof end;
end;

theorem :: CARD_3:12
  for x being object
  for f being Function
  st x in dom f & product f <> {}
  holds pi ((product f),x) = f.x
proof end;
\end{mizar}
It has not been shown, so far as I can tell, that ``\lstinline{pi (product F, i)}''
is a group.

The result ``If $F^{(1)}_{i}$ is a subgroup of $F^{(2)}_{i}$ for each
$i\in I$, then $\prod_{i\in I}F^{(1)}_{i}$ is a subgroup of $\prod_{i\in I}F^{(2)}_{i}$''
may be found~\mml{group21}:

\begin{mizar}
theorem Th20: :: GROUP_21:23
  for I being non empty set
  for F1, F2 being Group-Family of I
  st (for i being Element of I holds F1.i is Subgroup of F2.i)
  holds product F1 is Subgroup of product F2
proof end;
\end{mizar}

It also looks like a number of useful results has been proven in~\mml{group12}.

\begin{ddanger}
Product groups are under-developed in Mizar, in my opinion. The
universal property of products for groups is not established. Taking the
product of a finite set of groups is tedious and requires a lot more
work than it deserves. \emph{It would be very worth-while to write
quality-of-life results for product grups.} I'm not even sure how to
define the wreath product without a number of preliminary work done on
product groups. (I suspect this will be my second article to Mizar.)
\end{ddanger}
