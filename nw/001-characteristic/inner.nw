% -*- mode: poly-noweb; noweb-code-mode: mizar-mode; -*-
\section{Inner Automorphisms}\label{sec:characteristic:inner-automorphism}
We can now organize our treatment of inner automorphisms.

<<Inner Automorphisms>>=
<<Definition: [[inner]] for Automorphism>>

<<Theorem: [[id G]] is effectively inner>>

<<Register [[inner]] for [[Automorphism]]>>

<<Theorem: Relate [[Automorphism of G]] to elements of [[Aut G]]>>

<<Theorem: $f$ in [[InnAut G]] iff $f$ is [[inner Automorphism of G]]>>

<<Theorem: inner automorphism acting on subgroup is conjugate of argument>>

<<Theorem: Kernel of conjugation as endomorphism>>

<<Theorem: Conjugation by fixed element is an automorphism>>

<<Corollary: conjugation of given element is an \emph{inner} automorphism>>

<<Theorem: constructing inner automorphisms from group elements>>

<<Theorem: inner Automorphisms fix only normal Subgroups>>
@

\begin{definition}\index{Automorphism!inner}\index{inner!Automorphism|see {Automorphism}}
We call a group automorphism $f\in\Aut(G)$ \define{inner} if there is a
group element $g\in G$ such that for all $x\in G$ we have $f(x) = x^{g} = g^{-1}xg$.
That is, $f$ is just conjugation by a fixed group element.
\end{definition}

\begin{def-remark}[Notation: $\Inn(G)$]\index{$\Inn(G)$}
We denote the set of inner automorphisms of $G$ by $\Inn(G)$ and
informally we know $\Inn(G)\subset\Aut(G)$. (We will prove
$\Inn(G)\subset\Aut(G)$ later, I think.)
\end{def-remark}

<<Definition: [[inner]] for Automorphism>>=
definition
  let G;
  let IT be Automorphism of G;
  attr IT is inner means
  :Def2:
  ex a being Element of G st
  for x being Element of G holds IT.x = x |^ a;
end;
<<Outer as antonym of inner>>
@ %def Def2 inner

\begin{notation}\index{Automorphism!outer}
We also recall that an automorphism is called \define{Outer} if it is
not inner.
\end{notation}

Mizar let's us do this with the [[antonym]] construct within a
[[notation]] block.

<<Outer as antonym of inner>>=
notation
  let G be Group, f be Automorphism of G;
  antonym f is outer for f is inner;
end;
@ %def outer

\begin{voc}
Before rushing off to prove properties concerning inner and outer
automorphisms, we should add the attributes to our vocabulary file.
\end{voc}

<<DICT/GROUP-22.VOC>>=
Vinner
Vouter
@

\begin{theorem}[$\Id_{G}$ is effectively inner]\label{thm:characteristic:id-inner}
The identity endomorphism $\Id_{G}$ is an inner automorphism of $G$.
\end{theorem}

We will be registering ``inner'' as an attribute for ``Automorphism
of $G$''. This will require proving that there exists an inner
Automorphism of $G$. I've found the trivial examples are often best for
establishing the existence of such things, so we will prove [[id the carrier of G]] is
an inner Automorphism. This uses the fact, if $e\in G$ is the identity
element, then for any $g\in G$ we have conjugation $g^{e} = e^{-1}ge=g$
(proven in Theorem~\mml[Th19]{group3}).\MizThm{GROUP\_3}{19}

<<Theorem: [[id G]] is effectively inner>>=
theorem Th25:
  for x being Element of G holds (id the carrier of G).x = x |^ 1_G
  by GROUP_3:19;
@ %def Th25

\begin{registration}
Now registering [[inner]] for [[Automorphism]].
\end{registration}

<<Register [[inner]] for [[Automorphism]]>>=
registration
  let G;
  cluster inner for Automorphism of G;
  existence
  <<Proof of existence of an inner Automorphism>>
end;
@

\begin{proof}[Proof sketch (Existence of inner automorphism)]
The proof is a two punch knock-out. We take [[id the carrier of G]] to be the
morphism, [[1_g]] the group's identity element to be the element
[[id the carrier of G]] conjugates by, then from earlier (Theorem~\ref{thm:characteristic:id-inner})
we have [[id the carrier of G]] be inner.
\end{proof}

<<Proof of existence of an inner Automorphism>>=
proof
  reconsider i = id (the carrier of G) as Automorphism of G by GROUP_6:38;
  take i;
  take 1_G;
  thus thesis by Th25;
end;
@

\begin{theorem}[{$\varphi\in\aut(G)\iff \varphi$ is [[Automorphism of G]]}]
For any $\varphi$, we have $\varphi\in\Aut(G)$ if and only if
$\varphi\colon G\to G$ is an automorphism.
\end{theorem}

\begin{thm-remark}
Mizar has~\mml{autgroup}, an article which defines [[Aut G]] the
collection of functions on the underlying set $U(G)$ of a group $G$. We
can prove that $f\in\aut(G)$ if and only if $f$ is [[Automorphism of G]].
\end{thm-remark}

\begin{proof}[Proof outline]
  Like any ``if{}f'' statement, we have two steps to this proof:

  Step 1: $\varphi\in\Aut(g)\implies\varphi\colon G\to G$ is an
  Automorphism. This is involved and requires carving out a sub-proof.

  Step 2: $\varphi\in\Aut(g)\impliedby\varphi\colon G\to G$ is an
  Automorphism. This follows from how $\Aut(G)$ is defined in
  \verb#AUTGROUP:def 1#\MizDef{AUTGROUP}{1}.
\end{proof}

<<Theorem: Relate [[Automorphism of G]] to elements of [[Aut G]]>>=
theorem Th26:
  for G being strict Group, f being object
  holds (f in Aut G) iff (f is Automorphism of G)
proof
  let G be strict Group;
  let f be object;
  thus f in Aut G implies f is Automorphism of G
  <<Proof $f\in\aut(G)\implies f$ is [[Automorphism of G]]>>
  thus f is Automorphism of G implies f in Aut G by AUTGROUP:def 1;
  thus thesis;
end;
@ %def Th26

\begin{proof}[Sub-proof ($\Longrightarrow$)]
The forward direction is
straightforward. The only subtlety is, since we didn't assume anything
about $\varphi$, we should establish it's an endomorphism of $G$ along
the way. Then its membership in $\Aut(G)$ implies $\varphi$ is
bijective, and the result follows.
\end{proof}

<<Proof $f\in\aut(G)\implies f$ is [[Automorphism of G]]>>=
proof
  assume A1: f in Aut G;
  then reconsider f as Endomorphism of G by AUTGROUP:def 1;
  f is bijective by A1,AUTGROUP:def 1;
  hence thesis;
end;
@


\begin{theorem}
We have $\varphi\in\Inn\Aut(G)$ if and only if $\varphi$ is an inner
Automorphism of $G$.
\end{theorem}

\begin{proof}[Proof outline]
  We have two steps to our proof.

  Step 1: $\varphi\in\Inn\Aut(G)$ (in the sense of Definition~\mml[def4]{autgroup}\MizDef{AUTGROUP}{4})
  imply $\varphi$ is an inner automorphism of $G$.

  Step 2: $\varphi$ is an inner automorphism of $G$ implies $\varphi\in\Inn\Aut(G)$.

  Then the result follows.
\end{proof}

\begin{thm-remark}
We can relate the notion of an [[inner Automorphism of G]] with elements
of [[InnAut G]] from \mml{autgroup}. The only peculiarity is that
\mml{autgroup} requires $G$ to be a \emph{strict} group.
\end{thm-remark}

<<Theorem: $f$ in [[InnAut G]] iff $f$ is [[inner Automorphism of G]]>>=
<<Lemma: Elements of [[InnAut G]] are automorphisms>>

theorem Th27:
  for G being strict Group
  for f being object
  holds (f in InnAut G) iff (f is inner Automorphism of G)
proof
  let G be strict Group;
  let f be object;
  A1: f is Automorphism of G implies
      f is Element of Funcs (the carrier of G, the carrier of G) by FUNCT_2:9;
  thus (f in InnAut G) implies (f is inner Automorphism of G)
  <<Proof $f$ is in [[InnAut G]] $\implies$ ($f$ is inner automorphism)>>
  thus (f is inner Automorphism of G) implies (f in InnAut G)
  <<Proof ($f$ is inner automorphism) $\implies$ $f$ is in [[InnAut G]]>>
  thus thesis;
end;
@ %def Th27

\begin{proof}[Proof step ($\varphi\in\Inn\Aut(G)\implies\varphi$ is inner).]
The proof amounts to unwinding
definitions, but the subtlety is in first reconsidering $\varphi$ as an
Automorphism of $G$ thanks to our handy-dandy lemma.
\end{proof}

<<Proof $f$ is in [[InnAut G]] $\implies$ ($f$ is inner automorphism)>>=
proof
  assume B1: f in InnAut G;
  then reconsider f as Automorphism of G by Lm6;
  consider a being Element of G such that
  B2: for x being Element of G holds f.x = x |^ a
  by A1,B1,AUTGROUP:def 4;
  thus thesis by Def2,B2;
end;
@

\begin{proof}[Proof step ($\varphi$ is inner$\implies\varphi\in\Inn\Aut(G)$).]
This is again unwinding the definitions. The same subtlety lurks here,
requiring us to reconsider $f$ as an inner automorphism of $G$.
\end{proof}

<<Proof ($f$ is inner automorphism) $\implies$ $f$ is in [[InnAut G]]>>=
proof
  assume f is inner Automorphism of G;
  then reconsider f as inner Automorphism of G;
  consider a being Element of G such that
  B1: for x being Element of G holds f.x = x |^ a
  by Def2;
  thus thesis by A1,B1,AUTGROUP:def 4;
end;
@

\begin{lemma}
Any member of the group $\Inn\Aut(G)$ is an Automorphism of $G$.
\end{lemma}

It's relatively straightforward to show that if $f$ is an element of
[[InnAut G]], then $f$ is an [[Automorphism of G]]. We just unwind the
definitions.

<<Lemma: Elements of [[InnAut G]] are automorphisms>>=
Lm6:
  for G being strict Group
  for f being Element of InnAut G
  holds f is Automorphism of G
proof
  let G be strict Group;
  let f be Element of InnAut G;
  f is Element of Aut G by AUTGROUP:12;
  hence f is Automorphism of G by Th26;
end;
@ %def Lm6

\begin{theorem}\label{thm:characteristic:inner:image-of-inner-automorphism-on-subgroup}
Given any element $a\in G$, and any inner automorphism $\varphi$ of $G$ such
that $\forall x\in G, f(x) = x^{a} = a^{-1}xa$, it follows that the
image of a subgroup under $f$ is the conjugate of that subgroup
$\varphi(H) = H^{a}$.
\end{theorem}

\begin{proof}
Let $\varphi\in\End(G)$ be defined by hypothesis as $\varphi(x)=x^{a}$
for some fixed $a\in G$. We have $\varphi|_{H}(h)= h^{a}$ for any $h\in H$.
We show $y\in\varphi|_{H}(H)\iff y\in H^{a}$ in two sub-proofs. Then it
follows that $\varphi(H)=H^{a}$ by Definition~\mml[def6]{group2}\MizDef{GROUP\_2}{6}.
\end{proof}

<<Theorem: inner automorphism acting on subgroup is conjugate of argument>>=
theorem Th28:
  for a being Element of G
  for f being inner Automorphism of G
  st (for x being Element of G holds f.x = x |^ a)
  holds Image(f|H) = H |^ a
proof
  let a be Element of G,
      f be inner Automorphism of G;
  assume
A1: for x being Element of G holds f.x = x |^ a;
A2: for h being Element of G st h in H holds (f|H).h = h |^ a
  proof
    let h be Element of G;
    assume h in H;
    hence (f|H).h = f.h by Th1
                 .= h |^ a by A1;
  end;

A3: for y being Element of G st y in Image(f|H) holds y in H |^ a
  <<Proof $\forall y\in G, y\in f(H)\implies y\in H^{a}$>>
  for y being Element of G st y in H |^ a holds y in Image(f|H)
  <<Proof $\forall y\in G, y \in f(H)\impliedby y\in H^{a}$>>
  hence (H |^ a) = Image(f|H) by A3;
end;
@ %def Th28

\begin{proof}[{Proof step 1 ($\forall y\in G, y\in f(H)\implies y\in H^{a}$)}.]
The forward direction amounts to unwrapping the definition of $f(h) = h^{a}$.
Since $h\in H$, it follows $h^{a}\in H^{a}$ by Theorem~\mml[Th58]{group3}\MizThm{GROUP\_3}{58}.
\end{proof}

<<Proof $\forall y\in G, y\in f(H)\implies y\in H^{a}$>>=
proof
  let y be Element of G;
  assume y in Image(f|H);
  then consider h being Element of H such that
  B1: (f|H).h = y by GROUP_6:45;
  reconsider h as Element of G by GROUP_2:42;
  B2: h in H;
  then h |^ a = (f|H).h by A2
             .= y by B1;
  hence y in H |^ a by B2,GROUP_3:58;
end;
@

\begin{proof}[{Proof step 2 ($\forall y\in G, y \in f(H)\impliedby y\in H^{a}$)}.]
The proof in the backwards direction begins with $y\in H^{a}$ must look
like $y = g^{a}$ for some $g\in H$ by Theorem~\mml[Th58]{group3}\MizThm{GROUP\_3}{58},
and showing $y = f(g)$, which amounts to ``plug it in''.
\end{proof}

<<Proof $\forall y\in G, y \in f(H)\impliedby y\in H^{a}$>>=
proof
  let y be Element of G;
  assume y in H |^ a;
  then consider g being Element of G such that
  B1:    y=g|^a and
  B2:    g in H
  by GROUP_3:58;

  B3: (f|H).g = f.g by Th1,B2
             .= g |^ a by A1
             .= y by B1;
  thus y in Image(f|H) by B2,B3,GROUP_6:45;
end;
@

\begin{theorem}[Kernel of inner automorphism is trivial]\label{thm:characteristic:inner:ker-of-inner-is-trivial}
Let $G$ be a group, $\varphi\in\End(G)$ be defined by $\forall x\in
G,\varphi(x)=x^{a}$ for some fixed $a\in G$.
Then $\ker(\varphi)=\trivialSubgroup_{G}$.
\end{theorem}

We are proving something a little
more general, namely, any endomorphism $f\colon G\to G$ defined by
$f(x)=x^{a}$ (for some fixed $a\in G$) will have a trivial kernel.

\begin{proof}[Proof outline]
If $\varphi\in\End(G)$ is defined as $\forall x\in G,\varphi(x)=x^{a}$
for some fixed $a\in G$, then we will prove
$\ker(\varphi)\subgroup\trivialSubgroup_{G}$. We know from Theorem~\mml[Th65]{group2}%
\MizThm{GROUP\_2}{65} that $\trivialSubgroup_{G}\subgroup\ker(\varphi)$.
Since one is subgroup of the other (and vice-versa), we know from
Theorem~\mml[Th55]{group2}\MizThm{GROUP\_2}{55} they must be equal as subgroups.
\end{proof}

<<Theorem: Kernel of conjugation as endomorphism>>=
theorem Th29:
  for a being Element of G
  for f being Endomorphism of G
  st (for x being Element of G holds f.x = x |^ a)
  holds Ker f = (1).G
proof
  let a be Element of G;
  let f be Endomorphism of G;
  assume A1: for x being Element of G holds f.x = x |^ a;
  for x being Element of G holds x in Ker f implies x in (1).G
  <<Proof $\ker(f)\subset\trivialSubgroup$>>
  then A2: Ker f is Subgroup of (1).G by GROUP_2:58;

  A3: (1).G is Subgroup of Ker f by GROUP_2:65;
  thus Ker f = (1).G by A2,A3,GROUP_2:55;
end;
@ %def Th29

\begin{proof}[Sub-proof ($\ker(f)\subset\trivialSubgroup$)]
Let $x\in\ker(f)$ be arbitrary, then $f(x)=1_{G}$. But this means $x=1_{G}$
(according to Theorem~\mml[Th18]{group3}\MizThm{GROUP\_3}{18}). Thus
$x\in\trivialSubgroup_{G}$ by definition of the trivial subgroup.
\end{proof}

<<Proof $\ker(f)\subset\trivialSubgroup$>>=
proof
  let x be Element of G;
  assume x in Ker f;
  then 1_G = f.x by GROUP_6:41
          .= x |^ a by A1;
  then x = 1_G by GROUP_3:18;
  hence x in (1).G by GROUP_2:46;
end;
@

\begin{theorem}[{Conjugation by fixed element is an automorphism}]\label{thm:characteristic:inner:conjugation-by-fixed-element-is-an-automorphism}
Let $G$ be a group, $a\in G$ a fixed element.
The endomorphism $\varphi\colon G\to G$ defined by $\varphi(x)=x^{a}$ is, in fact,
an automorphism of $G$.
\end{theorem}

\begin{proof}[Proof outline]
We establish $\varphi$ is injective because it has a trivial kernel
(thanks to Theorem~\ref{thm:characteristic:inner:ker-of-inner-is-trivial} and
Theorem~\mml[Th56]{group6}\MizThm{GROUP\_6}{56}).
We then prove $\psi\in\End(G)$ exists such that $\varphi\circ\psi=\id_{G}$.
The existence of such a $\psi$ implies $\rng(\varphi)=G$, which implies
$\varphi$ is surjective. We then have $\varphi$, being both injective
and surjective, is bijective and moreover an automorphism.
\end{proof}

<<Theorem: Conjugation by fixed element is an automorphism>>=
theorem Th30:
  for a being Element of G
  for f being Endomorphism of G
  st (for x being Element of G holds f.x = x |^ a)
  holds f is Automorphism of G
proof
  let a be Element of G;
  let f be Endomorphism of G;
  assume A1: for x being Element of G holds f.x = x |^ a;
  then Ker f = (1).G by Th29;
  then A2: f is one-to-one by GROUP_6:56;
  ex fInv being Endomorphism of G st f*fInv = id (the carrier of G)
  <<Proof an endomorphism $f^{-1}$ exists>>

  then f is onto by FUNCT_2:18;
  hence f is Automorphism of G by A2;
end;
@ %def Th30

\begin{proof}[Sub-proof (Existence of inverse of conjugation)]
This is the long part of the proof, its length owing to showing every
detail. We can construct $\psi(x)=x^{a^{-1}}$ as a function of the
underlying set of the group. We just need to prove this is an
endomorphism (suffices to prove it respects the group binary operation),
and that $\psi$ is the inverse function of $\varphi$.
\end{proof}

<<Proof an endomorphism $f^{-1}$ exists>>=
proof
  deffunc F(Element of G) = ($1) |^ a";
  consider fInv be Function of the carrier of G, the carrier of G such that
  A3: for g being Element of G holds fInv.g = F(g) from FUNCT_2:sch 4;
  <<Establish $f^{-1}$ is an Endomorphism>>
  <<Establish $f^{-1}$ is the inverse function of $f$>>
  hence thesis;
end;
@

\begin{proof}[Sub-proof ($\psi$ is inverse function of $\varphi$).]
The proof is straightforward, simply compose the functions together and
show we get the identity function. We just have to unwind a lot of
definitions.
\end{proof}

<<Establish $f^{-1}$ is the inverse function of $f$>>=
for x being Element of G holds (f*fInv).x = (id the carrier of G).x
proof
  let x be Element of G;
  (f * fInv).x = f.(fInv.x) by FUNCT_2:15
              .= f.(x |^ a") by A3
              .= (x |^ a") |^ a by A1
              .= x |^ (a" * a) by GROUP_3:24
              .= x |^ 1_G by GROUP_1:def 5
              .= x by GROUP_3:19
              .= (id the carrier of G).x;
  hence thesis;
end;
then f*fInv = id the carrier of G;
@

\begin{proof}[Sub-proof ($\psi$ is an endomorphism)]
We need to show $\psi(x_{1}x_{2})=\psi(x_{1})\psi(x_{2})$, which follows
from the results of conjugation from the article~\mml{group3}.
\end{proof}

<<Establish $f^{-1}$ is an Endomorphism>>=
for x1,x2 being Element of G holds fInv.(x1 * x2) = fInv.x1 * fInv.x2
proof
  let x1,x2 be Element of G;
  A4: fInv.x1 = x1 |^ a" & fInv.x2 = x2 |^ a" by A3;
  fInv.(x1 * x2) = (x1 * x2) |^ a" by A3
                .= (x1 |^ a") * (x2 |^ a") by GROUP_3:23
                .= fInv.x1 * fInv.x2 by A4;
  hence thesis;
end;
then reconsider fInv as Endomorphism of G by GROUP_6:def 6;
@

\begin{corollary}\label{thm:characteristic:inner:conjugation-as-endomorphism-is-inner-automorphism}
Given a group element $a\in G$, we can always construct an
inner automorphism $f\in\Inn(G)$ defined by
$\forall x\in G,f(x)=x^{a}=a^{-1}xa$ conjugation by $a$.
\end{corollary}

\begin{proof}
We have shown in Theorem~\ref{thm:characteristic:inner:conjugation-by-fixed-element-is-an-automorphism} that conjugation is an automorphism, and by
definition it is inner.
\end{proof}

<<Corollary: conjugation of given element is an \emph{inner} automorphism>>=
theorem Th31:
  for a being Element of G
  for f being Endomorphism of G
  st (for x being Element of G holds f.x = x |^ a)
  holds f is inner Automorphism of G by Th30,Def2;
@ %def Th31

\begin{theorem}[Constructing inner automorphisms]
Let $a\in G$ be a group element. Then there exists an inner automorphism
$\varphi\in\Inn(G)$ such that for any $x\in G$ we have $\varphi(x)=x^{a}$.
\end{theorem}

\begin{proof}
We can show that, for any $a\in G$, we can construct a function of the
underlying set of $G$ to itself $\varphi\colon G\to G$ defined by
$\forall x\in G, \varphi(x)=x^{a}$. We have to show this is an
endomorphism, i.e., for any $x_{1},x_{2}\in G$ that
$\varphi(x_{1}x_{2})=\varphi(x_{1})\varphi(x_{2})$. The result follows
thanks to Corollary~\ref{thm:characteristic:inner:conjugation-as-endomorphism-is-inner-automorphism}.
\end{proof}

\begin{thm-remark}[Motivation]
Given a group $G$ and suppose we have an element $a\in G$, can we
construct an inner automorphism $f\in\Inn(G)$ such that
$\forall x\in G, f(x) = x^{a}$? Yes, we can do it!
A wiser way to organize these results may be to first show such an $f$
is an Endomorphism and it exists, then use that result in the proof that
it's an automorphism, and so on.
\end{thm-remark}

<<Theorem: constructing inner automorphisms from group elements>>=
theorem Th32:
  for a being Element of G
  holds ex f being inner Automorphism of G st (for x being Element of G
  holds f.x = x |^ a)
proof
  let a be Element of G;
  deffunc F(Element of G) = ($1) |^ a;
  consider f be Function of the carrier of G, the carrier of G such that
A1: for g being Element of G holds f.g = F(g) from FUNCT_2:sch 4;
  for x1,x2 being Element of G holds f.(x1 * x2) = f.x1 * f.x2
  proof
    let x1,x2 be Element of G;
    A2: f.x1 = x1 |^ a & f.x2 = x2 |^ a by A1;
    f.(x1 * x2) = (x1 * x2) |^ a by A1
               .= (x1 |^ a) * (x2 |^ a) by GROUP_3:23
               .= f.x1 * f.x2 by A2;
    hence thesis;
  end;
  then reconsider f as Endomorphism of G by GROUP_6:def 6;
  for x being Element of G holds f.x = x |^ a & f is inner Automorphism of G
  by A1,Th31;
  hence thesis;
end;
@ %def Th32

\begin{theorem}\label{thm:characteristic:inner:subgroup-is-normal-iff-invariant-under-every-inner-automorphism}
  Let $H\subgroup G$ be an arbitrary subgroup.
  Then $\forall\varphi\in\Inn(G),\varphi(H)=H$ if and only if
  $H\normalSubgroup G$.
\end{theorem}

\begin{thm-remark}
This is another, ``Well, I guess Mizar should have this, but I can't
find it, so here we go!''-type proof. The only quirk is the use of
[[strict Subgroup]], which is necessary because otherwise [[H |^ a]] is
strictly speaking a [[multMagma]], not a subgroup. And to assert two
subgroups are equal, we need a [[strict]] subgroup, which requires
adding a [[strict]] hypothesis.
\end{thm-remark}

\begin{thm-remark}[On \texttt{strict} hypothesis]
The [[strict]] condition is necessary to prove $H\normalSubgroup G$
implies $\forall\varphi\in\Inn(G),\varphi(H)=H$. Otherwise, we end up
with the slightly peculiar situation where $\varphi(H) = $
[[the multMagma of ]]$H$, which isn't terrible, but requires additional
steps later on in proving ``$K\subgroup N$ is characteristic and
$N\normalSubgroup G$ is [[strict]] normal implies $K\normalSubgroup G$.''
\end{thm-remark}

<<Theorem: inner Automorphisms fix only normal Subgroups>>=
theorem Th33:
  for H being strict Subgroup of G
  holds (H is normal) iff (for f being inner Automorphism of G
                           holds Image(f|H)=H)
proof
  let H be strict Subgroup of G;
  A1: (H is normal) implies (for f being inner Automorphism of G
                             holds Image(f|H)=H)
  <<Proof: normal implies fixed by inner automorphisms>>
  A2: not ((for f being inner Automorphism of G holds Image(f|H)=H)
           implies H is normal)
  implies contradiction
  <<Proof by contradiction: fixed by inner automorphisms implies normal>>
  thus thesis by A1,A2;
end;
@ %def Th33

\begin{proof}[Sub-proof (normal implies fixed by inner automorphisms)]
Assume $H\normalSubgroup G$ is a normal subgroup. Let
$\varphi\in\Inn(G)$ be an arbitrary inner automorphism. We have
$\varphi(x)=x^{a}$ for some fixed $a\in G$ and for any $x\in G$. Then
$\varphi(H) = H^{a}$ by Theorem~\ref{thm:characteristic:inner:image-of-inner-automorphism-on-subgroup} and $H^{a}=H$ by Definition~\mml[def13]{group3}%
\MizDef{GROUP\_3}{13} and definition of equality for
subgroups \mml[def6]{group2}\MizDef{GROUP\_2}{6}. Thus the result.
\end{proof}

<<Proof: normal implies fixed by inner automorphisms>>=
proof
  assume B1: H is normal;
  let f be inner Automorphism of G;
  consider a being Element of G such that
  B2: for x being Element of G holds f.x = x |^ a
  by Def2;
  Image(f|H) = H |^ a by B2,Th28
            .= the multMagma of H by B1,GROUP_3:def 13
            .= H;
  hence Image(f|H)=H;
end;
@

\begin{proof}[Sub-proof (fixed by inner automorphisms implies normal)]
We prove by contradiction, assuming $\forall\varphi\in\Inn(G)$ that $\varphi(H)=H$
but assuming for contradiction that $H$ is not a normal subgroup. We run
into the situation where $\varphi(H) = H^{a} = H$. But a subgroup is
normal if and only if $H^{a}=H$. Thus the contradiction.
\end{proof}

\begin{thm-remark}[Proof by contradiction in Mizar]\index{Proof!by Contradiction}
Mizar's proof by contradiction is rather curious. If we want to prove
$P\implies Q$ by contradiction, we assert $\neg(P\implies
Q)\implies\bot$. The proof we offer is about as satisfying as any other
proof by contradiction.
\end{thm-remark}

<<Proof by contradiction: fixed by inner automorphisms implies normal>>=
proof
  assume B1: for f being inner Automorphism of G holds Image(f|H)=H;
  assume not H is normal;
  then consider a being Element of G such that
  B2: H |^ a <> the multMagma of H
  by GROUP_3:def 13;
  consider f being inner Automorphism of G such that
  B3: (for x being Element of G holds f.x= x |^ a)
  by Th32;
  Image(f|H) = H |^ a by B3, Th28;
  hence contradiction by B1,B2;
end;
@
