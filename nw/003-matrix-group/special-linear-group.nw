% -*- mode: poly-noweb; noweb-code-mode: mizar-mode; -*-
\section{Special Linear Group}

The adjective ``special'' refers to having determinant 1.

<<Special Linear Group>>=
<<Theorem: If $\det(A)=\det(B)=1$, then $\det(AB)=1$>>

<<Theorem: determinant of identity matrix>>

<<Theorem: $A$ invertible implies $A^{-1}A=I$ and $AA^{-1}=I$>>

<<Theorem: if $\det(A)=1$, then $\det(A^{-1})=1$>>

<<Schema: Existence of subgroup of $\GL(n,\FF)$ (coarse version)>>

<<Schema: Existence of subgroup of $\GL(n,\FF)$ (nice version)>>

<<Definition: Special linear group>>

<<Register: $\SL(1, \FF)$ is trivial>>

<<Theorem: components of an elementary matrix of the first-kind>>

<<Theorem: Elementary matrices are triangular>>

<<Theorem: diagonal components of elementary matrices>>

<<Theorem: The identity matrix is a diagonal matrix>>

<<Register: identity matrix is diagonal>>

<<Theorem: determinant of elementary matrices>>

<<Theorem: Elementary matrices of the first kind are elements of the special linear group>>

<<Theorem: Elementary Matrices Generate $\SL(n,\FF)$>>
@

\begin{theorem}
Let $A$, $B$ be $n\times n$ matrices over $\FF$.
If $\det(A)=\det(B)=1$, then $\det(AB)=1$.
\end{theorem}

<<Theorem: If $\det(A)=\det(B)=1$, then $\det(AB)=1$>>=
theorem Th18:
  for A,B being Matrix of n,F
  st Det(A) = 1.F & Det(B) = 1.F
  holds Det(A * B) = 1.F
proof
  let A,B be Matrix of n,F;
  assume A1: Det(A) = 1.F;
  assume A2: Det(B) = 1.F;
  thus Det(A * B) = (Det A) * (Det B) by MATRIX11:62
                 .= 1.F * 1.F by A1,A2
                 .= 1.F;
end;
@ %def Th18

\begin{theorem}
The determinant of the identity matrix is 1.
\end{theorem}

<<Theorem: determinant of identity matrix>>=
theorem Th19:
  Det(1.(F, n)) = 1.F
proof
  n >= 1 by NAT_1:25;
  hence Det(1.(F, n)) = 1_ F by MATRIX_7:16
                     .= 1.F;
end;
@ %def Th19

\begin{theorem}
Let $A$ be an invertible $n\times n$ matrix over $\FF$.
Then $A^{-1}A=I$ and $AA^{-1}=I$.
\end{theorem}

<<Theorem: $A$ invertible implies $A^{-1}A=I$ and $AA^{-1}=I$>>=
theorem Th20:
  for A being invertible Matrix of n,F
  holds A * (A~) = 1.(F, n) & (A~) * A = 1.(F, n)
proof
  let A be invertible Matrix of n,F;
  A~ is_reverse_of A by MATRIX_6:def 4;
  then (A~) * A = A * (A~) & (A~) * A = 1.(F,n) by MATRIX_6:def 2;
  hence thesis;
end;
@ %def Th20

\begin{theorem}
Let $A$ be an invertible square matrix over $\FF$.
If $\det(A)=1$, then $\det(A^{-1})=1$.
\end{theorem}

<<Theorem: if $\det(A)=1$, then $\det(A^{-1})=1$>>=
theorem Th21:
  for A being invertible Matrix of n,F
  st Det(A) = 1.F
  holds Det(A~) = 1.F
proof
  let A be invertible Matrix of n,F;
  assume A1: Det(A) = 1.F;
  1.F = Det(1.(F, n)) by Th19
     .= Det(A * (A~)) by Th20
     .= (Det A) * (Det (A~)) by MATRIX11:62
     .= (1.F) * (Det (A~)) by A1
     .= Det (A~);
  hence thesis;
end;
@ %def Th21

\begin{road-map}
Before we define the special linear group, we should note that we will
use a recurring pattern: there is a subgroup of $\GL(n,\FF)$ defined by
some predicate $P[X]$ describing invertible square matrices over $\FF$
which is sufficiently nice (i.e., for any $X$, $Y$ such that $P[X]$ and
$P[Y]$, we have $P[XY]$ and $P[X^{-1}]$ and $P[I]$), then there is a
strict subgroup consisting of these elements. We can describe this using
a scheme, so we do the heavy lifting in the first scheme, then we make
it palatable in the second scheme. Then we will define the special
linear subgroup.
\end{road-map}

\begin{scheme}
Let $F_{1}$ be a field and $n_{1}$ be a positive natural number and
$P[X]$ be a unary predicate predicate of $n_{1}\times n_{1}$ matrices
over $F_{1}$.
When
\begin{enumerate}
\item $P[I]$ holds; and
\item for any $X,Y$ invertible $n_{1}\times n_{1}$ matrices
over $F_{1}$ such that $P[X]$ and $P[Y]$, we have $P[XY]$; and
\item for any $X$ invertible $n_{1}\times n_{1}$ matrix
over $F_{1}$ which satisfies $P[X]$, then we also have $P[X^{-1}]$;
\end{enumerate}
then there exists a strict subgroup $H\subgroup GL(n_{1}, F_{1})$
such that its underlying set is $\{\,A\in\GL(n_{1},F_{1}) : \exists
X\in\Mat(n_{1},F_{1}),P[X]\land A=X\,\}$.
\end{scheme}

\begin{sch-remark}
We want to use the subgroup criteria, established in
Theorem~\mml[Th52]{group2}; i.e., if we let
\begin{equation}
U = \{X\in\GL(n,\FF)\mid P[X] \},
\end{equation}
and if further
\begin{enumerate}
\item $U\neq\emptyset$;
\item for any $g_{1},g_{2}\in\GL(n,\FF)$ such that $g_{1}\in U$ and
  $g_{2}\in U$, then $g_{1}g_{2}\in U$; and
\item for any $g\in\GL(n,\FF)$ such that $g\in U$, then $g^{-1}\in U$;
\end{enumerate}
then there exists a subgroup $H\subgroup G$ such that the underlying set
of $H$ is $U$.
\end{sch-remark}


<<Schema: Existence of subgroup of $\GL(n,\FF)$ (coarse version)>>=
scheme
SubgroupSch { F1() -> Field,
              n1() -> positive Nat,
              P[Matrix of n1(), F1()] } :
  ex H being strict Subgroup of GL(n1(), F1()) st
  the carrier of H = { A where A is Element of GL(n1(), F1()) :
                       ex X being invertible Matrix of n1(), F1()
                       st P[X] & A = X }
provided
A1: P[1.(F1(), n1())] and
A2: for X,Y being invertible Matrix of n1(), F1()
    st P[X] & P[Y] holds P[X*Y] and
A3: for X being invertible Matrix of n1(), F1()
    st P[X]
    holds P[X~]
proof
  set U = {A where A is Element of GL(n1(), F1()) : ex X being invertible
  Matrix of n1(), F1() st P[X] & A = X};
  for x being object st x in U holds x in the carrier of GL(n1(), F1())
  proof
    let x be object;
    assume Z1: x in U;
    then ex A being Element of GL(n1(), F1()) st A = x & ex X being
    invertible Matrix of n1(), F1() st P[X] & A = X;
    then x in GL(n1(), F1());
    hence x in the carrier of GL(n1(), F1());
  end;
  then U c= the carrier of GL(n1(), F1()) by TARSKI:def 3;
  then reconsider U as Subset of GL(n1(), F1());

  A4: for x being invertible Matrix of n1(),F1()
  holds x in U iff P[x] & x in GL(n1(), F1())
  proof
    let x be invertible Matrix of n1(),F1();
    thus x in U implies (P[x] & x in GL(n1(), F1()))
    proof
      assume x in U;
      then ex A being Element of GL(n1(), F1()) st A = x
      & ex X being invertible Matrix of n1(),F1() st P[X] & A = X;
      hence thesis;
    end;
    assume P[x] & x in GL(n1(), F1());
    hence thesis;
  end;

  A5: 1_(GL(n1(), F1())) in U
  proof
    B1: 1.(F1(), n1()) is invertible Matrix of n1(),F1();
    1.(F1(), n1()) is Element of GL(n1(), F1()) by Th9;
    then B2: 1.(F1(), n1()) in GL(n1(), F1());
    B3: P[1.(F1(), n1())] by A1;
    1_(GL(n1(), F1())) = 1.(F1(), n1()) by Th16;
    hence thesis by A4, B1, B2, B3, Th19;
  end;
  A6: for g1,g2 being Element of GL(n1(), F1()) st g1 in U & g2 in U
  holds g1*g2 in U
  proof
    let g1,g2 be Element of GL(n1(), F1());
    assume B1: g1 in U;
    assume B2: g2 in U;
    then ex A being Element of GL(n1(), F1()) st A = g2 & ex Y being
    invertible Matrix of n1(),F1() st P[Y] & A = Y;
    then consider Y being invertible Matrix of n1(),F1() such that
    B3: P[Y] & g2 = Y;
    ex A being Element of GL(n1(), F1()) st A = g1 & ex Y being
    invertible Matrix of n1(),F1() st P[Y] & A = Y by B1;
    then consider X being invertible Matrix of n1(),F1() such that
    B4: P[X] & g1 = X;
    ex Z being invertible Matrix of n1(),F1() st P[Z] & g1*g2 = Z
    proof
      X*Y is invertible Matrix of n1(),F1() by MATRIX_6:36;
      then reconsider Z = X*Y as invertible Matrix of n1(),F1();
      take Z;
      thus P[Z] by B3,B4,A2;
      thus g1*g2 = Z by B3,B4,Th12;
    end;
    hence g1*g2 in U;
  end;
  
  A7: for g being Element of GL(n1(), F1()) st g in U
  holds g" in U
  proof
    let g be Element of GL(n1(), F1());
    assume B1: g in U;
    then ex A being Element of GL(n1(), F1()) st A = g & ex X being
    invertible Matrix of n1(),F1() st P[X] & A = X;
    then consider X being invertible Matrix of n1(),F1() such that
    B2: P[X] & g = X;
    B3: P[X~] by B2, A3;
    g" = X~ by B2, Th17;
    hence g" in U by B3;
  end;
  hence ex H being strict Subgroup of GL(n1(), F1()) st
  the carrier of H = { A where A is Element of GL(n1(), F1()) :
                       ex X being invertible Matrix of n1(), F1()
                       st P[X] & A = X } by A5, A6, GROUP_2:52;
end;
@ %def SubgroupSch

\begin{scheme}
Let $F_{1}$ be a field and $n_{1}$ be a positive natural number and
$P[X]$ be a unary predicate predicate of $n_{1}\times n_{1}$ matrices
over $F_{1}$.
When
\begin{enumerate}
\item $P[I]$ holds; and
\item for any $X,Y$ invertible $n_{1}\times n_{1}$ matrices
over $F_{1}$ such that $P[X]$ and $P[Y]$, we have $P[XY]$; and
\item for any $X$ invertible $n_{1}\times n_{1}$ matrix
over $F_{1}$ which satisfies $P[X]$, then we also have $P[X^{-1}]$;
\end{enumerate}
then there exists a strict subgroup $H\subgroup GL(n_{1}, F_{1})$
such that for any invertible $n_{1}\times n_{1}$ matrix over $F_{1}$, $X$,
we have $X\in H$ if and only if $X\in\GL(n_{1}, F_{1})$ and $P[X]$.
\end{scheme}

<<Schema: Existence of subgroup of $\GL(n,\FF)$ (nice version)>>=
scheme
SubgroupSch2 { F1() -> Field,
               n1() -> positive Nat,
               P[Matrix of n1(), F1()] } :
  ex H being strict Subgroup of GL(n1(), F1()) st
  for x being invertible Matrix of n1(), F1()
  holds x in H iff P[x] & x in GL(n1(), F1())
provided
A1: P[1.(F1(), n1())] and
A2: for X,Y being invertible Matrix of n1(), F1()
    st P[X] & P[Y] holds P[X*Y] and
A3: for X being invertible Matrix of n1(), F1()
    st P[X]
    holds P[X~]
proof
  consider H being strict Subgroup of GL(n1(), F1()) such that
  A4: the carrier of H = { A where A is Element of GL(n1(), F1()) :
                       ex X being invertible Matrix of n1(), F1()
                       st P[X] & A = X }
  from SubgroupSch(A1,A2,A3);
  take H;
  for x being invertible Matrix of n1(), F1()
  holds x in H iff P[x] & x in GL(n1(), F1())
  proof
    let x be invertible Matrix of n1(), F1();
    thus x in H implies P[x] & x in GL(n1(), F1())
    proof
      assume Z1: x in H;
      then ex A being Element of GL(n1(), F1()) st A = x &
      ex X being invertible Matrix of n1(),F1() st P[X] & A = X by A4;
      hence thesis;
    end;
    assume B1: P[x];
    assume B2: x in GL(n1(), F1());
    then reconsider A=x as Element of GL(n1(), F1());
    ex X being invertible Matrix of n1(),F1() st P[X] & A = X by B1,B2;
    then A in H by A4;
    hence x in H;
  end;
  hence thesis;
end;
@ %def SubgroupSch2

\begin{definition}
We define the \define{Special Linear Group} to be the subgroup of
$\GL(n,\FF)$ consisting of those elements whose determinant is $1$.
\end{definition}

<<Definition: Special linear group>>=
definition
  let n be positive Nat;
  let F be Field;
  func SL(n, F) -> strict Subgroup of GL(n, F) means :Def9:
  for x being invertible Matrix of n, F
  holds x in it iff (Det x) = 1.F & x in GL(n, F);
  existence
  proof
    defpred P[Matrix of n,F] means Det($1) = 1.F;
    A1: P[1.(F, n)] by Th19;
    A2: for X,Y being invertible Matrix of n,F
    st P[X] & P[Y] holds P[X*Y] by Th18;
    A3: for X being invertible Matrix of n,F
    st P[X]
    holds P[X~] by Th21;
    consider H being strict Subgroup of GL(n, F) such that
    A4: for x being invertible Matrix of n,F
    holds x in H iff P[x] & x in GL(n,F)
    from SubgroupSch2(A1,A2,A3); 
    take H;
    thus thesis by A4;
  end;
  uniqueness
  proof
    let H1, H2 be strict Subgroup of GL(n, F);
    assume A1: for x being invertible Matrix of n, F
    holds x in H1 iff (Det x) = 1.F & x in GL(n, F);
    assume A2: for x being invertible Matrix of n, F
    holds x in H2 iff (Det x) = 1.F & x in GL(n, F);
    
    for x being Element of GL(n, F) holds x in H1 iff x in H2
    proof
      let x be Element of GL(n, F);
      reconsider X=x as invertible Matrix of n, F by Th8;
      X in H1 iff Det X = 1.F & X in GL(n,F) by A1;
      then X in H1 iff X in H2 by A2;
      hence thesis;
    end;
    hence H1 = H2 by GROUP_2:def 6;
  end;
end;
@ %def SL Def9

\begin{voc}
We need to add ``[[SL]]'' to the vocabulary file.
\end{voc}

<<DICT/matgrp-1.voc>>=
OSL
@

\begin{lemma}
If $M$ is a $1\times 1$ matrix over $\FF$, then there exists some
$a\in\FF$ such that $M=[a]$.
\end{lemma}

<<Lemma: explicit formula for a $1\times 1$ matrix>>=
LmSingleton:
  for M being Matrix of 1,F
  ex a being Element of F
  st M = <*<* a *>*> & a = M * (1,1)
proof
  let M be Matrix of 1,F;
  A1: Indices M = [: Seg 1, Seg 1 :] & 1 in Seg 1
  by MATRIX_0:24, FINSEQ_1:2, TARSKI:def 1;
  then A2: [1,1] in Indices M by ZFMISC_1:87;
  then consider a being Element of F such that
  A3: a = M * (1, 1) by MATRIX_0:def 5;
  take a;
  <*<* a *>*> is Matrix of 1, F;
  thus M = <*<* a *>*>
  proof
    len M = 1 & len <*<* a *>*> = 1 & width M = 1 & width <*<* a *>*> = 1
    by MATRIX_0:24;
    then B1: len M = len <*<* a *>*> & width M = width <*<* a *>*>;
    for i,j being Nat st [i,j] in Indices M
    holds M * (i, j) = <*<* a *>*> * (i,j)
    proof
      let i,j be Nat;
      assume B2: [i,j] in Indices M;
      then i in Seg 1 & j in Seg 1 by A1, ZFMISC_1:87;
      then i = 1 & j = 1 by FINSEQ_1:2, TARSKI:def 1;
      hence M * (i, j) = <*<* a *>*> * (i,j) by B2,A2,A3, MATRIX_0:49;
    end;

    hence thesis by B1, MATRIX_0:21;
  end;
  thus a = M * (1,1) by A3;
end;
@ %def LmSingleton

\begin{lemma}
The $1\times 1$ identity matrix is $I_{1} = [1]$.
\end{lemma}

<<Lemma: the $1\times 1$ identity matrix is $[1]$>>=
<<Lemma: explicit formula for a $1\times 1$ matrix>>

LmSingletonId:
  1.(F, 1) = <*<* 1.F *>*>
proof
  consider a being Element of F such that
  A1: 1.(F, 1) = <*<* a *>*> & a = 1.(F,1) * (1,1)
  by LmSingleton;
  Indices 1.(F,1) = [: Seg 1, Seg 1 :] & 1 in Seg 1
  by MATRIX_0:24, FINSEQ_1:2, TARSKI:def 1;
  then [1,1] in Indices 1.(F,1) by ZFMISC_1:87;
  then 1.(F,1) * (1,1) = 1.F by MATRIX_1:def 3;
  hence thesis by A1;
end;
@ %def LmSingletonId

\begin{registration}
The group $\SL(1,\FF)$ is trivial.
\end{registration}

<<Register: $\SL(1, \FF)$ is trivial>>=
<<Lemma: the $1\times 1$ identity matrix is $[1]$>>

registration
  let F;
  cluster SL(1, F) -> trivial;
  correctness
  proof
    A1: the carrier of SL(1, F) c= {1.(F, 1)}
    proof
      the carrier of SL(1, F) c= the carrier of GL(1, F) by GROUP_2:def 5;
      then the carrier of SL(1, F) is Subset of the carrier of GL(1, F);
      then B1: the carrier of SL(1, F) is Subset of GL(1, F) by Def9;
      
      for x being object st x in {1.(F, 1)} holds x in the carrier of GL(1,F)
      proof
        let x be object;
        assume x in {1.(F, 1)};
        then x = 1.(F, 1) by TARSKI:def 1;
        then x is Element of GL(1, F) by Th9;
        hence x in the carrier of GL(1,F);
      end;
      then {1.(F,1)} c= the carrier of GL(1, F) by TARSKI:def 3;
      then B2: {1.(F, 1)} is Subset of GL(1, F) by TARSKI:def 1;

      for x being Element of GL(1, F) st x in the carrier of SL(1, F)
      holds x in {1.(F, 1)}
      proof
        let x be Element of GL(1, F);
        assume B3: x in the carrier of SL(1, F);
        reconsider X=x as invertible Matrix of 1, F by Th8;
        x in SL(1, F) by B3;
        then X in SL(1, F);
        then B4: (Det X) = 1.F & X in GL(1, F) by Def9;

        consider x1 being Element of F such that
        B5: X = <* <* x1 *> *> & x1 = X * (1, 1)
        by LmSingleton;
        
        B6: X = <* <* x1 *> *> by B5, LmSingleton, MATRIX_0:15, MATRIX_0:49;
        1.F = Det X by B4
           .= x1 by B5, MATRIX_3:34;
        then X = <* <* 1.F *> *> by B6
              .= 1.(F, 1) by LmSingletonId;
        then x = 1.(F, 1);
        hence x in {1.(F, 1)} by TARSKI:def 1;
      end;

      hence thesis by B1,B2,SUBSET_1:2;
    end;

    1.(F, 1) in SL(1, F) by LmIdentityOfSL;
    then for x being object st x in {1.(F,1)} holds x in the carrier of SL(1,F)
    by TARSKI:def 1,def 3;
    then {1.(F, 1)} c= the carrier of SL(1, F) by Def9, TARSKI:def 1,def 3;
    then the carrier of SL(1, F) = {1.(F, 1)} by A1, XBOOLE_0:def 10;
    hence thesis by GROUP_6:def 2;
  end;
end;
@ 

\begin{theorem}
The components of an elementary matrix of the first-kind $E_{\ell,k}(a)$
is given as:
\begin{enumerate}
\item $[E_{\ell,k}(a)]_{i,j}=1$ when $i=j$;
\item $[E_{\ell,k}(a)]_{i,j}=a$ when $i=\ell$ and $j=k$;
\item $[E_{\ell,k}(a)]_{i,j}=0$ when $i\neq j$ and either $i\neq\ell$ or $k\neq j$.
\end{enumerate}
\end{theorem}

<<Theorem: components of an elementary matrix of the first-kind>>=
theorem Th22:
  for a being Element of F
  for ell, k being Nat st ell <> k & [k, ell] in Indices 1.(F,n)
  for i,j being Nat st [i,j] in Indices 1.(F, n)
  holds (i = j implies RlineXScalar(1.(F, n), ell, k, a) * (i,j) = 1.F)
  & (i = ell & j = k implies RlineXScalar(1.(F, n), ell, k, a) * (i,j) = a)
  & ((i <> ell or j <> k) & i <> j
     implies RlineXScalar(1.(F, n), ell, k, a) * (i,j) = 0.F)
proof
  let a be Element of F;
  let ell, k be Nat;
  assume A1: ell <> k;
  assume A2: [k,ell] in Indices 1.(F,n);
  let i,j be Nat;
  assume A3: [i,j] in Indices 1.(F, n);
  then A4: i in dom (1.(F, n)) & j in Seg width (1.(F, n)) by ZFMISC_1:87;
  
  A5: i in dom 1.(F,n) & j in dom 1.(F,n) & k in dom 1.(F,n) & ell in
      dom 1.(F,n)
  proof
    width 1.(F,n) = n & len 1.(F,n) = n by MATRIX_0:23;
    then width 1.(F,n) = n & dom (1.(F,n)) = Seg n by FINSEQ_1:def 3;
    hence thesis by A3, A2, ZFMISC_1:87;
  end;
  
  A6: [k,j] in Indices 1.(F,n) & [ell, j] in Indices 1.(F,n)
  proof
    width 1.(F,n) = n & len 1.(F,n) = n by MATRIX_0:23;
    then B1: width 1.(F,n) = n & dom (1.(F,n)) = Seg n by FINSEQ_1:def 3;
    k in dom (1.(F, n)) & ell in Seg width (1.(F, n)) by A2, ZFMISC_1:87;
    hence thesis by A4, B1, ZFMISC_1:87;
  end;
  thus (i = j implies RlineXScalar(1.(F, n), ell, k, a) * (i,j) = 1.F)
  proof
    assume B1: i = j;
    per cases;
    suppose B2: i = ell;
      RlineXScalar(1.(F, n), ell, k, a) * (i,j)
       = a*((1.(F, n)) * (k, j)) + ((1.(F,n)) * (ell, j)) by A4,A5,B2,MATRIX12:def 3
      .= a*(0.F) + ((1.(F,n)) * (i, j)) by A1, A6, B1, B2, MATRIX_1:def 3
      .= (1.(F,n)) * (i, j) by RLVECT_1:4
      .= 1.F by A3, B1, MATRIX_1:def 3;
      hence thesis;
    end;
    suppose B3: i <> ell;
      RlineXScalar(1.(F, n), ell, k, a) * (i,j)
       = (1.(F, n)) * (i,j) by A4, A5, B3, MATRIX12:def 3
      .= 1.F by A3, B1, MATRIX_1:def 3;
      hence RlineXScalar(1.(F, n), ell, k, a) * (i,j) = 1.F;
    end;
  end;
  thus (i = ell & j = k implies RlineXScalar(1.(F, n), ell, k, a) * (i,j) = a)
  proof 
    assume B1: i = ell;
    assume B2: j = k;
    RlineXScalar(1.(F, n), ell, k, a) * (i,j)
     = a*((1.(F,n)) * (k,j)) + ((1.(F,n)) * (ell, j)) by A4,A5,B1,MATRIX12:def 3
    .= a*((1.(F,n)) * (k,j)) + 0.F by A1, A6, B2, MATRIX_1:def 3
    .= a*((1.(F,n)) * (k,j)) by RLVECT_1:4
    .= a*(1.F) by A6, B2, MATRIX_1:def 3
    .= a;
    hence RlineXScalar(1.(F, n), ell, k, a) * (i,j) = a;
  end;
  A7: i <> ell & i <> j
  implies RlineXScalar(1.(F, n), ell, k, a) * (i,j) = 0.F
  proof
    assume B1: i <> ell;
    assume B2: i <> j;
    thus RlineXScalar(1.(F, n), ell, k, a) * (i,j)
       = ((1.(F,n)) * (i, j)) by B1, A4, A5, MATRIX12:def 3
      .= 0.F by A3, B2, MATRIX_1:def 3;
  end;
  j <> k & i <> j
  implies RlineXScalar(1.(F, n), ell, k, a) * (i,j) = 0.F
  proof
    assume B1: j <> k;
    assume B2: i <> j;
    per cases;
    suppose i <> ell;
      hence thesis by A7,B2;
    end;
    suppose B3: i = ell;
      hence RlineXScalar(1.(F, n), ell, k, a) * (i,j)
       = a * ((1.(F,n)) * (k,j)) + ((1.(F,n)) * (ell, j))
         by A4, A5, MATRIX12:def 3
      .= a * (0.F) + ((1.(F,n)) * (i, j)) by A6, B1, B3, MATRIX_1:def 3
      .= 0.F + 0.F by A3, B2, MATRIX_1:def 3
      .= 0.F by RLVECT_1:4;
    end;
  end;
  hence (i <> ell or j <> k) & i <> j
  implies RlineXScalar(1.(F, n), ell, k, a) * (i,j) = 0.F by A7;
end;
@ %def Th22

\begin{theorem}
When $\ell < k$, $[E_{\ell,k}(a)]_{i,j} = 0$ for $i > j$; i.e., $E_{\ell,k}(a)$
is upper-triangular. Similarly, when $\ell > k$, $E_{\ell,k}(a)$ is lower-triangular.
\end{theorem}

<<Theorem: Elementary matrices are triangular>>=
theorem Th23:
  for a being Element of F
  for ell, k being Nat st [k, ell] in Indices 1.(F,n)
  holds (ell < k implies RlineXScalar(1.(F, n), ell, k, a) is upper_triangular)
  & (ell > k implies RlineXScalar(1.(F, n), ell, k, a) is lower_triangular)
proof
  let a be Element of F;
  let ell, k be Nat;
  assume A1: [k, ell] in Indices 1.(F,n);
  A2: Indices RlineXScalar(1.(F, n), ell, k, a) = Indices 1.(F, n)
  proof
    len 1.(F, n) = n & width 1.(F,n) = n by MATRIX_0:23;
    Indices 1.(F, n) = [: dom 1.(F,n), Seg n :] by MATRIX_0:23
                    .= [: Seg (len 1.(F,n)), Seg n :] by FINSEQ_1:def 3
                    .= [: Seg n, Seg n :] by MATRIX_0:23;
    hence thesis by MATRIX_0:24;
  end;
  thus (ell < k implies RlineXScalar(1.(F, n), ell, k, a) is upper_triangular)
  proof 
    assume A3: ell < k;
    for i,j being Nat
    st [i,j] in Indices RlineXScalar(1.(F, n), ell, k, a) & i > j
    holds RlineXScalar(1.(F, n), ell, k, a) * (i,j) = 0.F
    proof
      let i,j be Nat;
      assume B1: [i,j] in Indices RlineXScalar(1.(F, n), ell, k, a);
      assume B2: i > j;
      B3: [i,j] in Indices 1.(F, n) by A2,B1;
      per cases;
      suppose i = ell;
        hence thesis by A1,A2,A3,B1,B2,Th22;
      end;
      suppose i <> ell;
        hence thesis by A1,A2,A3,B1,B2,Th22;
      end;
    end;

    hence RlineXScalar(1.(F, n), ell, k, a) is upper_triangular;
  end;
  assume A4: ell > k;
  for i,j being Nat
  st [i,j] in Indices RlineXScalar(1.(F, n), ell, k, a) & i < j
  holds RlineXScalar(1.(F, n), ell, k, a) * (i,j) = 0.F
  proof
    let i,j be Nat;
    assume B1: [i,j] in Indices RlineXScalar(1.(F, n), ell, k, a);
    assume B2: i < j;
    B3: [i,j] in Indices 1.(F, n) by A2,B1;
    per cases;
    suppose i = ell;
      hence thesis by A1,A2,A4,B1,B2,Th22;
    end;
    suppose i <> ell;
      hence thesis by A1,A2,A4,B1,B2,Th22;
    end;
  end;

  hence RlineXScalar(1.(F, n), ell, k, a) is lower_triangular;
end;
@ %def Th23

\begin{theorem}
The diagonal of $E_{i,j}(\lambda)$ is equal to the diagonal of the
identity matrix.
\end{theorem}

<<Theorem: diagonal components of elementary matrices>>=
theorem Th24:
  for a being Element of F
  for ell, k being Nat st [k, ell] in Indices 1.(F,n) & ell <> k
  holds diagonal_of_Matrix RlineXScalar(1.(F, n), ell, k, a)
  = diagonal_of_Matrix 1.(F,n)
proof
  let a be Element of F;
  let ell, k be Nat;
  assume A1: [k, ell] in Indices 1.(F,n);
  assume A2: ell <> k;
  set LHS = diagonal_of_Matrix RlineXScalar(1.(F, n), ell, k, a);
  set RHS = diagonal_of_Matrix 1.(F,n);
  A3: len LHS = n by MATRIX_3:def 10
             .= len RHS by MATRIX_3:def 10;
  for i being Nat st 1 <= i & i <= len LHS holds LHS.i = RHS.i
  proof
    let i be Nat;
    assume B1: 1 <= i & i <= len LHS;
    then 1 <= i & i <= n by MATRIX_3:def 10;
    then B2: i in Seg n by FINSEQ_1:1;
    Indices 1.(F, n) = [: dom 1.(F,n), Seg n :] by MATRIX_0:23
                    .= [: Seg (len 1.(F,n)), Seg n :] by FINSEQ_1:def 3
                    .= [: Seg n, Seg n :] by MATRIX_0:23;
    then B3: [i, i] in Indices 1.(F,n) by B2, ZFMISC_1:87;
    thus LHS.i = RlineXScalar(1.(F, n), ell, k, a) * (i,i) by B2,MATRIX_3:def 10
    .= 1.F by A1,A2,B3,Th22
    .= (1.(F,n)) * (i, i) by B3, MATRIX_1:def 3
    .= RHS.i by B2,MATRIX_3:def 10;
  end;

  hence LHS = RHS by A3, FINSEQ_1:def 17;
end;
@ %def Th24

\begin{theorem}
The identity matrix is a diagonal matrix.
\end{theorem}

<<Theorem: The identity matrix is a diagonal matrix>>=
theorem Th25:
  1.(F, n) is diagonal
proof
  for i,j being Nat st [i, j] in Indices 1.(F,n) & (1.(F,n)) * (i,j) <> 0.F
  holds i=j
  proof
    let i,j be Nat;
    assume A1: [i, j] in Indices 1.(F,n);
    assume A2: (1.(F,n)) * (i,j) <> 0.F;
    per cases;
    suppose i <> j;
      then (1.(F,n)) * (i,j) = 0.F by A1, MATRIX_1:def 3;
      then contradiction by A2;
      hence thesis;
    end;
    suppose i=j;
      hence thesis;
    end;
  end;
  hence thesis;
end;
@ %def Th25

\begin{registration}
The identity matrix is diagonal.
\end{registration}

<<Register: identity matrix is diagonal>>=
registration
  let F,n;
  cluster 1.(F,n) -> diagonal;
  correctness by Th25;
end;
@

\begin{theorem}
The determinant of elementary matrices is unity, i.e., $\det(E_{ij}(\lambda))=1$.
\end{theorem}

<<Theorem: determinant of elementary matrices>>=
theorem Th26:
  for a being Element of F
  for ell, k being Nat st [k, ell] in Indices 1.(F,n) & ell <> k
  holds Det RlineXScalar(1.(F, n), ell, k, a) = 1.F
proof
  let a be Element of F;
  let ell, k be Nat;
  set M = RlineXScalar(1.(F, n), ell, k, a);
  assume A1: [k, ell] in Indices 1.(F,n);
  assume A2: ell <> k;
  A3: Det (1.(F, n)) = (the multF of F) $$ (diagonal_of_Matrix 1.(F,n))
  by MATRIX13:7;
  ell < k or ell > k by A2,XXREAL_0:1;
  then per cases;
  suppose ell < k;
    then M is upper_triangular by A1, Th23;
    then
    Det M = (the multF of F) $$ (diagonal_of_Matrix M) by MATRIX13:7
         .= (the multF of F) $$ (diagonal_of_Matrix 1.(F,n)) by A1,A2,Th24
         .= Det (1.(F, n)) by A3
         .= 1.F by Th19;
    hence thesis;
  end;
  suppose ell > k;
    then M is lower_triangular by A1, Th23; then
    Det M = (the multF of F) $$ (diagonal_of_Matrix M) by MATRIX13:8
         .= (the multF of F) $$ (diagonal_of_Matrix 1.(F,n)) by A1,A2,Th24
         .= Det (1.(F, n)) by A3
         .= 1.F by Th19;
    hence thesis;
  end;
end;
@ %def Th26

\begin{theorem}
Elementary matrices of the first kind are elements of the special linear group.
\end{theorem}

<<Theorem: Elementary matrices of the first kind are elements of the special linear group>>=
theorem Th27:
  for a being Element of F
  for ell, k being Nat st [k, ell] in Indices 1.(F,n) & ell <> k
  holds RlineXScalar(1.(F, n), ell, k, a) in SL(n, F)
proof
  let a be Element of F;
  let ell, k be Nat;
  assume A1: [k, ell] in Indices 1.(F,n);
  assume A2: ell <> k;
  then A3: Det RlineXScalar(1.(F, n), ell, k, a) = 1.F by A1, Th26;
  Indices 1.(F, n) = [: dom 1.(F,n), Seg n :] by MATRIX_0:23
                  .= [: dom 1.(F,n), Seg (len 1.(F,n)) :] by MATRIX_0:23
                  .= [: dom 1.(F,n), dom 1.(F,n) :] by FINSEQ_1:def 3;
  then k in dom (1.(F,n)) & ell in dom (1.(F,n)) by A1, ZFMISC_1:87;
  then
  A4: RlineXScalar(1.(F, n), ell, k, a) is invertible Matrix of n,F
  by A2, MATRIX12:13;
  A5: Det RlineXScalar(1.(F, n), ell, k, a) = 1.F by A1,A2,Th26;
  RlineXScalar(1.(F, n), ell, k, a) is Element of GL(n, F) by A4,Th8;
  then RlineXScalar(1.(F, n), ell, k, a) in GL(n, F);
  hence thesis by A4,A5,Def9;
end;
@ %def Th27

\begin{theorem}
The special linear group is generated by elementary matrices of the
first kind.
\end{theorem}

\begin{thm-remark}
The elemenetary matrices of the first kind are defined as
``\lstinline{RlineXScalar}'' in Definition~\mml[def3]{matrix12}.
We have
\begin{equation}
E_{\ell,k}(\lambda) = \begin{bmatrix}
  1 &        &         &        &   &        &   \\
    & \ddots &         &        &   &        &   \\
    &        & 1       &        &   &        &   \\
    &        &         & \ddots &   &        &   \\
    &        & \lambda &        & 1 &        &   \\
    &        &         &        &   & \ddots &   \\
    &        &         &        &   &        & 1
\end{bmatrix},
\end{equation}
where $\lambda$ is placed in column $\ell$ and row $k$. In particular,
when multiplying an $n\times m$ matrix on the left by a $n\times n$
elementary matrix $E_{\ell,k}(\lambda)$ corresponds to the row operation
$R_{\ell}\mapsto R_{\ell}+\lambda R_{k}$. This is established in
Theorem~\mml[Th5]{matrix12}. 

We will want to use facts established in
Theorems~\mml[Th6,Th7]{matrix13} stating the determinant of a triangular
matrix is the product of diagonal entries.

One direction is obvious, namely, the subgroup generated by all these
elementary matrices \emph{obviously} forms a subgroup of $\SL(n,\FF)$.
The other direction is not obvious to me, namely: \emph{every} element
of $\SL(n,\FF)$ may be written as a product of elementary matrices.
\end{thm-remark}

<<Theorem: Elementary Matrices Generate $\SL(n,\FF)$>>=
theorem
  for A being Subset of GL(n, F)
  st A = { g where g is Element of GL(n, F) :
           ex ell,k being Nat, a being Element of F
           st g = RlineXScalar(1.(F, n), ell, k, a) }
  holds SL(n, F) = gr A
proof
  let A be Subset of GL(n, F);
  assume A1: A = { g where g is Element of GL(n, F) :
                   ex ell,k being Nat, a being Element of F
                   st g = RlineXScalar(1.(F, n), ell, k, a) };
  thus SL(n, F) = gr A;
end;
@ 