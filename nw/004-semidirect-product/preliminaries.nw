% -*- mode: poly-noweb; noweb-code-mode: mizar-mode; -*-
\section{Preliminaries}

<<Preliminaries>>=
begin :: Preliminaries
<<Reservations for [[group_24.miz]]>>

<<Theorem: Dedekind modular identity>>

<<Theorem: If $H\subgroup G$, then the magma underlying $H$ is a strict subgroup of $G$.>>

<<Theorem: If $N\normalSubgroup G$, the magma underlying $N$ is a strict normal subgroup of $G$>>

<<Theorem: magma of $N$ = magma of $(N)_{H}$>>

<<Theorem: If $H_{i}=K_{i}$, then $H_{1}\cap H_{2}=K_{1}\cap K_{2}$>>

<<Theorem: If $H_{i}=K_{i}$, then $H_{1}\cdot H_{2}=K_{1}\cdot K_{2}$>>

<<Preliminaries about Automorphism Groups>>

<<Preliminaries about Product Groups>>
@

\begin{reserve}
Let $G$ be a strict group, let $A$ be a group, let $\varphi\colon A\to\Aut(G)$
be a group morphism.
\end{reserve}

<<Reservations for [[group_24.miz]]>>=
reserve G for strict Group;
reserve A for Group;
reserve phi for Homomorphism of A,AutGroup(G);
@ 

\begin{theorem}[Dedekind Modular Identity]
Let $A$, $B$, $C$ be subgroups of $G$.
If $A\subgroup C$, then $A(B\cap C)=(AB)\cap C$ as sets.
\end{theorem}

<<Theorem: Dedekind modular identity>>=
::$N Dedekinds ``Modular'' Identity
theorem DedekindModularProperty:
  for G being Group
  for A,B,C being Subgroup of G
  st A is Subgroup of C
  holds A * (B /\ C) = (A * B) /\ carr C
proof
  let G be Group;
  let A,B,C be Subgroup of G;
  assume A is Subgroup of C;
  then the carrier of A c= the carrier of C by GROUP_2:def 5;
  then the carrier of A c= carr C by GROUP_2:def 9;
  then A2: carr A c= carr C by GROUP_2:def 9;
  for x being Element of G
  st x in A * (B /\ C)
  holds x in (A * B) /\ carr C
  proof
    let x be Element of G;
    assume x in A * (B /\ C);
    then x in (carr A) * carr (B /\ C) by GROUP_4:def 8;
    then consider a,b being Element of G such that
    B2: x = a*b & a in carr A & b in carr (B /\ C)
    by GROUP_2:8;
    b in carr B /\ carr C by B2, GROUP_2:81;
    then B3: b in carr B & b in carr C by XBOOLE_0:def 4;
    then x in (carr A)*(carr B) by B2, GROUP_2:8;
    then B4: x in A*B by GROUP_4:def 8;
    x in carr C by A2,B2,B3,GROUP_2:74;
    hence x in (A * B) /\ carr C by B4, XBOOLE_0:def 4;
  end;
  then A3: A * (B /\ C) c= (A * B) /\ carr C;
  for x being Element of G
  st x in (A * B) /\ carr C
  holds x in A * (B /\ C)
  proof
    let x be Element of G;
    assume B1: x in (A * B) /\ carr C;
    then x in A * B & x in carr C by XBOOLE_0:def 4;
    then x in (carr A) * (carr B) by GROUP_4:def 8;
    then consider a,b being Element of G such that
    B2: x = a * b & a in carr A & b in carr B by GROUP_2:8;
    a" in carr C & x in carr C by A2, B1, B2, GROUP_2:75, XBOOLE_0:def 4;
    then B3: (a") * x in carr C by GROUP_2:74;
    b = 1_G * b by GROUP_1:def 4
     .= (a" * a) * b by GROUP_1:def 5
     .= (a") * x by B2, GROUP_1:def 3;
    then b in B & b in C by B2, B3, GROUP_2:def 9;
    then b in B /\ C by GROUP_2:82;
    then b in carr (B /\ C) by GROUP_2:def 9;
    then a * b in (carr A) * carr (B /\ C) by B2, GROUP_2:8;
    hence thesis by B2, GROUP_4:def 8;
  end;
  then (A * B) /\ carr C c= A * (B /\ C);
  hence A * (B /\ C) = (A * B) /\ carr C by A3, XBOOLE_0:def 10;
end;
@ %def DedekindModularProperty

\begin{theorem}
Let $H\subgroup G$.
Then the magma underlying $H$ is a strict subgroup of $G$.
\end{theorem}

<<Theorem: If $H\subgroup G$, then the magma underlying $H$ is a strict subgroup of $G$.>>=
theorem Th2:
  for G being Group
  for H being Subgroup of G
  holds the multMagma of H is strict Subgroup of G
proof
  let G be Group;
  let H be Subgroup of G;
  set H0 = the multMagma of H;
  A1: the multMagma of H is non empty Group-like;
  (the carrier of H0 c= the carrier of G)
  & (the multF of H0 = (the multF of G)||(the carrier of H0))
  by GROUP_2:def 5;
  hence the multMagma of H is strict Subgroup of G by A1,GROUP_2:def 5;
end;
@ %def Th2

\begin{theorem}
Let $N\normalSubgroup G$.
Then the magma underlying $N$ is a strict normal subgroup of $G$.
\end{theorem}

<<Theorem: If $N\normalSubgroup G$, the magma underlying $N$ is a strict normal subgroup of $G$>>=
theorem Th3:
  for G being Group
  for N being normal Subgroup of G
  holds the multMagma of N is strict normal Subgroup of G
proof
  let G be Group;
  let N be normal Subgroup of G;
  reconsider N0 = the multMagma of N as strict Subgroup of G by Th2;

  for g being Element of G
  holds N0 |^ g = N0
  proof
    let g be Element of G;
    B1: carr N = the carrier of N by GROUP_2:def 9
              .= carr N0 by GROUP_2:def 9;
    B2: N |^ g = the multMagma of N by GROUP_3:def 13;
    the carrier of (N0 |^ g) = (carr N) |^ g by B1,GROUP_3:def 6
                            .= the carrier of N0 by B2, GROUP_3:def 6;
    hence N0 |^ g = N0 by GROUP_2:59;
  end;
  hence the multMagma of N is strict normal Subgroup of G
  by GROUP_3:def 13;
end;
@ %def Th3

\begin{theorem}
Let $H\subgroup G$ and $N\normalSubgroup G$. If $N\subgroup H$,
then the magma underlying $N$ is equal to the magma underlying $(N)_{H}$.
\end{theorem}

<<Theorem: magma of $N$ = magma of $(N)_{H}$>>=
theorem
  for G being Group
  for H being Subgroup of G
  for N being normal Subgroup of G
  st N is Subgroup of H
  holds the multMagma of N = the multMagma of (H,N)`*`
proof
  let G be Group;
  let H be Subgroup of G;
  let N be normal Subgroup of G;
  assume Z1: N is Subgroup of H;
  then N is normal Subgroup of H by GROUP_6:8;
  then reconsider N1=the multMagma of N as strict normal Subgroup of H
  by Th3;
  thus the multMagma of N = the multMagma of N1
                         .= the multMagma of (H,N)`*` by GROUP_6:def 1;
end;
@

\begin{theorem}
Let $K\subgroup G$.
Let $H_{1},H_{2}\subgroup G$ and $K_{1},K_{2}\subgroup K$
be such that $H_{i}=K_{i}$ for $i=1,2$.
Then $H_{1}\cap H_{2}=K_{1}\cap K_{2}$.
\end{theorem}

<<Theorem: If $H_{i}=K_{i}$, then $H_{1}\cap H_{2}=K_{1}\cap K_{2}$>>=
theorem ThCapLemma:
  for G being Group
  for H1,H2,K being Subgroup of G
  for K1,K2 being Subgroup of K
  st the multMagma of H1 = the multMagma of K1
  & the multMagma of H2 = the multMagma of K2
  holds H1 /\ H2 = K1 /\ K2
proof
  let G be Group;
  let H1,H2,K be Subgroup of G;
  let K1,K2 be Subgroup of K;
  assume Z1: the multMagma of H1 = the multMagma of K1;
  assume Z2: the multMagma of H2 = the multMagma of K2;
  Z3: K1 /\ K2 is Subgroup of G by GROUP_2:56;
  Z4: for g being Element of G
  st g in H1 /\ H2 holds g in K1 /\ K2
  proof
    let g be Element of G;
    assume g in H1 /\ H2;
    then g in H1 & g in H2 by GROUP_2:82;
    then g in K1 & g in K2 by Z1,Z2;
    hence g in K1 /\ K2 by GROUP_2:82;
  end;

  for g being Element of G
  st g in K1 /\ K2 holds g in H1 /\ H2
  proof
    let g be Element of G;
    assume g in K1 /\ K2;
    then g in K1 & g in K2 by GROUP_2:82;
    then g in H1 & g in H2 by Z1,Z2;
    hence g in H1 /\ H2 by GROUP_2:82;
  end;
  hence H1 /\ H2 = K1 /\ K2 by Z3, Z4, GROUP_2:def 6;
end;
@ %def ThCapLemma

\begin{theorem}
Let $K\subgroup G$.
Let $H_{1},H_{2}\subgroup G$ and $K_{1},K_{2}\subgroup K$
be such that $H_{i}=K_{i}$ for $i=1,2$.
Then $H_{1}\cdot H_{2}=K_{1}\cdot K_{2}$.
\end{theorem}

<<Theorem: If $H_{i}=K_{i}$, then $H_{1}\cdot H_{2}=K_{1}\cdot K_{2}$>>=
theorem ThProdLemma:
  for G being Group
  for H1,H2,K being Subgroup of G
  for K1,K2 being Subgroup of K
  st the multMagma of H1 = the multMagma of K1
  & the multMagma of H2 = the multMagma of K2
  holds H1 * H2 = K1 * K2
proof
  let G be Group;
  let H1,H2,K be Subgroup of G;
  let K1,K2 be Subgroup of K;
  assume A1: the multMagma of H1 = the multMagma of K1;
  assume A2: the multMagma of H2 = the multMagma of K2;
  A3: carr H1 = the carrier of H1 by GROUP_2:def 9
             .= carr K1 by A1, GROUP_2:def 9;
  A4: carr H2 = the carrier of H2 by GROUP_2:def 9
             .= carr K2 by A2,GROUP_2:def 9;
  for x being object
  holds x in (carr H1) * (carr H2) iff x in (carr K1) * (carr K2)
  proof
    let x be object;
    thus x in (carr H1) * (carr H2) implies x in (carr K1) * (carr K2)
    proof
      assume x in (carr H1) * (carr H2);
      then consider h1,h2 being Element of G such that
      B1: x = h1*h2 & h1 in carr H1 & h2 in carr H2
      by GROUP_2:8;
      reconsider k1=h1,k2=h2 as Element of K by A3,A4,B1;
      k1*k2 = x by B1,GROUP_2:43;
      hence x in (carr K1) * (carr K2) by A3, A4, B1, GROUP_2:8;
    end;

    thus x in (carr K1) * (carr K2) implies x in (carr H1) * (carr H2)
    proof
      assume x in (carr K1) * (carr K2);
      then consider k1,k2 being Element of K such that
      B1: x = k1*k2 & k1 in carr K1 & k2 in carr K2
      by GROUP_2:8;
      reconsider h1=k1,h2=k2 as Element of G by GROUP_2:42;
      h1 * h2 = x by B1,GROUP_2:43;
      hence x in (carr H1) * (carr H2) by A3, A4, B1, GROUP_2:8;
    end;
  end;
  then A5: (carr H1) * (carr H2) = (carr K1) * (carr K2) by TARSKI:2;

  thus H1 * H2 = (carr H1) * (carr H2) by GROUP_4:def 8
              .= K1 * K2 by A5,GROUP_4:def 8;
end;
@ %def ThProdLemma