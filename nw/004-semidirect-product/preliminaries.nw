% -*- mode: poly-noweb; noweb-code-mode: mizar-mode; -*-
\section{Preliminaries about Automorphisms}


<<Preliminaries about Automorphism Groups>>=

<<Register: Element of $\Aut(G)$ is multiplicative>>

<<Register: $\id_{G}$ is a group automorphism>>

<<Register: The automorphism group is non-empty, strict, and consists of functions>>

<<Definition: [[phi /. a]] is an Endomorphism of G>>

<<Definition: $\varphi H\to\Aut(G)$ and $a\in A$, $\varphi(a)\in\Aut(G)$>>

<<Register: $\varphi\colon H\to\Aut(G)$ has $\varphi(a)$ is an endomorphism>>

<<Lemma: $\varphi(a_{1}a_{2})=\varphi(a_{1})\circ\varphi(a_{2})$>>

<<Register: Underlying set of $A\times G$ is non empty>>

<<Theorem: If $x\in G_{1}\times G_{2}$, then $x_{1}\in G_{1}$ and $x_{2}\in G_{2}$ and $\dom(x)=\{1,2\}$>>

<<Lemma: if $x\in U(A\times G)$, then $x_{1}\in A$ and $x_{2}\in G$>>

<<Lemma: $a\in A$ and $g\in G$ implies $(a,g)\in U(A\times G)$>>

@

\begin{registration}
An element of the set underlying $\Aut(G)$ is multiplicative (as a function).
\end{registration}

<<Register: Element of $\Aut(G)$ is multiplicative>>=
registration
  let G be strict Group;
  cluster -> multiplicative for Element of Aut G;
  correctness by AUTGROUP:def 1;
end;
@

\begin{registration}
For any group $G$, the identity function $\id_{G}$ is a group automorphism.
\end{registration}

<<Register: $\id_{G}$ is a group automorphism>>=
registration
  let A be Group;
  cluster id A -> multiplicative;
  correctness by GROUP_6:38;

  cluster id A -> bijective;
  coherence
  proof
    set f = id A;
    rng (id A) = the carrier of A;
    then (id A is one-to-one & id A is onto) by FUNCT_2:def 3;
    hence id A is bijective;
  end;
end;
@

\begin{registration}
The automorphism group is non-empty, strict, and consists of functions.
\end{registration}

<<Register: The automorphism group is non-empty, strict, and consists of functions>>=
registration
  let G be strict Group;
  cluster AutGroup G -> non empty strict constituted-Functions;
  coherence by MONOID_0:80;
end;
@ 

\begin{definition}
Let $a\in A$. Then we should think of $\varphi(a)$ as a Homomorphism of
$G$ to $G$.
\end{definition}

<<Definition: [[phi /. a]] is an Endomorphism of G>>=
definition
  let A, G, phi;
  let a be Element of A;
  func phi /. a -> Homomorphism of G,G equals phi.a; :: GROUP_24:def 1
  correctness
  proof
    phi.a is Element of Aut G;
    hence phi.a is Homomorphism of G, G;
  end;
end;
@

\begin{definition}
Let $H\subgroup A$, $G$ be groups, and $\varphi\colon H\to\Aut(G)$ be a
group morphism. If $a$ is an Element of $A$ and $a\in H$, then
$\varphi(h)$ is an function of $G$ to $G$.
\end{definition}

<<Definition: $\varphi H\to\Aut(G)$ and $a\in A$, $\varphi(a)\in\Aut(G)$>>=
definition
  let A,G;
  let H be Subgroup of A;
  let phi be Homomorphism of H,AutGroup G;
  let a be Element of A such that
A1: a in H;
  func phi /. a -> Function of G,G equals :: GROUP_24:def 2
  :Def2a: phi.a;
  correctness
  proof
    reconsider h=a as Element of H by A1;
    phi.h = phi.a & phi.h is Element of Aut G;
    hence thesis;
  end;
end;
@

\begin{registration}
In fact, $\varphi(a)$ is an automorphism.
\end{registration}

<<Register: $\varphi\colon H\to\Aut(G)$ has $\varphi(a)$ is an endomorphism>>=
registration
  let G be strict Group, A be Group;
  let phi be Homomorphism of A, AutGroup G;
  let a be Element of A;
  cluster phi/.a -> multiplicative;
  coherence;
end;
@ 

\begin{lemma}
For any $a_{1}$, $a_{2}\in A$ and group morphism $\varphi\colon A\to\Aut(G)$,
we have $\varphi(a_{1}a_{2})=\varphi(a_{1})\circ\varphi(a_{2})$.
\end{lemma}

<<Lemma: $\varphi(a_{1}a_{2})=\varphi(a_{1})\circ\varphi(a_{2})$>>=
LmCompose:
  for a1, a2 being Element of A
  holds phi.(a1 * a2) = (phi.a1) * (phi.a2) by GROUP_6:def 6;
@ %def LmCompose

\begin{registration}
The underlying set of the product group $A\times G$ is nonempty;
\end{registration}

<<Register: Underlying set of $A\times G$ is non empty>>=
registration
  let G, A;
  cluster product (Carrier <*A,G*>) -> non empty;
  correctness;
end;

registration
  let G, A be Group;
  cluster product (Carrier <*A,G*>) -> non empty;
  correctness;
end;
@

\begin{theorem}
  Let $G_{1}$, $G_{2}$ be groups.
  If $x\in G_{1}\times G_{2}$, then $x_{1}\in G_{1}$ and $x_{2}\in G_{2}$ and $\dom(x)=\{1,2\}$.
\end{theorem}

<<Theorem: If $x\in G_{1}\times G_{2}$, then $x_{1}\in G_{1}$ and $x_{2}\in G_{2}$ and $\dom(x)=\{1,2\}$>>=

theorem th4a: :: GROUP_24:1
  for G1,G2 being Group
  for x being Element of product (<*G1,G2*>)
  holds x.1 in G1 & x.2 in G2 & dom x = {1,2}
proof
  let G1,G2 be Group;
  let x be Element of product (<*G1,G2*>);
  A1: dom (Carrier <*G1,G2*>) = {1,2} by PARTFUN1:def 2;
  x in the carrier of product (<*G1,G2*>);
  then x in product (Carrier <*G1,G2*>) by GROUP_7:def 2;
  then consider f being Function such that
  A2: x = f
      & dom f = dom (Carrier <*G1,G2*>)
      & (for y being object st y in dom (Carrier <*G1,G2*>)
         holds f.y in (Carrier <*G1,G2*>).y)
  by CARD_3:def 5;

  thus x.1 in G1
  proof
    B1: <* G1, G2 *>.1 = G1 by FINSEQ_1:44;
    B2: 1 in dom (Carrier <*G1,G2*>) by A1, TARSKI:def 2;
    then ex R being 1-sorted st
    (R = <*G1,G2*>.1 & (Carrier <*G1,G2*>).1 = the carrier of R)
    by PRALG_1:def 15;
    hence thesis by A2, B1, B2;
  end;

  thus x.2 in G2
  proof
    B1: <* G1, G2 *>.2 = G2 by FINSEQ_1:44;
    B2: 2 in dom (Carrier <*G1,G2*>) by A1, TARSKI:def 2;
    then ex R being 1-sorted st
    (R = <*G1,G2*>.2 & (Carrier <*G1,G2*>).2 = the carrier of R)
    by PRALG_1:def 15;
    hence thesis by A2, B1, B2;
  end;
  thus dom x = {1,2} by A2,PARTFUN1:def 2; :: A1,
end;
@ 

\begin{lemma}
If $x\in U(A\times G)$ is an element of the underlying set, then
$x_{1}\in A$ and $x_{2}\in G$.
\end{lemma}

<<Lemma: if $x\in U(A\times G)$, then $x_{1}\in A$ and $x_{2}\in G$>>=
Lm0:
  for x being Element of product (Carrier <*A,G*>)
  holds x.1 in A & x.2 in G & dom x = {1,2}
proof
  let G1,G2 be Group;
  let x be Element of product (Carrier <*G1,G2*>);
  x is Element of the carrier of product <*G1,G2*> by GROUP_7:def 2;
  hence thesis by th4a;
end;
@ %def Lm0

\begin{lemma}
For $a\in A$ and $g\in G$, we have $(a,g)\in U(A\times G)$ be an element
of the underlying set.
\end{lemma}

<<Lemma: $a\in A$ and $g\in G$ implies $(a,g)\in U(A\times G)$>>=
Lm1:
  for a being Element of A, g being Element of G
  holds <*a,g*> in product (Carrier <*A,G*>)
proof
  let a be Element of A;
  let g be Element of G;
  <*a,g*> in the carrier of (product <*A,G*>);
  hence <*a,g*> in product (Carrier <*A,G*>) by GROUP_7:def 2;
end;
@ %def Lm1