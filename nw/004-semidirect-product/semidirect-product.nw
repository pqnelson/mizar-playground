% -*- mode: poly-noweb; noweb-code-mode: mizar-mode; -*-
\section{Definition and Properties of Semi-Direct Product}

There are two ways to define the semidirect product, because it depends
on how we defined the automorphism group $\aut(G)$ for a group $G$.
If multiplication in $\aut(G)$ is composition of automorphisms, then we
need to follow Bourbaki's definition (which we do). Finite group
theorists tend to work with the opposite conventions for $\aut(G)$, and
end up with the other definition for the semidirect product of groups.

<<Definition and Properties of Semi-Direct Products>>=
begin :: Semidirect Products of Groups
<<Definition: Semidirect Product>>

<<Register: semidirect product is nonempty strict constituted-Functions>>

<<Register: Elements of semidirect product are FinSequence-like>>

<<Theorem: Carrier of semidirect product is the carrier of the direct product>>

<<Theorem: $\forall a\in A,g\in G, \langle g,a\rangle\in G\rtimes_{\varphi}A$>>

<<Theorem: $x\in G\rtimes_{\varphi}A$ implies $x_{1}\in G$ and $x_{2}\in A$ and $\dom(x)=\{1,2\}$>>

<<Theorem: $x\in G\rtimes A$ implies $\exists a\in A,g\in G,x=\langle g,a\rangle$>>

<<Theorem: $\langle x,y\rangle$ satisfies the properties of an ordered pair>>

<<Theorem: $\langle a_{1},g_{1}\rangle\langle a_{2},g_{2}\rangle = \langle a_{1}a_{2}, g_{1}\cdot\varphi_{a_{1}}(g_{2})\rangle$>>

<<Theorem: $\varphi_{1_{A}}(g)=g$>>

<<Lemma: $e\in G\rtimes A$ be such that $e=\langle 1_{G}, 1_{A}\rangle$ is the identity element>>

<<Register: The semidirect product is a unital magma>>

<<Theorem: The unit for the semidirect product>>

<<Theorem: $(\varphi_{a_{1}}\circ\varphi_{a_{2}})(g)=\varphi_{a_{1}a_{2}}(g)$>>

<<Theorem: $(\varphi_{a^{-1}}\circ\varphi_{a})(g)=g$ and $(\varphi_{a}\circ\varphi_{a^{-1}})(g)=g$>>

<<Theorem: inverse element for semidirect product>>

<<Theorem: Criteria for unital magmas to be a group>>

<<Register: semidirect product is a group>>

<<Theorem: $\langle g,a\rangle^{-1} = \langle\varphi_{a^{-1}}(g^{-1}),a^{-1}\rangle$>>

<<Subsection: Inclusion morphisms>>

<<Theorem: $\varphi$ is trivial implies $A\ltimes_{\varphi}G=A\times G$>>
@

\begin{definition}
Let $A$, $G$ be groups and $\varphi\colon A\to\Aut(G)$ be a group morphism.
Then the \define{Semidirect Product} of $A$ and $G$ using $\varphi$
consists of the magma $G\rtimes_{\varphi}A$ whose underlying set is the
Cartesian product $U(G)\times U(A)$ and whose binary operator is defined
by:
\begin{equation}
(g_{1},a_{1})(g_{2},a_{2})=(g_{1}\varphi_{a_{1}}(g_{2}),a_{1},a_{2})
\end{equation}
for every $a_{1}$, $a_{2}\in A$ and $g_{1}$, $g_{2}\in G$.
\end{definition}

\begin{def-remark}
We will prove that $A\ltimes_{\varphi}G$ actually satisfies the
properties for being a group, in separate registrations.
\end{def-remark}

\begin{def-remark}
Since we are trying to obtain a morphism later on $\beta\colon G\times A\to S$
for which
\begin{align*}
\beta((g_{1},a_{1})(g_{2},a_{2})) &= g_{1}a_{1}g_{2}a_{2} \\
&= g_{1}a_{1}g_{2}(a_{1}^{-1}a_{1})a_{2}\\
&= g_{1}(a_{1}g_{2}a_{1}^{-1})a_{1}a_{2}\\
&= g_{1}g_{2}^{a_{1}^{-1}}a_{1}a_{2},
\end{align*}
it follows that we want our desired multiplication for $G\rtimes A$.
\end{def-remark}

<<Definition: Semidirect Product>>=
definition
  let G, A, phi;
  func semidirect_product (G, A, phi) -> non empty strict multMagma means
  :Def3:
  the carrier of it = product (Carrier <* G, A *>) &
  (for f, g being Element of product (Carrier <* G, A *>)
   ex h being Function, a1 being Element of A, g2 being Element of G
   st h = (the multF of it).(f, g) &
      a1 = f.2 & g2 = g.1 &
      h.1 = (the multF of G).(f.1, (phi/.a1)/. g2) &
      h.2 = (the multF of A).((f.2), (g.2)));
  existence
  <<Proof: Existence of Semidirect Products>>
  uniqueness
  <<Proof: Uniqueness of Semidirect Products>>
end;
@ %def Def3

\begin{voc}
Before moving on, we need to add [[semidirect_product]] to the
vocabulary list.
\end{voc}

<<DICT/group-24.voc>>=
Osemidirect_product
@ 

\begin{proof}[Proof (Existence)]
To prove the existence of the semidirect product, we really need to
construct the binary operator on the set. This requires a bit of
leg-work, and constitutes the bulk of the proof (sets $A_{1}$ and $A_{2}$).
\end{proof}

<<Proof: Existence of Semidirect Products>>=
proof
  set U = product (Carrier <* G, A *>);
  defpred P[Element of U, Element of U, set] means
  ex h being Function, a1 being Element of A, g2 being Element of G
  st h = $3 & a1 = $1.2 & g2 = $2.1 &
     h.1 = (the multF of G).($1.1, (phi/.a1)/. g2) &
     h.2 = (the multF of A).(($1.2), ($2.2));

  A1: for x, y being Element of U ex z being Element of U st P[x,y,z]
  proof
    let x, y be Element of U;
    y.1 in G by Lm0; then
    reconsider g2 = y.1 as Element of G;
    x.2 in A by Lm0; then
    reconsider a1 = x.2 as Element of A;
    
    set h = <* (the multF of G).(x.1,(phi/.a1)/.g2),
               (the multF of A).(x.2, y.2)*>;
    x.2 in A & y.2 in A & x.1 in G by Lm0; then
    (the multF of G).(x.1,(phi/.a1)/.g2) in G & (the multF of A).(x.2, y.2) in A
    by BINOP_1:17;
    then reconsider z = h as Element of product (Carrier <*G,A*>) by Lm1;
    take z;
    h = z & a1 = x.2 & g2 = y.1 &
    h.1 = (the multF of G).(x.1, (phi/.a1)/.g2) &
    h.2 = (the multF of A).(x.2, y.2) by FINSEQ_1:44;
    hence P[x, y, z];
  end;
  
  consider B being Function of [:U, U:],U such that
  A2: for a, b being Element of U holds P[a,b,B.(a,b)] from BINOP_1:sch 3(A1);

  set M = multMagma(# (product (Carrier <*G,A*>)), B #);
  reconsider M as non empty strict multMagma;
  take M;
  thus thesis by A2;
end;
@

\begin{proof}[Proof (Uniqueness)]
If we were to somehow assemble two such magmas, then really they would
be the same. They have the same underlying set (which is easy to prove)
and the same binary operator (which is the bulk of the proof).
\end{proof}

<<Proof: Uniqueness of Semidirect Products>>=
proof
  let M1,M2 being non empty strict multMagma;
  assume that
  A1: (the carrier of M1 = product (Carrier <* G, A *>) &
      (for f, g being Element of product (Carrier <* G, A *>)
       ex h being Function, a1 being Element of A, g2 being Element of G
       st h = (the multF of M1).(f, g) &
          a1 = f.2 & g2 = g.1 &
          h.1 = (the multF of G).(f.1, (phi/.a1)/. g2) &
          h.2 = (the multF of A).((f.2), (g.2)))) and
  A2: (the carrier of M2 = product (Carrier <* G, A *>) &
      (for f, g being Element of product (Carrier <* G, A *>)
       ex h being Function, a1 being Element of A, g2 being Element of G
       st h = (the multF of M2).(f, g) &
          a1 = f.2 & g2 = g.1 &
          h.1 = (the multF of G).(f.1, (phi/.a1)/. g2) &
          h.2 = (the multF of A).((f.2), (g.2))));
  
  for x,y being set st x in the carrier of M1 & y in the carrier of M1
  holds (the multF of M1).(x, y) = (the multF of M2).(x, y)
  <<Proof step: $\forall x,y, x\star_{M_{1}}y = x\star_{M_{2}}y$>>
  hence thesis by A1,A2,BINOP_1:1;
end;
@

\begin{proof}
Proving the binary operators are identical requires a bit of work, and
showing that they operate on pairs of elements to produce
$h_{1}=x\star_{M_{1}}y$ (in step $B_{3}$) and $h_{2}=x\star_{M_{2}}y$
(in step $B_{4}$) which are both pairs of elements (step $B_{5}$). Then
they are equal, since they are equal componentwise.
\end{proof}

<<Proof step: $\forall x,y, x\star_{M_{1}}y = x\star_{M_{2}}y$>>=
proof
  let x,y be set;
  assume B1: x in the carrier of M1;
  assume B2: y in the carrier of M1;
  reconsider f = x, g = y as Element of product (Carrier <*G,A*>) by A1,B1,B2;
  consider h1 being Function, a11 being Element of A, g12 being Element of G
  such that
  B3: h1 = (the multF of M1).(f, g) & a11 = f.2 & g12 = g.1
      & h1.1 = (the multF of G).(f.1, (phi/.a11)/. g12)
      & h1.2 = (the multF of A).((f.2), (g.2)) by A1;
  consider h2 being Function, a21 being Element of A, g22 being Element of G
  such that
  B4: h2 = (the multF of M2).(f, g) & a21 = f.2 & g22 = g.1
      & h2.1 = (the multF of G).(f.1, (phi/.a21)/. g22)
      & h2.2 = (the multF of A).((f.2), (g.2)) by A2;
  B5: dom h1 = {1,2} & dom h2 = {1,2}
  proof
    h1 in product (Carrier <*G,A*>) by A1, B3, BINOP_1:17;
    then ex f1 being Function st h1 = f1 & dom f1 = dom (Carrier <*G,A*>)
    & (for y1 being object st y1 in dom (Carrier <*G,A*>)
    holds f1.y1 in (Carrier <*G,A*>).y1)
    by CARD_3:def 5;
    hence dom h1 = {1,2} by PARTFUN1:def 2; 
    h2 in product (Carrier <*G,A*>) by A2, B4, BINOP_1:17;
    then ex f1 being Function st h2 = f1 & dom f1 = dom (Carrier <*G,A*>)
    & (for y1 being object st y1 in dom (Carrier <*G,A*>)
    holds f1.y1 in (Carrier <*G,A*>).y1)
    by CARD_3:def 5;
    hence dom h2 = {1,2} by PARTFUN1:def 2;
  end;
  for i being object st i in dom h1 holds h1.i = h2.i
  proof
    let i be object;
    assume i in dom h1;
    then per cases by B5, TARSKI:def 2;
    suppose i = 1;
      hence thesis by B3,B4;
    end;
    suppose i = 2;
      hence thesis by B3,B4;
    end;
  end;
  then h1 = h2 by B5, FUNCT_1:def 11;
  hence (the multF of M1).(x, y) = (the multF of M2).(x, y) by B3,B4;
end;  
@

\begin{registration}
The semidirect product's elements may be viewed as functions.
\end{registration}

<<Register: semidirect product is nonempty strict constituted-Functions>>=
registration
  let G, A, phi;
  cluster semidirect_product (G, A, phi) -> non empty strict constituted-Functions;
  coherence
  proof
    the carrier of semidirect_product (G, A, phi) = product (Carrier <*G,A*>)
    by Def3;
    hence thesis by MONOID_0:80;
  end;
end;
@

\begin{registration}
Elements of the semidirect product are ``Finite Sequence-like''.
\end{registration}

<<Register: Elements of semidirect product are FinSequence-like>>=
registration
  let G, A, phi;
  cluster -> FinSequence-like for Element of (semidirect_product (G, A, phi));
  coherence
  proof
    for b1 being Element of semidirect_product(G, A, phi)
    holds b1 is FinSequence-like
    proof
      let b1 be Element of semidirect_product(G, A, phi);
      b1 is Element of product (Carrier <*G,A*>) by Def3;
      hence b1 is FinSequence-like;
    end;
    hence thesis;
  end;
end;
@ 

\begin{theorem}
The underlying set of the semidirect product is the same as the
underlying set of the direct product.
\end{theorem}

<<Theorem: Carrier of semidirect product is the carrier of the direct product>>=
theorem Th10:
  the carrier of semidirect_product (G, A, phi) = the carrier of product <*G,A*>
proof
  thus the carrier of semidirect_product (G,A,phi)
   = product (Carrier <*G,A*>) by Def3
  .= the carrier of product <*G,A*> by GROUP_7:def 2;
end;
@ %def Th10

\begin{theorem}
For any $a\in A$ and $g\in G$, we have $\langle a,g\rangle$ be an
element of $G\rtimes_{\varphi}A$.
\end{theorem}

<<Theorem: $\forall a\in A,g\in G, \langle g,a\rangle\in G\rtimes_{\varphi}A$>>=
theorem Th11:
  for a being Element of A
  for g being Element of G
  holds <* g, a *> is Element of semidirect_product (G, A, phi) by Th10;
@ %def Th11

\begin{theorem}
For any $x\in G\rtimes_{\varphi}A$, we have $x_{1}\in A$ and $x_{2}\in G$
and $\dom(x)=\{1,2\}$.
\end{theorem}

<<Theorem: $x\in G\rtimes_{\varphi}A$ implies $x_{1}\in G$ and $x_{2}\in A$ and $\dom(x)=\{1,2\}$>>=
theorem Th12:
  for x being Element of semidirect_product (G, A, phi)
  holds x.1 in G & x.2 in A & dom x = {1,2}
proof
  let x be Element of semidirect_product (G, A, phi);
  x in the carrier of semidirect_product (G, A, phi);
  then x in the carrier of product <*G,A*> by Th10;
  hence thesis by Th7;
end;
@ %def Th12

\begin{theorem}
For each $x\in G\rtimes_{\varphi}A$, there is a corresponding $a\in A$
and $g\in G$ such that $x=\langle a,g\rangle$.
\end{theorem}

<<Theorem: $x\in G\rtimes A$ implies $\exists a\in A,g\in G,x=\langle g,a\rangle$>>=
theorem Th13:
  for x being Element of semidirect_product (G, A, phi)
  ex g being Element of G, a being Element of A
  st x = <* g, a *>
proof
  let x be Element of semidirect_product (G, A, phi);

  x.1 in G & x.2 in A by Th12;
  then consider g being Element of G, a being Element of A such that
  A1: g = x.1 & a = x.2;

  take g, a;
  
  dom x = {1,2} by Th12;
  then len x = 2 by FINSEQ_1:2,def 3;
  hence x = <* g, a *> by A1, FINSEQ_1:44;
end;
@ %def Th13

\begin{theorem}
Let $x_{1}$, $x_{2}$, $y_{1}$, $y_{2}$ be objects.
Then $\langle x_{1}, y_{1}\rangle = \langle x_{2},y_{2}\rangle$ if and
only if $x_{1}=x_{2}$ and $y_{1}=y_{2}$.
\end{theorem}

<<Theorem: $\langle x,y\rangle$ satisfies the properties of an ordered pair>>=
theorem Th14:
  for x1, x2, y1, y2 being object
  holds <*x1,y1*> = <*x2,y2*> iff (x1 = x2 & y1 = y2)
proof
  let x1, x2, y1, y2 be object;
  thus <*x1,y1*> = <*x2,y2*> implies (x1 = x2 & y1 = y2)
  proof
    assume A1: <*x1,y1*> = <*x2,y2*>;
    thus x1 = <*x1,y1*>.1 by FINSEQ_1:44
           .= x2 by A1, FINSEQ_1:44;
    thus y1 = <*x1,y1*>.2 by FINSEQ_1:44
           .= y2 by A1, FINSEQ_1:44;
  end;
  thus (x1 = x2 & y1 = y2) implies <*x1,y1*> = <*x2,y2*>;
end;
@ %def Th14

\begin{theorem}
Let $x$, $y\in A\ltimes G$, let $a_{1}$, $a_{2}\in A$, and let
$g_{1},g_{2}\in G$. If $x=\langle a_{1},g_{1}\rangle$ and $y=\langle a_{2},g_{2}\rangle$,
then $xy = \langle a_{1}a_{2}, g_{1}\cdot\varphi_{a_{1}}(g_{2})\rangle$.
\end{theorem}

<<Theorem: $\langle a_{1},g_{1}\rangle\langle a_{2},g_{2}\rangle = \langle a_{1}a_{2}, g_{1}\cdot\varphi_{a_{1}}(g_{2})\rangle$>>=
theorem Th15:
  for x,y being Element of semidirect_product (G, A, phi)
  for a1,a2 being Element of A
  for g1,g2 being Element of G
  st x = <*g1,a1*> & y = <*g2,a2*>
  holds x * y = <* g1 * ((phi /. a1)/.g2),a1 * a2 *>
proof
  let x,y be Element of semidirect_product (G, A, phi);
  let a1,a2 be Element of A;
  let g1,g2 be Element of G;
  assume A1: x = <*g1,a1*>;
  assume A2: y = <*g2,a2*>;
  
  x in product (Carrier <* G, A*>) & y in product (Carrier <*G,A*>)
  proof
    x in semidirect_product(G,A,phi);
    hence x in product (Carrier <*G,A*>) by Def3;
    y in semidirect_product(G,A,phi);
    hence y in product (Carrier <*G,A*>) by Def3;
  end;
  then consider h being Function, aa1 being Element of A, gg2 being Element
  of G such that
  A3: h = x * y & aa1 = x.2 & gg2 = y.1 
      & h.1 = (the multF of G).(x.1, (phi/.aa1)/.gg2)
      & h.2 = (the multF of A).(x.2, y.2) by Def3;
  A4: a1 = aa1 & g2 = gg2 by A1,A2,A3,FINSEQ_1:44;

  h.2 = (the multF of A).(x.2, a2) by A2,A3,FINSEQ_1:44
     .= (the multF of A).(a1, a2) by A1,FINSEQ_1:44;
  then A5: (x * y).2 = a1 * a2 by A3;
  A6: (x * y).1 = g1 * ((phi /. a1)/.g2) by A1,A3,A4,FINSEQ_1:44;
  dom (x * y) = {1,2} by Th12;
  then len (x * y) = 2 by FINSEQ_1:2,def 3;
  hence x * y = <* g1 * ((phi /. a1)/.g2), a1 * a2 *> by A5,A6,FINSEQ_1:44;
end;
@ %def Th15

\begin{theorem}
For any $g\in G$, we have $\varphi_{1_{A}}(g)=g$.
\end{theorem}

<<Theorem: $\varphi_{1_{A}}(g)=g$>>=
theorem Th16:
  for g being Element of G
  holds (phi /. 1_A) /. g = g
proof
  let g be Element of G;
  (phi /. 1_A) = 1_(AutGroup G) by GROUP_6:31
              .= id the carrier of G by AUTGROUP:9;
  hence (phi /. 1_A) /. g = g;
end;
@ %def Th16

\begin{registration}
The semidirect product is a unital magma.
\end{registration}

<<Register: The semidirect product is a unital magma>>=
registration
  let G, A, phi;
  cluster semidirect_product (G, A, phi) -> unital;
  correctness
  proof
    set S = semidirect_product (G, A, phi);
    reconsider e = <* 1_G, 1_A*> as Element of S by Th11;
    take e;
    let x be Element of S;
    consider g being Element of G, a being Element of A such that
    A1: x = <* g, a *>
    by Th13;

    A2: ((phi /. (1_A)) /. g) * 1_G = g * 1_G by Th16
                                   .= g by GROUP_1:def 4;
    A3: ((phi /. a) /. (1_G)) = 1_G by GROUP_6:31;
    thus x * e = <* g * ((phi /. a) /. 1_G), a * 1_A *> by A1, Th15
              .= <* g * ((phi /. a) /. 1_G), a *> by GROUP_1:def 4
              .= x by A1, A3, GROUP_1:def 4;
    thus e * x = <* 1_G * ((phi /. (1_A)) /. g), 1_A * a *> by A1,Th15
              .= <* 1_G * ((phi /. (1_A)) /. g), a *> by GROUP_1:def 4
              .= <* (1_G) * (g), a *> by A2, GROUP_1:def 4
              .= x by A1,GROUP_1:def 4;
  end;
end;
@

\begin{lemma}
Let $e\in A\ltimes G$ be such that $e=\langle 1_{A}, 1_{G}\rangle$.
Then for any $x\in A\ltimes G$, we have $xe=ex=x$.
\end{lemma}

<<Lemma: $e\in G\rtimes A$ be such that $e=\langle 1_{G}, 1_{A}\rangle$ is the identity element>>=
Lm8:
  for x,e being Element of semidirect_product (G, A, phi)
  st e = <* 1_G, 1_A*>
  holds x * e = x & e * x = x
proof
  let x,e be Element of semidirect_product (G, A, phi);
  assume A1: e = <* 1_G, 1_A*>;
  consider g being Element of G, a being Element of A such that
  A2: x = <* g, a *>
  by Th13;

  A3: ((phi /. (1_A)) /. g) * 1_G = g * 1_G by Th16
                                 .= g by GROUP_1:def 4;
  thus x * e = <* g * ((phi /. a) /. 1_G), a * 1_A *> by A1,A2, Th15
            .= <* g * ((phi /. a) /. 1_G), a *> by GROUP_1:def 4
            .= <* g * 1_G, a *> by GROUP_6:31
            .= x by A2, GROUP_1:def 4;
  thus e * x = <* 1_G * ((phi /. (1_A)) /. g), 1_A * a *> by A1,A2,Th15
            .= <* 1_G * ((phi /. (1_A)) /. g), a *> by GROUP_1:def 4
            .= <* (1_G) * g, a *> by A3, GROUP_1:def 4
            .= x by A2, GROUP_1:def 4;
end;
@ %def Lm8

\begin{theorem}
We have $1_{G\rtimes A}=\langle 1_{G}, 1_{A}\rangle$.
\end{theorem}

<<Theorem: The unit for the semidirect product>>=
theorem Th17:
  1_(semidirect_product (G, A, phi)) = <* 1_G, 1_A *>
proof
  reconsider e = <* 1_G, 1_A *> as Element of semidirect_product (G, A, phi)
  by Th10;
  for x being Element of semidirect_product (G, A, phi)
  holds x * e = x & e * x = x by Lm8;
  hence thesis by GROUP_1:def 4;
end;
@ %def Th17

\begin{theorem}
  For any $a_{1}$, $a_{2}\in A$ and any $g\in G$, we have
  $(\varphi_{a_{1}}\circ\varphi_{a_{2}})(g)=\varphi_{a_{1}a_{2}}(g)$.
\end{theorem}

<<Theorem: $(\varphi_{a_{1}}\circ\varphi_{a_{2}})(g)=\varphi_{a_{1}a_{2}}(g)$>>=
theorem Th18:
  for a1,a2 being Element of A
  for g being Element of G
  holds (phi.a1) . ((phi /. a2) . g) = (phi /. (a1 * a2)) . g
proof
  let a1,a2 be Element of A;
  let g be Element of G;
  phi . (a1 * a2) = (phi.a1) * (phi.a2) by GROUP_6:def 6;
  then (phi . (a1 * a2)) . g = ((phi/.a1) * (phi/.a2)).g by AUTGROUP:8
                            .= (phi.a1) . ((phi . a2) . g) by FUNCT_2:15;
  hence (phi.a1) . ((phi /. a2) . g) = (phi /. (a1 * a2)) . g; 
end;
@ %def Th18

\begin{theorem}
For any $a\in A$ and $g\in G$, we have $(\varphi_{a^{-1}}\circ\varphi_{a})(g)=g$
and $(\varphi_{a}\circ\varphi_{a^{-1}})(g)=g$.
\end{theorem}

<<Theorem: $(\varphi_{a^{-1}}\circ\varphi_{a})(g)=g$ and $(\varphi_{a}\circ\varphi_{a^{-1}})(g)=g$>>=
theorem Th19:
  for a being Element of A
  for g being Element of G
  holds (phi/.(a")) /. ((phi /. a) /. g) = g
  & (phi/. a) /. ((phi /. (a")) /. g) = g
proof
  let a be Element of A;
  let g be Element of G;
  thus (phi /. (a")) /. ((phi /. a) /. g) = (phi /. (a" * a)) /. g by Th18
                                         .= (phi /. 1_A) /. g by GROUP_1:def 5
                                         .= g by Th16;
  thus (phi/. a) /. ((phi /. (a")) /. g) = (phi /. (a * a")) /. g by Th18
                                        .= (phi /. 1_A) /. g by GROUP_1:def 5
                                        .= g by Th16;
end;
@ %def Th19

\begin{theorem}
Let $x\in A\ltimes G$ be $x=\langle a,g\rangle$, let $e=\langle 1_{A},1_{G}$.
Then $y=\langle a^{-1}, \varphi_{a^{-1}}(g^{-1})\rangle$ is the inverse
element to $x$, i.e., $xy=e$ and $yx=e$.
\end{theorem}

<<Theorem: inverse element for semidirect product>>=
theorem Th20:
  for x,y being Element of semidirect_product (G, A, phi)
  for a being Element of A
  for g being Element of G
  st x = <* g, a *> & y = <* (phi/.(a"))/.(g"), a" *>
  holds x * y = 1_(semidirect_product (G, A, phi)) & y * x = 1_(semidirect_product (G, A, phi))
proof
  let x,y be Element of semidirect_product (G, A, phi);
  let a be Element of A;
  let g be Element of G;
  assume A1: x = <* g, a *>;
  assume A2: y = <* (phi/.(a"))/.(g"), a" *>;
  A3: ((phi /. (a")) /. (g")) * ((phi/.(a"))/.(g))
   = (phi /. (a")) /. (g" * g) by GROUP_6:def 6
  .= (phi /. (a")) /. 1_G by GROUP_1:def 5
  .= 1_G by GROUP_6:31;
  A4: ((phi /. a) /. ((phi/.(a"))/.(g"))) = g" by Th19;

  thus x * y = <* g * ((phi/.a)/.((phi/.(a"))/.(g"))), a * (a") *> by A1,A2,Th15
            .= <* 1_G, a * a" *> by A4,GROUP_1:def 5
            .= <* 1_G, 1_A *> by GROUP_1:def 5
            .= 1_(semidirect_product (G, A, phi)) by Th17;
  thus y * x = <* (phi/.(a")).(g") * ((phi/. a")/.g), (a") * a *> by A1,A2,Th15
            .= <* 1_G, 1_A *> by A3,GROUP_1:def 5
            .= 1_(semidirect_product (G, A, phi)) by Th17;
end;
@ %def Th20

\begin{theorem}
Let $M$ be a non-empty unital magma.
If every $h\in M$ there is a corresponding $g\in M$ such that $hg=1$ and $gh=1$,
then $M$ is a group.
\end{theorem}

<<Theorem: Criteria for unital magmas to be a group>>=
theorem thUnitalMagmaIsGroupCond:
  for M being non empty unital multMagma
  st (for h being Element of M ex g being Element of M st h * g = 1_M
  & g * h = 1_M)
  holds M is Group-like
proof
  let M be non empty unital multMagma;
  assume A1: for h being Element of M
  ex g being Element of M
  st h * g = 1_M & g * h = 1_M;
  
  ex e being Element of M st
  for h being Element of M
  holds (h * e = h & e * h = h & ex g being Element of M st
  (h * g = e & g * h = e))
  proof
    take e = 1_M;
    let h be Element of M;
    thus h * e = h by GROUP_1:def 4;
    thus e * h = h by GROUP_1:def 4;
    thus thesis by A1;
  end;
  hence M is Group-like by A1, GROUP_1:def 1, GROUP_1:def 3;
end;
@ %def thUnitalMagmaIsGroupCond

\begin{registration}
The semidirect product really is a group after all.
\end{registration}

<<Register: semidirect product is a group>>=
registration
  let G be strict Group;
  let A be Group;
  let phi be Homomorphism of A, AutGroup G;
  cluster semidirect_product (G, A, phi) -> associative;
  correctness
  proof
    thus for x, y, z being Element of semidirect_product (G, A, phi)
    holds (x * y) * z = x * (y * z)
    proof
      let x, y, z be Element of semidirect_product (G, A, phi);
      consider g1 being Element of G, a1 being Element of A such that
      A1: x = <* g1, a1 *>
      by Th13;
      consider g2 being Element of G, a2 being Element of A such that
      A2: y = <* g2, a2 *>
      by Th13;
      consider g3 being Element of G, a3 being Element of A such that
      A3: z = <* g3, a3 *>
      by Th13;
      
      x * y = <* g1 * ((phi /. a1)/.g2), a1 * a2 *> by A1,A2,Th15;
      then A5: (x * y) * z = <* (g1 * ((phi /.a1)/.g2)) * ((phi/.(a1*a2))/.g3),
      (a1 * a2) * a3*> by A3,Th15;
      
      y * z = <* g2 * ((phi /. a2)/.g3), a2 * a3 *> by A2,A3,Th15;
      then A6: x * (y * z) = <* g1 * ((phi/.a1)/.(g2 * ((phi/.a2)/.g3))),
      a1 * (a2 * a3) *>
      by A1,Th15;
      
      A7: (a1 * a2) * a3 = a1 * (a2 * a3) by GROUP_1:def 3;
      
      ((phi /. a1) . ((phi /. a2).g3)) = (phi /. (a1 * a2)) . g3 by Th18;
      then 
      ((phi /. a1).(g2 * ((phi /. a2)/.g3)))
      = ((phi /. a1).g2) * ((phi /. (a1 * a2)) . g3) by GROUP_6:def 6;
      then (((phi /.a1)/.g2) * ((phi /. (a1 * a2)) /. g3))
      = ((phi /. a1)/.(g2 * ((phi /. a2)/.g3)));
      then 
      (g1 * ((phi /.a1)/.g2)) * ((phi /. (a1 * a2)) /. g3)
      = g1 * ((phi /. a1)/.(g2 * ((phi /. a2)/.g3)))
      by GROUP_1:def 3;
      hence thesis by A5,A6,A7;
    end;
  end;
  
  cluster semidirect_product (G, A, phi) -> Group-like;
  correctness
  proof
    set S=semidirect_product (G, A, phi);
    for h being Element of S ex g being Element of S st h * g = 1_S
    & g * h = 1_S
    proof
      let h be Element of S;
      consider g being Element of G, a being Element of A such that
      B1: h = <* g, a *> by Th13;
      reconsider y = <* (phi/.(a"))/.(g"), a" *> as Element of S by Th11;
      take y;
      thus h * y = 1_S by B1,Th20;
      thus y * h = 1_S by B1,Th20;
    end;
    hence thesis by thUnitalMagmaIsGroupCond;
  end;
end;

LmDoubleCheckSemidirectProductIsGroup:
  semidirect_product (G, A, phi) is Group;
@

\begin{theorem}
For any $x\in G\rtimes A$ which we write as $x=\langle g,a\rangle$, we
have $x^{-1}=\langle \varphi_{a^{-1}}(g^{-1}),a^{-1}\rangle$.
\end{theorem}

<<Theorem: $\langle g,a\rangle^{-1} = \langle\varphi_{a^{-1}}(g^{-1}),a^{-1}\rangle$>>=
theorem Th22:
  for a being Element of A
  for g being Element of G
  for x being Element of semidirect_product (G, A, phi)
  st x = <* g, a *>
  holds x" = <* (phi/.(a"))/.(g"), a" *>
proof
  set S = semidirect_product (G, A, phi);
  let a be Element of A;
  let g be Element of G;
  let x be Element of semidirect_product (G, A, phi);
  assume A1: x = <* g, a *>;
  reconsider y = <* (phi/.(a"))/.(g"), a" *> as Element of S by Th11;
  x * y = 1_S & y * x = 1_S by A1,Th20;
  then y = x" by GROUP_1:5;
  hence thesis;
end;
@ %def Th22


\subsection{Inclusion Morphisms}
We can inject $A\into A\ltimes G$ and $G\into A\ltimes G$ and the images
are isomorphic to $A$ and $G$, respectively.

<<Subsection: Inclusion morphisms>>=
<<Theorem: $(g_{1},1)(g_{2},1)=(g_{1}g_{2},1)$>>

<<Theorem: $(1,a_{1})(1,a_{2})=(1,a_{1}a_{2})$>>

<<Theorem: $N$ is normal iff $n^{a}\in N$>>

<<Definition: [[incl1]] includes $G$ into $G\rtimes A$>>

<<Register: [[incl1]] is a morphism and injective>>

<<Definition: [[incl2]] includes $A$ into $G\rtimes A$>>

<<Register: [[incl2]] is a morphism and injective>>

<<Theorem: Image of $j_{1}$ is a normal subgroup of $G\rtimes A$>>

<<Register: $\Im(j_{1})\normalSubgroup A\ltimes G$>>

<<Theorem: $\Im(j_{1})\cap\Im(j_{2})=\trivialSubgroup$>>

<<Theorem: every $x\in A\ltimes G$ is the product of elements $a\in\Im(j_{1})$ and $g\in\Im(j_{2})$ as $x=ga$>>

<<Theorem: $U(A\ltimes G)=\Im(j_{2})\Im(j_{1})$>>

<<Theorem: $\Im(j_{2})\vee\Im(j_{1})=A\ltimes G$>>

<<Theorem: $G\iso\Im(j_{1})$>>

<<Theorem: Let $g\in G$ and $a\in A$, then $(1,g)^{(a,1)}=(1,\varphi_{a^{-1}}(g))$>>
@ 

\begin{theorem}
We have $(g_{1},1)(g_{2},1)=(g_{1}g_{2},1)$.
\end{theorem}

<<Theorem: $(g_{1},1)(g_{2},1)=(g_{1}g_{2},1)$>>=
theorem Th23:
  for g1,g2 being Element of G
  for x,y,z being Element of semidirect_product (G, A, phi)
  st x = <*g1,1_A*> & y = <*g2,1_A*> & z = <*(g1*g2),1_A*>
  holds x * y = z
proof
  let g1,g2 be Element of G;
  let x,y,z be Element of semidirect_product (G, A, phi);
  assume A1: x = <*g1,1_A*>;
  assume A2: y = <*g2,1_A*>;
  assume A3: z = <*(g1*g2),1_A*>;
  thus x * y = <* g1 * ((phi /. 1_A) /. g2), (1_A) * (1_A) *> by A1,A2,Th15
            .= <* g1 * ((phi /. 1_A) /. g2), 1_A *> by GROUP_1:def 4
            .= z by A3, Th16;
end;
@ %def Th23

\begin{theorem}
We have $(1,a_{1})(1,a_{2})=(1,a_{1}a_{2})$.
\end{theorem}

<<Theorem: $(1,a_{1})(1,a_{2})=(1,a_{1}a_{2})$>>=
theorem Th24:
  for a1,a2 being Element of A
  for x,y,z being Element of semidirect_product (G, A, phi)
  st x = <*1_G,a1*> & y = <*1_G,a2*> & z = <*1_G,(a1*a2)*>
  holds x * y = z
proof
  let a1,a2 be Element of A;
  let x,y,z be Element of semidirect_product (G, A, phi);
  assume A1: x = <*1_G, a1*>;
  assume A2: y = <*1_G, a2*>;
  assume A3: z = <*1_G, (a1*a2)*>;
  thus x * y = <* 1_G * ((phi /. a1)/.(1_G)), a1 * a2 *> by A1,A2,Th15
            .= <* 1_G * (1_G), a1 * a2 *> by GROUP_6:31
            .= z by A3,GROUP_1:def 4;
end;
@ %def Th24

\begin{theorem}
Let $N\subgroup A$. Then $N$ is normal if and only if for each element
$a$, $h$ of $A$ such that $h\in N$, we have $h^{a}\in N$.
\end{theorem}

<<Theorem: $N$ is normal iff $n^{a}\in N$>>=
:: Compare to AUTGROUP:1, we do not need strictness
theorem Th25:
  for H being Subgroup of A
  holds H is normal
  iff (for a,h being Element of A st h in H holds h |^ a in H)
proof
  let H be Subgroup of A;
  thus H is normal implies (for a,h being Element of A st h in H
  holds h |^ a in H)
  proof
    assume B1: H is normal;
    let a,h be Element of A;
    assume B2: h in H;
    H |^ a = the multMagma of H by B1, GROUP_3:def 13; then
    B3: the carrier of (H |^ a) = the carrier of H;
    h |^ a in H |^ a by B2, GROUP_3:58;
    then h |^ a in the carrier of H |^ a;
    then h |^ a in the carrier of H by B3;
    then h |^ a in H;
    hence h |^ a in H;
  end;


  thus (for a,h being Element of A st h in H holds h |^ a in H) implies
  H is normal
  proof
    assume B1: for a,h being Element of A st h in H holds h |^ a in H;
    now
      let a be Element of A;
      thus H * a c= a * H
      proof
        set U = carr H;
        let q be object;
        assume q in H * a;
        then consider h being Element of A such that
        B2: q = h * a and
        B3: h in H by GROUP_2:104;
        h |^ a in H by B1, B3;
        then h |^ a in carr H by GROUP_2:def 9;
        then a * (h |^ a) in a * (carr H) by GROUP_2:27;
        then a * (((a") * h) * a) in a * (carr H) by GROUP_3:def 2;
        then a * ((a") * (h * a)) in a * (carr H) by GROUP_1:def 3;
        then (a * (a")) * (h * a) in a * (carr H) by GROUP_1:def 3;
        then ((a * (a")) * h) * a in a * (carr H) by GROUP_1:def 3;
        then ((1_A) * h) * a in a*(carr H) by GROUP_1:def 5;
        then h * a in a*(carr H) by GROUP_1:def 4;
        hence q in a * H by B2, GROUP_2:def 13;
      end;
    end;
    hence H is normal by GROUP_3:119;
  end;
end;
@ %def Th25

\begin{definition}
We define the function $j_{1}\colon G\to G\rtimes G$ by $j_{1}(g)=(g,1)$.
\end{definition}

<<Definition: [[incl1]] includes $G$ into $G\rtimes A$>>=
definition
  let G, A, phi;
  func incl1 (G, A, phi) -> Function of G, semidirect_product (G,A,phi) means
  :Def4:
  for g being Element of G holds it.g = <* g, 1_A *>;

  existence
  proof
    defpred P[Element of G, Element of semidirect_product(G,A,phi)] means
    $2 = <* $1, 1_A *>;

    A1: for x being Element of G
    ex y being Element of semidirect_product(G,A,phi)
    st P[x, y]
    proof
      let x be Element of G;
      reconsider y=<*x,1_A*> as Element of semidirect_product(G,A,phi)
      by Th11;
      take y;
      thus P[x, y];
    end;
    consider f being Function of G, semidirect_product(G, A, phi) such that
    A2: for x being Element of G holds P[x, f.x]
    from FUNCT_2:sch 3(A1);
    take f;
    thus thesis by A2;
  end;
  uniqueness
  proof
    for f1,f2 being Function of G, semidirect_product(G, A, phi)
    st (for g being Element of G holds f1.g = <*g,1_A*>)
     & (for g being Element of G holds f2.g = <*g,1_A*>)
    holds f1 = f2
    proof
      let f1,f2 be Function of G, semidirect_product(G, A, phi);
      assume A1: for g being Element of G holds f1.g = <*g,1_A*>;
      assume A2: for g being Element of G holds f2.g = <*g,1_A*>;
      for g being Element of G holds f1.g = f2.g
      proof
        let g be Element of G;
        thus f1.g = <*g,1_A*> by A1
                 .= f2.g by A2;
      end;

      hence f1 = f2 by FUNCT_2:def 8;
    end;
    hence thesis;
  end;
end;
@ %def incl1 Def4

\begin{voc}
We add [[incl1]] to our vocabulary.
\end{voc}

<<DICT/group-24.voc>>=
Oincl1
@

\begin{registration}
The inclusion function is a morphism and injective.
\end{registration}

<<Register: [[incl2]] is a morphism and injective>>=
:: Aschbacher, Finite Group Theory (10.1.2)
registration
  let G, A, phi;
  cluster incl2 (G, A, phi) -> multiplicative;
  coherence
  proof
    for a1,a2 being Element of A
    holds (incl2 (G, A, phi)).(a1 * a2)
    = ((incl2 (G, A, phi)).a1)*((incl2 (G, A, phi)).a2)
    proof
      let a1,a2 be Element of A;
      ((incl2 (G, A, phi)).a1) = <*1_G,a1*>
      & ((incl2 (G, A, phi)).a2) = <*1_G,a2*>
      & (incl2 (G, A, phi)).(a1 * a2) = <*1_G,a1*a2*> by Def5;
      hence thesis by Th24;
    end;
    hence thesis by GROUP_6:def 6;
  end;

  cluster incl2 (G, A, phi) -> one-to-one;
  coherence
  proof
    for a1,a2 being Element of A
    st (incl2 (G, A, phi)).a1 = (incl2 (G, A, phi)).a2
    holds a1 = a2
    proof
      let a1,a2 be Element of A;
      assume A1: (incl2 (G, A, phi)).a1 = (incl2 (G, A, phi)).a2;
      <*1_G, a1*> = (incl2 (G, A, phi)).a1 by Def5
                 .= <*1_G, a2*> by A1,Def5;
      hence a1 = a2 by Th14;
    end;
    hence incl2 (G, A, phi) is one-to-one by GROUP_6:1;
  end;
end;
@

\begin{definition}
We define $j_{2}\colon A\to G\rtimes A$.
\end{definition}

<<Definition: [[incl2]] includes $A$ into $G\rtimes A$>>=
definition
  let G, A, phi;
  func incl2 (G, A, phi) -> Function of A, semidirect_product (G,A,phi) means
  :Def5:
  for a being Element of A holds it.a = <* 1_G, a *>;
  existence
  proof
    defpred P[Element of A, Element of semidirect_product(G,A,phi)] means
    $2 = <* 1_G, $1 *>;

    A1: for x being Element of A
    ex y being Element of semidirect_product(G,A,phi)
    st P[x, y]
    proof
      let x be Element of A;
      reconsider y=<*1_G,x*> as Element of semidirect_product(G,A,phi)
      by Th11;
      take y;
      thus P[x, y];
    end;
    consider f being Function of A, semidirect_product(G, A, phi) such that
    A2: for x being Element of A holds P[x, f.x]
    from FUNCT_2:sch 3(A1);
    take f;
    thus thesis by A2;
  end;
  uniqueness
  proof
    for f1,f2 being Function of A, semidirect_product(G, A, phi)
    st (for a being Element of A holds f1.a = <*1_G,a*>)
     & (for a being Element of A holds f2.a = <*1_G,a*>)
    holds f1 = f2
    proof
      let f1,f2 be Function of A, semidirect_product(G, A, phi);
      assume A1: for a being Element of A holds f1.a = <*1_G,a*>;
      assume A2: for a being Element of A holds f2.a = <*1_G,a*>;
      for a being Element of A holds f1.a = f2.a
      proof
        let a be Element of A;
        thus f1.a = <*1_G,a*> by A1
                 .= f2.a by A2;
      end;

      hence f1 = f2 by FUNCT_2:def 8;
    end;
    hence thesis;
  end;
end;
@ %def incl2 Def5

\begin{voc}
We add [[incl2]] to our vocabulary.
\end{voc}

<<DICT/group-24.voc>>=
Oincl2
@


\begin{registration}
We observe $j_{1}$ is a morphism and injective.
\end{registration}

<<Register: [[incl1]] is a morphism and injective>>=
:: Aschbacher, Finite Group Theory (10.1.2)
registration
  let G, A, phi;
  cluster incl1 (G, A, phi) -> multiplicative;
  coherence
  proof
    for g1,g2 being Element of G
    holds (incl1 (G, A, phi)).(g1 * g2)
    = ((incl1 (G, A, phi)).g1)*((incl1 (G, A, phi)).g2)
    proof
      let g1,g2 be Element of G;
      ((incl1 (G, A, phi)).g1) = <*g1,1_A*>
      & ((incl1 (G, A, phi)).g2) = <*g2,1_A*>
      & (incl1 (G, A, phi)).(g1 * g2) = <*g1*g2,1_A*> by Def4;
      hence thesis by Th23;
    end;
    hence thesis by GROUP_6:def 6;
  end;
  cluster incl1 (G, A, phi) -> one-to-one;
  coherence
  proof
    for g1,g2 being Element of G
    st (incl1 (G, A, phi)).g1 = (incl1 (G, A, phi)).g2
    holds g1 = g2
    proof
      let g1,g2 be Element of G;
      assume A1: (incl1 (G, A, phi)).g1 = (incl1 (G, A, phi)).g2;
      <*g1, 1_A*> = (incl1 (G, A, phi)).g1 by Def4
                 .= <*g2,1_A*> by A1,Def4;
      hence g1 = g2 by Th14;
    end;
    hence incl1 (G, A, phi) is one-to-one by GROUP_6:1;
  end;
end;
@ 

\begin{theorem}
The image of $j_{1}$ is a normal subgroup of $G\rtimes A$.
\end{theorem}

<<Theorem: Image of $j_{1}$ is a normal subgroup of $G\rtimes A$>>=
:: Aschbacher, Finite Group Theory (10.1.3)
theorem Th26:
  Image (incl1 (G, A, phi)) is normal Subgroup of
  semidirect_product (G, A, phi)
proof
  for x,g being Element of semidirect_product (G, A, phi)
  st g in Image (incl1 (G, A, phi))
  holds g |^ x in Image (incl1 (G, A, phi))
  proof
    let x,g be Element of semidirect_product (G, A, phi);
    assume g in Image (incl1 (G, A, phi));
    then consider g0 being Element of G such that
    A1: g = (incl1 (G, A, phi)).g0 by GROUP_6:45;
    A2: g = <*g0,1_A*> by A1,Def4;
    consider x1 being Element of G, x2 being Element of A such that
    A3: x = <* x1, x2 *>
    by Th13;
    (x") = <* (phi/.(x2"))/.(x1"),x2" *> by A3, Th22;
    then (x") * g = <* ((phi/.(x2"))/.(x1"))*((phi/.(x2"))/.g0),
                       (x2") * (1_A) *> by A2,Th15
    .= <* ((phi/.(x2"))/.(x1"))*((phi/.(x2"))/.g0), (x2") *> by GROUP_1:def 4
    .= <* (phi/.(x2"))/.((x1") * g0), (x2") *> by GROUP_6:def 6;
    then A4: ((x") * g) * x
    = <* ((phi/.(x2"))/.((x1")*g0)*((phi/.(x2"))/.x1)), (x2") * x2 *> by A3,Th15
    .= <* ((phi/.(x2"))/.((x1")*g0)*((phi/.(x2"))/.x1)), 1_A *>
       by GROUP_1:def 5
    .= <* ((phi/.(x2"))/.(((x1") * g0) * x1)), 1_A *> by GROUP_6:def 6
    .= <* (phi /. (x2"))/.(g0 |^ x1), 1_A *> by GROUP_3:def 2;
    g |^ x = ((x") * g) * x by GROUP_3:def 2
    .= ((incl1 (G, A, phi)).((phi /. (x2"))/.(g0 |^ x1))) by A4,Def4;
    hence g |^ x in Image (incl1 (G, A, phi)) by GROUP_6:45;
  end;
  hence thesis by Th25;
end;
@ %def Th26

\begin{registration}
We register the fact $\Im(j_{1})\normalSubgroup A\ltimes G$.
\end{registration}

<<Register: $\Im(j_{1})\normalSubgroup A\ltimes G$>>=
registration
  let A, G, phi;
  cluster Image (incl1 (G, A, phi)) -> normal;
  correctness by Th26;
end;
@

\begin{theorem}
$\Im(j_{1})\cap\Im(j_{2})=\trivialSubgroup$.
\end{theorem}

<<Theorem: $\Im(j_{1})\cap\Im(j_{2})=\trivialSubgroup$>>=
theorem
  Image (incl2 (G, A, phi)) /\ Image (incl1 (G, A, phi))
  = (1).(semidirect_product(G,A,phi))
proof
  set ImA = Image (incl2 (G, A, phi)), ImG = Image (incl1 (G, A, phi));
  set S = semidirect_product(G,A,phi);
  for x being object st x in the carrier of (ImA /\ ImG)
  holds x in {1_S}
  proof
    let x be object;
    assume x in the carrier of (ImA /\ ImG);
    then x in ImA /\ ImG;
    then B1: x in ImA & x in ImG by GROUP_2:82;
    then consider g being Element of G such that
    B2: x = (incl1 (G, A, phi)).g
    by GROUP_6:45;
    consider a being Element of A such that
    B3: x = (incl2 (G, A, phi)).a
    by B1, GROUP_6:45;
    <*1_G, a*> = x by B3,Def5
              .= <*g,1_A*> by B2,Def4;
    then a = 1_A & 1_G = g by Th14;
    then x = <*1_G,1_A*> by B2,Def4
          .= 1_S by Th17;
    hence x in {1_S} by TARSKI:def 1;
  end;
  then the carrier of (ImA /\ ImG) c= {1_S};
  then the carrier of (ImA /\ ImG) c= the carrier of (1).S by GROUP_2:def 7;
  then A1: ImA /\ ImG is Subgroup of (1).S by GROUP_2:57;
  (1).S is Subgroup of ImA /\ ImG by GROUP_2:65;
  hence thesis by A1, GROUP_2:55;
end;
@

\begin{theorem}
Every element of $A\ltimes G$ is the product of an element of
$\Im(j_{1})$ with an element of $\Im(j_{2})$.
\end{theorem}

<<Theorem: every $x\in A\ltimes G$ is the product of elements $a\in\Im(j_{1})$ and $g\in\Im(j_{2})$ as $x=ga$>>=
theorem Th28:
  for x being Element of semidirect_product(G,A,phi)
  ex g being Element of G,a being Element of A
  st ((incl1(G,A,phi)).g) * ((incl2(G,A,phi)).a) = x
proof
  let x be Element of semidirect_product(G, A, phi);
  consider g being Element of G, a being Element of A such that
  A1: x = <*g,a*>
  by Th13;
  take g,a;
  ((incl1(G,A,phi)).g) = <*g,1_A*> & ((incl2(G,A,phi)).a) = <*1_G,a*>
  by Def4,Def5;
  hence ((incl1(G,A,phi)).g) * ((incl2(G,A,phi)).a)
  = <* g * ((phi /. (1_A))/.(1_G)), (1_A) * (a) *> by Th15
  .= <* g * (1_G), (1_A) * (a) *> by Th16
  .= <* g * (1_G), a *> by GROUP_1:def 4
  .= x by A1, GROUP_1:def 4;
end;
@ %def Th28

\begin{theorem}
$\Im(j_{2})\Im(j_{1})=U(A\ltimes G)$.
\end{theorem}

<<Theorem: $U(A\ltimes G)=\Im(j_{2})\Im(j_{1})$>>=
theorem Th29:
  (Image incl1(G,A,phi)) * (Image incl2(G,A,phi))
  = the carrier of (semidirect_product(G,A,phi))
proof
  for x being Element of semidirect_product (G, A, phi)
  holds x in (Image incl1(G,A,phi)) * (Image incl2(G,A,phi))
  proof
    let x be Element of semidirect_product (G, A, phi);
    
    consider g being Element of G, a being Element of A such that
    B1: ((incl1(G,A,phi)).g) * ((incl2(G,A,phi)).a) = x
    by Th28;
    ((incl1(G,A,phi)).g) in Image(incl1(G,A,phi)) &
    ((incl2(G,A,phi)).a) in Image(incl2(G,A,phi)) by GROUP_6:45;
    hence x in (Image incl1(G,A,phi)) * (Image incl2(G,A,phi))
    by B1, GROUP_5:4;
  end;
  then A1: the carrier of (semidirect_product(G,A,phi))
  c= (Image incl1(G,A,phi)) * (Image incl2(G,A,phi));
  set S = semidirect_product(G,A,phi);
  thus thesis by A1, XBOOLE_0:def 10;
end;
@ %def Th29

\begin{theorem}
$\Im(j_{2})\wedge\Im(j_{1})=A\ltimes G$.
\end{theorem}

<<Theorem: $\Im(j_{2})\vee\Im(j_{1})=A\ltimes G$>>=
theorem
  Image (incl1 (G, A, phi)) "\/" Image (incl2 (G, A, phi))
  = (semidirect_product(G,A,phi))
proof
  set S = semidirect_product(G,A,phi);
  set U = (Image incl1(G,A,phi)) * (Image incl2(G,A,phi));
  U c= the carrier of gr U by GROUP_4:def 4; then
  the carrier of S c= the carrier of (gr U) by Th29;
  then S = gr U by GROUP_2:61;
  hence thesis by GROUP_4:50;
end;
@

\begin{theorem}
We see $G\iso\Im(j_{2})$.
\end{theorem}

<<Theorem: $G\iso\Im(j_{1})$>>=
:: Aschbacher, Finite Group Theory (10.1.3)
theorem
  G, Image (incl1 (G, A, phi)) are_isomorphic by GROUP_6:68;
@ 

\begin{theorem}
Let $g\in G$ and $a\in A$. Then $(1,g)^{(a,1)}=(1,\varphi_{a^{-1}}(g))$.
\end{theorem}

<<Theorem: Let $g\in G$ and $a\in A$, then $(1,g)^{(a,1)}=(1,\varphi_{a^{-1}}(g))$>>=
:: Aschbacher, Finite Group Theory (10.1.4) --- note there is a sign
:: error in Aschbacher's book.
theorem
  for a being Element of A
  for g being Element of G
  holds ((incl1(G,A,phi)).g) |^ ((incl2(G,A,phi)).a)
  = <*(phi /. (a"))/.g,1_A *>
proof
  let a be Element of A;
  let g be Element of G;
  set xg = (incl1(G,A,phi)).g;
  set xa = (incl2(G,A,phi)).a;
  A1: xa = <* 1_G,a *> by Def5;
  then A2: xa" = <* ((phi/.(a"))/.((1_G)")), a" *> by Th22
              .= <* ((phi/.(a"))/.(1_G)), a" *> by GROUP_1:8
              .= <* 1_G, a" *> by GROUP_6:31;
  xg = <*g, 1_A*> by Def4; then
  (xa") * xg = <* (1_G) * ((phi /. (a"))/.g), (a") * (1_A) *> by A2,Th15
            .= <* (1_G) * ((phi /. (a"))/.g), (a") *> by GROUP_1:def 4
            .= <* ((phi /. (a"))/.g), (a") *> by GROUP_1:def 4;
  then ((xa")*xg)*xa = <* ((phi/.(a"))/.g)*((phi/.(a"))/.(1_G)), (a")*a *>
  by A1, Th15
                    .= <* ((phi /. (a"))/.g) * (1_G), (a")*a *> by GROUP_6:31
                    .= <* ((phi /. (a"))/.g), (a")*a *> by GROUP_1:def 4
                    .= <* ((phi /. (a"))/.g), 1_A *> by GROUP_1:def 5;
  hence thesis by GROUP_3:def 2;
end;
@

\begin{theorem}
When $\varphi_{a}(g)=g$ for all $a\in A$ and $g\in G$, we have
the semidirect product coincide with the direct product $A\ltimes_{\varphi}G=A\times G$.
\end{theorem}

\begin{proof}[Proof sketch]
Since the two groups share the same underlying set, we just need to
prove the binary operators coincide.
\end{proof}

<<Theorem: $\varphi$ is trivial implies $A\ltimes_{\varphi}G=A\times G$>>=
theorem
  semidirect_product (G, A, 1:(A, AutGroup G)) = product <*G, A*>
proof
  set S = semidirect_product (G, A, 1:(A, AutGroup G));
  A1: the carrier of S = the carrier of product <*G,A*> by Th10;
  set B1 = the multF of S;
  set B2 = the multF of product <*G, A*>;
  set U = product (Carrier <*G,A*>);
  the carrier of S = product (Carrier<*G,A*>) &
  B1 is BinOp of the carrier of S by Def3;
  then A2: B1 is BinOp of U;
  the carrier of product <*G,A*> = product (Carrier <*G,A*>) &
  the multF of product <*G,A*> is BinOp of the carrier of product <*G,A*>
  by GROUP_7:def 2; then
  A3: B2 is BinOp of U;

  for x,y being Element of product (Carrier <*G,A*>)
  holds B1.(x,y) = B2.(x,y)
  proof
    let x,y be Element of product (Carrier <*G,A*>);
    x is Element of S by Def3;
    then consider g1 being Element of G, a1 being Element of A such that
    A4: x = <*g1,a1*> by Th13;
    y is Element of S by Def3;
    then consider g2 being Element of G, a2 being Element of A such that
    A5: y = <*g2,a2*> by Th13;
    1:(A, AutGroup G) = A --> (1_(AutGroup G)) by GROUP_6:def 7; then
    (1:(A, AutGroup G)).a1 = id the carrier of G by AUTGROUP:9; then
    A6: (((1:(A, AutGroup G)) /. a1) /. g2) = g2;
    reconsider x0=x,y0=y as Element of S by Def3;
    A8: x0 * y0 = <* g1 * g2, a1 * a2 *> by A4, A5, A6, Th15;

    B2.(x,y) = <*g1,a1*> * <*g2, a2*> by A4,A5
            .= <* g1 * g2,a1 * a2 *> by GROUP_7:29;
    hence B1.(x,y) = B2.(x,y) by A8;
  end;
  hence thesis by A1,A2,A3,BINOP_1:2;
end;
@