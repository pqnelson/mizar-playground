% -*- mode: poly-noweb; noweb-code-mode: mizar-mode; -*-
\section{Definition and Properties of Semi-Direct Product}

There are two ways to define the semidirect product, because it depends
on how we defined the automorphism group $\aut(G)$ for a group $G$.
If multiplication in $\aut(G)$ is composition of automorphisms, then we
need to follow Bourbaki's definition (which we do). Finite group
theorists tend to work with the opposite conventions for $\aut(G)$, and
end up with the other definition for the semidirect product of groups.

<<Definition and Properties of Semi-Direct Products>>=
begin :: Semidirect Products of Groups
<<Definition: Semidirect Product>>

<<Register: semidirect product is nonempty strict constituted-Functions>>

<<Register: Elements of semidirect product are FinSequence-like>>

<<Theorem: Carrier of semidirect product is the carrier of the direct product>>

<<Theorem: $\forall a\in A,g\in G, \langle a,g\rangle\in A\ltimes_{\varphi}G$>>

<<Theorem: $x\in A\ltimes_{\varphi}G$ implies $x_{1}\in A$ and $x_{2}\in G$ and $\dom(x)=\{1,2\}$>>

<<Theorem: $x\in A\ltimes G$ implies $\exists a\in A,g\in G,x=\langle a,g\rangle$>>

<<Theorem: $\langle x,y\rangle$ satisfies the properties of an ordered pair>>

<<Theorem: $\langle a_{1},g_{1}\rangle\langle a_{2},g_{2}\rangle = \langle a_{1}a_{2}, g_{1}\cdot\varphi_{a_{1}}(g_{2})\rangle$>>

<<Theorem: $\varphi_{1_{A}}(g)=g$>>

<<Theorem: $(\varphi_{a_{1}}\circ\varphi_{a_{2}})(g)=\varphi_{a_{1}a_{2}}(g)$>>

<<Theorem: $(\varphi_{a^{-1}}\circ\varphi_{a})(g)=g$ and $(\varphi_{a}\circ\varphi_{a^{-1}})(g)=g$>>

<<Lemma: $e\in A\ltimes G$ be such that $e=\langle 1_{A}, 1_{G}\rangle$ is the identity element>>

<<Register: The semidirect product is a unital magma>>

<<Theorem: The unit for the semidirect product>>

<<Theorem: inverse element for semidirect product>>

<<Theorem: Criteria for unital magmas to be a group>>

<<Register: semidirect product is a group>>

<<Theorem: $\langle a,g\rangle^{-1} = \langle a^{-1}, \varphi_{a^{-1}}(g^{-1})\rangle$>>

<<Subsection: Inclusion morphisms>>

<<Theorem: $\varphi$ is trivial implies $A\ltimes_{\varphi}G=A\times G$>>
@

\begin{definition}
Let $A$, $G$ be groups and $\varphi\colon A\to\Aut(G)$ be a group morphism.
Then the \define{Semidirect Product} of $A$ and $G$ using $\varphi$
consists of the magma $A\ltimes_{\varphi}G$ whose underlying set is the
Cartesian product $U(A)\times U(G)$ and whose binary operator is defined
by:
\begin{equation}
(a_{1},g_{1})(a_{2},g_{2}) = (a_{1}a_{2}, g_{1}(\varphi_{a_{1}}(g_{2})))
\end{equation}
for every $a_{1}$, $a_{2}\in A$ and $g_{1}$, $g_{2}\in G$.
\end{definition}

\begin{def-remark}
  We will prove that $A\ltimes_{\varphi}G$ actually satisfies the
  properties for being a group, in separate registrations.
\end{def-remark}

<<Definition: Semidirect Product>>=
definition
  let A, G, phi;
  func semidirect_product (A, G, phi) -> non empty strict multMagma means
  :Def1:
  the carrier of it = product (Carrier <* A, G *>) &
  (for f, g being Element of product (Carrier <* A, G *>)
   ex h being Function, a1 being Element of A, g2 being Element of G
   st h = (the multF of it).(f, g) &
      a1 = f.1 & g2 = g.2 &
      h.1 = (the multF of A).((f.1), (g.1)) &
      h.2 = (the multF of G).(f.2, (phi/.a1)/. g2));
  existence
  <<Proof: Existence of Semidirect Products>>
  uniqueness
  <<Proof: Uniqueness of Semidirect Products>>
end;
@ %def Def1

\begin{voc}
Before moving on, we need to add [[semidirect_product]] to the
vocabulary list.
\end{voc}

<<DICT/group-24.voc>>=
Osemidirect_product
@ 

\begin{proof}[Proof (Existence)]
To prove the existence of the semidirect product, we really need to
construct the binary operator on the set. This requires a bit of
leg-work, and constitutes the bulk of the proof (sets $A_{1}$ and $A_{2}$).
\end{proof}

<<Proof: Existence of Semidirect Products>>=
proof
  set U = product (Carrier <* A, G *>);
  defpred P[Element of U, Element of U, set] means
  ex h being Function, a1 being Element of A, g2 being Element of G
  st h = $3 & a1 = $1.1 & g2 = $2.2 &
     h.1 = (the multF of A).(($1.1), ($2.1)) &
     h.2 = (the multF of G).($1.2, (phi/.a1)/. g2);

  A1: for x, y being Element of U ex z being Element of U st P[x,y,z]
  proof
    let x, y be Element of U;
    x.1 in A by Lm0; then
    reconsider a1 = x.1 as Element of A;
    y.2 in G by Lm0; then
    reconsider g2 = y.2 as Element of G;
    
    set h = <* (the multF of A).(x.1, y.1), (the multF of G).(x.2,(phi/.a1)/.g2)*>;
    x.1 in A & y.1 in A & x.2 in G by Lm0; then
    (the multF of A).(x.1, y.1) in A & (the multF of G).(x.2,(phi/.a1)/.g2)
    in G by BINOP_1:17;
    then reconsider z = h as Element of product (Carrier <*A,G*>) by Lm1;
    take z;
    h = z & a1 = x.1 & g2 = y.2 &
    h.1 = (the multF of A).(x.1, y.1) &
    h.2 = (the multF of G).(x.2, (phi/.a1)/.g2) by FINSEQ_1:44;
    hence P[x, y, z];
  end;
  
  consider B being Function of [:U, U:],U such that
  A2: for a, b being Element of U holds P[a,b,B.(a,b)] from BINOP_1:sch 3(A1);

  set M = multMagma(# (product (Carrier <*A,G*>)), B #);
  reconsider M as non empty strict multMagma;
  take M;
  thus thesis by A2;
end;
@

\begin{proof}[Proof (Uniqueness)]
If we were to somehow assemble two such magmas, then really they would
be the same. They have the same underlying set (which is easy to prove)
and the same binary operator (which is the bulk of the proof).
\end{proof}

<<Proof: Uniqueness of Semidirect Products>>=
proof
  let M1,M2 being non empty strict multMagma;
  assume that
  A1: (the carrier of M1 = product (Carrier <* A, G *>) &
      (for f, g being Element of product (Carrier <* A, G *>)
       ex h being Function, a1 being Element of A, g2 being Element of G
       st h = (the multF of M1).(f, g) &
          a1 = f.1 & g2 = g.2 &
          h.1 = (the multF of A).((f.1), (g.1)) &
          h.2 = (the multF of G).(f.2, (phi/.a1)/. g2))) and
  A2: (the carrier of M2 = product (Carrier <* A, G *>) &
      (for f, g being Element of product (Carrier <* A, G *>)
       ex h being Function, a1 being Element of A, g2 being Element of G
       st h = (the multF of M2).(f, g) &
          a1 = f.1 & g2 = g.2 &
          h.1 = (the multF of A).((f.1), (g.1)) &
          h.2 = (the multF of G).(f.2, (phi/.a1)/. g2)));
  
  for x,y being set st x in the carrier of M1 & y in the carrier of M1
  holds (the multF of M1).(x, y) = (the multF of M2).(x, y)
  <<Proof step: $\forall x,y, x\star_{M_{1}}y = x\star_{M_{2}}y$>>
  hence thesis by A1,A2,BINOP_1:1;
end;
@

\begin{proof}
Proving the binary operators are identical requires a bit of work, and
showing that they operate on pairs of elements to produce
$h_{1}=x\star_{M_{1}}y$ (in step $B_{3}$) and $h_{2}=x\star_{M_{2}}y$
(in step $B_{4}$) which are both pairs of elements (step $B_{5}$). Then
they are equal, since they are equal componentwise.
\end{proof}

<<Proof step: $\forall x,y, x\star_{M_{1}}y = x\star_{M_{2}}y$>>=
proof
  let x,y be set;
  assume B1: x in the carrier of M1;
  assume B2: y in the carrier of M1;
  reconsider f = x, g = y as Element of product (Carrier <*A,G*>) by A1,B1,B2;
  consider h1 being Function, a11 being Element of A, g12 being Element of G
  such that
  B3: h1 = (the multF of M1).(f, g) & a11 = f.1 & g12 = g.2
      & h1.1 = (the multF of A).((f.1), (g.1))
      & h1.2 = (the multF of G).(f.2, (phi/.a11)/. g12) by A1;
  consider h2 being Function, a21 being Element of A, g22 being Element of G
  such that
  B4: h2 = (the multF of M2).(f, g) & a21 = f.1 & g22 = g.2
      & h2.1 = (the multF of A).((f.1), (g.1))
      & h2.2 = (the multF of G).(f.2, (phi/.a21)/. g22) by A2;
  B5: dom h1 = {1,2} & dom h2 = {1,2}
  proof
    h1 in product (Carrier <*A,G*>) by A1, B3, BINOP_1:17;
    then ex f1 being Function st h1 = f1 & dom f1 = dom (Carrier <*A,G*>)
    & (for y1 being object st y1 in dom (Carrier <*A,G*>)
    holds f1.y1 in (Carrier <*A,G*>).y1)
    by CARD_3:def 5;
    hence dom h1 = {1,2} by PARTFUN1:def 2; 
    h2 in product (Carrier <*A,G*>) by A2, B4, BINOP_1:17;
    then ex f1 being Function st h2 = f1 & dom f1 = dom (Carrier <*A,G*>)
    & (for y1 being object st y1 in dom (Carrier <*A,G*>)
    holds f1.y1 in (Carrier <*A,G*>).y1)
    by CARD_3:def 5;
    hence dom h2 = {1,2} by PARTFUN1:def 2;
  end;
  for i being object st i in dom h1 holds h1.i = h2.i
  proof
    let i be object;
    assume i in dom h1;
    then per cases by B5, TARSKI:def 2;
    suppose i = 1;
      hence thesis by B3,B4;
    end;
    suppose i = 2;
      hence thesis by B3,B4;
    end;
  end;
  then h1 = h2 by B5, FUNCT_1:def 11;
  hence (the multF of M1).(x, y) = (the multF of M2).(x, y) by B3,B4;
end;  
@

\begin{registration}
The semidirect product's elements may be viewed as functions.
\end{registration}

<<Register: semidirect product is nonempty strict constituted-Functions>>=
registration
  let A, G, phi;
  cluster semidirect_product (A, G, phi) -> non empty strict constituted-Functions;
  coherence
  proof
    the carrier of semidirect_product (A, G, phi) = product (Carrier <*A,G*>)
    by Def1;
    hence thesis by MONOID_0:80;
  end;
end;
@

\begin{registration}
Elements of the semidirect product are ``Finite Sequence-like''.
\end{registration}

<<Register: Elements of semidirect product are FinSequence-like>>=
registration
  let A, G, phi;
  cluster -> FinSequence-like for Element of (semidirect_product (A, G, phi));
  coherence
  proof
    for b1 being Element of semidirect_product(A, G, phi)
    holds b1 is FinSequence-like
    proof
      let b1 be Element of semidirect_product(A, G, phi);
      b1 is Element of product (Carrier <*A,G*>) by Def1;
      hence b1 is FinSequence-like;
    end;
    hence thesis;
  end;
end;
@ 

\begin{theorem}
The underlying set of the semidirect product is the same as the
underlying set of the direct product.
\end{theorem}

<<Theorem: Carrier of semidirect product is the carrier of the direct product>>=
theorem th2:
  the carrier of semidirect_product (A, G, phi) = the carrier of product <*A,G*>
proof
  thus the carrier of semidirect_product (A,G,phi)
   = product (Carrier <*A,G*>) by Def1
  .= the carrier of product <*A,G*> by GROUP_7:def 2;
end;
@ %def th2

\begin{theorem}
For any $a\in A$ and $g\in G$, we have $\langle a,g\rangle$ be an
element of $A\ltimes_{\varphi}G$.
\end{theorem}

<<Theorem: $\forall a\in A,g\in G, \langle a,g\rangle\in A\ltimes_{\varphi}G$>>=
theorem th3:
  for a being Element of A
  for g being Element of G
  holds <* a, g *> is Element of semidirect_product (A, G, phi) by th2;
@ %def th3

\begin{theorem}
For any $x\in A\ltimes_{\varphi}G$, we have $x_{1}\in A$ and $x_{2}\in G$
and $\dom(x)=\{1,2\}$.
\end{theorem}

<<Theorem: $x\in A\ltimes_{\varphi}G$ implies $x_{1}\in A$ and $x_{2}\in G$ and $\dom(x)=\{1,2\}$>>=
theorem th4:
  for x being Element of semidirect_product (A, G, phi)
  holds x.1 in A & x.2 in G & dom x = {1,2}
proof
  let x be Element of semidirect_product (A, G, phi);
  x in the carrier of semidirect_product (A, G, phi);
  then x in the carrier of product <*A,G*> by th2;
  hence thesis by th4a;
end;
@ %def th4

\begin{theorem}
For each $x\in A\ltimes_{\varphi}G$, there is a corresponding $a\in A$
and $g\in G$ such that $x=\langle a,g\rangle$.
\end{theorem}

<<Theorem: $x\in A\ltimes G$ implies $\exists a\in A,g\in G,x=\langle a,g\rangle$>>=
theorem th5:
  for x being Element of semidirect_product (A, G, phi)
  ex a being Element of A, g being Element of G
  st x = <* a, g *>
proof
  let x be Element of semidirect_product (A, G, phi);

  x.1 in A & x.2 in G by th4;
  then consider a being Element of A, g being Element of G such that
  A1: a = x.1 & g = x.2;

  take a, g;
  
  dom x = {1,2} by th4;
  then len x = 2 by FINSEQ_1:2,def 3;
  hence x = <* a, g *> by A1, FINSEQ_1:44;
end;
@ %def th5

\begin{theorem}
Let $x_{1}$, $x_{2}$, $y_{1}$, $y_{2}$ be objects.
Then $\langle x_{1}, y_{1}\rangle = \langle x_{2},y_{2}\rangle$ if and
only if $x_{1}=x_{2}$ and $y_{1}=y_{2}$.
\end{theorem}

<<Theorem: $\langle x,y\rangle$ satisfies the properties of an ordered pair>>=
theorem Lm1:
  for x1, x2, y1, y2 being object
  holds <*x1,y1*> = <*x2,y2*> iff (x1 = x2 & y1 = y2)
proof
  let x1, x2, y1, y2 be object;
  thus <*x1,y1*> = <*x2,y2*> implies (x1 = x2 & y1 = y2)
  proof
    assume A1: <*x1,y1*> = <*x2,y2*>;
    thus x1 = <*x1,y1*>.1 by FINSEQ_1:44
           .= x2 by A1, FINSEQ_1:44;
    thus y1 = <*x1,y1*>.2 by FINSEQ_1:44
           .= y2 by A1, FINSEQ_1:44;
  end;
  thus (x1 = x2 & y1 = y2) implies <*x1,y1*> = <*x2,y2*>;
end;
@ 

\begin{theorem}
Let $x$, $y\in A\ltimes G$, let $a_{1}$, $a_{2}\in A$, and let
$g_{1},g_{2}\in G$. If $x=\langle a_{1},g_{1}\rangle$ and $y=\langle a_{2},g_{2}\rangle$,
then $xy = \langle a_{1}a_{2}, g_{1}\cdot\varphi_{a_{1}}(g_{2})\rangle$.
\end{theorem}

<<Theorem: $\langle a_{1},g_{1}\rangle\langle a_{2},g_{2}\rangle = \langle a_{1}a_{2}, g_{1}\cdot\varphi_{a_{1}}(g_{2})\rangle$>>=
theorem th6:
  for x,y being Element of semidirect_product (A, G, phi)
  for a1,a2 being Element of A
  for g1,g2 being Element of G
  st x = <*a1,g1*> & y = <*a2,g2*>
  holds x * y = <* a1 * a2, g1 * ((phi /. a1)/.g2) *>
proof
  let x,y be Element of semidirect_product (A, G, phi);
  let a1,a2 be Element of A;
  let g1,g2 be Element of G;
  assume A1: x = <*a1,g1*>;
  assume A2: y = <*a2,g2*>;
  
  x in product (Carrier <* A, G*>) & y in product (Carrier <*A,G*>)
  proof
    x in semidirect_product(A,G,phi);
    hence x in product (Carrier <*A,G*>) by Def1;
    y in semidirect_product(A,G,phi);
    hence y in product (Carrier <*A,G*>) by Def1;
  end;
  then consider h being Function, aa1 being Element of A, gg2 being Element
  of G such that
  A3: h = x * y & aa1 = x.1 & gg2 = y.2 & h.1 = (the multF of A).(x.1, y.1) 
      & h.2 = (the multF of G).(x.2, (phi/.aa1)/.gg2) by Def1;
  A4: a1 = aa1 & g2 = gg2 by A1,A2,A3,FINSEQ_1:44;

  h.1 = (the multF of A).(x.1, a2) by A2,A3,FINSEQ_1:44
     .= (the multF of A).(a1, a2) by A1,FINSEQ_1:44;
  then A5: (x * y).1 = a1 * a2 by A3;
  A6: (x * y).2 = g1 * ((phi /. a1)/.g2) by A1,A3,A4,FINSEQ_1:44;
  dom (x * y) = {1,2} by th4;
  then len (x * y) = 2 by FINSEQ_1:2,def 3;
  hence x * y = <* a1 * a2, g1 * ((phi /. a1)/.g2) *> by A5,A6,FINSEQ_1:44;
end;
@ %def th6

\begin{theorem}
For any $g\in G$, we have $\varphi_{1_{A}}(g)=g$.
\end{theorem}

<<Theorem: $\varphi_{1_{A}}(g)=g$>>=
theorem th7:
  for g being Element of G
  holds (phi /. 1_A) /. g = g
proof
  let g be Element of G;
  (phi /. 1_A) = 1_(AutGroup G) by GROUP_6:31
              .= id the carrier of G by AUTGROUP:9;
  hence (phi /. 1_A) /. g = g;
end;
@ %def th7

\begin{registration}
The semidirect product is a unital magma.
\end{registration}

<<Register: The semidirect product is a unital magma>>=
registration
  let G, A, phi;
  cluster semidirect_product (A, G, phi) -> unital;
  correctness
  proof
    set S = semidirect_product (A, G, phi);
    reconsider e = <* 1_A, 1_G*> as Element of S by th3;
    take e;
    let x be Element of S;
    consider a being Element of A, g being Element of G such that
    A1: x = <* a, g *>
    by th5;

    A2: ((phi /. (1_A)) /. g) * 1_G = g * 1_G by th7
                                   .= g by GROUP_1:def 4;
    A3: ((phi /. a) /. (1_G)) = 1_G by GROUP_6:31;
    thus x * e = <* a * 1_A, g * ((phi /. a) /. 1_G) *> by A1, th6
              .= <* a, g * ((phi /. a) /. 1_G) *> by GROUP_1:def 4
              .= x by A1, A3, GROUP_1:def 4;
    thus e * x = <* 1_A * a, 1_G * ((phi /. (1_A)) /. g) *> by A1,th6
              .= <* a, 1_G * ((phi /. (1_A)) /. g) *> by GROUP_1:def 4
              .= <* a, (1_G) * (g) *> by A2, GROUP_1:def 4
              .= x by A1,GROUP_1:def 4;
  end;
end;
@

\begin{lemma}
Let $e\in A\ltimes G$ be such that $e=\langle 1_{A}, 1_{G}\rangle$.
Then for any $x\in A\ltimes G$, we have $xe=ex=x$.
\end{lemma}

<<Lemma: $e\in A\ltimes G$ be such that $e=\langle 1_{A}, 1_{G}\rangle$ is the identity element>>=
Lm8:
  for x,e being Element of semidirect_product (A, G, phi)
  st e = <* 1_A, 1_G*>
  holds x * e = x & e * x = x
proof
  let x,e be Element of semidirect_product (A, G, phi);
  assume A1: e = <* 1_A, 1_G*>;
  consider a being Element of A, g being Element of G such that
  A2: x = <* a, g *>
  by th5;

  A3: ((phi /. (1_A)) /. g) * 1_G = g * 1_G by th7
                                 .= g by GROUP_1:def 4;
  thus x * e = <* a * 1_A, g * ((phi /. a) /. 1_G) *> by A1,A2, th6
            .= <* a, g * ((phi /. a) /. 1_G) *> by GROUP_1:def 4
            .= <* a, g * 1_G *> by GROUP_6:31
            .= x by A2, GROUP_1:def 4;
  thus e * x = <* 1_A * a, 1_G * ((phi /. (1_A)) /. g) *> by A1,A2,th6
            .= <* a, 1_G * ((phi /. (1_A)) /. g) *> by GROUP_1:def 4
            .= <* a, (1_G) * g *> by A3, GROUP_1:def 4
            .= x by A2, GROUP_1:def 4;
end;
@ %def Lm8

\begin{theorem}
We have $1_{A\ltimes G}=\langle 1_{A}, 1_{G}\rangle$.
\end{theorem}

<<Theorem: The unit for the semidirect product>>=
theorem th14:
  1_(semidirect_product (A, G, phi)) = <* 1_A, 1_G *>
proof
  reconsider e = <* 1_A, 1_G *> as Element of semidirect_product (A, G, phi)
  by th2;
  for x being Element of semidirect_product (A, G, phi)
  holds x * e = x & e * x = x by Lm8;
  hence thesis by GROUP_1:def 4;
end;
@ %def th14

\begin{theorem}
  For any $a_{1}$, $a_{2}\in A$ and any $g\in G$, we have
  $(\varphi_{a_{1}}\circ\varphi_{a_{2}})(g)=\varphi_{a_{1}a_{2}}(g)$.
\end{theorem}

<<Theorem: $(\varphi_{a_{1}}\circ\varphi_{a_{2}})(g)=\varphi_{a_{1}a_{2}}(g)$>>=
theorem th9:
  for a1,a2 being Element of A
  for g being Element of G
  holds (phi.a1) . ((phi /. a2) . g) = (phi /. (a1 * a2)) . g
proof
  let a1,a2 be Element of A;
  let g be Element of G;
  phi . (a1 * a2) = (phi.a1) * (phi.a2) by GROUP_6:def 6;
  then (phi . (a1 * a2)) . g = ((phi/.a1) * (phi/.a2)).g by AUTGROUP:8
                            .= (phi.a1) . ((phi . a2) . g) by FUNCT_2:15;
  hence (phi.a1) . ((phi /. a2) . g) = (phi /. (a1 * a2)) . g; 
end;
@ %def th9

\begin{theorem}
For any $a\in A$ and $g\in G$, we have $(\varphi_{a^{-1}}\circ\varphi_{a})(g)=g$
and $(\varphi_{a}\circ\varphi_{a^{-1}})(g)=g$.
\end{theorem}

<<Theorem: $(\varphi_{a^{-1}}\circ\varphi_{a})(g)=g$ and $(\varphi_{a}\circ\varphi_{a^{-1}})(g)=g$>>=
theorem th10:
  for a being Element of A
  for g being Element of G
  holds (phi/.(a")) /. ((phi /. a) /. g) = g
  & (phi/. a) /. ((phi /. (a")) /. g) = g
proof
  let a be Element of A;
  let g be Element of G;
  thus (phi /. (a")) /. ((phi /. a) /. g) = (phi /. (a" * a)) /. g by th9
                                         .= (phi /. 1_A) /. g by GROUP_1:def 5
                                         .= g by th7;
  thus (phi/. a) /. ((phi /. (a")) /. g) = (phi /. (a * a")) /. g by th9
                                        .= (phi /. 1_A) /. g by GROUP_1:def 5
                                        .= g by th7;
end;
@ %def th10

\begin{theorem}
Let $x\in A\ltimes G$ be $x=\langle a,g\rangle$, let $e=\langle 1_{A},1_{G}$.
Then $y=\langle a^{-1}, \varphi_{a^{-1}}(g^{-1})\rangle$ is the inverse
element to $x$, i.e., $xy=e$ and $yx=e$.
\end{theorem}

<<Theorem: inverse element for semidirect product>>=
theorem th11:
  for x,y being Element of semidirect_product (A, G, phi)
  for a being Element of A
  for g being Element of G
  st x = <* a, g *> & y = <* a", (phi/.(a"))/.(g") *>
  holds x * y = 1_(semidirect_product (A, G, phi)) & y * x = 1_(semidirect_product (A, G, phi))
proof
  let x,y be Element of semidirect_product (A, G, phi);
  let a be Element of A;
  let g be Element of G;
  assume A1: x = <* a, g *>;
  assume A2: y = <* a", (phi/.(a"))/.(g") *>;
  A3: ((phi /. (a")) /. (g")) * ((phi/.(a"))/.(g))
   = (phi /. (a")) /. (g" * g) by GROUP_6:def 6
  .= (phi /. (a")) /. 1_G by GROUP_1:def 5
  .= 1_G by GROUP_6:31;
  A4: ((phi /. a) /. ((phi/.(a"))/.(g"))) = g" by th10;

  thus x * y = <* a * (a"), g * ((phi/.a)/.((phi/.(a"))/.(g"))) *> by A1,A2,th6
            .= <* a * a", 1_G *> by A4,GROUP_1:def 5
            .= <* 1_A, 1_G *> by GROUP_1:def 5
            .= 1_(semidirect_product (A, G, phi)) by th14;
  thus y * x = <* (a") * a, (phi/.(a")).(g") * ((phi/. a")/.g)*> by A1,A2,th6
            .= <* 1_A, 1_G *> by A3,GROUP_1:def 5
            .= 1_(semidirect_product (A, G, phi)) by th14;
end;
@ %def th11

\begin{theorem}
Let $M$ be a non-empty unital magma.
If every $h\in M$ there is a corresponding $g\in M$ such that $hg=1$ and $gh=1$,
then $M$ is a group.
\end{theorem}

<<Theorem: Criteria for unital magmas to be a group>>=
theorem thUnitalMagmaIsGroupCond:
  for M being non empty unital multMagma
  st (for h being Element of M ex g being Element of M st h * g = 1_M
  & g * h = 1_M)
  holds M is Group-like
proof
  let M be non empty unital multMagma;
  assume A1: for h being Element of M
  ex g being Element of M
  st h * g = 1_M & g * h = 1_M;
  
  ex e being Element of M st
  for h being Element of M
  holds (h * e = h & e * h = h & ex g being Element of M st
  (h * g = e & g * h = e))
  proof
    take e = 1_M;
    let h be Element of M;
    thus h * e = h by GROUP_1:def 4;
    thus e * h = h by GROUP_1:def 4;
    thus thesis by A1;
  end;
  hence M is Group-like by A1, GROUP_1:def 1, GROUP_1:def 3;
end;
@ %def thUnitalMagmaIsGroupCond

\begin{registration}
The semidirect product really is a group after all.
\end{registration}

<<Register: semidirect product is a group>>=
registration
  let G be strict Group;
  let A be Group;
  let phi be Homomorphism of A, AutGroup G;
  cluster semidirect_product (A, G, phi) -> associative;
  correctness
  proof
    thus for x, y, z being Element of semidirect_product (A, G, phi)
    holds (x * y) * z = x * (y * z)
    proof
      let x, y, z be Element of semidirect_product (A, G, phi);
      consider a1 being Element of A, g1 being Element of G such that
      A1: x = <* a1, g1 *>
      by th5;
      consider a2 being Element of A, g2 being Element of G such that
      A2: y = <* a2, g2 *>
      by th5;
      consider a3 being Element of A, g3 being Element of G such that
      A3: z = <* a3, g3 *>
      by th5;
      
      x * y = <* a1 * a2, g1 * ((phi /. a1)/.g2) *> by A1,A2,th6;
      then A5: (x * y) * z = <* (a1 * a2) * a3, (g1 * ((phi /.a1)/.g2)) *
      ((phi /. (a1 * a2)) /. g3)*> by A3,th6;
      
      y * z = <* a2 * a3, g2 * ((phi /. a2)/.g3) *> by A2,A3,th6;
      then A6: x * (y * z) = <* a1 * (a2 * a3),
      g1 * ((phi /. a1)/.(g2 * ((phi /. a2)/.g3))) *>
      by A1,th6;
      
      A7: (a1 * a2) * a3 = a1 * (a2 * a3) by GROUP_1:def 3;
      
      ((phi /. a1) . ((phi /. a2).g3)) = (phi /. (a1 * a2)) . g3 by th9;
      then 
      ((phi /. a1).(g2 * ((phi /. a2)/.g3)))
      = ((phi /. a1).g2) * ((phi /. (a1 * a2)) . g3) by GROUP_6:def 6;
      then (((phi /.a1)/.g2) * ((phi /. (a1 * a2)) /. g3))
      = ((phi /. a1)/.(g2 * ((phi /. a2)/.g3)));
      then 
      (g1 * ((phi /.a1)/.g2)) * ((phi /. (a1 * a2)) /. g3)
      = g1 * ((phi /. a1)/.(g2 * ((phi /. a2)/.g3)))
      by GROUP_1:def 3;
      hence thesis by A5,A6,A7;
    end;
  end;
  
  cluster semidirect_product (A, G, phi) -> Group-like;
  correctness
  proof
    set S=semidirect_product (A, G, phi);
    for h being Element of S ex g being Element of S st h * g = 1_S
    & g * h = 1_S
    proof
      let h be Element of S;
      consider a being Element of A, g being Element of G such that
      B1: h = <* a, g *> by th5;
      reconsider y = <* a", (phi/.(a"))/.(g") *> as Element of S by th3;
      take y;
      thus h * y = 1_S by B1,th11;
      thus y * h = 1_S by B1,th11;
    end;
    hence thesis by thUnitalMagmaIsGroupCond;
  end;
end;

LmDoubleCheckSemidirectProductIsGroup:
  semidirect_product (A, G, phi) is Group;
@

\begin{theorem}
For any $x\in A\ltimes G$ which we write as $x=\langle a,g\rangle$, we
have $x^{-1}=\langle a^{-1}, \varphi_{a^{-1}}(g^{-1})\rangle$.
\end{theorem}

<<Theorem: $\langle a,g\rangle^{-1} = \langle a^{-1}, \varphi_{a^{-1}}(g^{-1})\rangle$>>=
theorem th15:
  for a being Element of A
  for g being Element of G
  for x being Element of semidirect_product (A, G, phi)
  st x = <* a, g *>
  holds x" = <* a", (phi/.(a"))/.(g") *>
proof
  set S = semidirect_product (A, G, phi);
  let a be Element of A;
  let g be Element of G;
  let x be Element of semidirect_product (A, G, phi);
  assume A1: x = <* a, g *>;
  reconsider y = <* a", (phi/.(a"))/.(g") *> as Element of S by th3;
  x * y = 1_S & y * x = 1_S by A1,th11;
  then y = x" by GROUP_1:5;
  hence thesis;
end;
@ %def th15


\subsection{Inclusion Morphisms}
We can inject $A\into A\ltimes G$ and $G\into A\ltimes G$ and the images
are isomorphic to $A$ and $G$, respectively.

<<Subsection: Inclusion morphisms>>=
<<Theorem: $(1,g_{1})(1,g_{2})=(1,g_{1}g_{2})$>>

<<Theorem: $(a_{1},1)(a_{2},1)=(a_{1}a_{2},1)$>>

<<Theorem: $N$ is normal iff $n^{a}\in N$>>

<<Definition: [[incl1]] includes $A$ into $A\ltimes G$>>

<<Register: [[incl1]] is a morphism and injective>>

<<Definition: [[incl2]] includes $G$ into $A\ltimes G$>>

<<Register: [[incl2]] is a morphism and injective>>

<<Theorem: Image of $j_{2}$ is a normal subgroup of $A\ltimes G$>>

<<Register: $\Im(j_{2})\normalSubgroup A\ltimes G$>>

<<Theorem: $\Im(j_{1})\cap\Im(j_{2})=\trivialSubgroup$>>

<<Theorem: every $x\in A\ltimes G$ is the product of elements $a\in\Im(j_{1})$ and $g\in\Im(j_{2})$ as $x=ga$>>

<<Theorem: $U(A\ltimes G)=\Im(j_{2})\Im(j_{1})$>>

<<Theorem: $\Im(j_{2})\vee\Im(j_{1})=A\ltimes G$>>

<<Theorem: $G\iso\Im(j_{2})$>>

<<Theorem: Let $g\in G$ and $a\in A$, then $(1,g)^{(a,1)}=(1,\varphi_{a^{-1}}(g))$>>
@ 

\begin{theorem}
We have $(1,g_{1})(1,g_{2})=(1,g_{1}g_{2})$.
\end{theorem}

<<Theorem: $(1,g_{1})(1,g_{2})=(1,g_{1}g_{2})$>>=
theorem th16:
  for g1,g2 being Element of G
  for x,y,z being Element of semidirect_product (A, G, phi)
  st x = <*1_A, g1*> & y = <*1_A, g2*> & z = <*1_A, (g1*g2)*>
  holds x * y = z
proof
  let g1,g2 be Element of G;
  let x,y,z be Element of semidirect_product (A, G, phi);
  assume A1: x = <*1_A, g1*>;
  assume A2: y = <*1_A, g2*>;
  assume A3: z = <*1_A, (g1*g2)*>;
  thus x * y = <* (1_A) * (1_A), g1 * ((phi /. 1_A) /. g2) *> by A1,A2,th6
            .= <* 1_A, g1 * ((phi /. 1_A) /. g2) *> by GROUP_1:def 4
            .= z by A3, th7;
end;
@ %def th16

\begin{theorem}
We have $(a_{1},1)(a_{2},1)=(a_{1}a_{2},1)$.
\end{theorem}

<<Theorem: $(a_{1},1)(a_{2},1)=(a_{1}a_{2},1)$>>=
theorem th17:
  for a1,a2 being Element of A
  for x,y,z being Element of semidirect_product (A, G, phi)
  st x = <*a1, 1_G*> & y = <*a2, 1_G*> & z = <*(a1*a2),1_G*>
  holds x * y = z
proof
  let a1,a2 be Element of A;
  let x,y,z be Element of semidirect_product (A, G, phi);
  assume A1: x = <*a1, 1_G*>;
  assume A2: y = <*a2, 1_G*>;
  assume A3: z = <*(a1*a2),1_G*>;
  thus x * y = <* a1 * a2, 1_G * ((phi /. a1)/.(1_G)) *> by A1,A2,th6
            .= <* a1 * a2, 1_G * (1_G) *> by GROUP_6:31
            .= z by A3,GROUP_1:def 4;
end;
@ %def th17

\begin{theorem}
Let $N\subgroup A$. Then $N$ is normal if and only if for each element
$a$, $h$ of $A$ such that $h\in N$, we have $h^{a}\in N$.
\end{theorem}

<<Theorem: $N$ is normal iff $n^{a}\in N$>>=
:: Compare to AUTGROUP:1, we do not need strictness
theorem th23:
  for H being Subgroup of A
  holds H is normal
  iff (for a,h being Element of A st h in H holds h |^ a in H)
proof
  let H be Subgroup of A;
  thus H is normal implies (for a,h being Element of A st h in H
  holds h |^ a in H) by GROUP_23:72;

  thus (for a,h being Element of A st h in H holds h |^ a in H) implies
  H is normal
  proof
    assume B1: for a,h being Element of A st h in H holds h |^ a in H;
    now
      let a be Element of A;
      thus H * a c= a * H
      proof
        set U = carr H;
        let q be object;
        assume q in H * a;
        then consider h being Element of A such that
        B2: q = h * a and
        B3: h in H by GROUP_2:104;
        h |^ a in H by B1, B3;
        then h |^ a in carr H by GROUP_2:def 9;
        then a * (h |^ a) in a * (carr H) by GROUP_2:27;
        then a * (((a") * h) * a) in a * (carr H) by GROUP_3:def 2;
        then a * ((a") * (h * a)) in a * (carr H) by GROUP_1:def 3;
        then (a * (a")) * (h * a) in a * (carr H) by GROUP_1:def 3;
        then ((a * (a")) * h) * a in a * (carr H) by GROUP_1:def 3;
        then ((1_A) * h) * a in a*(carr H) by GROUP_1:def 5;
        then h * a in a*(carr H) by GROUP_1:def 4;
        hence q in a * H by B2, GROUP_2:def 13;
      end;
    end;
    hence H is normal by GROUP_3:119;
  end;
end;
@ %def th23

\begin{definition}
We define the function $j_{1}\colon A\to A\ltimes G$ by $j_{1}(a)=(a,1)$.
\end{definition}

<<Definition: [[incl1]] includes $A$ into $A\ltimes G$>>=
definition
  let A, G, phi;
  func incl1 (A, G, phi) -> Function of A, semidirect_product (A,G,phi) means
  :Def10:
  for a being Element of A holds it.a = <* a, 1_G *>;

  existence
  proof
    defpred P[Element of A, Element of semidirect_product(A,G,phi)] means
    $2 = <* $1, 1_G *>;

    A1: for x being Element of A
    ex y being Element of semidirect_product(A,G,phi)
    st P[x, y]
    proof
      let x be Element of A;
      reconsider y=<*x,1_G*> as Element of semidirect_product(A,G,phi)
      by th3;
      take y;
      thus P[x, y];
    end;
    consider f being Function of A, semidirect_product(A, G, phi) such that
    A2: for x being Element of A holds P[x, f.x]
    from FUNCT_2:sch 3(A1);
    take f;
    thus thesis by A2;
  end;
  uniqueness
  proof
    for f1,f2 being Function of A, semidirect_product(A, G, phi)
    st (for a being Element of A holds f1.a = <*a,1_G*>)
     & (for a being Element of A holds f2.a = <*a,1_G*>)
    holds f1 = f2
    proof
      let f1,f2 be Function of A, semidirect_product(A, G, phi);
      assume A1: for a being Element of A holds f1.a = <*a,1_G*>;
      assume A2: for a being Element of A holds f2.a = <*a,1_G*>;
      for a being Element of A holds f1.a = f2.a
      proof
        let a be Element of A;
        thus f1.a = <*a,1_G*> by A1
                 .= f2.a by A2;
      end;

      hence f1 = f2 by FUNCT_2:def 8;
    end;
    hence thesis;
  end;
end;
@ %def incl1 Def10

\begin{voc}
We add [[incl1]] to our vocabulary.
\end{voc}

<<DICT/group-24.voc>>=
Oincl1
@

\begin{registration}
The inclusion function is a morphism and injective.
\end{registration}

<<Register: [[incl1]] is a morphism and injective>>=
:: Aschbacher, Finite Group Theory (10.1.2)
registration
  let A, G, phi;
  cluster incl1 (A, G, phi) -> multiplicative;
  coherence
  proof
    for a1,a2 being Element of A
    holds (incl1 (A, G, phi)).(a1 * a2)
    = ((incl1 (A, G, phi)).a1)*((incl1 (A, G, phi)).a2)
    proof
      let a1,a2 be Element of A;
      ((incl1 (A, G, phi)).a1) = <*a1,1_G*>
      & ((incl1 (A, G, phi)).a2) = <*a2,1_G*>
      & (incl1 (A, G, phi)).(a1 * a2) = <*a1*a2, 1_G*> by Def10;
      hence thesis by th17;
    end;
    hence thesis by GROUP_6:def 6;
  end;

  cluster incl1 (A, G, phi) -> one-to-one;
  coherence
  proof
    for a1,a2 being Element of A
    st (incl1 (A, G, phi)).a1 = (incl1 (A, G, phi)).a2
    holds a1 = a2
    proof
      let a1,a2 be Element of A;
      assume A1: (incl1 (A, G, phi)).a1 = (incl1 (A, G, phi)).a2;
      <*a1, 1_G*> = (incl1 (A, G, phi)).a1 by Def10
                 .= <*a2, 1_G*> by A1,Def10;
      hence a1 = a2 by Lm1;
    end;
    hence incl1 (A, G, phi) is one-to-one by GROUP_6:1;
  end;
end;
@

\begin{definition}
We define $j_{2}\colon G\to A\ltimes G$.
\end{definition}

<<Definition: [[incl2]] includes $G$ into $A\ltimes G$>>=
definition
  let A, G, phi;
  func incl2 (A, G, phi) -> Function of G, semidirect_product (A,G,phi) means
  :Def11:
  for g being Element of G holds it.g = <* 1_A, g *>;
  existence
  proof
    defpred P[Element of G, Element of semidirect_product(A,G,phi)] means
    $2 = <* 1_A, $1 *>;

    A1: for x being Element of G
    ex y being Element of semidirect_product(A,G,phi)
    st P[x, y]
    proof
      let x be Element of G;
      reconsider y=<*1_A,x*> as Element of semidirect_product(A,G,phi)
      by th3;
      take y;
      thus P[x, y];
    end;
    consider f being Function of G, semidirect_product(A, G, phi) such that
    A2: for x being Element of G holds P[x, f.x]
    from FUNCT_2:sch 3(A1);
    take f;
    thus thesis by A2;
  end;
  uniqueness
  proof
    for f1,f2 being Function of G, semidirect_product(A, G, phi)
    st (for g being Element of G holds f1.g = <*1_A,g*>)
     & (for g being Element of G holds f2.g = <*1_A,g*>)
    holds f1 = f2
    proof
      let f1,f2 be Function of G, semidirect_product(A, G, phi);
      assume A1: for g being Element of G holds f1.g = <*1_A,g*>;
      assume A2: for g being Element of G holds f2.g = <*1_A,g*>;
      for g being Element of G holds f1.g = f2.g
      proof
        let g be Element of G;
        thus f1.g = <*1_A,g*> by A1
                 .= f2.g by A2;
      end;

      hence f1 = f2 by FUNCT_2:def 8;
    end;
    hence thesis;
  end;
end;
@ %def incl2 Def11

\begin{registration}
We observe $j_{2}$ is a morphism and injective.
\end{registration}

<<Register: [[incl2]] is a morphism and injective>>=
:: Aschbacher, Finite Group Theory (10.1.2)
registration
  let A, G, phi;
  cluster incl2 (A, G, phi) -> multiplicative;
  coherence
  proof
    for g1,g2 being Element of G
    holds (incl2 (A, G, phi)).(g1 * g2)
    = ((incl2 (A, G, phi)).g1)*((incl2 (A, G, phi)).g2)
    proof
      let g1,g2 be Element of G;
      ((incl2 (A, G, phi)).g1) = <*1_A,g1*>
      & ((incl2 (A, G, phi)).g2) = <*1_A,g2*>
      & (incl2 (A, G, phi)).(g1 * g2) = <*1_A,g1*g2*> by Def11;
      hence thesis by th16;
    end;
    hence thesis by GROUP_6:def 6;
  end;
  cluster incl2 (A, G, phi) -> one-to-one;
  coherence
  proof
    for g1,g2 being Element of G
    st (incl2 (A, G, phi)).g1 = (incl2 (A, G, phi)).g2
    holds g1 = g2
    proof
      let g1,g2 be Element of G;
      assume A1: (incl2 (A, G, phi)).g1 = (incl2 (A, G, phi)).g2;
      <*1_A, g1*> = (incl2 (A, G, phi)).g1 by Def11
                 .= <*1_A, g2*> by A1,Def11;
      hence g1 = g2 by Lm1;
    end;
    hence incl2 (A, G, phi) is one-to-one by GROUP_6:1;
  end;
end;
@ 

\begin{theorem}
The image of $j_{2}$ is a normal subgroup of $A\ltimes G$.
\end{theorem}

<<Theorem: Image of $j_{2}$ is a normal subgroup of $A\ltimes G$>>=
:: Aschbacher, Finite Group Theory (10.1.3)
theorem Th28:
  Image (incl2 (A, G, phi)) is normal Subgroup of
  semidirect_product (A, G, phi)
proof
  for x,g being Element of semidirect_product (A, G, phi)
  st g in Image (incl2 (A, G, phi))
  holds g |^ x in Image (incl2 (A, G, phi))
  proof
    let x,g be Element of semidirect_product (A, G, phi);
    assume g in Image (incl2 (A, G, phi));
    then consider g0 being Element of G such that
    A1: g = (incl2 (A, G, phi)).g0 by GROUP_6:45;
    A2: g = <*1_A,g0*> by A1,Def11;
    consider x1 being Element of A, x2 being Element of G such that
    A3: x = <* x1, x2 *>
    by th5;
    (x") = <* x1", (phi/.(x1"))/.(x2") *> by A3, th15;
    then (x") * g = <* (x1") * (1_A),
                       ((phi/.(x1"))/.(x2"))*((phi/.(x1"))/.g0) *> by A2,th6
    .= <* (x1"), ((phi/.(x1"))/.(x2"))*((phi/.(x1"))/.g0) *> by GROUP_1:def 4
    .= <* (x1"), (phi/.(x1"))/.((x2") * g0) *> by GROUP_6:def 6;
    then A4: ((x") * g) * x
    = <* (x1") * x1, ((phi/.(x1"))/.((x2")*g0)*((phi/.(x1"))/.x2)) *> by A3,th6
    .= <* 1_A, ((phi/.(x1"))/.((x2")*g0)*((phi/.(x1"))/.x2)) *>
       by GROUP_1:def 5
    .= <* 1_A, ((phi/.(x1"))/.(((x2") * g0) * x2)) *> by GROUP_6:def 6
    .= <* 1_A, (phi /. (x1"))/.(g0 |^ x2) *> by GROUP_3:def 2;
    g |^ x = ((x") * g) * x by GROUP_3:def 2
    .= ((incl2 (A, G, phi)).((phi /. (x1"))/.(g0 |^ x2))) by A4,Def11;
    hence g |^ x in Image (incl2 (A, G, phi)) by GROUP_6:45;
  end;
  hence thesis by th23;
end;
@ %def Th28

\begin{registration}
We register the fact $\Im(j_{2})\normalSubgroup A\ltimes G$.
\end{registration}

<<Register: $\Im(j_{2})\normalSubgroup A\ltimes G$>>=
registration
  let A, G, phi;
  cluster Image (incl2 (A, G, phi)) -> normal;
  correctness by Th28;
end;
@

\begin{theorem}
$\Im(j_{1})\cap\Im(j_{2})=\trivialSubgroup$.
\end{theorem}

<<Theorem: $\Im(j_{1})\cap\Im(j_{2})=\trivialSubgroup$>>=
theorem Th29:
  Image (incl1 (A, G, phi)) /\ Image (incl2 (A, G, phi))
  = (1).(semidirect_product(A,G,phi))
proof
  set ImA = Image (incl1 (A, G, phi)), ImG = Image (incl2 (A, G, phi));
  set S = semidirect_product(A,G,phi);
  for x being object st x in the carrier of (ImA /\ ImG)
  holds x in {1_S}
  proof
    let x be object;
    assume x in the carrier of (ImA /\ ImG);
    then x in ImA /\ ImG;
    then B1: x in ImA & x in ImG by GROUP_2:82;
    then consider a being Element of A such that
    B2: x = (incl1 (A, G, phi)).a
    by GROUP_6:45;
    consider g being Element of G such that
    B3: x = (incl2 (A, G, phi)).g
    by B1, GROUP_6:45;
    <*a, 1_G*> = x by B2,Def10
              .= <*1_A,g*> by B3,Def11;
    then a = 1_A & 1_G = g by Lm1;
    then x = <*1_A,1_G*> by B2,Def10
          .= 1_S by th14;
    hence x in {1_S} by TARSKI:def 1;
  end;
  then the carrier of (ImA /\ ImG) c= {1_S};
  then the carrier of (ImA /\ ImG) c= the carrier of (1).S by GROUP_2:def 7;
  then A1: ImA /\ ImG is Subgroup of (1).S by GROUP_2:57;
  (1).S is Subgroup of ImA /\ ImG by GROUP_2:65;
  hence thesis by A1, GROUP_2:55;
end;
@ %def Th29

\begin{theorem}
Every element of $A\ltimes G$ is the product of an element of
$\Im(j_{1})$ with an element of $\Im(j_{2})$.
\end{theorem}

<<Theorem: every $x\in A\ltimes G$ is the product of elements $a\in\Im(j_{1})$ and $g\in\Im(j_{2})$ as $x=ga$>>=
theorem Th30:
  for x being Element of semidirect_product(A,G,phi)
  ex a being Element of A, g being Element of G
  st ((incl2(A,G,phi)).g) * ((incl1(A,G,phi)).a) = x
proof
  let x be Element of semidirect_product(A, G, phi);
  consider a being Element of A, g being Element of G such that
  A1: x = <*a,g*>
  by th5;
  take a,g;
  ((incl2(A,G,phi)).g) = <*1_A,g*> & ((incl1(A,G,phi)).a) = <*a,1_G*>
  by Def10,Def11;
  hence ((incl2(A,G,phi)).g) * ((incl1(A,G,phi)).a)
  = <* (1_A) * (a), g * ((phi /. (1_A))/.(1_G)) *> by th6
  .= <* (1_A) * (a), g * (1_G) *> by th7
  .= <* a, g * (1_G) *> by GROUP_1:def 4
  .= x by A1, GROUP_1:def 4;
end;
@ %def Th30

\begin{theorem}
$\Im(j_{2})\Im(j_{1})=U(A\ltimes G)$.
\end{theorem}

<<Theorem: $U(A\ltimes G)=\Im(j_{2})\Im(j_{1})$>>=
theorem Th31:
  (Image incl2(A,G,phi)) * (Image incl1(A,G,phi))
  = the carrier of (semidirect_product(A,G,phi))
proof
  for x being Element of semidirect_product (A, G, phi)
  holds x in (Image incl2(A,G,phi)) * (Image incl1(A,G,phi))
  proof
    let x be Element of semidirect_product (A, G, phi);
    consider a being Element of A, g being Element of G such that
    B1: ((incl2(A,G,phi)).g) * ((incl1(A,G,phi)).a) = x
    by Th30;
    ((incl2(A,G,phi)).g) in Image(incl2(A,G,phi)) &
    ((incl1(A,G,phi)).a) in Image(incl1(A,G,phi)) by GROUP_6:45;
    hence x in (Image incl2(A,G,phi)) * (Image incl1(A,G,phi))
    by B1, GROUP_5:4;
  end;
  then A1: the carrier of (semidirect_product(A,G,phi))
  c= (Image incl2(A,G,phi)) * (Image incl1(A,G,phi));
  set S = semidirect_product(A,G,phi);
  thus thesis by A1, XBOOLE_0:def 10;
end;
@ %def Th31

\begin{theorem}
$\Im(j_{2})\wedge\Im(j_{1})=A\ltimes G$.
\end{theorem}

<<Theorem: $\Im(j_{2})\vee\Im(j_{1})=A\ltimes G$>>=
theorem Th32:
  Image (incl2 (A, G, phi)) "\/" Image (incl1 (A, G, phi))
  = (semidirect_product(A,G,phi))
proof
  set S = semidirect_product(A,G,phi);
  set U = (Image incl2(A,G,phi)) * (Image incl1(A,G,phi));
  U c= the carrier of gr U by GROUP_4:def 4; then
  the carrier of S c= the carrier of (gr U) by Th31;
  then S = gr U by GROUP_2:61;
  hence thesis by GROUP_4:50;
end;
@ %def Th32

\begin{theorem}
We see $G\iso\Im(j_{2})$.
\end{theorem}

<<Theorem: $G\iso\Im(j_{2})$>>=
:: Aschbacher, Finite Group Theory (10.1.3)
theorem
  G, Image (incl2 (A, G, phi)) are_isomorphic by GROUP_6:68;
@ 

\begin{theorem}
Let $g\in G$ and $a\in A$. Then $(1,g)^{(a,1)}=(1,\varphi_{a^{-1}}(g))$.
\end{theorem}

<<Theorem: Let $g\in G$ and $a\in A$, then $(1,g)^{(a,1)}=(1,\varphi_{a^{-1}}(g))$>>=
:: Aschbacher, Finite Group Theory (10.1.4) --- note there is a sign
:: error in Aschbacher's book.
theorem Th27:
  for a being Element of A
  for g being Element of G
  holds ((incl2(A,G,phi)).g) |^ ((incl1(A,G,phi)).a)
  = <*1_A, (phi /. (a"))/.g *>
proof
  let a be Element of A;
  let g be Element of G;
  set xg = (incl2(A,G,phi)).g;
  set xa = (incl1(A,G,phi)).a;
  A1: xa = <* a, 1_G *> by Def10;
  then A2: xa" = <* a", ((phi/.(a"))/.((1_G)")) *> by th15
              .= <* a", ((phi/.(a"))/.(1_G)) *> by GROUP_1:8
              .= <* a", 1_G *> by GROUP_6:31;
  xg = <*1_A, g*> by Def11; then
  (xa") * xg = <* (a") * (1_A), (1_G) * ((phi /. (a"))/.g) *> by A2,th6
            .= <* (a"), (1_G) * ((phi /. (a"))/.g) *> by GROUP_1:def 4
            .= <* (a"), ((phi /. (a"))/.g) *> by GROUP_1:def 4;
  then ((xa")*xg)*xa = <* (a")*a, ((phi/.(a"))/.g)*((phi/.(a"))/.(1_G)) *>
  by A1, th6
                    .= <* (a") * a, ((phi /. (a"))/.g) * (1_G) *> by GROUP_6:31
                    .= <* (a") * a, ((phi /. (a"))/.g) *> by GROUP_1:def 4
                    .= <* 1_A, ((phi /. (a"))/.g) *> by GROUP_1:def 5;
  hence thesis by GROUP_3:def 2;
end;
@ %def Th27

\begin{theorem}
When $\varphi_{a}(g)=g$ for all $a\in A$ and $g\in G$, we have
the semidirect product coincide with the direct product $A\ltimes_{\varphi}G=A\times G$.
\end{theorem}

\begin{proof}[Proof sketch]
Since the two groups share the same underlying set, we just need to
prove the binary operators coincide.
\end{proof}

<<Theorem: $\varphi$ is trivial implies $A\ltimes_{\varphi}G=A\times G$>>=
theorem
  semidirect_product (A, G, 1:(A, AutGroup G)) = product <*A, G*>
proof
  set S = semidirect_product (A, G, 1:(A, AutGroup G));
  A1: the carrier of S = the carrier of product <*A,G*> by th2;
  set B1 = the multF of S;
  set B2 = the multF of product <*A, G*>;
  set U = product (Carrier <*A,G*>);
  the carrier of S = product (Carrier<*A,G*>) &
  B1 is BinOp of the carrier of S by Def1;
  then A2: B1 is BinOp of U;
  the carrier of product <*A,G*> = product (Carrier <*A,G*>) &
  the multF of product <*A,G*> is BinOp of the carrier of product <*A,G*>
  by GROUP_7:def 2; then
  A3: B2 is BinOp of U;

  for x,y being Element of product (Carrier <*A,G*>)
  holds B1.(x,y) = B2.(x,y)
  proof
    let x,y be Element of product (Carrier <*A,G*>);
    x is Element of S by Def1;
    then consider a1 being Element of A, g1 being Element of G such that
    A4: x = <*a1,g1*> by th5;
    y is Element of S by Def1;
    then consider a2 being Element of A, g2 being Element of G such that
    A5: y = <*a2,g2*> by th5;
    1:(A, AutGroup G) = A --> (1_(AutGroup G)) by GROUP_6:def 7; then
    (1:(A, AutGroup G)).a1 = id the carrier of G by AUTGROUP:9; then
    A6: (((1:(A, AutGroup G)) /. a1) /. g2) = g2;
    reconsider x0=x,y0=y as Element of S by Def1;
    A8: x0 * y0 = <* a1 * a2, g1 * g2 *> by A4, A5, A6, th6;

    B2.(x,y) = <*a1,g1*> * <*a2,g2*> by A4,A5
            .= <* a1 * a2, g1 * g2 *> by GROUP_7:29;
    hence B1.(x,y) = B2.(x,y) by A8;
  end;
  hence thesis by A1,A2,A3,BINOP_1:2;
end;
@