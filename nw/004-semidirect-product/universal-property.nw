% -*- mode: poly-noweb; noweb-code-mode: mizar-mode; -*-
\section{Universal Property}

\begin{theorem}
Let $S=A\ltimes_{\varphi}G$. For any group $H$ and group morphisms
$f_{1}\colon A\to H$ and $f_{2}\colon G\to H$ such that
$f_{2}(\varphi_{a}(g))=f_{1}(a^{-1})f_{2}(g)f_{1}(a)$ for $a\in A$ and
$g\in G$,
then there exists a group morphism $f\colon S\to H$ such that
\begin{enumerate}
\item $f_{1} =f\circ j_{1}$
\item $f_{2} = f \circ j_{2}$
\item $f$ is injective iff $f_{1}$ is injective and $f_{2}$ is injective
  and $A\cap H=\trivialSubgroup$
\item $f$ is surjective iff $f_{1}(A)f_{2}(G)=H$.
\end{enumerate}
\end{theorem}

\begin{thm-remark}
This universal property may be found in Bourbaki's \emph{General Topology},
chapter III, \S2.10.
\end{thm-remark}

<<Universal Property of Semidirect Product Groups>>=
<<Lemma: $(g_{1}\cdot (g_{2} \cdot  g_{3} \cdot  g_{4}))\cdot g_{5} = g_{1}\cdot g_{2}\cdot g_{3}\cdot g_{4}\cdot g_{5}$>>

:: Bourbaki, General Topology, Chapter 3, section 2, p.240, Proposition 27
::$N Universal Property of Semidirect Products (Bourbaki~\cite[III \S2.10]{bourbaki2013general})
theorem
  for H,G being Group
  for N being strict Group
  for f being Homomorphism of N,G
  for g being Homomorphism of H,G
  for phi being Homomorphism of H,AutGroup N
  st (for n being Element of N
      for h being Element of H
      holds f.((phi/.h).n) = (g.h) * (f.n) * (g.(h")))
  ex k being Homomorphism of semidirect_product(N,H,phi), G
  st f = k * (incl1 (N, H, phi)) & g = k * (incl2 (N, H, phi))
proof
  let H,G be Group;
  let N be strict Group;
  let f be Homomorphism of N,G;
  let g be Homomorphism of H,G;
  let phi be Homomorphism of H,AutGroup N;
  assume A1: for n being Element of N
  for h being Element of H
  holds f.((phi/.h).n) = (g.h) * (f.n) * (g.(h"));
  set S = semidirect_product(N,H,phi);
  <<Proof step: construct $k(n,h)=f(n)g(h)$>>

  take k;

  A4: for n being Element of N
  for h being Element of H
  holds k.(<*n,h*>)=(f.n)*(g.h)
  proof
    let n be Element of N;
    let h be Element of H;
    reconsider x=<*n,h*> as Element of S by th3;
    P[x,k.x] by A3;
    hence k.(<*n,h*>)=(f.n)*(g.h) by Lm1;
  end;

  for n being Element of N
  holds f.n = (k * (incl1 (N, H, phi))).n
  proof
    let n be Element of N;
    thus (k * (incl1 (N, H, phi))).n = k.((incl1 (N, H, phi)).n) by FUNCT_2:15
    .= k.(<*n, 1_H*>) by Def10
    .= (f.n)*(g.(1_H)) by A4
    .= (f.n)*(1_G) by GROUP_6:31
    .= f.n by GROUP_1:def 4;
  end;
  hence f = k * (incl1 (N, H, phi)) by FUNCT_2:63;

  for h being Element of H
  holds g.h = (k * (incl2 (N, H, phi))).h
  proof
    let h be Element of H;
    thus (k * (incl2 (N, H, phi))).h = k.((incl2 (N, H, phi)).h) by FUNCT_2:15
    .= k.(<*1_N,h*>) by Def11
    .= (f.(1_N))*(g.h) by A4
    .= (1_G)*(g.h) by GROUP_6:31
    .= g.h by GROUP_1:def 4;
  end;
  hence g = k * (incl2 (N, H, phi)) by FUNCT_2:63;
end;
@

\begin{proof}
Constructing $k$ is straightforward but tedious.
We first construct it as a function, then demonstrate it is a group
morphism. 
\end{proof}

<<Proof step: construct $k(n,h)=f(n)g(h)$>>=
defpred P[Element of S, Element of G] means
for n being Element of N,h being Element of H st $1 = <*n,h*> holds $2 = (f.n)*(g.h);

A2: for x being Element of S
ex y being Element of G
st P[x,y]
proof
  let x be Element of S;
  consider n being Element of N, h being Element of H such that
  B1: x = <*n,h*> by th5;
  take y = (f.n) * (g.h);
  for n1 being Element of N,h1 being Element of H st x = <*n1,h1*>
  holds y = (f.n1)*(g.h1)
  proof
    let n1 be Element of N;
    let h1 be Element of H;
    assume x = <*n1,h1*>;
    then n=n1 & h=h1 by B1,Lm1;
    hence y = (f.n1)*(g.h1);
  end;
  hence P[x,y] by B1;
end;

consider k being Function of S, G such that
A3: for x being Element of S holds P[x,k.x]
from FUNCT_2:sch 3(A2);

for x1,x2 being Element of S
holds k.(x1 * x2) = (k.x1) * (k.x2)
proof
  let x1,x2 be Element of S;
  consider n1 being Element of N, h1 being Element of H such that
  B1: x1 = <*n1,h1*> by th5;
  consider n2 being Element of N, h2 being Element of H such that
  B2: x2 = <*n2,h2*> by th5;
  P[x1, k.x1] by A3;
  then B3: k.x1 = (f.n1)*(g.h1) by B1;
  P[x2, k.x2] by A3;
  then B4: k.x2 = (f.n2)*(g.h2) by B2;
  B5: x1 * x2 = <* n1 * ((phi /. h1)/.n2), h1 * h2*> by B1,B2,th6;
  P[x1 * x2, k.(x1 * x2)] by A3;
  hence k.(x1 * x2) = (f.(n1 * ((phi/.h1)/.n2))) * (g.(h1 * h2)) by B5
  .= ((f.n1)*(f.((phi/.h1)/.n2))) * (g.(h1 * h2)) by GROUP_6:def 6
  .= (f.n1)*(f.((phi/.h1)/.n2))*(g.(h1 * h2)) by GROUP_1:def 3
  .= (f.n1)*((g.h1) * (f.n2) * (g.(h1")))*(g.(h1 * h2)) by A1
  .= (f.n1)*(g.h1)*(f.n2)*(g.(h1"))*(g.(h1 * h2)) by LmAssoc
  .= ((f.n1)*(g.h1)*(f.n2)*(g.(h1")))*(g.(h1 * h2)) by GROUP_1:def 3
  .= ((f.n1)*(g.h1)*(f.n2)*(g.(h1")))*((g.h1)*(g.h2)) by GROUP_6:def 6
  .= ((f.n1)*(g.h1)*(f.n2)*(g.(h1")))*(g.h1)*(g.h2) by GROUP_1:def 3
  .= (f.n1)*(g.h1)*(f.n2)*(g.(h1"))*(g.h1)*(g.h2) by GROUP_1:def 3
  .= (f.n1)*(g.h1)*(f.n2)*((g.(h1"))*(g.h1))*(g.h2) by GROUP_1:def 3
  .= (f.n1)*(g.h1)*(f.n2)*(g.((h1")*h1))*(g.h2) by GROUP_6:def 6
  .= (f.n1)*(g.h1)*(f.n2)*(g.(1_H))*(g.h2) by GROUP_1:def 5
  .= (f.n1)*(g.h1)*(f.n2)*(1_G)*(g.h2) by GROUP_6:31
  .= (f.n1)*(g.h1)*(f.n2)*(g.h2) by GROUP_1:def 4
  .= ((f.n1)*(g.h1))*(f.n2)*(g.h2) by GROUP_1:def 3
  .= (k.x1)*(f.n2)*(g.h2) by B3,A3
  .= (k.x1)*((f.n2)*(g.h2)) by GROUP_1:def 3
  .= (k.x1)*(k.x2) by B4, A3;
end;
then reconsider k as Homomorphism of S,G by GROUP_6:def 6;
@



\begin{lemma}
Let $g_{1}$, \dots, $g_{5}\in G$.
Then $(g_{1}\cdot (g_{2} \cdot  g_{3} \cdot  g_{4}))\cdot g_{5} = g_{1}\cdot g_{2}\cdot g_{3}\cdot g_{4}\cdot g_{5}$.
\end{lemma}

<<Lemma: $(g_{1}\cdot (g_{2} \cdot  g_{3} \cdot  g_{4}))\cdot g_{5} = g_{1}\cdot g_{2}\cdot g_{3}\cdot g_{4}\cdot g_{5}$>>=
LmAssoc:
  for G being Group
  for g1,g2,g3,g4,g5 being Element of G
  holds (g1*(g2 * g3 * g4))*g5 = g1*g2*g3*g4*g5
proof
  let G be Group;
  let g1,g2,g3,g4,g5 be Element of G;
  thus (g1*(g2 * g3 * g4))*g5
  = (g1*(g2*(g3*g4)))*g5 by GROUP_1:def 3
  .= ((g1*g2)*(g3*g4))*g5 by GROUP_1:def 3
  .= (g1*g2*g3*g4)*g5 by GROUP_1:def 3
  .= g1*g2*g3*g4*g5 by GROUP_1:def 3;
end;
@ %def LmAssoc