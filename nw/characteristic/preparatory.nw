% -*- mode: poly-noweb; noweb-code-mode: mizar-mode; -*-
% This is placed here so the LaTeX is organized correctly, thanks to
% problems with noweb :(
\section{Article Body}\label{sec:characteristic:preparatory:start}

The article body is where the magic happens. Now we can start making
definitions, stating theorems, proving results. The basic structure of
our article can be cleaved in two: first we state and prove ``helper
lemmas'', which probably belong somewhere else, but currently are not
located anywhere in the Mizar library. The second half are our results
concerning characteristic subgroups.

Just to give some idea of what we're doing, we will have to define a
notion of [[Automorphism]]. We will also have to prove a number of
results concerning [[Automorphism]]s. After all, a characteristic
subgroup is one which is left invariant under any automorphism of its
parent group.

Once that has been squared away, we will define a notion of a
[[characteristic]] subgroup. Then we will prove results right away.

<<[[char.miz]] article body>>=
<<Helper lemmas and registrations for [[char.miz]]>>

<<Inner and outer automorphisms>>
<<Characteristic subgroup results>>
@

\section{Preparatory results}
There are a lot of recurring patterns which can be isolated into helper
functions---err, lemmas. I'm sure many (if not all) are already present
somewhere in the Mizar Mathematical Library, but I couldn't find them. I
am placing them within their own ``section'', because if I ever submit
the result to the Mizar Mathematical Library, they will either be
removed (and relocated to the relevant articles) or the editors will
know what I should have done instead.

<<Helper lemmas and registrations for [[char.miz]]>>=
begin :: Preparatory Work
reserve X for set;

<<Register: the identity function is surjective and bijective>>
<<Theorem: $g\in H\subgroup G$ and $K\subgroup G$, then $H\subgroup K\implies g\in K$>>
<<Theorem: restriction of group morphism acts on elements like the original>>
<<Theorem: Subgroups invariant under conjugation are normal>>
<<Theorem: if $f$ is bijective, then $(f^{-1})^{-1}=f$>>
<<Theorem: if $f\colon X\to Y$ is bijective, then $f\circ f^{-1}=\id_{Y}$>>
<<Theorem: $f\colon X\into Y$ and $x\notin A\subset X$ implies $f(x)\notin f(A)$>>

begin :: Nontrivial Groups and Subgroups
<<Register: non trivial for Group>>
<<Register: trivial groups and trivial subgroups>>
<<Register: non trivial for Subgroup>>
<<Theorem: trivial groups look like $\trivialSubgroup$>>
<<Theorem: $H\subgroup G$, $H$ is trivial implies $H=\trivialSubgroup_{G}$>>
<<Theorem: for $H\subgroup G$ and $K\subgroup G$ both trivial, $H=K$>>
<<Theorem: $H\subgroup K$ and $K\subgroup G$, then $K=\trivialSubgroup\implies H=\trivialSubgroup$>>

begin :: Proper Subgroups
<<Definition: proper subgroup>>
<<Theorem: $H\subgroup G$ is proper iff the underlying sets are different>>
<<Theorem: $H\subgroup G$ is proper iff $G\setminus H\neq\emptyset$>>
<<Register: proper subgroup for nontrivial groups>>
<<Register: proper subgroups of nontrivial subgroups>>
<<Lemma: maximal subgroups are proper>>
<<Register: maximal subgroups are proper>>
<<Theorem: $H\properSubgroup K\subgroup G$ and $H\neq K$ implies $K$ is nontrivial>>
@


\begin{registration}
We begin by registering the identity function as being surjective and
bijective. This should have been done in \mml{funct2}, but hey, what can
you do?
\end{registration}

<<Register: the identity function is surjective and bijective>>=
registration
  let X;
  cluster id X -> onto;
  coherence;
end;

registration
  let X;
  cluster id X -> bijective;
  coherence;
end;
@

\begin{theorem}
  Let $G$ be a group and $g\in G$ be any element of the group,
  $H_{1}\subgroup G$ and $H_{2}\subgroup G$ be subgroups.
  If $H_{1}\subgroup H_{2}$ and $g\in H_{1}$, then $g\in H_{2}$.
\end{theorem}

This is another ``Well, duh'' lemma that I couldn't find in Mizar's library.

<<Theorem: $g\in H\subgroup G$ and $K\subgroup G$, then $H\subgroup K\implies g\in K$>>=
LmSubgroupEltTrans:
  for G being Group
  for H1,H2 being Subgroup of G
  for g being Element of G
  st g in H1 & H1 is Subgroup of H2
  holds g in H2 by GROUP_2:40;
@ %def LmSubgroupEltTrans

\begin{theorem}[{Restriction of Group Morphisms to Subgroups}]\label{thm:characteristic:preparatory:restrict-group-morphism-to-subgroups}
If $f\colon G_{1}\to G_{2}$ is a group morphism and $H\subgroup G_{1}$
is a subgroup, then for any $h\in H$ we have $f(h) = f|_{H}(h)$.
\end{theorem}

\begin{lem-remark}
Mizar proves that, if $f\colon X\to Y$ is a set theoretic function and
$A\subset X$ is an arbitrary subset, then $\forall a\in X$ we have
$a\in A\implies f|_{A}(a)=f(a)$. But this doesn't generalize to
morphisms, sadly, because a group is like a chocolate-covered set.

So we just prove for any pair of groups $G_{1}$ and $G_{2}$,
for any subgroup $H\subgroup G_{1}$,
for any group morphism $f\colon G_{1}\to G_{2}$, and for arbitrary $h\in G_{1}$,
we have $h\in H\implies f|_{H}(h) = f(h)$. The reasoning is that we can
always look at the set-theoretic function $U(f)$ underlying $f$, then
piggie-back off earlier results establishing the desired claim
(Theorem~\mml[Th49]{funct1}\MizThm{FUNCT\_1}{49}, to be precise).
\end{lem-remark}


<<Theorem: restriction of group morphism acts on elements like the original>>=
theorem Th2:
  for G1,G2 being Group
  for H being Subgroup of G1
  for f being Homomorphism of G1,G2
  for h being Element of G1
  st h in H
  holds (f|H).h = f.h
proof
  let G1,G2 be Group;
  let H be Subgroup of G1;
  let f be Homomorphism of G1,G2;
  let h be Element of G1;
  assume h in H;
  then h is Element of H;
  then (f|(the carrier of H)).h = f.h by FUNCT_1:49;
  hence (f|H).h = f.h by GRSOLV_1:def 2;
end;
@ %def Th2

\begin{theorem}
Let $H_{1}\subgroup G$ and $H_{2}\subgroup G$.
If $H_{1}\subgroup H_{2}$ and $H_{2}\subgroup H_{1}$, then
$H_{1}=H_{2}$.
\end{theorem}

\begin{lem-remark}
Pretty simple, right? \dots I'm not sure I'll need this, but here it is if I do.
\end{lem-remark}

<<Theorem: Sandwich Subgroup Equality Theorem>>=
theorem Th3:
  for H1 being strict Subgroup of G
  for H2 being strict Subgroup of G
  st (H1 is Subgroup of H2) & (H2 is Subgroup of H1)
  holds H1 = H2
proof
  let H1 be strict Subgroup of G;
  let H2 be strict Subgroup of G;
  assume H1 is Subgroup of H2 & H2 is Subgroup of H1;
  then the multMagma of H1 = the multMagma of H2 by GROUP_2:55;
  hence thesis;
end;
@ %def Th3

\begin{theorem}\label{thm:characteristic:preparatory:normal-subgroup}
Let $H\subgroup G$ be such that $\forall a\in G, a^{-1}Ha=H$.
Then $H\normalSubgroup G$ is a normal subgroup.
\end{theorem}

\begin{lem-remark}
The current theorems in~\mml{group3} require $H$ to be a \emph{strict}
subgroup, but this strictness condition is not necessary.
\end{lem-remark}


<<Theorem: Subgroups invariant under conjugation are normal>>=
theorem Th4:
for G being Group
for H being Subgroup of G
st (for a being Element of G holds H |^ a = H)
holds H is normal Subgroup of G
proof
  let G be Group;
  let H be Subgroup of G;
  assume for a being Element of G holds H |^ a = H;
  then for a being Element of G holds the carrier of H |^ a = the carrier of H;
  then for a being Element of G holds the multMagma of H = H |^ a by GROUP_2:59;
  hence H is normal Subgroup of G by GROUP_3:def 13;
end;
@ %def Th4

\begin{theorem}\label{thm:characteristic:preparatory:lm10}
If $f\colon X\to Y$ is a bijective function of non-empty sets, then $(f^{-1})^{-1}=f$.
\end{theorem}

\begin{proof}[Proof outline]
Let $f\colon X\to Y$ be bijective. Then $g=f^{-1}$ is a bijective
function from $Y$ to $X$. And $h=g^{-1}$ is a bijective function from
$X$ to $Y$. Then for any $x\in X$, we have $f(x)=h(x)$. This proves the claim.
\end{proof}

<<Theorem: if $f$ is bijective, then $(f^{-1})^{-1}=f$>>=
theorem Th5:
  for X,Y being non empty set
  for f being Function of X,Y
  st f is bijective
  holds (f")" = f
proof
  let X,Y be non empty set;
  let f be Function of X,Y;
  assume A1: f is bijective;
  then A2: dom f = X & rng f = Y & f is one-to-one by FUNCT_2:def 3,def 1;
  reconsider g = f" as Function of Y,X by A2,FUNCT_2:25;
  A3: g is bijective by A1,GROUP_6:63;
  then dom g = Y & rng g = X & g is one-to-one by FUNCT_2:def 1,def 3;
  g is one-to-one & rng g = X implies g" is Function of X,Y
  by FUNCT_2:25;
  then reconsider h = g" as Function of X,Y by A3,FUNCT_2:def 3;

  for x being object st x in X holds h.x = f.x
  <<Proof: $\forall x, x\in X\implies h(x) = f(x)$>>
  then h = f;
  hence (f")" = f;
end;
@ %def Th5

\begin{proof}[Proof step ($\forall x\in X, h(x)=f(x)$)]
  Let $x\in X$ be arbitrary. Consider
  \begin{subequations}\label{eq:characteristic:preparatory:lm-finvinv-is-inv}
  \begin{equation}
    y=f(x).
  \end{equation}
  Then $x=g(y)$ --- i.e.,
  $x = f^{-1}(y)$ --- implies
  \begin{equation}
  h(x) = g^{-1}(x) = y.
  \end{equation}
  \end{subequations}
  But since $y=y$ we from
  Eqs~\eqref{eq:characteristic:preparatory:lm-finvinv-is-inv}
  prove $h(x)=f(x)$.
\end{proof}

<<Proof: $\forall x, x\in X\implies h(x) = f(x)$>>=
proof
  let x be object;
  assume x in X;
  then reconsider x as Element of X;
  consider y being object such that
  Z1: y = f.x;
  Z2: x = g.y by A1,Z1,FUNCT_2:26;
  then h.x = y by A1,Z1,FUNCT_2:26
          .= f.x by Z1;
  hence thesis;
end;
@

\begin{theorem}\label{thm:characteristic:preparatory:f-finv-y-is-y}
If $f\colon X\to Y$ is a bijective function of sets, then for any $y\in Y$
we have $f(f^{-1}(y))=y$.
\end{theorem}

\begin{proof}[Proof sketch]
Let $f\colon X\to Y$ be bijective. Then $g\colon Y\to X$ given by
$g=f^{-1}$ is bijective. Mizar knows $g^{-1}(g(y))=y$ for $y=f(x)$. Then
plugging in the definition of $g$ and using
Theorem~\ref{thm:characteristic:preparatory:lm10} to transform
$(f^{-1})^{-1}=f$, together gives the result.
\end{proof}

\begin{lem-remark}
Mizar has the opposite result in its library, namely, Theorem~\mml[Th26]{funct2}\MizThm{FUNCT\_2}{26}
states that $f^{-1}(f(x))=x$ provided $f$ is injective.
\end{lem-remark}

<<Theorem: if $f\colon X\to Y$ is bijective, then $f\circ f^{-1}=\id_{Y}$>>=
theorem Th6:
  for X,Y being non empty set
  for f being Function of X,Y
  st f is bijective
  for y being Element of Y
  holds f.((f").y) = y
proof
  let X,Y be non empty set;
  let f be Function of X,Y;
  assume A1: f is bijective;
  let y be Element of Y;
  f is onto by A1;
  then reconsider g = f" as Function of Y,X by A1,FUNCT_2:25;
  y = (g").(g.y) by A1,FUNCT_2:26
   .= f.((f").y) by Th5,A1;
  hence thesis;
end;
@ %def Th6

\begin{theorem}\label{thm:characteristic:preparatory:x-notin-A-implies-fx-notin-fA}
Let $f\colon X\into Y$ be an injective function of non-empty sets, let
$A\subset X$ be a non-empty subset, let $x\in X$ be any element such
that $x\notin A$. Then $f(x)\notin f(A)$.
\end{theorem}

\begin{proof}[Proof sketch]
We prove that, if $f(x)\in f(A)$, then we get a contradiction with the
hypothesis $x\notin A$ or $f$ is injective.
\end{proof}

<<Theorem: $f\colon X\into Y$ and $x\notin A\subset X$ implies $f(x)\notin f(A)$>>=
theorem Th7:
  for X,Y being non empty set
  for A being non empty Subset of X
  for x being Element of X
  st not x in A
  for f being Function of X,Y
  st f is one-to-one
  holds not f.x in (f .: A)
proof
  let X,Y be non empty set;
  let A be non empty Subset of X;
  let x be Element of X;
  assume A1: not x in A;
  let f be Function of X,Y;
  assume f is one-to-one;
  then A2: not ex a being object st x in dom f & a in dom f & a in A & f.a = f.x
  by A1, FUNCT_1:def 4;
  dom f = X by FUNCT_2:def 1;
  then A3: x in dom f;
  f.x in (f .: A) iff ex a being object st a in dom f & a in A & f.x = f.a
  by FUNCT_1:def 6;
  then f.x in (f .: A) implies contradiction by A2,A3;
  hence not (f.x in (f .: A));
end;
@ %def Th7




%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\section{Nontrivial Groups}
We will be using nontrivial groups later. Recall, a group $G$ is
nontrivial if $G\neq\trivialSubgroup_{G}$. It is defined (or
\emph{overloaded}) in \mml[def2]{group6}\MizDef{GROUP\_6}[02]{2}:

\begin{mizar}
definition
  let G be non empty 1-sorted;
  redefine attr G is trivial means
  :Def2:
  ex x being object st the carrier of G = {x};
  compatibility
  :: ...
end;
\end{mizar}

We register the negated version ``\texttt{non trivial}'' for groups and
subgroups.


\begin{proposition}[{\mml[Th3]{group1}\MizThm{GROUP\_1}[03]{3}}]
The real numbers equipped with addition form a group.
\end{proposition}

\begin{remark}
We will need this to prove the existence of nontrivial groups and, later,
serve as an example of a nontrivial group with a proper subgroup.
\end{remark}

\begin{registration}\mizindex{Nontrivial@\texttt{non trivial}}
We have a notion of ``\texttt{non trivial}'' groups, and at least one
exists (namely, the real numbers as an Abelian group).
\end{registration}

<<Register: non trivial for Group>>=
registration
  cluster non trivial for Group;
  existence
  proof
    reconsider G = multMagma (# REAL, addreal #) as Group by GROUP_1:3;
    take G;
    thus not (G is trivial);
  end;
end;
@ %def trivial

\begin{registration}\index{Subgroup!Trivial}\index{Group!Trivial}\index{Trivial!Subgroup|see{Subgroup,Trivial}}\index{Trivial!Group|see{Group,Trivial}}\mizindex{Trivial@\texttt{trivial}}
We need to register the adjective ``trivial'' for groups and
subgroups. \emph{Every} group --- strict or not, trivial or not, hairy
or bald --- has a trivial subgroup. Similarly, \emph{every} subgroup has
a trivial subgroup.
\end{registration}

<<Register: trivial groups and trivial subgroups>>=
registration
  let G be Group;
  cluster trivial for Subgroup of G;
  existence
  proof
    take (1).G;
    thus thesis;
  end;
  let H be Subgroup of G;
  cluster trivial for Subgroup of H;
  existence
  proof
    take (1).H;
    thus thesis;
  end;
end;
@ %def trivial

\begin{registration}\index{Subgroup!Nontrivial}
For any non trivial group $G$, we can find a nontrivial subgroup
$H\subgroup G$, namely $G$ itself.
\end{registration}

<<Register: non trivial for Subgroup>>=
registration
  let G be non trivial Group;
  cluster non trivial for Subgroup of G;
  existence
  proof
    A1: the carrier of G <> {1_G} by GROUP_6:def 2;
    reconsider H=G as Subgroup of G by GROUP_2:54;
    the carrier of H <> {1_G} by A1;
    hence thesis;
  end;

  cluster strict non trivial for Subgroup of G;
  existence
  <<Proof: existence of strict nontrivial subgroup of $G$>>
end;
@


\begin{proof}[Proof outline (Existence of nontrivial subgroup)]
We basically take the strict group underlying $G$ as an example of a
nontrivial subgroup.
\end{proof}

<<Proof: existence of strict nontrivial subgroup of $G$>>=
proof
  set H = multMagma (#the carrier of G, the multF of G#);
  reconsider H as Group-like non empty multMagma;
  the multF of H = (the multF of G)||the carrier of H;
  then H is Subgroup of G by GROUP_2:def 5;
  then H is strict Subgroup of G & H is non trivial;
  hence thesis;
end;
@

\begin{theorem}\label{thm:preparatory:trivialIff1}
A group $G$ is trivial if $G=\trivialSubgroup$.
\end{theorem}

One direction has been proven in Theorem~\mml[Th10]{group6}, so we just
need to prove the forward direction.

\begin{proof}[Proof sketch]

\end{proof}

<<Theorem: trivial groups look like $\trivialSubgroup$>>=
theorem Th8:
  for G being Group
  holds G is trivial iff the multMagma of G = (1).G
proof
  let G be Group;
  thus G is trivial implies the multMagma of G = (1).G
  proof
    assume G is trivial;
    then consider x being object such that
    A1: the carrier of G = {x}
    by GROUP_6:def 2;
    x = 1_G by A1, TARSKI:def 1;
    then the carrier of G = {1_G} by A1;
    then the carrier of G = the carrier of (1).G by GROUP_2:def 7;
    hence the multMagma of G = (1).G by GROUP_2:61;
  end;
  thus the multMagma of G = (1).G implies G is trivial;
  thus thesis;
end;
@ %def Th8

\begin{theorem}\label{thm:characteristic:preparatory:trivial-subgroups-look-like-1}
If $H\subgroup G$ is trivial, then $H=\trivialSubgroup$.
\end{theorem}

\begin{proof}
\emph{Trivial}, thanks to Theorem~\ref{thm:preparatory:trivialIff1}.
\end{proof}

<<Theorem: $H\subgroup G$, $H$ is trivial implies $H=\trivialSubgroup_{G}$>>=
theorem Th9:
  for G being Group
  for H being Subgroup of G
  st H is trivial
  holds the multMagma of H = (1).G
proof
  let G be Group;
  let H be Subgroup of G;
  assume H is trivial;
  then the multMagma of H = (1).H by Th8
                         .= (1).G by GROUP_2:63;
  hence thesis;
end;

@ %def Th9

\begin{theorem}
If $H\subgroup G$ and $K\subgroup G$ are both trivial, then $H=K$ as groups.
\end{theorem}

\begin{proof}
If $H$ and $K$ are both trivial, then by Theorem~\ref{thm:characteristic:preparatory:trivial-subgroups-look-like-1} they both look like $\trivialSubgroup$
and thus are equal to each other as groups.
\end{proof}

<<Theorem: for $H\subgroup G$ and $K\subgroup G$ both trivial, $H=K$>>=
theorem Th10:
  for G being Group
  for H being trivial Subgroup of G
  for K being trivial Subgroup of G
  holds the multMagma of H = the multMagma of K
proof
  let G be Group;
  let H be trivial Subgroup of G;
  let K be trivial Subgroup of G;
  the multMagma of H = (1).G by Th9
                    .= the multMagma of K by Th9;
  hence thesis;
end;
@ %def Th10

\begin{theorem}
If $H\subgroup K$ and $K\subgroup G$ and $K=\trivialSubgroup_{G}$, then $H=\trivialSubgroup_{G}$.
\end{theorem}

\begin{proof}
We have $\trivialSubgroup\subgroup H\subgroup\trivialSubgroup$ imply
$H=\trivialSubgroup$. But since we didn't use strict subgroups, we need
to work with their underlying sets and the fact $\{1_{G}\}\subset
H\subset\{1_{G}\}$ implies $H=\{1_{G}\}$ by Definition~\mml[def10]{xboole0}.
\end{proof}

<<Theorem: $H\subgroup K$ and $K\subgroup G$, then $K=\trivialSubgroup\implies H=\trivialSubgroup$>>=
theorem Th11:
  for G being Group
  for K being trivial Subgroup of G
  for H being Subgroup of G
  st H is Subgroup of K
  holds H is trivial Subgroup of G
proof
  let G be Group;
  let K be trivial Subgroup of G;
  let H be Subgroup of G;
  assume A1: H is Subgroup of K;
  the carrier of H = {1_G}
  proof
    the multMagma of K = (1).G by Th9;
    then the carrier of K = {1_G} by GROUP_2:def 7;
    then B1: the carrier of H c= {1_G} by A1,GROUP_2:def 5;
    (1).G is Subgroup of H by GROUP_2:65;
    then the carrier of (1).G c= the carrier of H by GROUP_2:def 5;
    then {1_G} c= the carrier of H by GROUP_2:def 7;
    hence the carrier of H = {1_G} by B1,XBOOLE_0:def 10;
  end;
  then ex x being object st the carrier of H = {x};
  hence H is trivial Subgroup of G by GROUP_6:def 2;
end;
@ %def Th11

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\section{Proper Subgroups}\index{Subgroup!Proper}
When we have a [nontrivial] group $G$, we can discuss the notion of a
proper subgroup $H\properSubgroup G$ in analogy to the notion of a
proper subset $X\properSubset Y$.


\begin{definition}\index{Subgroup!Proper|textbf}
Let $G$ be a group. We call a subgroup $H\subgroup G$ \define{Proper} if
$H\neq G$. We typically denote $H\properSubgroup G$ to reflect it is proper.
\end{definition}

\begin{def-remark}
The implementation for Mizar is a bit quirky. I looked at how maximal
subgroups were defined, because maximal subgroups are necessarily
proper. Maximal subgroups were defined (\mml[def 6]{group4}\MizDef{GROUP\_4}[06]{6})
using the condition \lstinline|the multMagma of H <> the multMagma of G|.
\end{def-remark}\mizindex{Proper@\texttt{proper}}

<<Definition: proper subgroup>>=
definition
  let G be Group;
  let IT be Subgroup of G;
  attr IT is proper means
  :Def10:
  the multMagma of IT <> the multMagma of G;
end;
reserve G for Group;
reserve H for Subgroup of G;
@ %def Def10 proper

\begin{registration}
For any nontrivial group $G$, we can find a proper subgroup $H\subgroup G$,
namely the trivial subgroup $H=\trivialSubgroup_{G}$.
\end{registration}

<<Register: proper subgroup for nontrivial groups>>=

registration
  let G be non trivial Group;
  cluster proper for Subgroup of G;
  existence
  proof
    take (1).G;
    thus (1).G is proper;
  end;
end;

reserve G for non trivial Group;
reserve H for Subgroup of G;
@

\begin{theorem}
Let $H\subgroup G$ be a subgroup. Then $H\properSubgroup G$ is proper if
and only if the underlying set of $G$ differs from the underlying set of $H$.
\end{theorem}

\begin{proof}[Proof outline]
The only way a group could be different, since they are magmas
satisfying some properties, is either if the underlying set differs or
if the group operation differs. But since we know $H$ is a subgroup of
$G$, we know the group operation for $H$ is just the restricted version
of the group operation for $G$. Thus we are forced to accept the
underlying sets must be different. This argument works backwards as well
as forwards.
\end{proof}

<<Theorem: $H\subgroup G$ is proper iff the underlying sets are different>>=
theorem Th12:
  H is proper iff the carrier of H <> the carrier of G
proof
  (the carrier of H c= the carrier of G) & (the multF of H =
  (the multF of G)||(the carrier of H)) by GROUP_2:def 5;
  hence thesis;
end;
@ %def Th12

\begin{theorem}\label{thm:characteristic:preparatory:proper-subgroups-have-set-diff-neq-empty}
  Let $H\subgroup G$ be a subgroup.
  Then $H$ is a proper subgroup if and only if the set difference is nonempty
  $G\setminus H\neq\emptyset$.
\end{theorem}

\begin{lem-remark}
This version, as stated, is a little sloppy. We should more precisely
state the set difference of the underlying set $U(G)$ of $G$ with the
underlying set $U(H)$ of $H$ is nonempty $U(G)\setminus U(H)\neq\emptyset$.
\end{lem-remark}

\begin{proof}[Proof outline]
  There are two sub-proofs:
  \begin{enumerate}
  \item $H$ is proper implies $U(G)\setminus U(H)\neq\emptyset$
  \item $U(G)\setminus U(H)\neq\emptyset$ implies $H$ is proper.\qedhere
  \end{enumerate}
\end{proof}

<<Theorem: $H\subgroup G$ is proper iff $G\setminus H\neq\emptyset$>>=
reserve h,x,y for object;

theorem Th13:
  H is proper iff (the carrier of G) \ (the carrier of H) is non empty set
proof
  set UG = the carrier of G;
  set UH = the carrier of H;
  thus H is proper implies (the carrier of G) \ (the carrier of H) is non empty set
  <<Sub-proof: $H\properSubgroup G\implies G\setminus H\neq\emptyset$>>
  thus (the carrier of G) \ (the carrier of H) is non empty set implies H is proper
  <<Sub-proof: $H\properSubgroup G\impliedby G\setminus H\neq\emptyset$>>
  thus thesis;
end;
@ %def Th13

\begin{proof}[Sub-proof outline]
  If $H\properSubgroup G$ is a proper subgroup, then the underlying set
  $U(H)$ of $H$ is a subset of the underlying set $U(G)$ of $G$ --- i.e.,
  $U(H)\subset U(G)$ --- and $U(H)\neq U(G)$. Then there is some element
  $g\in G$ for which $g\notin H$. Then $U(G)\setminus U(H)\neq\emptyset$.
\end{proof}

<<Sub-proof: $H\properSubgroup G\implies G\setminus H\neq\emptyset$>>=
proof
  assume A1: H is proper;
  UH c= UG & UH <> UG by A1,Th12, GROUP_2:def 5;
  then (for x holds x in UH implies x in UG) &
  the carrier of H <> the carrier of G;
  then (for x holds x in UH implies x in UG) &
  not (for x holds x in UH iff x in UG) by TARSKI:2;
  hence (the carrier of G) \ (the carrier of H) is non empty set
  by XBOOLE_0:def 5;
end;
@

\begin{proof}[Sub-proof outline]
Assume $G\setminus H\neq\emptyset$. Then there exists some $y\in
G\setminus H$, i.e., $y\in G$ and $y\notin H$. But we've found an
element in $G$ that's not in $H$. So by the extensional notion of set
equality, these are clearly different sets. Thus $H\properSubgroup G$
\end{proof}

<<Sub-proof: $H\properSubgroup G\impliedby G\setminus H\neq\emptyset$>>=
proof
  assume A1: (the carrier of G) \ (the carrier of H) is non empty set;
  set GH = UG \ UH;
  not (GH is empty) by A1;
  then ex x st x in GH by XBOOLE_0:def 1;
  then consider y such that
  A2: y in GH;
  y in UG & not y in UH by A2, XBOOLE_0:def 5;
  then UG <> UH;
  then the multMagma of G <> the multMagma of H;
  hence H is proper;
end;
@

\begin{registration}
For any nontrivial group $G$, we can call \define{Proper Subgroup} of
$G$ a subgroup which is proper.
\end{registration}

<<Register: proper subgroup for nontrivial groups>>=
registration
  let G be non trivial Group;
  cluster proper for Subgroup of G;
  existence
  proof
    take (1).G;
    thus (1).G is proper;
  end;
end;
@

\begin{registration}
For any nontrivial group $G$ with nontrivial subgroup $H\subgroup G$, we
can call \define{Proper Subgroup} of $H$ a subgroup $K\subgroup H$ which is proper.
\end{registration}

<<Register: proper subgroups of nontrivial subgroups>>=
reserve G for non trivial Group;
reserve H for Subgroup of G;

registration
  let G be non trivial Group;
  let H be non trivial Subgroup of G;
  cluster proper for Subgroup of H;
  existence
  proof
    take (1).H;
    thus (1).H is proper;
  end;
end;
@

\begin{lemma}
Maximal subgroups are also proper subgroups.
\end{lemma}

The proof is literally, ``Look at the definitions!''

<<Lemma: maximal subgroups are proper>>=
Lm1:
  for G being Group
  for H being Subgroup of G
  st H is maximal
  holds H is proper by GROUP_4:def 6;
@ %def Lm1

\begin{registration}
We can now automatically take advantage of the fact that, a maximal
subgroup for a nontrivial group is implicitly a proper subgroup.
\end{registration}

<<Register: maximal subgroups are proper>>=
registration
  let G be non trivial Group;
  cluster maximal -> proper for Subgroup of G;
  coherence by Lm1;
end;
@

\begin{theorem}
If $H\properSubgroup K$ is proper and $K\subgroup G$ and $H\neq K$, then
$K$ is a nontrivial group.
\end{theorem}

\begin{proof}[Proof outline]
  Assume for contradiction that $K=\trivialSubgroup$.
  Then combined with $H\properSubgroup K$ and $H\neq K$ implies
  $H=\trivialSubgroup$ and this contradicts $H\properSubgroup K$ proper.
\end{proof}

<<Theorem: $H\properSubgroup K\subgroup G$ and $H\neq K$ implies $K$ is nontrivial>>=
theorem Th15:
  for G being non trivial Group
  for H being proper Subgroup of G
  for K being Subgroup of G
  st H is Subgroup of K & the multMagma of H <> the multMagma of K
  holds K is non trivial Subgroup of G
proof
  let G be non trivial Group;
  let H be proper Subgroup of G;
  let K be Subgroup of G;
  assume A1: H is Subgroup of K;
  assume A2: the multMagma of H <> the multMagma of K;
  not (K is non trivial Subgroup of G) implies contradiction
  proof
    assume B1: not K is non trivial Subgroup of G;
    then K is trivial Subgroup of G;
    then H is trivial Subgroup of G by A1,Th11;
    then the multMagma of H = the multMagma of K by B1,Th10;
    hence contradiction by A2;
  end;
  hence K is non trivial Subgroup of G;
end;
@ %def Th15
