% -*- mode: poly-noweb; noweb-code-mode: mizar-mode; -*-
% This is placed here so the LaTeX is organized correctly, thanks to
% problems with noweb :(
\section{Article Body}\label{sec:characteristic:preparatory:start}

\M
The article body is where the magic happens. Now we can start making
definitions, stating theorems, proving results. The basic structure of
our article can be cleaved in two: first we state and prove ``helper
lemmas'', which probably belong somewhere else, but currently are not
located anywhere in the Mizar library. The second half are our results
concerning characteristic subgroups.

Just to give some idea of what we're doing, we will have to define a
notion of [[Automorphism]]. We will also have to prove a number of
results concerning [[Automorphism]]s. After all, a characteristic
subgroup is one which is left invariant under any automorphism of its
parent group.

Once that has been squared away, we will define a notion of a
[[characteristic]] subgroup. Then we will prove results right away.

<<[[char.miz]] article body>>=
<<Helper lemmas and registrations for [[char.miz]]>>

<<Inner and outer automorphisms>>
<<Characteristic subgroup results>>

@

\subsection{Preparatory results}

\N{Helper lemmas}
There are a lot of recurring patterns which can be isolated into helper
functions---err, lemmas. I'm sure many (if not all) are already present
somewhere in the Mizar Mathematical Library, but I couldn't find them. I
am placing them within their own ``section'', because if I ever submit
the result to the Mizar Mathematical Library, they will either be
removed (and relocated to the relevant articles) or the editors will
know what I should have done instead.

<<Helper lemmas and registrations for [[char.miz]]>>=
begin :: preparatory work
reserve X for set;

<<Lemma: $f\colon X\onto Y\implies f(X)=Y$>>
<<Register: the identity function is surjective and bijective>>
<<Lemma: A group element in the subgroup is a subgroup element>>
<<Lemma: $g\in H\subgroup G$ and $K\subgroup G$, then $H\subgroup K\implies g\in K$>>
<<Lemma: restriction of group morphism acts on elements like the original>>
<<Lemma: Subgroups invariant under conjugation are normal>>
<<Lemma: Morphism is surjective if its image is its codomain>>
<<Lemma: $H\subgroup K\subgroup G_{1}$ and $f\colon G_{1}\to G_{2}$ is a morphism, then $(f|_{K})|_{H}=f|_{H}\colon H\to G_{2}$ is a morphism>>
<<Theorem: if $f$ is bijective, then $(f^{-1})^{-1}=f$>>
<<Theorem: if $f\colon X\to Y$ is bijective, then $f\circ f^{-1}=\id_{Y}$>>
<<Theorem: if $f\colon X\to Y$ is bijective, then there exists an inverse function and $f^{-1}\colon Y\to X$ is bijective>>
<<Theorem: bijections have inverses>>

@

\M
We need to prove, if $f\colon X\onto Y$ is a surjective function of
sets, then $f(X)=Y$.

\begin{remark}[Mizar notation \texttt{f.:A} and \texttt{f.x}]\index{\texttt{f.:A}!Mizar for $f(A)$}\index{\texttt{f.x}!Mizar for $f(x)$}
We should caution Mizar's notation for writing something like, if
$f\colon X\to Y$ is a function of sets and $A\subset X$, then
$f(A)=\{f(a)\in Y|a\in A\}$ is denoted as [[f.:A]] instead of $f(A)$,
and [[f.a]] instead of $f(a)$. This is because $f$ is a set theoretic
function, a collection of ``(input, output)'' ordered pairs. Mizar
distinguishes $f(x)$ as function application from ``functors'' (i.e.,
parametrized terms) by using $f(x)=\mathtt{f.x}$ for mathematical functions.
\end{remark}

<<Lemma: $f\colon X\onto Y\implies f(X)=Y$>>=
LmOntoMapsEverything:
for X,Y being non empty set
for f being Function of X,Y
st f is onto
holds f .: X = Y
proof
  let X,Y be non empty set;
  let f be Function of X,Y;
  assume f is onto;
  then A1: rng f = Y;
  A2: dom f = X by FUNCT_2:def 1;
  then A3: for y being object
  holds y in rng f iff ex x being object st x in X & y = f.x
  by FUNCT_1:def 3;
  A4: for y being object holds y in (f .: X) iff
  ex x being object st x in X & x in X & y = f.x
  by A2,FUNCT_1:def 6;

  C1: for y being object holds y in (f .: X) implies y in rng f
  <<Proof: $y\in f(X)\implies y\in\mathrm{rng}(f)$>>

  C2: for y being object holds y in rng f implies y in (f .: X)
  <<Proof: $y\in\mathrm{rng}(f)\implies y\in f(X)$>>
  for y being object holds y in rng f iff y in (f .: X) by C1,C2;
  then rng f = f .: X by TARSKI:2;
  hence f .: X = Y by A1;
end;

@ %def LmOntoMapsEverything

<<Proof: $y\in f(X)\implies y\in\mathrm{rng}(f)$>>=
proof
  let y be object;
  assume y in (f .: X);
  then consider x being object such that
  B1: x in X and x in X and B2: y = f.x
  by A4;
  thus y in rng f by A3,B1,B2;
end;

@

<<Proof: $y\in\mathrm{rng}(f)\implies y\in f(X)$>>=
proof
  let y be object;
  assume y in rng f;
  then consider x being object such that
  B1: x in X and B2: y = f.x
  by A3;
  thus y in (f .: X) by A4,B1,B2;
end;

@

\M
We begin by registering the identity function as being surjective and
bijective. This should have been done in [[FUNCT_2]], but hey, what can
you do?

<<Register: the identity function is surjective and bijective>>=
registration
  let X;
  cluster id X -> onto;
  coherence;
end;

registration
  let X;
  cluster id X -> bijective;
  coherence;
end;

@

\M
We also need to prove that, if we have a subgroup $H\subgroup G$ and
$h\in G$ is an arbitrary element of the underlying set, then $h\in H$
implies the typing judgement ``[[h is Element of H]]''.

\begin{remark}\index{\texttt{h in H}!Mizar for $h\in H$}
Mizar writes ``[[h in H]]'' instead of ``$h\in H$''. The statement
``[[h is Element of H]]'' is a typing declaration roughly analogous to
``$h{:}H$''; strictly speaking, it's a predicate in the object language,
not a judgement in the metalanguage.
\end{remark}

<<Lemma: A group element in the subgroup is a subgroup element>>=
LmSubgroupElt:
for G being Group
for H being Subgroup of G
for h being Element of G st h in H holds h is Element of H
proof
  let G be Group,
      H be Subgroup of G,
      h be Element of G;
  assume h in H;
  then h is Element of the carrier of H by STRUCT_0:def 5;
  then h is Element of H;
  hence thesis;
end;

@ %def LmSubgroupElt

\begin{lemma}
  Let $G$ be a group and $g\in G$ be any element of the group,
  $H_{1}\subgroup G$ and $H_{2}\subgroup G$ be subgroups.
  If $H_{1}\subgroup H_{2}$ and $g\in H_{1}$, then $g\in H_{2}$.
\end{lemma}

This is another ``Well, duh'' lemma that I couldn't find in Mizar's library.

<<Lemma: $g\in H\subgroup G$ and $K\subgroup G$, then $H\subgroup K\implies g\in K$>>=
LmSubgroupEltTrans:
  for G being Group
  for H1,H2 being Subgroup of G
  for g being Element of G
  st g in H1 & H1 is Subgroup of H2
  holds g in H2
proof
  let G be Group;
  let H1,H2 be Subgroup of G;
  let g be Element of G;
  assume A1: g in H1 & H1 is Subgroup of H2;
  then g is Element of H1 by LmSubgroupElt;
  then g in H2 by A1, GROUP_2:40;
  hence thesis;
end;

@ %def LmSubgroupEltTrans

\N{Restriction of Group Morphisms to Subgroups}
Mizar proves that, if $f\colon X\to Y$ is a set theoretic function and
$A\subset X$ is an arbitrary subset, then $\forall a\in X$ we have
$a\in A\implies f|_{A}(a)=f(a)$. But this doesn't generalize to
morphisms, sadly, because a group is like a chocolate-covered set.

So we just prove for any pair of groups $G_{1}$ and $G_{2}$,
for any subgroup $H\subgroup G_{1}$,
for any group morphism $f\colon G_{1}\to G_{2}$, and for arbitrary $h\in G_{1}$,
we have $h\in H\implies f|_{H}(h) = f(h)$. The reasoning is that we can
always look at the set-theoretic function $U(f)$ underlying $f$, then
piggie-back off earlier results establishing the desired claim
(Theorem~49 of [[FUNCT_1]], to be precise).

<<Lemma: restriction of group morphism acts on elements like the original>>=
Lm3: for G1,G2 being Group
for H being Subgroup of G1
for f being Homomorphism of G1,G2
for h being Element of G1
st h in H
holds (f|H).h = f.h
proof
  let G1,G2 be Group;
  let H be Subgroup of G1;
  let f be Homomorphism of G1,G2;
  let h be Element of G1;
  assume h in H;
  then h is Element of H by LmSubgroupElt;
  then (f|(the carrier of H)).h = f.h by FUNCT_1:49;
  then (f|H).h = f.h by GRSOLV_1:def 2;
  hence thesis;
end;

@ %def Lm3

\M
Let $H_{1}\subgroup G$ and $H_{2}\subgroup G$.
If $H_{1}\subgroup H_{2}$ and $H_{2}\subgroup H_{1}$, then
$H_{1}=H_{2}$. Pretty simple, right? \dots I'm not sure I'll need this,
but here it is if I do.

<<Lemma: Sandwich Subgroup Equality Theorem>>=
LmSandwich: for H1 being strict Subgroup of G
for H2 being strict Subgroup of G
st (H1 is Subgroup of H2) & (H2 is Subgroup of H1)
holds H1 = H2
proof
  let H1 be strict Subgroup of G;
  let H2 be strict Subgroup of G;
  assume H1 is Subgroup of H2 & H2 is Subgroup of H1;
  then the multMagma of H1 = the multMagma of H2 by GROUP_2:55;
  hence thesis;
end;

@ %def LmSandwich

\begin{remark}[On ``strict'' mathematical gadgets]\index{strict@\texttt{strict}|textbf}
Mizar has a notion of a ``[[strict]]'' attribute. What does it mean?
Well, sometimes a mathematical gadget is ``composite''; like a vector
space over a field is an Abelian group with vector addition, but it also
has other stuff (like scalar multiplication). This ``other stuff''
prevents it from being a ``bald-faced Abelian group''. Just as black
holes have no hair, strict groups have no hair: it's just a
[[multMagma]] satisfying the group properties (in Mizar, at least).
\end{remark}

\M Let $H\subgroup G$ be such that $\forall a\in G, a^{-1}Ha=H$.
Then $H\normalSubgroup G$ is a normal subgroup. The current theorems in
[[GROUP_3]] require $H$ to be a \emph{strict} subgroup, but this
strictness condition is not necessary.

<<Lemma: Subgroups invariant under conjugation are normal>>=
LmNormalSubgroup:
for G being Group
for H being Subgroup of G
st (for a being Element of G holds H |^ a = H)
holds H is normal Subgroup of G
proof
  let G be Group;
  let H be Subgroup of G;
  assume for a being Element of G holds H |^ a = H;
  then for a being Element of G holds the carrier of H |^ a = the carrier of H;
  then for a being Element of G holds the multMagma of H = H |^ a by GROUP_2:59;
  hence H is normal Subgroup of G by GROUP_3:def 13;
end;

@ %def LmNormalSubgroup

\begin{theorem}
  Let $\varphi\colon G_{1}\to G_{2}$ be a group morphism.
  If $\varphi(G_{1})=G_{2}$, then $\varphi$ is surjective.
\end{theorem}

Mizar has this lemma in its library, but it has a more stringent
requirement that $G_{2}$ be a [[strict Group]], which is totally unnecessary.

<<Lemma: Morphism is surjective if its image is its codomain>>=
LmEpi:
  for G1,G2 being Group
  for f being Homomorphism of G1,G2
  st Image(f) = G2
  holds f is onto
proof
  let G1,G2 be Group;
  let f be Homomorphism of G1,G2;
  assume A1: Image(f) = G2;
  then rng f = the carrier of Image f by GROUP_6:44
            .= the carrier of G2 by A1;
  then f is onto by FUNCT_2:def 3;
  hence thesis;
end;

@ %def LmEpi

\begin{lemma}
Let $G_{1}$ and $G_{2}$ be groups, $K\subgroup G_{1}$ and $H\subgroup K$
be subgroups. If $f\colon G_{1}\to G_{2}$ is a group morphism, then
$(f|_{K})|_{H}=f|_{H}\colon H\to G_{2}$ is a group morphism.
\end{lemma}

Something like this was proven in [[FUNCT_1:50]], but I couldn't apply
that theorem (not even to functions of sets).

<<Lemma: $H\subgroup K\subgroup G_{1}$ and $f\colon G_{1}\to G_{2}$ is a morphism, then $(f|_{K})|_{H}=f|_{H}\colon H\to G_{2}$ is a morphism>>=
LmRestrictCollapse:
for G,G2 being Group
for N being Subgroup of G
for H being Subgroup of N
for f being Homomorphism of G,G2
holds (f|N)|H = f|H
proof
  let G,G2 be Group;
  let N be Subgroup of G;
  let H be Subgroup of N;
  let f be Homomorphism of G,G2;
  set UN = the carrier of N;
  set UH = the carrier of H;
  UH c= UN by GROUP_2:def 5;
  then A1: (f|UN)|UH = f|UH & (f|UN) = f|N & f|UH = f|H
  by RELAT_1:73,74,GRSOLV_1:def 2;
  (f|N)|H = (f|N)|UH by GRSOLV_1:def 2
         .= (f|UN)|UH by GRSOLV_1:def 2;
  hence (f|N)|H = f|H by A1;
end;

@ %def LmRestrictCollapse


<<Theorem: if $f$ is bijective, then $(f^{-1})^{-1}=f$>>=
Lm10:
for X,Y being non empty set
for f being Function of X,Y
st f is bijective
holds (f")" = f
proof
  let X,Y be non empty set;
  let f be Function of X,Y;
  assume A1: f is bijective;
  then B1: dom f = X & rng f = Y & f is one-to-one by FUNCT_2:def 3,def 1;
  f is one-to-one & rng f = Y implies f" is Function of Y,X
  by FUNCT_2:25;
  then consider g being Function of Y,X such that
  A2: g = f"
  by A1,B1,FUNCT_2:25;
  A3: g is bijective by A1,A2,GROUP_6:63;
  then B2: dom g = Y & rng g = X & g is one-to-one by FUNCT_2:def 1,def 3;
  g is one-to-one & rng g = X implies g" is Function of X,Y
  by A3,FUNCT_2:25;
  then consider h being Function of X,Y such that
  A4: h = g"
  by A3,FUNCT_2:def 1, def 3,25;
  A5: h is bijective by A3,A4,GROUP_6:63;

  for x being object st x in X holds h.x = f.x
  <<Proof: $\forall x, x\in X\implies h(x) = f(x)$>>
  then h = f by FUNCT_2:12;
  hence (f")" = f by A2,A4;
end;

@ %def Lm10

<<Proof: $\forall x, x\in X\implies h(x) = f(x)$>>=
proof
  let x be object;
  assume Z1: x in X;
  then reconsider x as Element of X;
  Y <> {};
  then consider y being object such that
  Z3: y = f.x
  by FUNCT_2:5;
  Z6: g is one-to-one & y in dom g by Z3, A2, B2;
  f is one-to-one & x in dom f by A1, Z1,B1;
  then Z4: x = (f").y by Z3,FUNCT_2:26;
  then Z5: x = g.y by A2;
  h.x = (g").x by A4
     .= (g").(g.y) by Z4,A2
     .= y by Z6,FUNCT_2:26;
  then h.x = f.x by Z3;
  hence thesis;
end;

@

\begin{theorem}
If $f\colon X\to Y$ is a bijective function of sets, then for any $y\in Y$
we have $f(f^{-1}(y))=y$.
\end{theorem}

Mizar has the opposite result in its library, namely, \verb#FUNCT_2:26#
states that $f^{-1}(f(x))=x$ provided $f$ is injective.

<<Theorem: if $f\colon X\to Y$ is bijective, then $f\circ f^{-1}=\id_{Y}$>>=
Lm11:
for X,Y being non empty set
for f being Function of X,Y
st f is bijective
for y being Element of Y
holds f.((f").y) = y
proof
  let X,Y be non empty set;
  let f be Function of X,Y;
  assume A1: f is bijective;
  let y be Element of Y;
  (rng f = Y & f is one-to-one) &
  (f is one-to-one & rng f = Y implies f" is Function of Y,X)
  by A1,FUNCT_2:def 3, 25;
  then consider g being Function of Y,X such that
  A2: g = f"
  by A1;
  g = f" & f is bijective by A1,A2;
  then g is bijective by A1, GROUP_6:63;
  then y = (g").(g.y) by A2,FUNCT_2:26
        .= ((f")").((f").y) by A2
        .= f.((f").y) by Lm10,A1;
  hence thesis by Lm10;
end;

@ %def Lm11

\begin{theorem}
  If $f\colon X\to Y$ is a bijective function of sets,
  then its inverse function $f^{-1}\colon Y\to X$ exists (as defined by
  $f\circ f^{-1}=\id_{Y}$ and $f^{-1}\circ f=\id_{X}$).
\end{theorem}

<<Theorem: bijections have inverses>>=
Lm12:
for X,Y being non empty set
for f being Function of X,Y
st f is bijective
holds f*(f") = id Y & (f")*f = id X
proof
  let X,Y be non empty set;
  let f be Function of X,Y;
  assume A1: f is bijective;
  then consider g being Function of Y,X such that
  A2: g = f" and
  A3: g is bijective
  by Lm13;
  A4: g" = f by A1,A2,A3,Lm10;
  for y being object st y in Y holds (f*(f")).y = (id Y).y
  <<Proof: $\forall y, y\in Y\implies (f\circ f^{-1})(y) = \id_{Y}(y)$>>
  then f*g = id Y by A2,FUNCT_2:12;
  hence f*(f") = id Y by A2,FUNCT_2:12;
  for x being object st x in X holds ((f")*f).x = (id X).x
  <<Proof: $\forall x,x\in X\implies (f^{-1}\circ f)(x) = \id_{X}(x)$>>
  then g*f = id X by A2,FUNCT_2:12;
  hence (f")*f = id X by A2,FUNCT_2:12;
end;

@ %def Lm12

<<Proof: $\forall y, y\in Y\implies (f\circ f^{-1})(y) = \id_{Y}(y)$>>=
proof
  let y be object;
  assume Z1: y in Y;
  then (Y <> {} & f" is Function of Y,X & f is Function) implies (f*(f")).y=f.((f").y)
  by FUNCT_2:15;
  Z2: Y <> {} & f is Function & g is Function of Y,X & g = f" by A2;
  then (f*g).y=f.(g.y) by Z1,FUNCT_2:15
             .= y by A4,A3,Z1,Z2, FUNCT_2:26
             .= (id Y).y by Z1,FUNCT_1:18;
  hence thesis by A2;
end;

@

<<Proof: $\forall x,x\in X\implies (f^{-1}\circ f)(x) = \id_{X}(x)$>>=
proof
  let x be object;
  assume Z1: x in X;
  then (X <> {} & f is Function of X,Y & f" is Function) implies ((f")*f).x=(f").(f.x)
  by FUNCT_2:15;
  Z2: X <> {} & f is Function of X,Y & f" is Function by A2;
  then (g*f).x = g.(f.x) by Z1,FUNCT_2:15
              .= x by A2,Z1,Z2,A1,FUNCT_2:26
              .= (id X).x by Z1,FUNCT_1:18;
  hence thesis by A2;
end;

@

\begin{theorem}
If $f\colon X\to Y$ is bijective, then there exists an inverse function
$f^{-1}\colon Y\to X$ and $f^{-1}\colon Y\to X$ is bijective.
\end{theorem}

<<Theorem: if $f\colon X\to Y$ is bijective, then there exists an inverse function and $f^{-1}\colon Y\to X$ is bijective>>=
Lm13:
for X,Y being non empty set
for f being Function of X,Y
st f is bijective
holds ex g being Function of Y,X st g = f" & g is bijective
proof
  let X,Y be non empty set;
  let f be Function of X,Y;
  assume A1: f is bijective;
  then B1: dom f = X & rng f = Y & f is one-to-one by FUNCT_2:def 3,def 1;
  f is one-to-one & rng f = Y implies f" is Function of Y,X
  by FUNCT_2:25;
  then consider g being Function of Y,X such that
  A2: g = f"
  by A1,B1,FUNCT_2:25;
  take g;
  thus g = f" by A2;
  thus g is bijective by A1,A2, GROUP_6:63;
end;

@ %def Lm13
