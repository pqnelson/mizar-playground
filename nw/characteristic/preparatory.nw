% -*- mode: poly-noweb; noweb-code-mode: mizar-mode; -*-
% This is placed here so the LaTeX is organized correctly, thanks to
% problems with noweb :(
\section{Article Body}\label{sec:characteristic:preparatory:start}

The article body is where the magic happens. Now we can start making
definitions, stating theorems, proving results. The basic structure of
our article can be cleaved in two: first we state and prove ``helper
lemmas'', which probably belong somewhere else, but currently are not
located anywhere in the Mizar library. The second half are our results
concerning characteristic subgroups.

Just to give some idea of what we're doing, we will have to define a
notion of [[Automorphism]]. We will also have to prove a number of
results concerning [[Automorphism]]s. After all, a characteristic
subgroup is one which is left invariant under any automorphism of its
parent group.

Once that has been squared away, we will define a notion of a
[[characteristic]] subgroup. Then we will prove results right away.

<<[[char.miz]] article body>>=
<<Helper lemmas and registrations for [[char.miz]]>>

<<Inner and outer automorphisms>>
<<Characteristic subgroup results>>

@

\section{Preparatory results}
There are a lot of recurring patterns which can be isolated into helper
functions---err, lemmas. I'm sure many (if not all) are already present
somewhere in the Mizar Mathematical Library, but I couldn't find them. I
am placing them within their own ``section'', because if I ever submit
the result to the Mizar Mathematical Library, they will either be
removed (and relocated to the relevant articles) or the editors will
know what I should have done instead.

<<Helper lemmas and registrations for [[char.miz]]>>=
begin :: preparatory work
reserve X for set;

<<Lemma: $f\colon X\onto Y\implies f(X)=Y$>>
<<Register: the identity function is surjective and bijective>>
<<Lemma: A group element in the subgroup is a subgroup element>>
<<Lemma: $g\in H\subgroup G$ and $K\subgroup G$, then $H\subgroup K\implies g\in K$>>
<<Lemma: restriction of group morphism acts on elements like the original>>
<<Lemma: Subgroups invariant under conjugation are normal>>
<<Lemma: Morphism is surjective if its image is its codomain>>
<<Lemma: $\varphi\colon G_{1}\onto G_{2}$ is surjective implies $\varphi(G_{1})=G_{2}$>>
<<Lemma: $H\subgroup K\subgroup G_{1}$ and $f\colon G_{1}\to G_{2}$ is a morphism, then $(f|_{K})|_{H}=f|_{H}\colon H\to G_{2}$ is a morphism>>
<<Lemma: if $f$ is bijective, then $(f^{-1})^{-1}=f$>>
<<Lemma: if $f\colon X\to Y$ is bijective, then $f\circ f^{-1}=\id_{Y}$>>
<<Lemma: if $f\colon X\to Y$ is bijective, then there exists an inverse function and $f^{-1}\colon Y\to X$ is bijective>>
<<Lemma: bijections have inverses>>
<<Register: non trivial for Group>>
<<Register: trivial groups and trivial subgroups>>
<<Register: non trivial for Subgroup>>
<<Definition: proper subgroup>>
<<Lemma: $H\subgroup G$ is proper iff the underlying sets are different>>
<<Lemma: $H\subgroup G$ is proper iff $G\setminus H\neq\emptyset$>>
<<Register: proper subgroup for nontrivial groups>>
<<Lemma: maximal subgroups are proper>>
<<Register: maximal subgroups are proper>>
<<Lemma: $H\subgroup G$ and $\exists x\in G,x\notin H$ implies $H$ is proper>>
<<Lemma: $H\subgroup G$ is proper implies $\exists x\in G, x\notin H$>>
<<Lemma: $f\colon X\into Y$ and $x\notin A\subset X$ implies $f(x)\notin f(A)$>>
<<Lemma: trivial groups look like $\trivialSubgroup$>>
<<Lemma: Trivial groups have no proper subgroups>>
<<Lemma: Trivial groups have no maximal subgroup>>

@

\begin{lemma}
If $f\colon X\onto Y$ is a surjective function of sets, then $f(X)=Y$.
\end{lemma}

\begin{proof}[Proof outline]
  Assume $f\colon X\onto Y$ is surjective. Then by definition its range
  is its codomain $\rng(f)=Y$.
  We just need to show $\rng(f)=Y$ implies $f(X)=Y$, and $f(X)=Y$
  implies $\rng(f)$.
\end{proof}

\begin{lem-remark}[Note to Alex]
This should be refactored out into another theorem, namely
$\rng(f)=f(X)$. The result claimed follows immediately.
\end{lem-remark}

\begin{lem-remark}[Mizar notation \texttt{f.:A} and \texttt{f.x}]\index{\texttt{f.:A}!Mizar for $f(A)$}\index{\texttt{f.x}!Mizar for $f(x)$}
We should caution Mizar's notation for writing something like, if
$f\colon X\to Y$ is a function of sets and $A\subset X$, then
$f(A)=\{f(a)\in Y|a\in A\}$ is denoted as [[f.:A]] instead of $f(A)$,
and [[f.a]] instead of $f(a)$. This is because $f$ is a set theoretic
function, a collection of ``(input, output)'' ordered pairs. Mizar
distinguishes $f(x)$ as function application from ``functors'' (i.e.,
parametrized terms) by using $f(x)=\mathtt{f.x}$ for mathematical functions.
\end{lem-remark}

<<Lemma: $f\colon X\onto Y\implies f(X)=Y$>>=
LmOntoMapsEverything:
for X,Y being non empty set
for f being Function of X,Y
st f is onto
holds f .: X = Y
proof
  let X,Y be non empty set;
  let f be Function of X,Y;
  assume A1: f is onto;
  dom f = X by FUNCT_2:def 1;
  then
  <<Defining formulas for $\rng(f)$ and $f(X)$>>;

  C1: for y being object holds y in (f .: X) implies y in rng f
  <<Proof: $y\in f(X)\implies y\in\rng(f)$>>

  C2: for y being object holds y in rng f implies y in (f .: X)
  <<Proof: $y\in f(X)\impliedby y\in\rng(f)$>>

  for y being object holds y in rng f iff y in (f .: X) by C1,C2;
  hence f .: X = rng f by TARSKI:2
              .= Y by A1;
end;

@ %def LmOntoMapsEverything

\begin{proof}[Proof step (conditions for $\rng(f)$, $f(X)$)]
We can look up the definitions for [[rng f]] and [[f .: X]], which
give the logical equivalences:
\end{proof}

<<Defining formulas for $\rng(f)$ and $f(X)$>>=
A2: (for y being object
holds y in rng f iff ex x being object st x in X & y = f.x) &
(for y being object
holds y in (f .: X) iff ex x being object st x in X & x in X & y = f.x)
by FUNCT_1:def 3,def 6

@

\begin{proof}[Proof step ($f(X)\subset\rng(f)$)]
Proving the forward direction amounts to unfolding definitions.
\end{proof}

<<Proof: $y\in f(X)\implies y\in\rng(f)$>>=
proof
  let y be object;
  assume y in (f .: X);
  then consider x being object such that
  B1: x in X and x in X and B2: y = f.x
  by A2;
  thus y in rng f by A2,B1,B2;
end;

@

\begin{proof}[Proof step ($f(X)\supset\rng(f)$)]
Proving the backward direction amounts to unfolding the same
definitions, in the opposite order.
\end{proof}

<<Proof: $y\in f(X)\impliedby y\in\rng(f)$>>=
proof
  let y be object;
  assume y in rng f;
  then consider x being object such that
  B1: x in X and B2: y = f.x
  by A2;
  thus y in (f .: X) by A2,B1,B2;
end;

@

\begin{registration}
We begin by registering the identity function as being surjective and
bijective. This should have been done in \mml{funct2}, but hey, what can
you do?
\end{registration}

<<Register: the identity function is surjective and bijective>>=
registration
  let X;
  cluster id X -> onto;
  coherence;
end;

registration
  let X;
  cluster id X -> bijective;
  coherence;
end;

@

\begin{lemma}
If $H\subgroup G$ is a subgroup and $h$ is an element of $G$ such that
$h\in H$, then $h$ is an element of $H$.
\end{lemma}

We also need to prove that, if we have a subgroup $H\subgroup G$ and
$h\in G$ is an arbitrary element of the underlying set, then $h\in H$
implies the typing judgement ``[[h is Element of H]]''.

\begin{lem-remark}\index{\texttt{h in H}!Mizar for $h\in H$}
Mizar writes ``[[h in H]]'' instead of ``$h\in H$''. The statement
``[[h is Element of H]]'' is a typing declaration roughly analogous to
``$h{:}H$''; strictly speaking, it's a predicate in the object language,
not a judgement in the metalanguage.
\end{lem-remark}

<<Lemma: A group element in the subgroup is a subgroup element>>=
LmSubgroupElt:
for G being Group
for H being Subgroup of G
for h being Element of G st h in H holds h is Element of H
proof
  let G be Group,
      H be Subgroup of G,
      h be Element of G;
  assume h in H;
  then h is Element of the carrier of H by STRUCT_0:def 5;
  then h is Element of H;
  hence thesis;
end;

@ %def LmSubgroupElt

\begin{lemma}
  Let $G$ be a group and $g\in G$ be any element of the group,
  $H_{1}\subgroup G$ and $H_{2}\subgroup G$ be subgroups.
  If $H_{1}\subgroup H_{2}$ and $g\in H_{1}$, then $g\in H_{2}$.
\end{lemma}

This is another ``Well, duh'' lemma that I couldn't find in Mizar's library.

<<Lemma: $g\in H\subgroup G$ and $K\subgroup G$, then $H\subgroup K\implies g\in K$>>=
LmSubgroupEltTrans:
  for G being Group
  for H1,H2 being Subgroup of G
  for g being Element of G
  st g in H1 & H1 is Subgroup of H2
  holds g in H2
proof
  let G be Group;
  let H1,H2 be Subgroup of G;
  let g be Element of G;
  assume A1: g in H1 & H1 is Subgroup of H2;
  then g is Element of H1 by LmSubgroupElt;
  then g in H2 by A1, GROUP_2:40;
  hence thesis;
end;

@ %def LmSubgroupEltTrans

\begin{lemma}[{Restriction of Group Morphisms to Subgroups}]\label{lemma:characteristic:preparatory:restrict-group-morphism-to-subgroups}
If $f\colon G_{1}\to G_{2}$ is a group morphism and $H\subgroup G_{1}$
is a subgroup, then for any $h\in H$ we have $f(h) = f|_{H}(h)$.
\end{lemma}

\begin{lem-remark}
Mizar proves that, if $f\colon X\to Y$ is a set theoretic function and
$A\subset X$ is an arbitrary subset, then $\forall a\in X$ we have
$a\in A\implies f|_{A}(a)=f(a)$. But this doesn't generalize to
morphisms, sadly, because a group is like a chocolate-covered set.

So we just prove for any pair of groups $G_{1}$ and $G_{2}$,
for any subgroup $H\subgroup G_{1}$,
for any group morphism $f\colon G_{1}\to G_{2}$, and for arbitrary $h\in G_{1}$,
we have $h\in H\implies f|_{H}(h) = f(h)$. The reasoning is that we can
always look at the set-theoretic function $U(f)$ underlying $f$, then
piggie-back off earlier results establishing the desired claim
(Theorem~\mml[Th49]{funct1}\MizThm{FUNCT\_1}{49}, to be precise).
\end{lem-remark}


<<Lemma: restriction of group morphism acts on elements like the original>>=
Lm3: for G1,G2 being Group
for H being Subgroup of G1
for f being Homomorphism of G1,G2
for h being Element of G1
st h in H
holds (f|H).h = f.h
proof
  let G1,G2 be Group;
  let H be Subgroup of G1;
  let f be Homomorphism of G1,G2;
  let h be Element of G1;
  assume h in H;
  then h is Element of H by LmSubgroupElt;
  then (f|(the carrier of H)).h = f.h by FUNCT_1:49;
  then (f|H).h = f.h by GRSOLV_1:def 2;
  hence thesis;
end;

@ %def Lm3

\begin{lemma}
Let $H_{1}\subgroup G$ and $H_{2}\subgroup G$.
If $H_{1}\subgroup H_{2}$ and $H_{2}\subgroup H_{1}$, then
$H_{1}=H_{2}$.
\end{lemma}

\begin{lem-remark}
Pretty simple, right? \dots I'm not sure I'll need this, but here it is if I do.
\end{lem-remark}

<<Lemma: Sandwich Subgroup Equality Theorem>>=
LmSandwich: for H1 being strict Subgroup of G
for H2 being strict Subgroup of G
st (H1 is Subgroup of H2) & (H2 is Subgroup of H1)
holds H1 = H2
proof
  let H1 be strict Subgroup of G;
  let H2 be strict Subgroup of G;
  assume H1 is Subgroup of H2 & H2 is Subgroup of H1;
  then the multMagma of H1 = the multMagma of H2 by GROUP_2:55;
  hence thesis;
end;

@ %def LmSandwich

\begin{lemma}\label{lemma:characteristic:preparatory:normal-subgroup}
Let $H\subgroup G$ be such that $\forall a\in G, a^{-1}Ha=H$.
Then $H\normalSubgroup G$ is a normal subgroup.
\end{lemma}

\begin{lem-remark}
The current theorems in~\mml{group3} require $H$ to be a \emph{strict}
subgroup, but this strictness condition is not necessary.
\end{lem-remark}


<<Lemma: Subgroups invariant under conjugation are normal>>=
LmNormalSubgroup:
for G being Group
for H being Subgroup of G
st (for a being Element of G holds H |^ a = H)
holds H is normal Subgroup of G
proof
  let G be Group;
  let H be Subgroup of G;
  assume for a being Element of G holds H |^ a = H;
  then for a being Element of G holds the carrier of H |^ a = the carrier of H;
  then for a being Element of G holds the multMagma of H = H |^ a by GROUP_2:59;
  hence H is normal Subgroup of G by GROUP_3:def 13;
end;

@ %def LmNormalSubgroup

\begin{lemma}
  Let $\varphi\colon G_{1}\to G_{2}$ be a group morphism.
  If $\varphi(G_{1})=G_{2}$, then $\varphi$ is surjective.
\end{lemma}

Mizar has this lemma in its library, but it has a more stringent
requirement that $G_{2}$ be a [[strict Group]], which is totally
unnecessary. We weaken the conditions further, allowing $G_{2}$ to have
some bizarre exotic structure.

<<Lemma: Morphism is surjective if its image is its codomain>>=
LmEpi:
  for G1,G2 being Group
  for f being Homomorphism of G1,G2
  st the multMagma of Image(f) = the multMagma of G2
  holds f is onto
proof
  let G1,G2 be Group;
  let f be Homomorphism of G1,G2;
  assume A1: the multMagma of Image(f) = the multMagma of G2;
  then rng f = the carrier of Image f by GROUP_6:44
            .= the carrier of G2 by A1;
  then f is onto by FUNCT_2:def 3;
  hence thesis;
end;

@ %def LmEpi

\begin{lemma}
  If $\varphi\colon G_{1}\onto G_{2}$ is a surjective group morphism,
  then $\varphi(G_{1}) = G_{2}$.
\end{lemma}

The only subtlety to this claim is that we either require $G_{2}$ being
a \emph{strict} group, or we prove [[the multMagma of G2 = the multMagma of Image(f)]].

<<Lemma: $\varphi\colon G_{1}\onto G_{2}$ is surjective implies $\varphi(G_{1})=G_{2}$>>=
LmEpi2:
  for G1,G2 being Group
  for f being Homomorphism of G1,G2
  st f is onto
  holds the multMagma of Image(f) = the multMagma of G2
proof
  let G1,G2 be Group;
  let f be Homomorphism of G1,G2;
  assume A1: f is onto;
  reconsider GG = Image(f) as Group;
  A2: the carrier of G2 = rng(f) by A1
                       .= the carrier of Image(f) by GROUP_6:44
                       .= the carrier of GG;
  the multF of GG = (the multF of G2)||(the carrier of G2) by A2,GROUP_2:def 5
                 .= the multF of G2;
  hence the multMagma of Image(f) = the multMagma of G2 by A2;
end;

@ %def LmEpi2

\begin{lemma}
Let $G_{1}$ and $G_{2}$ be groups, $K\subgroup G_{1}$ and $H\subgroup K$
be subgroups. If $f\colon G_{1}\to G_{2}$ is a group morphism, then
$(f|_{K})|_{H}=f|_{H}\colon H\to G_{2}$ is a group morphism.
\end{lemma}

\begin{lem-remark}
Something like this was proven in Theorem~\mml[Th50]{funct1}\MizThm{FUNCT\_1}{50},
but I couldn't apply that theorem (not even to functions of sets).
\end{lem-remark}

<<Lemma: $H\subgroup K\subgroup G_{1}$ and $f\colon G_{1}\to G_{2}$ is a morphism, then $(f|_{K})|_{H}=f|_{H}\colon H\to G_{2}$ is a morphism>>=
LmRestrictCollapse:
for G,G2 being Group
for N being Subgroup of G
for H being Subgroup of N
for f being Homomorphism of G,G2
holds (f|N)|H = f|H
proof
  let G,G2 be Group;
  let N be Subgroup of G;
  let H be Subgroup of N;
  let f be Homomorphism of G,G2;
  set UN = the carrier of N;
  set UH = the carrier of H;
  UH c= UN by GROUP_2:def 5;
  then A1: (f|UN)|UH = f|UH & (f|UN) = f|N & f|UH = f|H
  by RELAT_1:73,74,GRSOLV_1:def 2;
  (f|N)|H = (f|N)|UH by GRSOLV_1:def 2
         .= (f|UN)|UH by GRSOLV_1:def 2;
  hence (f|N)|H = f|H by A1;
end;

@ %def LmRestrictCollapse

\begin{lemma}\label{lm:characteristic:preparatory:lm10}
If $f\colon X\to Y$ is a bijective function of non-empty sets, then $(f^{-1})^{-1}=f$.
\end{lemma}

\begin{proof}[Proof outline]
Let $f\colon X\to Y$ be bijective. Then $g=f^{-1}$ is a bijective
function from $Y$ to $X$. And $h=g^{-1}$ is a bijective function from
$X$ to $Y$. Then for any $x\in X$, we have $f(x)=h(x)$. This proves the claim.
\end{proof}

<<Lemma: if $f$ is bijective, then $(f^{-1})^{-1}=f$>>=
Lm10:
for X,Y being non empty set
for f being Function of X,Y
st f is bijective
holds (f")" = f
proof
  let X,Y be non empty set;
  let f be Function of X,Y;
  assume A1: f is bijective;
  then A2: dom f = X & rng f = Y & f is one-to-one by FUNCT_2:def 3,def 1;
  f is one-to-one & rng f = Y implies f" is Function of Y,X
  by FUNCT_2:25;
  reconsider g = f" as Function of Y,X by A2,FUNCT_2:25;
  A3: g is bijective by A1,GROUP_6:63;
  then A4: dom g = Y & rng g = X & g is one-to-one by FUNCT_2:def 1,def 3;
  g is one-to-one & rng g = X implies g" is Function of X,Y
  by FUNCT_2:25;
  then reconsider h = g" as Function of X,Y by A3,FUNCT_2:def 1, def 3,25;

  for x being object st x in X holds h.x = f.x
  <<Proof: $\forall x, x\in X\implies h(x) = f(x)$>>
  then h = f by FUNCT_2:12;
  hence (f")" = f;
end;

@ %def Lm10

\begin{proof}[Proof step ($\forall x\in X, h(x)=f(x)$)]
  Let $x\in X$ be arbitrary. Consider
  \begin{subequations}\label{eq:characteristic:preparatory:lm-finvinv-is-inv}
  \begin{equation}
    y=f(x).
  \end{equation}
  Then $x=g(y)$ --- i.e.,
  $x = f^{-1}(y)$ --- implies
  \begin{equation}
  h(x) = g^{-1}(x) = y.
  \end{equation}
  \end{subequations}
  But since $y=y$ we from
  Eqs~\eqref{eq:characteristic:preparatory:lm-finvinv-is-inv}
  prove $h(x)=f(x)$.
\end{proof}

<<Proof: $\forall x, x\in X\implies h(x) = f(x)$>>=
proof
  let x be object;
  assume Z1: x in X;
  then reconsider x as Element of X;
  consider y being object such that
  Z2: y = f.x
  by FUNCT_2:5;
  Z3: g is one-to-one & y in dom g by A4, Z2;
  f is one-to-one & x in dom f by A1, Z1, A2;
  then Z4: x = (f").y by Z2,FUNCT_2:26
            .= g.y;
  h.x = (g").x
     .= (g").(g.y) by Z4
     .= y by Z3,FUNCT_2:26;
  then h.x = f.x by Z2;
  hence thesis;
end;

@

\begin{lemma}\label{lemma:characteristic:preparatory:f-finv-y-is-y}
If $f\colon X\to Y$ is a bijective function of sets, then for any $y\in Y$
we have $f(f^{-1}(y))=y$.
\end{lemma}

\begin{proof}[Proof sketch]
Let $f\colon X\to Y$ be bijective. Then $g\colon Y\to X$ given by
$g=f^{-1}$ is bijective. Mizar knows $g^{-1}(g(y))=y$ for $y=f(x)$. Then
plugging in the definition of $g$ and using
Lemma~\ref{lm:characteristic:preparatory:lm10} to transform
$(f^{-1})^{-1}=f$, together gives the result.
\end{proof}

\begin{lem-remark}
Mizar has the opposite result in its library, namely, Theorem~\mml[Th26]{funct2}\MizThm{FUNCT\_2}{26}
states that $f^{-1}(f(x))=x$ provided $f$ is injective.
\end{lem-remark}

<<Lemma: if $f\colon X\to Y$ is bijective, then $f\circ f^{-1}=\id_{Y}$>>=
Lm11:
for X,Y being non empty set
for f being Function of X,Y
st f is bijective
for y being Element of Y
holds f.((f").y) = y
proof
  let X,Y be non empty set;
  let f be Function of X,Y;
  assume A1: f is bijective;
  let y be Element of Y;
  f is onto by A1;
  then reconsider g = f" as Function of Y,X by A1,FUNCT_2:25;
  g is bijective by A1, GROUP_6:63;
  then y = (g").(g.y) by FUNCT_2:26
        .= ((f")").((f").y)
        .= f.((f").y) by Lm10,A1;
  hence thesis by Lm10;
end;

@ %def Lm11

\begin{lemma}
  If $f\colon X\to Y$ is a bijective function of sets,
  then its inverse function $f^{-1}\colon Y\to X$ exists (as defined by
  $f\circ f^{-1}=\id_{Y}$ and $f^{-1}\circ f=\id_{X}$).
\end{lemma}

\begin{proof}[Proof sketch]
We need to prove two results. We have, for any $y\in Y$,
$f(f^{-1}(y))=y$ establish the result that $f\circ f^{-1}=\id_{Y}$. The
other result follows from, for any $x\in X$, that $f^{-1}(f(x))=x$ which
implies $f^{-1}\circ f=\id_{X}$.
\end{proof}

<<Lemma: bijections have inverses>>=
Lm12:
for X,Y being non empty set
for f being Function of X,Y
st f is bijective
holds f*(f") = id Y & (f")*f = id X
proof
  let X,Y be non empty set;
  let f be Function of X,Y;
  assume A1: f is bijective;
  then consider g being Function of Y,X such that
  A2: g = f" and
  A3: g is bijective
  by Lm13;
  A4: g" = f by A1,A2,A3,Lm10;
  for y being object st y in Y holds (f*(f")).y = (id Y).y
  <<Proof: $\forall y, y\in Y\implies (f\circ f^{-1})(y) = \id_{Y}(y)$>>
  then f*g = id Y by A2,FUNCT_2:12;
  hence f*(f") = id Y by A2,FUNCT_2:12;
  for x being object st x in X holds ((f")*f).x = (id X).x
  <<Proof: $\forall x,x\in X\implies (f^{-1}\circ f)(x) = \id_{X}(x)$>>
  then g*f = id X by A2,FUNCT_2:12;
  hence (f")*f = id X by A2,FUNCT_2:12;
end;

@ %def Lm12

\begin{proof}[Proof step ($\forall y, y\in Y\implies (f\circ f^{-1})(y) = \id_{Y}(y)$)]
The result follows from $(f\circ f^{-1})(y)=f(f^{-1}(y))=y$.
\end{proof}

<<Proof: $\forall y, y\in Y\implies (f\circ f^{-1})(y) = \id_{Y}(y)$>>=
proof
  let y be object;
  assume Z1: y in Y;
  then (f*g).y=f.(g.y) by FUNCT_2:15
             .= y by A2,A3,A4,Z1, FUNCT_2:26
             .= (id Y).y by Z1,FUNCT_1:18;
  hence thesis by A2;
end;

@

\begin{proof}[Proof step ($\forall x,x\in X\implies (f^{-1}\circ f)(x) = \id_{X}(x)$)]
The result follows from $(f^{-1}\circ f)(x) = f^{-1}(f(x))=x$.
\end{proof}

<<Proof: $\forall x,x\in X\implies (f^{-1}\circ f)(x) = \id_{X}(x)$>>=
proof
  let x be object;
  assume Z1: x in X;
  then (g*f).x = g.(f.x) by FUNCT_2:15
              .= x by A1,A2,Z1,FUNCT_2:26
              .= (id X).x by Z1,FUNCT_1:18;
  hence thesis by A2;
end;

@

\begin{lemma}
If $f\colon X\to Y$ is bijective, then there exists an inverse function
$f^{-1}\colon Y\to X$ and $f^{-1}\colon Y\to X$ is bijective.
\end{lemma}

\begin{proof}[Proof sketch]
This follows from Theorem~\mml[Th63]{group6}\MizThm{GROUP\_6}{63} which
asserts $f$ is bijective and $g=f^{-1}$ together implies $g$ is
bijective. We just need to prove the existence of $g=f^{-1}$, which is
handled by Theorem~\mml[Th25]{funct2}.\MizThm{FUNCT\_2}{25}
\end{proof}

<<Lemma: if $f\colon X\to Y$ is bijective, then there exists an inverse function and $f^{-1}\colon Y\to X$ is bijective>>=
Lm13:
for X,Y being non empty set
for f being Function of X,Y
st f is bijective
holds ex g being Function of Y,X st g = f" & g is bijective
proof
  let X,Y be non empty set;
  let f be Function of X,Y;
  assume A1: f is bijective;
  then dom f = X & rng f = Y & f is one-to-one & f is onto by FUNCT_2:def 3,def 1;
  then reconsider g = f" as Function of Y,X by FUNCT_2:25;
  take g;
  thus g = f";
  thus g is bijective by A1, GROUP_6:63;
end;

@ %def Lm13

\subsection{Nontrivial Groups}
We will be using nontrivial groups later. Recall, a group $G$ is
nontrivial if $G\neq\trivialSubgroup_{G}$. It is defined (or
\emph{overloaded}) in \mml[def2]{group6}\MizDef{GROUP\_6}[02]{2}:

\begin{mizar}
definition
  let G be non empty 1-sorted;
  redefine attr G is trivial means
  :Def2:
  ex x being object st the carrier of G = {x};
  compatibility
  :: ...
end;
\end{mizar}

We register the negated version ``\texttt{non trivial}'' for groups and
subgroups.


\begin{proposition}[{\mml[Th3]{group1}\MizThm{GROUP\_1}[03]{3}}]
The real numbers equipped with addition form a group.
\end{proposition}

\begin{remark}
We will need this to prove the existence of nontrivial groups and, later,
serve as an example of a nontrivial group with a proper subgroup.
\end{remark}

\begin{registration}
We have a notion of ``\texttt{non trivial}'' groups, and at least one
exists (namely, the real numbers as an Abelian group).
\end{registration}

<<Register: non trivial for Group>>=
registration
  cluster non trivial for Group;
  existence
  proof
    reconsider G = multMagma (# REAL, addreal #) as Group by GROUP_1:3;
    take G;
    the carrier of G <> {1_G};
    hence not (G is trivial);
  end;
end;

@ %def trivial

\begin{registration}\index{Trivial!Subgroup}\index{Trivial!Group}
We need to register the adjective ``trivial'' for groups and
subgroups. \emph{Every} group --- strict or not, trivial or not, hairy
or bald --- has a trivial subgroup. Similarly, \emph{every} subgroup has
a trivial subgroup.
\end{registration}

<<Register: trivial groups and trivial subgroups>>=
registration
  let G be Group;
  cluster trivial for Subgroup of G;
  existence
  proof
    take (1).G;
    thus thesis by GROUP_6:10;
  end;
  let H be Subgroup of G;
  cluster trivial for Subgroup of H;
  existence
  proof
    take (1).H;
    thus thesis by GROUP_6:10;
  end;
end;

@ %def trivial

\begin{registration}
For any non trivial group $G$, we can find a nontrivial subgroup
$H\subgroup G$, namely $G$ itself.
\end{registration}

<<Register: non trivial for Subgroup>>=
registration
  let G be non trivial Group;
  cluster non trivial for Subgroup of G;
  existence
  proof
    A1: the carrier of G <> {1_G} by GROUP_6:def 2;
    reconsider H=G as Subgroup of G by GROUP_2:54;
    the carrier of H <> {1_G} by A1;
    hence thesis;
  end;
end;

@

\subsection{Proper Subgroups}
When we have a [nontrivial] group $G$, we can discuss the notion of a
proper subgroup $H\properSubgroup G$ in analogy to the notion of a
proper subset $X\properSubset Y$.


\begin{definition}
Let $G$ be a group. We call a subgroup $H\subgroup G$ \define{Proper} if
$H\neq G$. We typically denote $H\properSubgroup G$ to reflect it is proper.
\end{definition}

\begin{def-remark}
The implementation for Mizar is a bit quirky. I looked at how maximal
subgroups were defined, because maximal subgroups are necessarily
proper. Maximal subgroups were defined (\mml[def 6]{group4}\MizDef{GROUP\_4}[06]{6})
using the condition \lstinline|the multMagma of H <> the multMagma of G|.
\end{def-remark}

<<Definition: proper subgroup>>=
definition
  let G be Group;
  let IT be Subgroup of G;
  attr IT is proper means
  :Def10:
  the multMagma of IT <> the multMagma of G;
end;
reserve G for Group;
reserve H for Subgroup of G;

@ %def Def10 proper

\begin{registration}
For any nontrivial group $G$, we can find a proper subgroup $H\subgroup G$,
namely the trivial subgroup $H=\trivialSubgroup_{G}$.
\end{registration}

<<Register: proper subgroup for nontrivial groups>>=

registration
  let G be non trivial Group;
  cluster proper for Subgroup of G;
  existence
  proof
    take (1).G;
    G is non trivial;
    then not (ex x being object st the carrier of G = {x}) by GROUP_6:def 2;
    then the carrier of G <> {1_G};
    then the carrier of G <> the carrier of (1).G;
    hence (1).G is proper;
  end;
end;

reserve G for non trivial Group;
reserve H for Subgroup of G;

@

\begin{lemma}
Let $H\subgroup G$ be a subgroup. Then $H\properSubgroup G$ is proper if
and only if the underlying set of $G$ differs from the underlying set of $H$.
\end{lemma}

\begin{proof}[Proof outline]
The only way a group could be different, since they are magmas
satisfying some properties, is either if the underlying set differs or
if the group operation differs. But since we know $H$ is a subgroup of
$G$, we know the group operation for $H$ is just the restricted version
of the group operation for $G$. Thus we are forced to accept the
underlying sets must be different. This argument works backwards as well
as forwards.
\end{proof}

<<Lemma: $H\subgroup G$ is proper iff the underlying sets are different>>=
Lm99:
  H is proper iff the carrier of H <> the carrier of G
proof
  H is Subgroup of G;
  then (the carrier of H c= the carrier of G) & (the multF of H =
  (the multF of G)||(the carrier of H)) by GROUP_2:def 5;
  hence thesis;
end;

@ %def Lm99

\begin{lemma}
  Let $H\subgroup G$ be a subgroup.
  Then $H$ is a proper subgroup if and only if the set difference is nonempty
  $G\setminus H\neq\emptyset$.
\end{lemma}

\begin{lem-remark}
This version, as stated, is a little sloppy. We should more precisely
state the set difference of the underlying set $U(G)$ of $G$ with the
underlying set $U(H)$ of $H$ is nonempty $U(G)\setminus U(H)\neq\emptyset$.
\end{lem-remark}

<<Lemma: $H\subgroup G$ is proper iff $G\setminus H\neq\emptyset$>>=
reserve h,x,y for object;

Lm100:
  H is proper iff (the carrier of G) \ (the carrier of H) is non empty set
proof
  set UG = the carrier of G;
  set UH = the carrier of H;
  thus H is proper implies (the carrier of G) \ (the carrier of H) is non empty set
  <<Sub-proof: $H\properSubgroup G\implies G\setminus H\neq\emptyset$>>
  thus (the carrier of G) \ (the carrier of H) is non empty set implies H is proper
  <<Sub-proof: $H\properSubgroup G\impliedby G\setminus H\neq\emptyset$>>
  thus thesis;
end;

@ %def Lm100

\begin{proof}[Sub-proof outline]
  If $H\properSubgroup G$ is a proper subgroup, then the underlying set
  $U(H)$ of $H$ is a subset of the underlying set $U(G)$ of $G$ --- i.e.,
  $U(H)\subset U(G)$ --- and $U(H)\neq U(G)$. Then there is some element
  $g\in G$ for which $g\notin H$. Then $U(G)\setminus U(H)\neq\emptyset$.
\end{proof}

<<Sub-proof: $H\properSubgroup G\implies G\setminus H\neq\emptyset$>>=
proof
  assume A1: H is proper;
  UH c= UG & UH <> UG by A1,Lm99, GROUP_2:def 5;
  then (for x holds x in UH implies x in UG) &
  the carrier of H <> the carrier of G by TARSKI:def 3;
  then (for x holds x in UH implies x in UG) &
  not (for x holds x in UH iff x in UG) by TARSKI:2;
  hence (the carrier of G) \ (the carrier of H) is non empty set
  by XBOOLE_0:def 5;
end;

@

\begin{proof}[Sub-proof outline]
Assume $G\setminus H\neq\emptyset$. Then there exists some $y\in
G\setminus H$, i.e., $y\in G$ and $y\notin H$. But we've found an
element in $G$ that's not in $H$. So by the extensional notion of set
equality, these are clearly different sets. Thus $H\properSubgroup G$
\end{proof}

<<Sub-proof: $H\properSubgroup G\impliedby G\setminus H\neq\emptyset$>>=
proof
  assume A1: (the carrier of G) \ (the carrier of H) is non empty set;
  set GH = UG \ UH;
  A2: UH c= UG by GROUP_2:def 5;
  not (GH is empty) by A1;
  then A3: ex x st x in GH by XBOOLE_0:def 1;
  then consider y such that
  A4: y in GH;
  y in UG & not y in UH by A4, XBOOLE_0:def 5;
  then UG <> UH by TARSKI:2;
  then the multMagma of G <> the multMagma of H by GROUP_2:def 5;
  hence H is proper by Def10;
end;

@

\begin{registration}
For any nontrivial group $G$, we can call \define{Proper Subgroup} of
$G$ a subgroup which is proper.
\end{registration}

<<Register: proper subgroup for nontrivial groups>>=
registration
  let G be non trivial Group;
  cluster proper for Subgroup of G;
  existence
  proof
    take (1).G;
    G is non trivial;
    then not (ex x being object st the carrier of G = {x}) by GROUP_6:def 2;
    then the carrier of G <> {1_G};
    then the carrier of G <> the carrier of (1).G;
    hence (1).G is proper;
  end;
end;

@

\begin{lemma}
Maximal subgroups are also proper subgroups.
\end{lemma}

The proof is literally, ``Look at the definitions!''

<<Lemma: maximal subgroups are proper>>=
Lm98:
  for G being Group
  for H being Subgroup of G
  st H is maximal
  holds H is proper
proof
  let H be Subgroup of G;
  assume H is maximal;
  hence H is proper by Def10,GROUP_4:def 6;
end;

@ %def Lm98

\begin{registration}
We can now automatically take advantage of the fact that, a maximal
subgroup for a nontrivial group is implicitly a proper subgroup.
\end{registration}

<<Register: maximal subgroups are proper>>=
registration
  let G be non trivial Group;
  cluster maximal -> proper for Subgroup of G;
  coherence by Lm98;
end;

@

\begin{lemma}
If $H\properSubgroup G$ is a proper subgroup, then there exists some
$x\in G$ such that $x\notin H$.
\end{lemma}

<<Lemma: $H\subgroup G$ is proper implies $\exists x\in G, x\notin H$>>=
Lm101:
  for H being Subgroup of G
  st H is proper Subgroup of G
  holds ex x being Element of G st not (x in H)
proof
  let H be Subgroup of G;
  assume A1: H is proper Subgroup of G;
  then (the carrier of G) \ (the carrier of H) is non empty by Lm100;
  then consider x being object such that
  A2: x in (the carrier of G) \ (the carrier of H)
  by XBOOLE_0:def 1;
  reconsider x as Element of G by A2, STRUCT_0:def 5;
  take x;
  thus not (x in H) by A2, STRUCT_0:def 5, XBOOLE_0:def 5;
end;

@ %def Lm101

\begin{lemma}\label{lemma:characteristic:preparatory:x-notin-A-implies-fx-notin-fA}
Let $f\colon X\into Y$ be an injective function of non-empty sets, let
$A\subset X$ be a non-empty subset, let $x\in X$ be any element such
that $x\notin A$. Then $f(x)\notin f(A)$.
\end{lemma}

<<Lemma: $f\colon X\into Y$ and $x\notin A\subset X$ implies $f(x)\notin f(A)$>>=
Lm18:
  for X,Y being non empty set
  for A being non empty Subset of X
  for x being Element of X
  st not x in A
  for f being Function of X,Y
  st f is one-to-one
  holds not f.x in (f .: A)
proof
  let X,Y be non empty set;
  let A be non empty Subset of X;
  let x be Element of X;
  assume A1: not x in A;
  let f be Function of X,Y;
  assume f is one-to-one;
  then A2: not ex y being object st x in dom f & y in dom f & y in A & f.y = f.x
  by A1, FUNCT_1:def 4;
  dom f = X by FUNCT_2:def 1;
  then A3: x in dom f;
  f.x in (f .: A) iff ex a being object st a in dom f & a in A & f.x = f.a
  by FUNCT_1:def 6;
  then f.x in (f .: A) implies contradiction by A2,A3;
  hence not (f.x in (f .: A));
end;

@ %def Lm18

\begin{lemma}
If $H\subgroup G$ is a subgroup such that there is an $x\in G$ but
$x\notin H$, then $H$ is a proper subgroup of $G$.
\end{lemma}

<<Lemma: $H\subgroup G$ and $\exists x\in G,x\notin H$ implies $H$ is proper>>=
Lm101:
  for H being Subgroup of G
  st (ex x being Element of G st not (x in H))
  holds H is proper Subgroup of G
proof
  let H be Subgroup of G;
  given x being Element of G such that
  A1: not (x in H);
  set UG = the carrier of G;
  set UH = the carrier of H;
  x in G & not (x in H) by A1,STRUCT_0:def 5;
  then x in UG & not (x in UH);
  then x in (UG \ UH) by XBOOLE_0:def 5;
  then (UG \ UH) is non empty by XBOOLE_0:def 1;
  hence H is proper Subgroup of G by Lm100;
end;

@ %def Lm101

<<Lemma: trivial groups look like $\trivialSubgroup$>>=
LmGroupIsTrivialIff1:
  for G being Group
  holds G is trivial iff the multMagma of G = (1).G
proof
  let G be Group;
  thus G is trivial implies the multMagma of G = (1).G
  proof
    assume G is trivial;
    then consider x being object such that
    A1: the carrier of G = {x}
    by GROUP_6:def 2;
    :: prove x = 1_G
    1_G in G;
    then x = 1_G by A1, TARSKI:def 1, STRUCT_0:def 5;
    then the carrier of G = {1_G} by A1;
    then the carrier of G = the carrier of (1).G by GROUP_2:def 7, TARSKI:2;
    hence the multMagma of G = (1).G by GROUP_2:61;
  end;
  thus the multMagma of G = (1).G implies G is trivial by GROUP_6:10;
  thus thesis;
end;

@

\begin{lemma}
If $G=\trivialSubgroup$ is trivial, then it has no proper subgroups.
\end{lemma}

\begin{lem-remark}
The argument that two sets $X$ and $Y$ are equal iff they are subsets of
each other $X\subset Y\land Y\subset X$ is precisely what
\mml[def10]{xboole0}\MizDef{XBOOLE\_0}{10} offers.
\end{lem-remark}

<<Lemma: Trivial groups have no proper subgroups>>=
LmTrivHasNoProperSubgroups:
  for G being Group
  st G is trivial
  holds not (ex H being Subgroup of G st H is proper)
proof
  let G be Group;
  assume A1: G is trivial;
  then A2: the multMagma of G = (1).G
  by LmGroupIsTrivialIff1;
  then A3: the carrier of G = {1_G} by GROUP_2:def 7;
  A4: for H being Subgroup of G holds not H is proper
  proof
    let H be Subgroup of G;
    B1: the carrier of H c= {1_G} by A3, GROUP_2:def 5;
    (1).G is Subgroup of H by GROUP_2:65;
    then the carrier of (1).G c= the carrier of H by GROUP_2:def 5;
    then {1_G} c= the carrier of H by GROUP_2:def 7;
    then {1_G} c= the carrier of H & the carrier of H c= {1_G} by B1;
    then the carrier of H = {1_G} by XBOOLE_0:def 10;
    then the carrier of H = the carrier of G by A3;
    hence not H is proper by Lm99;
  end;
  hence not (ex H being Subgroup of G st H is proper);
end;

@

\begin{lemma}
If $G$ is trivial, then it cannot have a maximal subgroup.
\end{lemma}

Certainly, if there is no hope for $G$ having a proper subgroup, we
should have no hope for maximal subgroups.

<<Lemma: Trivial groups have no maximal subgroup>>=
for G being Group
st G is trivial
holds not (ex H being Subgroup of G st H is maximal)
proof
  let G be Group;
  assume A1: G is trivial;
  then not (ex H being Subgroup of G st H is proper) by LmTrivHasNoProperSubgroups;
  hence not (ex H being Subgroup of G st H is maximal) by Lm98;
end;

@
