% -*- mode: poly-noweb; noweb-code-mode: mizar-mode; -*-
% This is placed here so the LaTeX is organized correctly, thanks to
% problems with noweb :(
\section{Article Body}

\M
The article body is where the magic happens.

<<[[char.miz]] article body>>=
<<Helper lemmas and registrations for [[char.miz]]>>

<<Inner and outer automorphisms>>
<<Characteristic subgroup results>>

@

\subsection{Preparatory results}

<<Helper lemmas and registrations for [[char.miz]]>>=
begin :: preparatory work
reserve X for set;

<<Lemma: $f\colon X\onto Y\implies f(X)=Y$>>
<<Register: the identity function is surjective and bijective>>
<<Lemma: A group element in the subgroup is a subgroup element>>
<<Lemma: $g\in H\subgroup G$ and $K\subgroup G$, then $H\subgroup K\implies g\in K$>>
<<Lemma: restriction of group morphism acts on elements like the original>>
<<Lemma: Subgroups invariant under conjugation are normal>>
<<Lemma: Morphism is surjective if its image is its codomain>>
<<Lemma: $H\subgroup K\subgroup G_{1}$ and $f\colon G_{1}\to G_{2}$ is a morphism, then $(f|_{K})|_{H}=f|_{H}\colon H\to G_{2}$ is a morphism>>

@

\M
We need to prove, if $f\colon X\onto Y$ is a surjective function of
sets, then $f(X)=Y$.

<<Lemma: $f\colon X\onto Y\implies f(X)=Y$>>=
LmOntoMapsEverything:
for X,Y being non empty set
for f being Function of X,Y
st f is onto
holds f .: X = Y
proof
  let X,Y be non empty set;
  let f be Function of X,Y;
  assume f is onto;
  then A0: rng f = Y;
  A1: dom f = X by FUNCT_2:def 1;
  A2: for y being object
  holds y in rng f iff ex x being object st x in X & y = f.x
  by A1, FUNCT_1:def 3;
  A3: for y being object holds y in (f .: X) iff
  ex x being object st x in X & x in X & y = f.x
  by A1,FUNCT_1:def 6;

  B1: for y being object holds y in (f .: X) implies y in rng f
  <<Proof: $y\in f(X)\implies y\in\mathrm{rng}(f)$>>

  B2: for y being object holds y in rng f implies y in (f .: X)
  <<Proof: $y\in\mathrm{rng}(f)\implies y\in f(X)$>>
  for y being object holds y in rng f iff y in (f .: X) by B1,B2;
  then rng f = f .: X by TARSKI:2;
  hence f .: X = Y by A0;
end;

@ %def LmOntoMapsEverything

<<Proof: $y\in f(X)\implies y\in\mathrm{rng}(f)$>>=
proof
  let y be object;
  assume y in (f .: X);
  then consider x being object such that
  A10: x in X and x in X and A11: y = f.x
  by A3;
  thus y in rng f by A10,A11,A2;
end;

@

<<Proof: $y\in\mathrm{rng}(f)\implies y\in f(X)$>>=
proof
  let y be object;
  assume y in rng f;
  then consider x being object such that
  A20: x in X and A21: y = f.x
  by A2;
  thus y in (f .: X) by A20,A21,A3;
end;

@

\M
We begin by registering the identity function as being surjective and
bijective. This should have been done in [[FUNCT_2]], but hey, what can
you do?

<<Register: the identity function is surjective and bijective>>=
registration
  let X;
  cluster id X -> onto;
  coherence;
end;

registration
  let X;
  cluster id X -> bijective;
  coherence;
end;

@

\M
We also need to prove that, if we have a subgroup $H\subgroup G$ and
$h\in G$ is an arbitrary element of the underlying set, then $h\in H$
implies the typing judgement ``[[h is Element of H]]''.

<<Lemma: A group element in the subgroup is a subgroup element>>=
LmSubgroupElt:
for G being Group
for H being Subgroup of G
for h being Element of G st h in H holds h is Element of H
proof
  let G be Group,
      H be Subgroup of G,
      h be Element of G;
  assume h in H;
  then h is Element of the carrier of H by STRUCT_0:def 5;
  then h is Element of H;
  hence thesis;
end;

@ %def LmSubgroupElt

\begin{lemma}
  Let $G$ be a group and $g\in G$ be any element of the group,
  $H_{1}\subgroup G$ and $H_{2}\subgroup G$ be subgroups.
  If $H_{1}\subgroup H_{2}$ and $g\in H_{1}$, then $g\in H_{2}$.
\end{lemma}

This is another ``Well, duh'' lemma that I couldn't find in Mizar's library.

<<Lemma: $g\in H\subgroup G$ and $K\subgroup G$, then $H\subgroup K\implies g\in K$>>=
LmSubgroupEltTrans:
  for G being Group
  for H1,H2 being Subgroup of G
  for g being Element of G
  st g in H1 & H1 is Subgroup of H2
  holds g in H2
proof
  let G be Group;
  let H1,H2 be Subgroup of G;
  let g be Element of G;
  assume A1: g in H1 & H1 is Subgroup of H2;
  then g is Element of H1 by LmSubgroupElt;
  then g in H2 by A1, GROUP_2:40;
  hence thesis;
end;

@ %def LmSubgroupEltTrans

\N{Restriction of Group Morphisms to Subgroups}
Mizar proves that, if $f\colon X\to Y$ is a set theoretic function and
$A\subset X$ is an arbitrary subset, then $\forall a\in X$ we have
$a\in A\implies f|_{A}(a)=f(a)$. But this doesn't generalize to
morphisms, sadly, because a group is like a chocolate-covered set.

So we just prove for any pair of groups $G_{1}$ and $G_{2}$,
for any subgroup $H\subgroup G_{1}$,
for any group morphism $f\colon G_{1}\to G_{2}$, and for arbitrary $h\in G_{1}$,
we have $h\in H\implies f|_{H}(h) = f(h)$. The reasoning is that we can
always look at the set-theoretic function $U(f)$ underlying $f$, then
piggie-back off earlier results establishing the desired claim
(Theorem~49 of [[FUNCT_1]], to be precise).

<<Lemma: restriction of group morphism acts on elements like the original>>=
Lm3: for G1,G2 being Group
for H being Subgroup of G1
for f being Homomorphism of G1,G2
for h being Element of G1
st h in H
holds (f|H).h = f.h
proof
  let G1,G2 be Group;
  let H be Subgroup of G1;
  let f be Homomorphism of G1,G2;
  let h be Element of G1;
  assume h in H;
  then h is Element of H by LmSubgroupElt;
  then (f|(the carrier of H)).h = f.h by FUNCT_1:49;
  then (f|H).h = f.h by GRSOLV_1:def 2;
  hence thesis;
end;

@ %def Lm3

\M
Let $H_{1}\subgroup G$ and $H_{2}\subgroup G$.
If $H_{1}\subgroup H_{2}$ and $H_{2}\subgroup H_{1}$, then
$H_{1}=H_{2}$. Pretty simple, right? \dots I'm not sure I'll need this,
but here it is if I do.

<<Lemma: Sandwich Subgroup Equality Theorem>>=
LmSandwich: for H1 being strict Subgroup of G
for H2 being strict Subgroup of G
st (H1 is Subgroup of H2) & (H2 is Subgroup of H1)
holds H1 = H2
proof
  let H1 be strict Subgroup of G;
  let H2 be strict Subgroup of G;
  assume H1 is Subgroup of H2 & H2 is Subgroup of H1;
  then the multMagma of H1 = the multMagma of H2 by GROUP_2:55;
  hence thesis;
end;

@ %def LmSandwich

\begin{remark}[On ``strict'' mathematical gadgets]\index{strict@\texttt{strict}|textbf}
Mizar has a notion of a ``[[strict]]'' attribute. What does it mean?
Well, sometimes a mathematical gadget is ``composite''; like a vector
space over a field is an Abelian group with vector addition, but it also
has other stuff (like scalar multiplication). This ``other stuff''
prevents it from being a ``bald-faced Abelian group''. Just as black
holes have no hair, strict groups have no hair: it's just a
[[multMagma]] satisfying the group properties (in Mizar, at least).
\end{remark}

\M Let $H\subgroup G$ be such that $\forall a\in G, a^{-1}Ha=H$.
Then $H\normalSubgroup G$ is a normal subgroup. The current theorems in
[[GROUP_3]] require $H$ to be a \emph{strict} subgroup, but this
strictness condition is not necessary.

<<Lemma: Subgroups invariant under conjugation are normal>>=
LmNormalSubgroup:
for G being Group
for H being Subgroup of G
st (for a being Element of G holds H |^ a = H)
holds H is normal Subgroup of G
proof
  let G be Group;
  let H be Subgroup of G;
  assume A0: for a being Element of G holds H |^ a = H;
  then for a being Element of G holds the carrier of H |^ a = the carrier of H;
  then for a being Element of G holds the multMagma of H = H |^ a by GROUP_2:59;
  hence H is normal Subgroup of G by GROUP_3:def 13;
end;

@ %def LmNormalSubgroup

\begin{theorem}
  Let $\varphi\colon G_{1}\to G_{2}$ be a group morphism.
  If $\varphi(G_{1})=G_{2}$, then $\varphi$ is surjective.
\end{theorem}

Mizar has this lemma in its library, but it has a more stringent
requirement that $G_{2}$ be a [[strict Group]], which is totally unnecessary.

<<Lemma: Morphism is surjective if its image is its codomain>>=
LmEpi:
  for G1,G2 being Group
  for f being Homomorphism of G1,G2
  st Image(f) = G2
  holds f is onto
proof
  let G1,G2 be Group;
  let f be Homomorphism of G1,G2;
  assume A1: Image(f) = G2;
  then rng f = the carrier of Image f by GROUP_6:44
  .= the carrier of G2 by A1;
  then f is onto by FUNCT_2:def 3;
  hence thesis;
end;

@

\begin{lemma}
Let $G_{1}$ and $G_{2}$ be groups, $K\subgroup G_{1}$ and $H\subgroup K$
be subgroups. If $f\colon G_{1}\to G_{2}$ is a group morphism, then
$(f|_{K})|_{H}=f|_{H}\colon H\to G_{2}$ is a group morphism.
\end{lemma}

Something like this was proven in [[FUNCT_1:50]], but I couldn't apply
that theorem (not even to functions of sets).

<<Lemma: $H\subgroup K\subgroup G_{1}$ and $f\colon G_{1}\to G_{2}$ is a morphism, then $(f|_{K})|_{H}=f|_{H}\colon H\to G_{2}$ is a morphism>>=
LmRestrictCollapse:
for G,G2 being Group
for K being Subgroup of G
for H being Subgroup of K
for f being Homomorphism of G,G2
holds (f|K)|H = f|H
proof
  let G,G2 be Group;
  let K be Subgroup of G;
  let H be Subgroup of K;
  let f be Homomorphism of G,G2;
  set UG = the carrier of G;
  set UK = the carrier of K;
  set UH = the carrier of H;
  UH c= UK by GROUP_2:def 5;
  then C1: (f|UK)|UH = f|UH by RELAT_1:73,74;
  f|K is Homomorphism of K,G2;
  (f|K)|H is Homomorphism of H,G2;
  C2: (f|UK) = f|K &
  f|UH = f|H by GRSOLV_1:def 2;
  (f|K)|H = (f|K)|UH by GRSOLV_1:def 2
  .= (f|UK)|UH by GRSOLV_1:def 2;
  then (f|K)|H = f|H by C1,C2;
  hence thesis;
end;

@
