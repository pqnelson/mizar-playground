% -*- mode: poly-noweb; noweb-code-mode: mizar-mode; -*-

\section{Centralizers of Characteristic Subgroups}\label{sec:characteristic:centralizer}

<<Centralizers of Characteristic Subgroups>>=
<<Definition: Centralizer of Subset>>

<<Theorem: $A\subset G$ and $g\in G$, have $g\in C_{G}(A)\iff(\forall a\in A, ga=ag)$>>

<<Theorem: $A\subset B\subset G\implies C_{G}(B)\subgroup C_{G}(A)$>>

<<Definition: Centralizer of Subgroup>>

<<Theorem: carrier of $C_{G}(H) = \{b\in G\mid\forall a\in H, ba=ab\}$>>

<<Theorem: Let $g\in G$. Then $g\in C_{G}(H)\iff\forall h\in H,gh=hg$>>

<<Theorem: $A\subset G\implies A\subset C_{G}(C_{G}(A))$>>

<<Theorem: Centralizer of characteristic subgroups is characteristic>>

<<Definition: $\forall a\in G$, $\{a\}\subset G$>>

<<Theorem: $\{x\}=\{y\}\iff x=y$>>

<<Definition: Normalizer of group element>>

<<Theorem: $h\in N_{G}(a)\iff a^{h}=a$>>

<<Theorem: $A\subset G$, $C_{G}(A) = \bigcap_{a\in A}N_{G}(a)$>>

<<Theorem: $|H_{1}\cap H_{2}|=|H_{1}|=|H_{2}|\implies H_{1}=H_{2}$>>

<<Theorem: $\forall a,b,c\in\NN, c\neq0\land c|a\land c|b\implies a|b$>>

<<Theorem: $a,b,c\in\NN, b|c\land\gcd(ab,c)=1\implies b=1$>>

<<Theorem: $G_{1}/N_{1}\iso G_{2}/N_{2}\implies |N_{2}|\cdot|G_{1}|=|N_{1}|\cdot|G_{2}|$>>

<<Theorem: $K,N\normalSubgroup G\implies |KN|\cdot|K\cap N|=|K|\cdot|N|$>>

<<Theorem: $f_{2}(f_{1}(A))=(f_{2}\circ f_{1})(A)$ for group morphisms>>

<<Theorem: $N\normalSubgroup G$ with $|N|$ and $[G:N]$ coprime implies $N$ is characteristic>>

<<Theorem: $\varphi\in\Aut(G)$, $\varphi(N)=N$, $\exists\sigma\in\Aut(G/N),\sigma(xN)=\varphi(x)N$>>

<<Theorem: $H$ char $G$ and $H\subgroup K\subgroup G$, then $H\normalSubgroup K$>>

<<Theorem: $H\subgroup K\subgroup G$, $H$ char $G$, $K/H$ char $G/H$ implies $K$ is characteristic>>

<<Theorem: $H\subgroup G$, $H\subgroup C_{G}(H)\iff$ $H$ is commutative>>

<<Theorem: $C_{G}(G)=Z(G)$>>

<<Theorem: $N\normalSubgroup G\implies C_{G}(H)\normalSubgroup G$>>

<<Theorem: $\forall h\in H,n\in N_{G}(H), n^{-1}hn\in H$>>

<<Theorem: $\forall H\subgroup G, H\subgroup N_{G}(H)$>>

<<Lemma: $C_{G}(H)\subgroup N_{G}(H)$>>

<<Theorem: $C_{G}(H)\normalSubgroup N_{G}(H)$>>
@

\begin{definition}\label{defn:characteristic-subgroup:centralizer-of-subset}
  Let $G$ be a group, let $A\subset G$ be a subset of $G$.
  We define the \define{Centralizer} of $A$ to be the subgroup of $G$
  given by
  \begin{equation}
C_{G}(A) = \{g\in G\mid \forall a\in A, ag=ga\}.
  \end{equation}
\end{definition}

\begin{def-remark}
We not only need to prove the existence of centralizers, but also the
uniqueness (since we speak of \emph{the} centralizer of $A$).
\end{def-remark}
\begin{def-remark}
Note that \mml{weddwitt} defines the centralizer of a group \emph{element},
but nothing further (well, nothing further about group centralizers).
\end{def-remark}

\begin{proof}[Proof sketch of existence]
Basically, we have four steps to proving the existence of a centralizer
subgroup $C_{G}(A)$:
\begin{enumerate}
\item it contains the identity element $1_{G}\in C_{G}(A)$;
\item its carrier is a subset of $G$, $C_{G}(A)\subset G$;
\item it is closed under the group operation;
\item it is closed under inversion.
\end{enumerate}
Then from these claims, it follows $C_{G}(A)$ is a subgroup.
\end{proof}

\begin{proof}[Proof sketch of uniqueness]
Suppose we have two subgroups $H_{1}\subgroup G$ and $H_{2}\subgroup G$
satisfying the definition of being a centralizer of $A$.
Then $H_{1}=H_{2}$ since they contain the same elements.
\end{proof}

<<Definition: Centralizer of Subset>>=
definition
  let G be Group;
  let A be Subset of G;
  func Centralizer A -> strict Subgroup of G means
  :Def4:
  the carrier of it = { b where b is Element of G :
                        for a being Element of G st a in A holds a*b = b*a };
  existence
  proof
    set Car = {b where b is Element of G :
               for a being Element of G st a in A holds a*b = b*a };
    C1: 1_G in Car
    <<Proof: $1_{G}\in C_{G}(A)$>>

    for x being object st x in Car holds x in the carrier of G
    <<Proof: $\forall x, x\in C_{G}(A)\implies x\in G$>>
    then C2: Car is Subset of G by TARSKI:def 3;

    C3: for g1,g2 being Element of G st g1 in Car & g2 in Car
    holds g1*g2 in Car
    <<Proof: $C_{G}(A)$ closed under multiplication>>

    C4: for g being Element of G st g in Car holds g" in Car
    <<Proof: $\forall g\in C_{G}(A), g^{-1}\in C_{G}(A)$>>

    thus thesis by C1,C2,C3,C4,GROUP_2:52;
  end;
  uniqueness
  proof
    let H1,H2 be strict Subgroup of G such that
A1: the carrier of H1 = {b where b is Element of G : for a being Element of G
                                                     st a in A
                                                     holds a*b = b*a } and
A2: the carrier of H2 = {b where b is Element of G : for a being Element of G
                                                     st a in A
                                                     holds a*b = b*a };
    for g being Element of G holds g in H1 iff g in H2 by A1,A2;
    hence thesis;
  end;
end;
@ %def Centralizer Def4

<<Proof: $\forall x, x\in C_{G}(A)\implies x\in G$>>=
proof
  let x be object;
  assume x in Car;
  then ex g being Element of G
       st (x = g) & (for a being Element of G st a in A holds a*g = g*a);
  hence thesis;
end;

@

<<Proof: $1_{G}\in C_{G}(A)$>>=
proof
  for a being Element of G st a in A holds 1_G*a = a*1_G
  proof
    let a be Element of G;
    assume a in A;
    1_G*a = a by GROUP_1:def 4
         .= a*1_G by GROUP_1:def 4;
    hence thesis;
  end;
  hence thesis;
end;
@

<<Proof: $C_{G}(A)$ closed under multiplication>>=
proof
  let g1,g2 be Element of G;
  assume B1: g1 in Car;
  assume B2: g2 in Car;
  B3: ex z1 being Element of G st (z1 = g1) & (for a being Element of G
  st a in A holds a*z1 = z1*a) by B1;
  B4: ex z2 being Element of G st (z2 = g2) & (for a being Element of G
  st a in A holds a*z2 = z2*a) by B2;
  for a being Element of G st a in A holds a*(g1*g2)=(g1*g2)*a
  proof
    let a be Element of G;
    assume Z1: a in A;
    a*(g1*g2) = (a*g1)*g2 by GROUP_1:def 3
             .= (g1*a)*g2 by Z1,B3
             .= g1*(a*g2) by GROUP_1:def 3
             .= g1*(g2*a) by Z1,B4
             .= g1*g2*a by GROUP_1:def 3;
    hence thesis;
  end;
  hence thesis;
end;
@

<<Proof: $\forall g\in C_{G}(A), g^{-1}\in C_{G}(A)$>>=
proof
  let g be Element of G;
  assume Z1: g in Car;
  then Z2: ex z1 being Element of G st (z1 = g) & (for a being
  Element of G st a in A holds z1*a=a*z1);
  for a being Element of G st a in A holds g" * a = a * g"
  proof
    let a be Element of G;
    assume Z3: a in A;
    g" * ((a*g) * g") = g" * ((g*a) * g") by Z3,Z2
                     .= (g" * (g * a)) * g" by GROUP_1:def 3
                     .= ((g" * g) * a) * g"  by GROUP_1:def 3
                     .= (1_G * a) * g"  by GROUP_1:def 5
                     .= a * g"  by GROUP_1:def 4;
    then g" * a = a * g" by GROUP_3:1;
    hence thesis;
  end;
  hence thesis;
end;
@

\begin{theorem}
Let $G$ be a group, $A\subset G$ be any subset, $g\in G$ be any group element.
We have $g\in C_{G}(A)$ if and only if for any $a\in A$, $ag=ga$.
\end{theorem}

\begin{thm-remark}
This allows us to use the fact that $g\in C_{G}(A)$ and $a\in A$ implies
$ag=ga$.
\end{thm-remark}

<<Theorem: $A\subset G$ and $g\in G$, have $g\in C_{G}(A)\iff(\forall a\in A, ga=ag)$>>=
theorem Th57:
  for G being Group
  for A being Subset of G
  for g being Element of G
  holds (for a being Element of G st a in A holds g*a = a*g) iff
        g is Element of Centralizer A
proof
  let G be Group;
  let A be Subset of G;
  let g be Element of G;
A1: the carrier of Centralizer A = {b where b is Element of G : for a
  being Element of G st a in A holds b*a=a*b} by Def4;
  hereby
    assume for a being Element of G st a in A holds g*a = a*g;
    then g in the carrier of Centralizer A by A1;
    hence g is Element of Centralizer A;
  end;
  assume g is Element of Centralizer A;
  then g in the carrier of Centralizer A;
  then ex b being Element of G st (b = g) & (for a being Element of G st a in A
  holds b*a = a*b) by A1;
  hence thesis;
end;
@ %def Th57


\begin{theorem}
  Let $G$ be a group, let $A\subset B\subset G$ be subsets.
  Then $C_{G}(B)\subgroup C_{G}(A)$.
\end{theorem}

<<Theorem: $A\subset B\subset G\implies C_{G}(B)\subgroup C_{G}(A)$>>=
theorem Th58:
  for G being Group
  for A,B being Subset of G
  st A c= B
  holds Centralizer B is Subgroup of Centralizer A
proof
  let G be Group;
  let A,B be Subset of G;
  assume A1: A c= B;
  for g being Element of G st g in Centralizer B
  holds g in Centralizer A
  proof
    let g be Element of G;
    assume B1: g in Centralizer B;
    then for a being Element of G st a in A
    holds g*a = a*g by A1,Th57;
    then g is Element of Centralizer A by Th57;
    hence thesis;
  end;
  hence Centralizer B is Subgroup of Centralizer A by GROUP_2:58;
end;
@ %def Th58

\begin{definition}
Let $G$ be a group, $H\subgroup G$ be a subgroup.
We define the \define{Centralizer} of $H$ is the subgroup whose
underlying set is
\begin{equation}
C_{G}(H) = \{g\in G\mid \forall h\in H, hg=gh\}.
\end{equation}
\end{definition}

\begin{def-remark}
This basically amounts to Definition~\ref{defn:characteristic-subgroup:centralizer-of-subset}
applied to the underlying set of a subgroup of $G$. The proofs of
existence and uniqueness carry over.
\end{def-remark}
\begin{def-remark}
We also follow Mizar's example in Definition~\mml[def15]{group3} of the
normalizer of $H\subgroup G$, based off the
Definition~\mml[def14]{group3} of the normalizer for a subset $A\subset G$.
Consequently we only need a [[correctness;]] assertion. This is briefly
mentioned at the very end of \S2 of ``Mizar in a nutshell''.
\end{def-remark}

<<Definition: Centralizer of Subgroup>>=
definition
  let G be Group;
  let H be Subgroup of G;
  func Centralizer H -> strict Subgroup of G means
  :Def9:
  it = Centralizer carr H;
  correctness;
end;
@ %def Def9

\begin{theorem}
  Let $H\subgroup G$. Then the set underlying $C_{G}(H)$ is precisely
  $\{b\in G\mid\forall a\in H, ba=ab\}$.
\end{theorem}

\begin{thm-remark}
  This may seem silly and redundant, but we need to explicitly spell out
  what the underlying set of the centralizer for a subgroup \emph{is},
  if we want to use it later.
\end{thm-remark}

\begin{proof}[Proof outline]
  There are two steps to the proof. First, we prove
  $C_{G}(H) \subset \{b\in G\mid\forall a\in H, ba=ab\}$.
  Next we prove $\{b\in G\mid\forall a\in H, ba=ab\}\subset C_{G}(H)$.
  The result follows.
\end{proof}

<<Theorem: carrier of $C_{G}(H) = \{b\in G\mid\forall a\in H, ba=ab\}$>>=
theorem Th59:
  for G being Group
  for H being Subgroup of G
  holds the carrier of Centralizer H = {b where b is Element of G : for a
  being Element of G st a in H holds b*a=a*b}
proof
  let G be Group;
  let H be Subgroup of G;
  set A = carr H;
  set Car = {b where b is Element of G : for a being Element of G st a in H
                                         holds b*a=a*b};

  A1: the carrier of Centralizer A = {b where b is Element of G : for a
  being Element of G st a in A holds b*a=a*b} by Def4;
  for x being object st x in Car holds x in the carrier of Centralizer H
  proof
    let x be object;
    assume B1: x in Car;
    ex b being Element of G
    st (x = b) & (for a being Element of G st a in carr(H) holds b*a=a*b)
    proof
      consider b being Element of G such that
      B2: x = b and
      B3: for a being Element of G st a in H holds b*a=a*b
      by B1;
      B4: for a being Element of G st a in carr H holds b*a=a*b
      proof
        let a be Element of G;
        assume Z1: a in carr H;
        then a in the carrier of H;
        then a in H;
        hence b*a=a*b by B3;
      end;
      hence thesis by B2;
    end;
    then x in the carrier of Centralizer carr H by A1;
    hence thesis by Def9;
  end;
  then A3: Car c= the carrier of Centralizer H;

  for x being object st x in the carrier of Centralizer H holds x in Car
  proof
    let x be object;
    assume B1: x in the carrier of Centralizer H;
    then B2: x in the carrier of Centralizer carr H by Def9;
    then B3: ex b being Element of G
    st (x = b) & (for a being Element of G st a in carr(H) holds b*a=a*b)
    by A1;
    ex b being Element of G
    st (x=b) & (for a being Element of G st a in H holds b*a=a*b)
    proof
      consider b being Element of G such that
      Z1: x = b  & (for a being Element of G st a in carr H holds b*a=a*b)
      by B3;
      for a being Element of G st a in H holds b*a=a*b by Z1;
      hence thesis by Z1;
    end;
    then x in Car;
    hence thesis;
  end;
  then the carrier of Centralizer H c= Car;
  hence thesis by A3,XBOOLE_0:def 10;
end;

@ %def Th59

\begin{theorem}
Let $H\subgroup G$ and $g\in G$.
Then $g\in C_{G}(H)$ if and only if for any $a\in H$ we have $ga=ag$.
\end{theorem}

\begin{proof}[Proof sketch]
This boils down to relying on previous results for the centralizer of
the set underlying $H$.
\end{proof}

<<Theorem: Let $g\in G$. Then $g\in C_{G}(H)\iff\forall h\in H,gh=hg$>>=
theorem Th60:
  for G being Group
  for H being Subgroup of G
  for g being Element of G
  holds (for a being Element of G st a in H holds g*a = a*g) iff
        g is Element of Centralizer H
proof
  let G be Group;
  let H be Subgroup of G;
  let g be Element of G;
A1: the carrier of Centralizer H = {b where b is Element of G : for a
  being Element of G st a in H holds b*a=a*b} by Th59;
  hereby
    assume B1: for a being Element of G st a in H holds g*a = a*g;
    then g in the carrier of Centralizer H by A1;
    hence g is Element of Centralizer H;
  end;
  assume A2: g is Element of Centralizer H;
  then g in the carrier of Centralizer H;
  then ex b being Element of G st (b = g) & (for a being Element of G st a in H
  holds b*a = a*b) by A1;
  hence thesis;
end;
@ %def Th60

\begin{theorem}
Let $A\subset G$ be a subset of a group. Then $A\subset C_{G}(C_{G}(A))$.
\end{theorem}

<<Theorem: $A\subset G\implies A\subset C_{G}(C_{G}(A))$>>=
theorem Th61:
  for G being Group
  for A being Subset of G
  holds A is Subset of Centralizer (Centralizer A)
proof
  let G be Group;
  let A be Subset of G;
  set H = Centralizer A;
  for g being object
  st g in A
  holds g in the carrier of Centralizer H
  proof
    let g be object;
    assume B1: g in A;
    then reconsider g as Element of G;
    for h being Element of G st h in H
    holds g*h = h*g by B1,Th57;
    then g is Element of Centralizer H by Th60;
    hence thesis;
  end;
  then A c= the carrier of Centralizer H;
  hence A is Subset of Centralizer H;
end;
@ %def Th61

\begin{theorem}\label{thm:characteristic:centralizer:centralizer-of-char-group-is-char}
  Let $G$ be a group, let $K\subgroup G$ be a characteristic subgroup.
  Then its centralizer $C_{G}(K)$ is a characteristic subgroup.
\end{theorem}

<<Theorem: Centralizer of characteristic subgroups is characteristic>>=
theorem Th62:
  for G being Group
  for K being strict characteristic Subgroup of G
  holds (Centralizer K) is characteristic Subgroup of G
proof
  let G be Group;
  let K be strict characteristic Subgroup of G;
  for phi being Automorphism of G
  for x being Element of G
  st x in Centralizer K
  holds phi.x in Centralizer K
  proof
    let phi be Automorphism of G;
    let x be Element of G;
    assume B1: x in Centralizer K;
    set y = phi.x;
    reconsider psi = phi" as Automorphism of G by GROUP_6:62;
    for k being Element of G st k in K holds y*k = k*y
    proof
      let k be Element of G;
      assume C1: k in K;
      then C2: psi.k in K by Th50;
      set j = psi.k;
      C3: j in K by C2;
      then x*j = j*x by B1,Th60;
      then phi.(x*j) = phi.(j*x)
                    .= phi.j * phi.x by GROUP_6:def 6;
      then phi.x * phi.j = phi.j * phi.x by GROUP_6:def 6;
      then y * phi.(psi.k) = phi.(psi.k) * y
                          .= k * y by Th4;
      hence y * k = k * y by Th4;
    end;
    then y is Element of Centralizer K by Th60;
    hence thesis;
  end;
  hence Centralizer K is characteristic Subgroup of G by Th50;
end;
@ %def Th62

\begin{definition*}
Let $G$ be a group, let $a\in G$ be any group element. Then the
singleton $\{a\}$ is a subset of $G$.
\end{definition*}

\begin{def-remark}
Singletons in Mizar seem to be just a ``generic set'', so this claim is
really a \emph{redefinition} of a singleton set to narrow its type to
\lstinline{Subset of G}. This is necessary for defining the normalizer
of a group element (or the centralizer for a group element).
\end{def-remark}

<<Definition: $\forall a\in G$, $\{a\}\subset G$>>=
definition
  let G be Group;
  let a be Element of G;
  redefine func {a} -> Subset of G;
  coherence
  proof
    for x being object st x in {a}
    holds x in the carrier of G
    proof
      let x be object;
      assume x in {a};
      then x = a by TARSKI:def 1;
      hence thesis;
    end;
    then {a} c= the carrier of G;
    hence {a} is Subset of G;
  end;
end;
@

\begin{definition*}
Let $G$ be a group, let $a\in G$. The \define{Normalizer of $a$} is the
strict subgroup of $G$ given by $N_{G}(\{a\})$.
\end{definition*}

<<Definition: Normalizer of group element>>=
definition
  let G be Group;
  let a be Element of G;
  func Normalizer a -> strict Subgroup of G equals
  Normalizer{a};
  correctness;
end;
@

\begin{theorem}
For any $x$, $y$ we have $\{x\}=\{y\}$ if and only if $x=y$.
\end{theorem}

<<Theorem: $\{x\}=\{y\}\iff x=y$>>=
theorem Th63:
  for x,y being object
  holds {x} = {y} iff x = y by ZFMISC_1:3;
@ %def Th63

\begin{theorem}
Let $G$ be a group, $a\in G$ an arbitrary group element.
We have $x\in N_{G}(a)$ if and only if there is some $h\in G$
such that $x=h$ and conjugates $a = a^{h}$.
\end{theorem}

<<Theorem: $h\in N_{G}(a)\iff a^{h}=a$>>=
theorem Th64:
  for G being Group
  for a,x being Element of G
  holds x in Normalizer a iff ex h being Element of G st x = h & a |^ h = a
proof
  let G be Group;
  let a,x be Element of G;
  A1: x in Normalizer{a} iff ex h being Element of G st x = h & {a} |^ h = {a}
  by GROUP_3:129;
  then A2: x in Normalizer{a} iff {a} |^ x = {a};
  A3: {a} |^ x = {a} |^ {x}
              .= {a |^ x} by GROUP_3:37;
  {a |^ x} = {a} iff a |^ x = a by Th63;
  then x in Normalizer{a} iff a |^ x = a by A2,A3;
  hence thesis;
end;
@ %def Th64

\begin{theorem}
Let $G$ be a group and $A\subset G$.
Then $C_{G}(A) = \bigcap_{a\in A}N_{G}(a)$.
\end{theorem}

\begin{proof}[Proof outline]
We prove this in two steps. Step one $C_{G}(A)\subset \bigcap_{a\in A} N_{G}(a)$.
Step two $\bigcap_{a\in A} N_{G}(a) \subset C_{G}(A)$.
\end{proof}

<<Theorem: $A\subset G$, $C_{G}(A) = \bigcap_{a\in A}N_{G}(a)$>>=
theorem Th65:
  for G being Group
  for A being non empty Subset of G
  holds the carrier of Centralizer A = meet {B where B is Subset of G :
  ex H being strict Subgroup of G st B = the carrier of H &
  (ex a being Element of G st a in A & H = Normalizer a)}
proof
  let G be Group;
  let A be non empty Subset of G;
  defpred P[strict Subgroup of G] means (ex a being Element of G
                                         st a in A & $1 = Normalizer a);
  set Fam = {B where B is Subset of G :
  ex H being strict Subgroup of G st B = the carrier of H & P[H]};
  A1: Fam <> {}
  proof
    consider a being object such that
    B1: a in A
    by XBOOLE_0:def 1;
    reconsider a as Element of G by B1;
    consider H being strict Subgroup of G such that
    B2: H = Normalizer a;
    carr H in Fam by B1,B2;
    hence thesis;
  end;
  for x being object st x in the carrier of Centralizer A
  holds x in meet Fam
  <<Proof: $\forall x, x\in C_{G}(A)\implies x\in\bigcap_{a}N_{G}(a)$>>
  then A2: the carrier of Centralizer A c= meet Fam;

  for x being object st x in meet Fam
  holds x in the carrier of Centralizer A
  <<Proof: $\forall x, x\in\bigcap_{a}N_{G}(a)\implies x\in C_{G}(A)$>>
  then meet Fam c= the carrier of Centralizer A;
  hence thesis by A2, XBOOLE_0:def 10;
end;
@ %def Th65

<<Proof: $\forall x, x\in C_{G}(A)\implies x\in\bigcap_{a}N_{G}(a)$>>=
proof
  let x be object;
  assume Z1: x in the carrier of Centralizer A;
  then Z2: x in Centralizer A;
  then x in G by GROUP_2:40;
  then reconsider g = x as Element of G;
  for X being set st X in Fam
  holds x in X
  proof
    let X be set;
    assume C1: X in Fam;
    then consider B being Subset of G such that
    C2: B = X and
    C3: ex H being strict Subgroup of G
        st B = the carrier of H &
           (ex a being Element of G st a in A & H = Normalizer a);
    consider H being strict Subgroup of G, a being Element of G such that
    C4: B = the carrier of H & a in A & H = Normalizer a by C3;
    g is Element of Centralizer A by Z2;
    then (for a1 being Element of G st a1 in A holds g*a1 = a1*g) by Th57;
    then C5: g*a = a*g by C4;
    C6: a |^ g = g" * a * g
              .= g" * (a * g) by GROUP_1:def 3
              .= g" * (g * a) by C5
              .= (g" * g) * a by GROUP_1:def 3
              .= (1_G) * a by GROUP_1:def 5
              .= a by GROUP_1:def 4;
    {a} |^ g = {a} |^ {g}
            .= {a |^ g} by GROUP_3:37
            .= {a} by C6;
    then g in Normalizer a by GROUP_3:129;
    then g in H by C4;
    then x in H;
    then x in the carrier of H;
    then x in B by C4;
    hence x in X by C2;
  end;
  hence x in meet Fam by A1,SETFAM_1:def 1;
end;
@

<<Proof: $\forall x, x\in\bigcap_{a}N_{G}(a)\implies x\in C_{G}(A)$>>=
proof
  let x be object;
  assume B1: x in meet Fam;
  B2: ex H being strict Subgroup of G st P[H]
  proof
    consider X being object such that
    C1: X in Fam by A1, XBOOLE_0:def 1;
    consider B being Subset of G such that
    C2: B = X and
    C3: ex H being strict Subgroup of G
        st B = the carrier of H & P[H]
    by C1;
    thus thesis by C3;
  end;

  consider K being strict Subgroup of G such that
  B3: the carrier of K = meet Fam
  from GROUP_4:sch 1(B2);

  B4: x in K by B1,B3;
  then reconsider g = x as Element of G by GROUP_2:42;
  B5: g in K by B4;
  then B6: for X being set st X in Fam
           holds g in X by B3,SETFAM_1:def 1;

  B7: for a being Element of G st a in A
  holds g in Normalizer a
  proof
    let a be Element of G;
    assume C1: a in A;
    set H = Normalizer a;
    P[H] by C1;
    then carr H in Fam;
    then g in carr H by B6;
    hence g in Normalizer a;
  end;
  for a being Element of G st a in A holds g*a = a*g
  proof
    let a be Element of G;
    assume C1: a in A;
    then g in Normalizer a by B7;
    then consider h being Element of G such that
    C2: g = h & a |^ h = a
    by Th64;
    C3: a = g" * a * g by C2
         .= g" * (a * g) by GROUP_1:def 3;
    g * a = g * (g" * (a * g)) by C3
         .= (g * g") * (a * g) by GROUP_1:def 3
         .= 1_G * (a * g) by GROUP_1:def 5
         .= a * g by GROUP_1:def 4;
    hence g*a = a*g;
  end;

  then g is Element of Centralizer A by Th57;
  then g in Centralizer A;
  then x in Centralizer A;
  hence thesis;
end;
@



\begin{theorem}
If $H_{1}\subgroup G$ and $H_{2}\subgroup G$ are subgroups such that
$|H_{1}\cap H_{2}|=|H_{1}|$ and $|H_{1}\cap H_{2}|=|H_{2}|$, then $H_{1}=H_{2}$.
\end{theorem}

<<Theorem: $|H_{1}\cap H_{2}|=|H_{1}|=|H_{2}|\implies H_{1}=H_{2}$>>=
theorem Th66:
  for G being finite Group
  for H1,H2 being strict Subgroup of G
  st card(H1 /\ H2) = card H1 & card(H1 /\ H2) = card H2
  holds H1 = H2
proof
  let G be finite Group;
  let H1,H2 be strict Subgroup of G;
  assume A1: card(H1 /\ H2) = card H1;
  assume A2: card(H1 /\ H2) = card H2;
  A3: H1 /\ H2 = H1
  proof
    reconsider H12 = H1 /\ H2 as strict Subgroup of H1 by GROUP_2:88;
    multMagma(# the carrier of H12, the multF of H12 #)
    = multMagma(# the carrier of H1, the multF of H1 #) by A1,GROUP_2:73;
    hence thesis;
  end;
  H1 /\ H2 = H2
  proof
    reconsider H12 = H1 /\ H2 as strict Subgroup of H2 by GROUP_2:88;
    multMagma(# the carrier of H12, the multF of H12 #)
    = multMagma(# the carrier of H2, the multF of H2 #) by A2,GROUP_2:73;
    hence thesis;
  end;
  hence thesis by A3;
end;
@ %def Th66

\begin{theorem}
For any natural numbers $a,b,c$ with $c\neq0$. If $c|a$ and $c|b$, then $a|b$.
\end{theorem}

<<Theorem: $\forall a,b,c\in\NN, c\neq0\land c|a\land c|b\implies a|b$>>=
theorem Th67:
  for a,b,c being Nat
  st c <> 0 & c*a divides c*b
  holds a divides b
proof
  let a,b,c be Nat;
  assume A1: c <> 0;
  assume A2: c*a divides c*b;
  then consider q being Integer such that
  A3: c*b = c*a*q by INT_1:def 3;
  b*c = a*q*c by A3;
  then A4: b = a*q by A1,XCMPLX_1:5;
  take q;
  thus thesis by A4;
end;
@ %def Th67

\begin{theorem}
For any natural numbers $a,b,c\in\NN$ with $b|c$ and $\gcd(ab,c)=1$, we
have $b=1$.
\end{theorem}

<<Theorem: $a,b,c\in\NN, b|c\land\gcd(ab,c)=1\implies b=1$>>=
theorem Th68:
  for a,b,c being Nat
  st b<>0 & b divides c & a*b,c are_coprime
  holds b=1
proof
  let a,b,c be Nat;
  assume A1: b<>0;
  assume A2: b divides c;
  assume A3: a*b,c are_coprime;
  b divides a*b by INT_1:def 3;
  then b divides (a*b gcd c) by A2,INT_2:22;
  then b divides 1 by A3, INT_2:def 3;
  hence b=1 by INT_2:13;
end;
@ %def Th68

\begin{theorem}
If $N_{1}\normalSubgroup G_{1}$ and $N_{2}\normalSubgroup G_{2}$ and
$G_{1}/N_{1}\iso G_{2}/N_{2}$, then $|N_{2}|\cdot|G_{1}|=|N_{1}|\cdot|G_{2}|$.
\end{theorem}

<<Theorem: $G_{1}/N_{1}\iso G_{2}/N_{2}\implies |N_{2}|\cdot|G_{1}|=|N_{1}|\cdot|G_{2}|$>>=
theorem Th69:
  for G1,G2 being finite Group
  for N1 being normal Subgroup of G1
  for N2 being normal Subgroup of G2
  st G1./.N1, G2./.N2 are_isomorphic
  holds card(N2)*card(G1)=card(N1)*card(G2)
proof
  let G1,G2 be finite Group;
  let N1 be normal Subgroup of G1;
  let N2 be normal Subgroup of G2;
  assume A1: G1./.N1, G2./.N2 are_isomorphic;
  then card(G1./.N1) = card(G2./.N2) by GROUP_6:73
                    .= index N2 by GROUP_6:27;
  then A2: index N1 = index N2 by GROUP_6:27;
  set k = index N1;
  A3: k = index N2 by A2;
  A4: card(G1) = card(N1) * index(N1) by GROUP_2:147
              .= card(N1) * k;
  A5: card(G2) = card(N2) * index(N2) by GROUP_2:147
              .= card(N2) * k by A3;
  then card(N1)*card(G2) = card(N1)*card(N2)*k
                        .= card(N2)*card(N1)*k
                        .= card(N2)*(card(N1)*k)
                        .= card(N2)*card(G1) by A4;
  hence thesis;
end;
@ %def Th69

\begin{theorem}
Let $G$ be a finite group.
If $K\normalSubgroup G$ and $N\normalSubgroup G$ are such that $|K|=|N|$,
then $|K\cap N|\cdot|KN|=|K|\cdot|N|$.
\end{theorem}

<<Theorem: $K,N\normalSubgroup G\implies |KN|\cdot|K\cap N|=|K|\cdot|N|$>>=
theorem Th70:
  for G being finite Group
  for K,N being strict normal Subgroup of G
  for m,d being Nat
  st m = card N & m = card K & d = card(K /\ N)
  holds d*card(N "\/" K) = m*m
proof
  let G be finite Group;
  let K,N be strict normal Subgroup of G;
  let m,d be Nat;
  assume A1: m = card N;
  assume A2: m = card K;
  assume A3: d = card(K /\ N);
  reconsider B=K as Subgroup of G;
  A4: card(B) = m by A2;

  N is Subgroup of B "\/" N by GROUP_4:60;
  then A5: (B "\/" N,N)`*` = the multMagma of N by GROUP_6:def 1;

  (B "\/" N)./.(B "\/" N,N)`*`, B./.(B /\ N) are_isomorphic by GROUP_6:81;
  then card(B /\ N)*card(B "\/" N) = card(B) * card((B "\/" N,N)`*`) by Th69;
  then d*card(B "\/" N) = card(B) * card((B "\/" N,N)`*`) by A3
                       .= card(B)*card(N) by A5
                       .= card(B)*m by A1
                       .= m*m by A4;
  hence d*card(N "\/" K) = m*m;
end;
@ %def Th70

\begin{theorem}[{\cite[Th2.1.3]{gorenstein1980finite}}]
Let $G$ be a finite group, $N\normalSubgroup G$.
If $\gcd(|N|, [G:N])=1$, then $N$ is a characteristic subgroup of $G$.
\end{theorem}

<<Theorem: $N\normalSubgroup G$ with $|N|$ and $[G:N]$ coprime implies $N$ is characteristic>>=
theorem Th71:
  for G being finite Group
  for N being strict normal Subgroup of G
  st card N, index N are_coprime
  holds N is characteristic Subgroup of G
proof
  let G be finite Group;
  let N be strict normal Subgroup of G;
  assume A1: card N, index N are_coprime;
  consider m being Nat such that
  A2: m = card N;
  A6: m <> 0 by A2;
  consider n being Nat such that
  A3: n = index N;
  A4: card G = m*n by A2,A3,GROUP_2:147;
  A5: for phi being Automorphism of G holds Image(phi|N) = N
  proof
    let phi be Automorphism of G;
    set K = Image(phi|N);
    K is normal Subgroup of G by Th49;
    then reconsider K as strict normal Subgroup of G;
    K = phi .: N by GRSOLV_1:def 3;
    then N,K are_isomorphic by Th19;
    then B1: card K = card N by GROUP_6:73;
    set d = card(N /\ K);
    B10: d <> 0;
    B11: d = card(N /\ K);
    B2: d divides m
    proof
      N /\ K is Subgroup of N by GROUP_2:88;
      then card(N /\ K) divides card N by GROUP_2:148;
      hence thesis by A2;
    end;
    then consider q being Nat such that
    B21: m = d*q by NAT_D:def 3;
    B22: q<>0 by B21,A6;
    B3: card(N "\/" K) = m*q
    proof
      K /\ N = N /\ K
      proof
        carr(K /\ N) = (carr K) /\ (carr N) by GROUP_2:def 10
                    .= carr(N) /\ carr(K)
                    .= carr(N /\ K) by GROUP_2:def 10;
        then carr(K /\ N) = carr(N /\ K);
        hence thesis by GROUP_2:59;
      end;
      then m = card N & d = card(K /\ N) by A2;
      then d*card(N "\/" K) = m*m by B1,Th70;
      then d*card(N "\/" K) = m*(d*q) by B21
                           .= m*d*q
                           .= d*m*q
                           .= d*(m*q);
      then card(N "\/" K) = m*q by XCMPLX_1:5;
      hence thesis;
    end;
    B4: m*q divides m*n by A4,B3,GROUP_2:148;
    then B5: q divides n by A6,Th67;
    d*q, n are_coprime by A1,A2,A3,B21;
    then q=1 by B22,B5,Th68;
    then m = d by B21;
    then card(K) = card(N /\ K) & card(N) = card(N /\ K) by A2,B1;
    then N=K by Th66;
    hence Image(phi|N) = N;
  end;
  thus N is characteristic Subgroup of G by A5,Def3;
end;
@ %def Th71

\begin{theorem}
Let $f_{1}\colon G_{1}\to G_{2}$, $f_{2}\colon G_{2}\to G_{3}$ be group morphisms.
If $A\subset G_{1}$, then $f_{2}(f_{1}(A))=(f_{2}\circ f_{1})(A)$.
\end{theorem}

<<Theorem: $f_{2}(f_{1}(A))=(f_{2}\circ f_{1})(A)$ for group morphisms>>=
theorem Th72:
  for G1,G2,G3 being Group
  for f1 being Homomorphism of G1,G2
  for f2 being Homomorphism of G2,G3
  for A being Subgroup of G1
  holds the multMagma of f2 .: (f1 .: A) = the multMagma of ((f2 * f1) .: A)
proof
  let G1,G2,G3 be Group;
  let f1 be Homomorphism of G1,G2;
  let f2 be Homomorphism of G2,G3;
  let A be Subgroup of G1;
  for z being Element of G3
  holds z in f2 .: (f1 .: A) iff z in (f2 * f1) .: A
  proof
    let z be Element of G3;
    thus z in f2 .: (f1 .: A) implies z in (f2 * f1) .: A
    proof
      assume A1: z in f2 .: (f1 .: A);
      then z in the carrier of f2 .: (f1 .: A);
      then z in f2 .: (the carrier of f1 .: A) by GRSOLV_1:8;
      then consider y being object such that
      A2: y in dom f2 and
      A3: y in the carrier of (f1 .: A) and
      A4: z = f2.y by FUNCT_1:def 6;
      y in f1 .: (the carrier of A) by A3,GRSOLV_1:8;
      then consider x being object such that
      A5: x in dom f1 & x in the carrier of A & y = f1.x by FUNCT_1:def 6;
      A6: x in dom(f2 * f1) by A2,A5,FUNCT_1:11;
      z = f2.y by A4;
      then z = f2.(f1.x) by A5;
      then A7: z = (f2 * f1).x by A6,FUNCT_1:12;
      x in dom(f2 * f1) & x in the carrier of A & z = (f2 * f1).x by A5,A6,A7;
      then z in (f2 * f1) .: the carrier of A by FUNCT_1:def 6;
      hence thesis by GRSOLV_1:8;
    end;

    thus z in (f2 * f1) .: A implies z in f2 .: (f1 .: A)
    proof
      assume A1: z in (f2 * f1) .: A;
      then z in the carrier of (f2 * f1) .: A;
      then z in (f2 * f1) .: the carrier of A by GRSOLV_1:8;
      then consider x being object such that
      A2: x in dom (f2 * f1) & x in the carrier of A & z = (f2 * f1).x
      by FUNCT_1:def 6;
      A3: x in dom f1 & f1.x in dom f2 by A2,FUNCT_1:11;
      A4: (f2 * f1).x = f2.(f1.x) by A2,FUNCT_1:12;

      set y = f1.x;
      x in dom f1 & x in the carrier of A & y = f1.x by A2,A3;
      then A5: y in f1 .: (the carrier of A) by FUNCT_1:def 6;
      z = (f2 * f1).x by A2
       .= f2.(f1.x) by A4
       .= f2.y;
      then y in dom f2 & y in f1.:(the carrier of A) & z = f2.y by A3,A5;
      then z in f2 .: (f1 .: (the carrier of A)) by FUNCT_1:def 6;
      then z in f2 .: (the carrier of (f1 .: A)) by GRSOLV_1:8;
      then z in the carrier of (f2 .: (f1 .: A)) by GRSOLV_1:8;
      hence z in f2 .: (f1 .: A);
    end;

  end;

  hence the multMagma of f2 .: (f1 .: A) = the multMagma of ((f2 * f1) .: A)
  by GROUP_2:60;
end;
@ %def Th72

\begin{theorem}
Let $N\normalSubgroup G$, $\varphi\in\Aut(G)$ such that $\varphi(N)=N$.
($N$ need not be characteristic.)
Then there exists an automorphism $\sigma\in\Aut(G/N)$ such that for any
$x\in G$, $\sigma(xN)=\varphi(x)N$.
\end{theorem}

<<Theorem: $\varphi\in\Aut(G)$, $\varphi(N)=N$, $\exists\sigma\in\Aut(G/N),\sigma(xN)=\varphi(x)N$>>=
theorem Th73:
  for G being Group
  for N being strict normal Subgroup of G
  for phi being Automorphism of G
  st Image(phi|N) = N
  ex sigma being Automorphism of G./.N
  st (for x being Element of G holds sigma.(x*N) = (phi.x)*N)
proof
  let G be Group;
  let N be strict normal Subgroup of G;
  let phi be Automorphism of G;
  assume A1: Image(phi|N) = N;
  defpred P[set,set] means ex a being Element of G st $1 = a*N & $2 = (phi.a)*N;
  A2: for x being Element of G./.N ex y being Element of G./.N st P[x,y]
  proof
    let x be Element of G./.N;
    x in Cosets N;
    then consider a being Element of G such that
    B1: x = a*N by GROUP_2:def 15;
    (phi.a)*N in Cosets N by GROUP_2:def 15;
    then consider y being Element of G./.N such that
    B2: y = (phi.a)*N;
    take y;
    thus P[x,y] by B1,B2;
  end;

  consider sigma being Function of G./.N, G./.N such that
  A3: for x being Element of G./.N holds P[x, sigma.x]
  from FUNCT_2:sch 3(A2);

  A4: for a being Element of G holds sigma.(a*N) = (phi.a)*N
  proof
    let a be Element of G;
    a*N in Cosets N by GROUP_2:def 15;
    then consider x being Element of G./.N such that
    B2: x = a*N;

    P[x, sigma.x] by A3;
    then consider b being Element of G such that
    B3: x = b*N & sigma.x = (phi.b)*N;
    a*N = b*N by B2,B3;
    then B6: b" * a in N by GROUP_2:114;
    consider n being Element of G such that
    B7: n = b" * a & n in N by B6;

    b*n = b*(b" * a) by B7
    .= (b * b") * a by GROUP_1:def 3
    .= 1_G * a by GROUP_1:def 5
    .= a by GROUP_1:def 4;
    then B8: b*n = a;

    dom phi = the carrier of G & n in N by B7, FUNCT_2:def 1;
    then phi.n in phi .: (the carrier of N) by FUNCT_1:def 6;
    then phi.n in the carrier of (phi .: N) by GRSOLV_1:8;
    then phi.n in Image(phi|N) by GRSOLV_1:def 3;
    then B9: phi.n in N by A1;
    B10: phi.n * N = carr N by B9, GROUP_2:113;
    phi.a * N = phi.(b * n) * N by B8
             .= (phi.b * phi.n) * N by GROUP_6:def 6
             .= phi.b * (phi.n * N) by GROUP_2:105
             .= phi.b * N by B10
             .= sigma.x by B3;
    hence thesis by B2;
  end;

  for x,y being Element of G./.N holds sigma.(x*y) = sigma.x * sigma.y
  proof
    let x,y be Element of G./.N;
    P[x, sigma.x] by A3;
    then consider a being Element of G such that
    B1: x = a*N & sigma.x = (phi.a)*N;
    P[y, sigma.y] by A3;
    then consider b being Element of G such that
    B2: y = b*N & sigma.y = (phi.b)*N;
    B3: for g1,g2 being Element of G holds (g1*N)*(g2*N) = g1*g2*N
    proof
      let g1,g2 be Element of G;
      (g1*N)*(g2*N) = (g1 * N) * (N * g2) by GROUP_3:117
                   .= g1 * N * N * g2 by GROUP_3:11
                   .= g1 * (N*N) * g2 by GROUP_4:45
                   .= g1 * N * g2 by GROUP_2:76
                   .= g1 * (N * g2) by GROUP_2:106
                   .= g1 * (g2 * N) by GROUP_3:117
                   .= g1 * g2 * N by GROUP_2:105;
      hence thesis;
    end;

    B4: x * y = @x * @y by GROUP_6:def 3
             .= (a * N) * (b * N) by B1,B2
             .= a * b * N by B3;

    B5: (sigma.x) * (sigma.y) = phi.(a*b) * N
    proof
      sigma.x * sigma.y = @(sigma.x) * @(sigma.y) by GROUP_6:def 3
                       .= ((phi.a)*N) * ((phi.b) * N) by B1,B2
                       .= (phi.a) * (phi.b) * N by B3
                       .= phi.(a*b) * N by GROUP_6:def 6;
      hence thesis;
    end;
    sigma.(x * y) = sigma.(a * b * N) by B4
                 .= phi.(a*b) * N by A4
                 .= sigma.x * sigma.y by B5;
    hence sigma.(x*y) = sigma.x * sigma.y;
  end;

  then reconsider sigma as Homomorphism of G./.N, G./.N by GROUP_6:def 6;
  sigma is bijective
  proof
    BB1: for x being Element of G holds x*N in Ker sigma iff x in N
    proof
      let x be Element of G;
      x*N in Left_Cosets N by GROUP_2:def 15;
      then reconsider z = x*N as Element of G./.N;
      B1: z = x*N;
      B2: dom phi = the carrier of G by FUNCT_2:def 1;
      B3: (phi").(phi.x) = x by FUNCT_2:26;
      thus x*N in Ker sigma implies x in N
      proof
        assume Z1: (x*N) in Ker sigma;
        then z in Ker sigma;
        then sigma.z = 1_(G./.N) by GROUP_6:41;
        then Z2: sigma.(x*N) = 1_(G./.N)
                            .= carr N by GROUP_6:24;
        (phi.x)*N = sigma.(x*N) by A4
                 .= carr N by Z2;
        then Z3: phi.x in N by GROUP_2:113;
        then phi.x in Image(phi|N) by A1;
        then Z33: phi.x in phi .: N by GRSOLV_1:def 3;
        Z4: phi" is Automorphism of G by GROUP_6:62;
        consider psi being Automorphism of G such that
        Z7: psi = phi" and
            the multMagma of Image(phi|Image(psi|N)) = the multMagma of N
        by Th17;
        reconsider i = id the carrier of G as Automorphism of G by GROUP_6:38;
        the carrier of G <> {} & phi is onto;
        then Z7a: psi * phi = id the carrier of G by Z7,FUNCT_2:29;
        dom psi = the carrier of G by FUNCT_2:def 1;
        then phi.x in dom psi;
        then psi.(phi.x) in psi .: (the carrier of (phi .: N)) by Z33,FUNCT_1:def 6;
        then psi.(phi.x) in the carrier of (psi .: (phi .: N)) by GRSOLV_1:8;
        then psi.(phi.x) in psi .: (phi .: N);
        then psi.(phi.x) in (psi * phi) .: N by Th72;
        then (phi").(phi.x) in (psi * phi) .: N by Z7;
        then x in (psi * phi) .: N by B3;
        then x in i .: N by Z7a;
        then x in the carrier of (i .: N);
        then x in i .: (the carrier of N) by GRSOLV_1:8;
        then Z8: x in (id the carrier of G) .: (the carrier of N);
        the carrier of N is Subset of the carrier of G by GROUP_2:def 5;
        then x in (the carrier of N) by Z8,FUNCT_1:92;
        hence x in N;
      end;
      thus x in N implies x*N in Ker sigma
      proof
        assume Z1: x in N;
        then x * N = carr N by GROUP_2:113;
        then Z2: x * N = 1_(G./.N) by GROUP_6:24;
        then sigma.(x*N) = 1_(G./.N) by GROUP_6:31;
        hence x*N in Ker sigma by Z2,GROUP_6:41;
      end;
    end;
    for x being Element of G./.N holds x in Ker sigma iff x in (1).(G./.N)
    proof
      let x be Element of G./.N;
      thus x in Ker sigma implies x in (1).(G./.N)
      proof
        assume Z1: x in Ker sigma;
        x in G./.N;
        then consider g being Element of G such that
        Z2: x = g*N by GROUP_2:def 15;
        g*N in Ker sigma by Z1,Z2;
        then g in N by BB1;
        then g*N = carr N by GROUP_2:113;
        then g*N = 1_(G./.N) by GROUP_6:24;
        then g*N in {1_(G./.N)} by TARSKI:def 1;
        then g*N in (1).(G./.N) by GROUP_2:def 7;
        hence x in (1).(G./.N) by Z2;
      end;
      thus x in (1).(G./.N) implies x in Ker sigma
      proof
        assume Z1: x in (1).(G./.N);
        then x in the carrier of (1).(G./.N);
        then x in {1_(G./.N)} by GROUP_2:def 7;
        then x = 1_(G./.N) by TARSKI:def 1;
        then sigma.x = 1_(G./.N) by GROUP_6:31;
        hence x in Ker sigma by GROUP_6:41;
      end;
    end;

    then Ker sigma = (1).(G./.N);
    hence sigma is one-to-one by GROUP_6:56;

    for y being Element of G./.N holds y in Image sigma
    proof
      let y be Element of G./.N;
      y in G./.N;
      then consider b being Element of G such that
      B2: y = b*N by GROUP_2:def 15;
      reconsider psi = phi" as Automorphism of G by GROUP_6:62;
      consider a being Element of G such that
      B3: a = psi.b;
      a*N in Cosets N by GROUP_2:def 15;
      then a*N in G./.N;
      then consider x being Element of G./.N such that
      B4: x = a*N;
      a = (phi").b by B3;
      then phi.a = phi.((phi").b)
      .= b by Th4;
      then B5: phi.a = b;
      sigma.x = sigma.(a*N) by B4
      .= (phi.a)*N by A4
      .= b*N by B5
      .= y by B2;
      then y = sigma.x;
      then y in Image sigma by GROUP_6:45;
      hence thesis;
    end;
    then Image sigma = G./.N by GROUP_2:62;
    hence sigma is onto by GROUP_6:57;
  end;
  then reconsider sigma as Automorphism of G./.N;
  take sigma;
  let x be Element of G;
  thus sigma.(x*N) = (phi.x)*N by A4;
end;
@ %def Th73

\begin{theorem}
Let $G$ be a finite group $H\subgroup K\subgroup G$ and $H$ be a characteristic
subgroup. Then $H$ is a normal subgroup of $K$.
\end{theorem}

<<Theorem: $H$ char $G$ and $H\subgroup K\subgroup G$, then $H\normalSubgroup K$>>=
theorem Th74:
  for G being finite Group
  for H being strict characteristic Subgroup of G
  for K being strict Subgroup of G
  st H is Subgroup of K
  holds H is normal Subgroup of K
proof
  let G be finite Group;
  let H be strict characteristic Subgroup of G;
  let K be strict Subgroup of G;
  assume A1: H is Subgroup of K;
  Ker (nat_hom H) = H by GROUP_6:43;
  then A3: for g being Element of G
  holds g in Ker (nat_hom H) iff g in H;

  reconsider R = Ker ((nat_hom H)|K) as strict Subgroup of K;

  A4: for k being Element of K
  holds k in H iff k in Ker ((nat_hom H)|K)
  proof
    let k be Element of K;
    reconsider g=k as Element of G by GROUP_2:42;
    B1: g = k;
    B2: g in K;
    thus k in H implies k in Ker ((nat_hom H)|K)
    proof
      assume k in H;
      then C1: g in H;
      C2: g in K;
      g in Ker (nat_hom H) iff g in H by A3;
      then (nat_hom H).g = 1_(G./.H) by C1,GROUP_6:41;
      then ((nat_hom H)|K).g = 1_(G./.H) by C2,Th1;
      then ((nat_hom H)|K).k = 1_(G./.H);
      then k in Ker ((nat_hom H)|K) by GROUP_6:41;
      hence thesis;
    end;
    thus k in Ker ((nat_hom H)|K) implies k in H
    proof
      assume C1: k in Ker ((nat_hom H)|K);
      then C2: g in Ker ((nat_hom H)|K);
      ((nat_hom H)|K).g = (nat_hom H).g by B2,Th1;
      then (nat_hom H).g = 1_(G./.H) by C2,GROUP_6:41;
      then g in Ker (nat_hom H) by GROUP_6:41;
      then g in H by GROUP_6:43;
      hence k in H;
    end;

  end;
  reconsider H1=H as strict Subgroup of K by A1;
  for k being Element of K holds k in H1 iff k in R by A4;
  then the multMagma of R = the multMagma of H1 by GROUP_2:60;
  hence thesis;
end;
@ %def Th74

\begin{theorem}[{Gorenstein~\cite[Th2.1.2(iv)]{gorenstein1980finite}}]
Let $G$ be a finite group, $H$ a characteristic subgroup of $G$, and
$H\subgroup K\subgroup G$. If $K/H$ is a characteristic subgroup of $G/H$,
then $K$ is a characteristic subgroup of $G$.
\end{theorem}

<<Theorem: $H\subgroup K\subgroup G$, $H$ char $G$, $K/H$ char $G/H$ implies $K$ is characteristic>>=
:: Gorenstein, Finite Groups, Theorem 2.1.2 (iv)
theorem Th75:
  for G being finite Group
  for H being strict characteristic Subgroup of G
  for K being strict Subgroup of G
  st H is Subgroup of K &
  K./.(K,H)`*` is characteristic Subgroup of G./.H
  holds K is characteristic Subgroup of G
proof
  let G be finite Group;
  let H be strict characteristic Subgroup of G;
  let K be strict Subgroup of G;
  assume A1: H is Subgroup of K;
  assume A2: K./.(K,H)`*` is characteristic Subgroup of G./.H;
  A3: (K,H)`*` = H by A1,GROUP_6:def 1;
  then A4: H is normal Subgroup of K;
  for phi being Automorphism of G
  for k being Element of G st k in K
  holds phi.k in K
  proof
    let phi be Automorphism of G;
    let k be Element of G;
    assume B1: k in K;
    Image(phi|H) = H by Def3;
    then consider sigma being Automorphism of G./.H such that
    B2: for x being Element of G holds sigma.(x*H) = (phi.x)*H
    by Th73;
    consider J being strict characteristic Subgroup of G./.H such that
    B3: J = (K./.(K,H)`*`) by A2;
    B4: for j being Element of G./.H st j in J holds sigma.j in J by Th50;

    B5: for k1 being Element of G st k1*H in J holds k1 in K
    proof
      let k1 be Element of G;
      assume C1: k1*H in J;
      C2: the carrier of H = the carrier of ((K,H)`*`) by A1,GROUP_6:def 1;
      k1*H = k1*(carr H)
          .= k1*(K,H)`*` by C2;
      then C3: k1*H = k1*(K,H)`*`;
      set x = k1*(K,H)`*`;
      x in J by C1,C3;
      then x in (K./.(K,H)`*`) by B3;
      then C5: x in Cosets (K,H)`*`;
      then consider a being Element of K such that
      C6: x = a*(K,H)`*` by GROUP_2:def 15;

      reconsider a1 = a as Element of G by GROUP_2:42;
      C7: a1 in K;
      for j1 being object holds j1 in a*(K,H)`*` iff j1 in a1*H
      proof
        let j1 be object;
        thus j1 in a*(K,H)`*` implies j1 in a1*H
        proof
          assume Z1: j1 in a*(K,H)`*`;
          Z2: H = (K,H)`*` by A1,GROUP_6:def 1;
          consider g1 being Element of K such that
          Z3: j1 = a*g1 & g1 in (K,H)`*` by Z1,GROUP_2:103;
          reconsider g=g1 as Element of G by GROUP_2:42;
          a*g1 = a1*g by GROUP_2:43;
          then Z4: j1 = a1*g by Z3;
          g in H by Z2,Z3;
          then a1*g in a1*H by GROUP_2:103;
          hence j1 in a1*H by Z4;
        end;

        thus j1 in a1*H implies j1 in a*(K,H)`*`
        proof
          assume Z1: j1 in a1*H;
          then consider g1 being Element of G such that
          Z2: j1 = a1*g1 & g1 in H by GROUP_2:103;
          Z3: H = (K,H)`*` by A1, GROUP_6:def 1;
          then reconsider g=g1 as Element of K by Z2,GROUP_2:42;
          g in H by Z2;
          then Z4: g in (K,H)`*` by Z3;
          a1*g1 = a*g by GROUP_2:43;
          then Z5: j1 = a*g by Z2;
          g in (K,H)`*` by Z4;
          then a*g in a*(K,H)`*` by GROUP_2:103;
          hence j1 in a*(K,H)`*` by Z5;
        end;

      end;
      then a*(K,H)`*` = a1*H by TARSKI:2;
      then a1*H = x by C6
               .= k1*H by C3;
      then (a1") * k1 in H by GROUP_2:114;
      then (a1") * k1 in K by A4,GROUP_2:41;
      then C8: a1 * ((a1") * k1) in K by C7,GROUP_2:50;
      a1 * ((a1") * k1) = (a1 * a1") * k1 by GROUP_1:def 3
                       .= 1_G * k1 by GROUP_1:def 5
                       .= k1 by GROUP_1:def 4;
      hence k1 in K by C8;
    end;

    for k1 being Element of G holds k1 in K iff k1*H in J
    proof
      let k1 be Element of G;
      thus k1 in K implies k1*H in J
      proof
        assume C1: k1 in K;
        then reconsider k2=k1 as Element of K;
        C2: k2*((K,H)`*`) in J by B3, GROUP_2:def 15;
        C3: k1 = k2;
        for x being object holds x in k2*carr((K,H)`*`) iff x in k1*carr(H)
        proof
          let x be object;
          thus x in k2*carr((K,H)`*`) implies x in k1*carr(H)
          proof
            assume Z1: x in k2*carr((K,H)`*`);
            x in k2*((K,H)`*`) iff
            ex g being Element of K st (x = k2*g & g in (K,H)`*`)
            by GROUP_2:103;
            then consider huh being Element of K such that
            Z2: x = k2*huh & huh in (K,H)`*` by Z1;
            huh in (K,H)`*` by Z2;
            then Z3: huh in H by A3;
            reconsider huh2=huh as Element of G by GROUP_2:42;
            set x2 = k1*huh2;
            x = k1*huh2 by Z2,GROUP_2:43;
            hence thesis by Z3,GROUP_2:27;
          end;
          assume Z2: x in k1*carr(H);
          then consider h1 being Element of G such that
          Z3: x = k1*h1 & h1 in carr(H) by GROUP_2:27;
          the multMagma of ((K,H)`*`) = the multMagma of H by A1, GROUP_6:def 1;
          then Z4: the carrier of H = the carrier of ((K,H)`*`);
          h1 in carr H by Z3;
          then Z5: h1 in carr((K,H)`*`) by Z4;
          Z6: h1 in H by Z3;
          then reconsider h2=h1 as Element of K by A1,GROUP_2:42;
          reconsider H1=H as normal Subgroup of K by A4;
          Z7: k1*h1 = k2*h2 by GROUP_2:43;
          then Z8: x = k2*h2 by Z3;
          the multMagma of ((K,H)`*`) = the multMagma of H by A1,GROUP_6:def 1;
          then Z11: the carrier of H = the carrier of ((K,H)`*`);
          Z9: carr H1 = carr H;
          h2 in H1 by Z6;
          then k2*h2 in k2*carr(H1) by GROUP_2:27;
          then x in k2*carr(H1) by Z8;
          then x in k2*carr((K,H)`*`) by Z11;
          then x in k2*carr((K,H)`*`);
          hence x in k2*carr((K,H)`*`);
        end;

        then k2*carr(((K,H)`*`)) = k1*carr(H) by TARSKI:2
                                .= k1*H;
        hence k1*H in J by C2;
      end;
      thus k1*H in J implies k1 in K by B5;
    end;
    then B6: k*H in J by B1;
    then reconsider kH = k*H as Element of G./.H by GROUP_2:42;
    kH in J by B6;
    then sigma.(kH) in J by B4;
    then sigma.(k*H) in J & sigma.(k*H) = (phi.k)*H by B2;
    then (phi.k)*H in J;
    then phi.k in K by B5;
    hence thesis;
  end;

  hence K is characteristic Subgroup of G by Th50;
end;
@ %def Th75

\begin{theorem}
Let $H\subgroup G$.
Then $H\subgroup C_{G}(H)$ if and only if $H$ is a commutative group.
\end{theorem}

<<Theorem: $H\subgroup G$, $H\subgroup C_{G}(H)\iff$ $H$ is commutative>>=
theorem Th76:
  for G being Group
  for H being Subgroup of G
  holds H is Subgroup of Centralizer H iff H is commutative Group
proof
  let G be Group;
  let H be Subgroup of G;
  thus H is Subgroup of Centralizer H implies H is commutative Group
  proof
    assume A1: H is Subgroup of Centralizer H;
    A2: for g,h being Element of G st g in H & h in H holds g*h=h*g
    proof
      let g,h be Element of G;
      assume B1: g in H;
      assume B2: h in H;
      g in Centralizer H by B1,A1,GROUP_2:40;
      hence g*h=h*g by B2,Th60;
    end;
    for g,h being Element of H holds g*h=h*g
    proof
      let g,h be Element of H;
      B1: g in H & h in H;
      reconsider g1=g, h1=h as Element of G by GROUP_2:42;
      g1 in H & h1 in H;
      then B2: g1*h1=h1*g1 by A2;
      g*h = g1*h1 by GROUP_2:43
         .= h1*g1 by B2
         .= h*g by GROUP_2:43;
      hence thesis;
    end;
    hence thesis by GROUP_1:def 12;
  end;

  thus H is commutative Group implies H is Subgroup of Centralizer H
  proof
    assume A1: H is commutative Group;
    A2: for g,h being Element of H holds g*h=h*g by A1,GROUP_1:def 12;
    A3: for g,h being Element of G st g in H & h in H holds g*h=h*g
    proof
      let g,h be Element of G;
      assume B1: g in H;
      assume B2: h in H;
      reconsider g1=g,h1=h as Element of H by B1,B2;
      g*h = g1*h1 by GROUP_2:43
         .= h1*g1 by A2
         .= h*g by GROUP_2:43;
      hence g*h=h*g;
    end;
    for g being Element of G st g in H holds g in Centralizer H
    proof
      let g be Element of G;
      assume B1: g in H;
      for a being Element of G st a in H holds g*a = a*g by B1,A3;
      then g is Element of Centralizer H by Th60;
      hence g in Centralizer H;
    end;
    hence thesis by GROUP_2:58;
  end;
end;
@ %def Th76

\begin{theorem}
For any group $G$, $C_{G}(G)=Z(G)$.
\end{theorem}

<<Theorem: $C_{G}(G)=Z(G)$>>=
theorem Th77:
  for G being Group
  holds Centralizer (Omega).G = center G
proof
  let G be Group;
  for g being Element of G holds g in Centralizer (Omega).G iff g in center G
  proof
    let g be Element of G;
    thus g in Centralizer (Omega).G implies g in center G
    proof
      assume B1: g in Centralizer (Omega).G;
      then B2: for a being Element of G st a in (Omega).G holds g*a = a*g by Th60;
      for a being Element of G holds g*a = a*g
      proof
        let a be Element of G;
        a in (Omega).G;
        hence g*a = a*g by B2;
      end;
      hence g in center G by GROUP_5:77;
    end;

    thus g in center G implies g in Centralizer (Omega).G
    proof
      assume B1: g in center G;
      then for b being Element of G holds g*b = b*g by GROUP_5:77;
      then for b being Element of G st b in (Omega).G holds g*b = b*g;
      then g is Element of Centralizer (Omega).G by Th60;
      hence thesis;
    end;

  end;
  hence Centralizer (Omega).G = center G;
end;
@ %def Th77

\begin{theorem}
Let $N\normalSubgroup G$ be a subgroup. Then $C_{G}(N)\normalSubgroup G$.
\end{theorem}

<<Theorem: $N\normalSubgroup G\implies C_{G}(H)\normalSubgroup G$>>=
theorem Th78:
  for G being Group
  for N being normal Subgroup of G
  holds Centralizer N is normal Subgroup of G
proof
  let G be Group;
  let N be normal Subgroup of G;

  A2: for g,n being Element of G st n in N holds n |^ g in N
  proof
    let g,n be Element of G;
    assume B1: n in N;
    the multMagma of N = the multMagma of (N |^ g) by GROUP_3:def 13;
    then B2: the carrier of N = the carrier of (N |^ g);
    n |^ g in N |^ g by B1,GROUP_3:58;
    then n |^ g in the carrier of (N |^ g);
    then n |^ g in the carrier of N by B2;
    hence thesis;
  end;

  A3: for g,x,n being Element of G st x in Centralizer N & n in N
  holds (x |^ g)*n = n*(x |^ g)
  proof
    let g,x,n be Element of G;
    assume B1: x in Centralizer N;
    assume B2: n in N;
    consider n2 being Element of G such that
    B3: n2 = g * n * g";
    B4: n2 = n |^ g" by B3;
    then B5: n2 in N by B2,A2;
    x is Element of Centralizer N by B1;
    then x * n2 = n2 * x by B5,Th60;
    then (x * n2) |^ g = (n2 * x) |^ g
                      .= (n2 |^ g) * (x |^ g) by GROUP_3:23;
    then (x |^ g) * (n2 |^ g) = (n2 |^ g) * (x |^ g) by GROUP_3:23
                             .= n * (x |^ g) by B4,GROUP_3:25;
    then (x |^ g) * n = n * (x |^ g) by B4,GROUP_3:25;
    hence thesis;
  end;

  A4: for g,z being Element of G st z in Centralizer N holds z |^ g in Centralizer N
  proof
    let g,z be Element of G;
    assume Z1: z in Centralizer N;
    then for n being Element of G st n in N holds
    (z |^ g)*n = n*(z |^ g) by A3;
    then (z |^ g) is Element of Centralizer N by Th60;
    hence z |^ g in Centralizer N;
  end;


  for g being Element of G holds (Centralizer N) |^ g = Centralizer N
  proof
    let g be Element of G;

    B1: for z being Element of G st z in Centralizer N
    holds z |^ g in Centralizer N by A4;
    for z being Element of G
    holds z in (Centralizer N) |^ g iff z in (Centralizer N)
    proof
      let z be Element of G;
      hereby
        assume z in (Centralizer N) |^ g;
        then (z |^ g") in ((Centralizer N) |^ g) |^ g" by GROUP_3:58;
        then (z |^ g") in Centralizer N by GROUP_3:62;
        then (z |^ g") |^ g in Centralizer N by A4;
        hence z in (Centralizer N) by GROUP_3:25;
      end;
      assume z in (Centralizer N);
      then (z |^ g") in (Centralizer N) by A4;
      then (z |^ g") |^ g in (Centralizer N) |^ g by GROUP_3:58;
      hence z in (Centralizer N) |^ g by GROUP_3:25;
    end;

    hence (Centralizer N) |^ g = Centralizer N;
  end;
  hence Centralizer N is normal Subgroup of G by GROUP_3:def 13;
end;
@ %def Th78

\begin{theorem}
Let $H\subgroup G$, $h\in H$ and $n\in N_{G}(H)$. Then
$h^{n}=n^{-1}hn\in H$.
\end{theorem}

<<Theorem: $\forall h\in H,n\in N_{G}(H), n^{-1}hn\in H$>>=
theorem Th79:
  for G being Group
  for H being Subgroup of G
  for h,n being Element of G
  st h in H & n in Normalizer H
  holds h |^ n in H
proof
  let G be Group;
  let H be Subgroup of G;
  let h,n be Element of G;
  assume A1: h in H;
  assume A2: n in Normalizer H;
  then n" in Normalizer H by GROUP_2:51;
  then n" in Normalizer (carr H);
  then consider g being Element of G such that
  A3: n" = g & (carr H) |^ g = carr H by GROUP_3:129;
  h in carr H by A1;
  then h in (carr H) |^ g by A3;
  then consider h1 being Element of G such that
  A4: h = h1 |^ g & h1 in carr H by GROUP_3:41;
  h |^ n = (h1 |^ (n")) |^ n by A3,A4
        .= h1 by GROUP_3:25;
  then h |^ n in carr H by A4;
  hence h |^ n in H;
end;
@ %def Th79

\begin{theorem}
For any subgroup $H\subgroup G$, we have $H\subgroup N_{G}(H)$.
\end{theorem}

<<Theorem: $\forall H\subgroup G, H\subgroup N_{G}(H)$>>=
theorem Th80:
  for G being Group
  for H being Subgroup of G
  holds H is Subgroup of Normalizer H
proof
  let G be Group;
  let H be Subgroup of G;
  A1: for g being Element of G st g in H
  for x being Element of G st x in (carr H) |^ g holds x in carr H
  proof
    let g be Element of G;
    assume B1: g in H;
    let x be Element of G;
    assume B2: x in (carr H) |^ g;
    then consider h being Element of G such that
    B3: x = h |^ g & h in carr H by GROUP_3:41;
    B4: h in H by B3;
    g" in H by B1,GROUP_2:51;
    then g" * h in H by B4, GROUP_2:50;
    then (g" * h)*g in H by B1, GROUP_2:50;
    then g" * h * g in H;
    then h |^ g in H;
    then x in H by B3;
    hence x in carr H;
  end;

  for g being Element of G st g in H holds g in Normalizer H
  proof
    let g be Element of G;
    assume B1: g in H;
    B2: for x being Element of G st x in (carr H) |^ g holds x in carr H by A1,B1;
    for x being Element of G st x in carr H holds x in (carr H) |^ g
    proof
      let x be Element of G;
      thus x in carr H implies x in (carr H) |^ g
      proof
        assume B3: x in carr H;
        then B4: x in H;
        set h = x |^ g";
        B5: h = g * x * g";
        g" in H by B1,GROUP_2:51;
        then x * g" in H by B4,GROUP_2:50;
        then g * (x * g") in H by B1,GROUP_2:50;
        then g * x * g" in H by GROUP_1:def 3;
        then h in H;
        then B4: h in (carr H);
        B5: h |^ g = (x |^ g") |^ g
                  .= x by GROUP_3:25;
        h |^ g in (carr H) |^ g by B4, GROUP_3:41;
        hence x in (carr H) |^ g by B5;
      end;
    end;
    then (carr H) |^ g c= carr H & carr H c= (carr H) |^ g by B2;
    then (carr H) |^ g = carr H by XBOOLE_0:def 10;
    then g in Normalizer carr H by GROUP_3:129;
    hence g in Normalizer H;
  end;

  hence H is Subgroup of Normalizer H by GROUP_2:58;
end;
@ %def Th80

\begin{lemma}
Let $H\subgroup G$ be a subgroup. Then $C_{G}(H)\subgroup N_{G}(H)$.
\end{lemma}

\begin{proof}[Proof sketch]
We find every $g\in C_{G}(H)$ also lives in $g\in N_{G}(H)$. The result
follows immediately.
\end{proof}

<<Lemma: $C_{G}(H)\subgroup N_{G}(H)$>>=
Lm8:
  for G being Group
  for H being Subgroup of G
  holds Centralizer H is strict Subgroup of Normalizer H
proof
  let G be Group;
  let H be Subgroup of G;
  set Z = Centralizer H;
  for g being Element of G st g in Centralizer H holds g in Normalizer H
  <<Proof: $\forall g\in G, g\in C_{G}(H)\implies g\in N_{G}(H)$>>
  hence Centralizer H is strict Subgroup of Normalizer H by GROUP_2:58;
end;
@ %def Lm8

\begin{proof}[Proof step ($\forall g\in G, g\in C_{G}(H)\implies g\in N_{G}(H)$).]
We will show, for any arbitrary $g\in C_{G}(H)$, that $H^{g} = H$. This
implies $g\in N_{G}(H)$ by definition of the normalizer.
\end{proof}

<<Proof: $\forall g\in G, g\in C_{G}(H)\implies g\in N_{G}(H)$>>=
proof
  let g be Element of G;
  assume A1: g in Centralizer H;
  then g is Element of Centralizer H;
  then A2: for a being Element of G st a in H holds g*a = a*g by Th60;
  A3: for a being Element of G st a in H holds a = (g")*a*g
  proof
    let a be Element of G;
    assume B1: a in H;
    then g*a = a*g by A2;
    then g" * (a * g) = g" * (g*a)
                     .= (g" * g)*a by GROUP_1:def 3
                     .= (1_G)*a by GROUP_1:def 5
                     .= a by GROUP_1:def 4;
    hence a = (g")*a*g by GROUP_1:def 3;
  end;

  for a being Element of G holds a in H iff a in (H |^ g)
  proof
    let a be Element of G;
    thus a in H implies a in H |^ g
    proof
      assume B1: a in H;
      then a = a |^ g by A3;
      hence a in H |^ g by B1, GROUP_3:58;
    end;
    thus a in H |^ g implies a in H
    proof
      assume B1: a in H |^ g;
      then consider h being Element of G such that
      B2: a = h |^ g & h in H
      by GROUP_3:58;
      (h |^ g) = a |^ g by B2,A3;
      then h = a by GROUP_3:16;
      hence a in H by B2;
    end;
  end;
  then the multMagma of H = the multMagma of (H |^ g) by GROUP_2:60;
  then A5: carr H = carr(H |^ g);
  the carrier of H |^ g = carr(H) |^ g by GROUP_3:def 6;
  then carr H = carr(H) |^ g by A5;
  then ex h being Element of G st h=g & carr(H) |^ h = carr H;
  then g in Normalizer carr(H) by GROUP_3:129;
  hence g in Normalizer H;
end;
@

\begin{theorem}
Let $H\subgroup G$. Then $C_{G}(H)\normalSubgroup N_{G}(H)$.
\end{theorem}

\begin{proof}[Proof sketch]
We will show $(B_{4})$, for arbitrary $z\in C_{G}(H)$ and $n\in N_{G}(H)$, that
$z^{n}=n^{-1}zn$ commutes with any $h\in H$. This implies $(B_{5})$ that
$z^{n}\in C_{G}(H)$ and thus $C_{G}(H)\subgroup n^{-1}C_{G}(H)n$. It
follows that $C_{G}(H)\normalSubgroup N_{G}(H)$.
\end{proof}

<<Theorem: $C_{G}(H)\normalSubgroup N_{G}(H)$>>=
theorem Th81:
  for G being Group
  for H being Subgroup of G
  holds Centralizer H is strict normal Subgroup of Normalizer H
proof
  let G be Group;
  let H be Subgroup of G;

  (Centralizer H) is normal Subgroup of Normalizer H
  proof
    reconsider Z=Centralizer H as strict Subgroup of Normalizer H by Lm8;
    set N = Normalizer H;

    B60: for z being Element of N
    holds (for n being Element of N st n in H holds z*n = n*z) iff
          z is Element of Z
    <<Proof: $\forall z\in N_{G}(H), (\forall n\in N_{G}(H), n\in H\implies zn=nz)\iff z\in C_{G}(H)$>>


    B3: for z,n,h being Element of N
    st z in Z & n in N & h in H
    holds h |^ (z |^ n) = h
    proof
      let z,n,h be Element of N;
      assume C1: z in Z;
      assume C2: n in N;
      assume C3: h in H;
      C4: h |^ (z |^ n) = (z |^ n)" * h * (z |^ n)
      .= (z" |^ n) * h * (z |^ n) by GROUP_3:26
      .= (n" * z" * n) * h * (n" * z * n)
      .= ((n" * z") * n) * h * (n" * (z * n)) by GROUP_1:def 3
      .= ((n" * z") * n) * (h * (n" * (z * n))) by GROUP_1:def 3
      .= (n" * z") * (n * (h * (n" * (z * n)))) by GROUP_1:def 3
      .= (n" * z") * (n * h * (n" * (z * n))) by GROUP_1:def 3
      .= (n" * z") * ((n * h) * (n" * (z * n)))
      .= (n" * z") * (((n * h) * n") * (z * n)) by GROUP_1:def 3
      .= (n" * z") * ((n * h) * n") * (z * n) by GROUP_1:def 3
      .= (n" * z") * (n * h * n") * (z * n);

      C4a: for a,b being Element of G
      holds a in N & b in H & b in N implies a*b*a" in H & a*b*a" in N
      proof
        let a,b be Element of G;
        assume D1: a in N;
        assume D2: b in H;
        assume D3: b in N;
        D4: a*b in N by D1,D3,GROUP_2:50;
        D5: a" in N by D1,GROUP_2:51;
        then D6: a*b*a" in N by D4,GROUP_2:50;
        a" in N & b in H by D2,D5;
        then b |^ a" in H by Th79;
        then a*b*a" in H;
        hence a*b*a" in H;
        thus a*b*a" in N by D6;
      end;

      n * h * n" in H
      proof
        reconsider h1=h,n1=n as Element of G by GROUP_2:42;
        n1" = n" by GROUP_2:48;
        then h1*n1" = h*n" by GROUP_2:43;
        then n1*(h1*n1") = n*(h*n") by GROUP_2:43
                        .= n*h*n" by GROUP_1:def 3;
        then D1: n1*h1*n1" = n*h*n" by GROUP_1:def 3;
        h1 in N & n1 in N & h1 in H by C3;
        then n1*h1*n1" in H & n1*h1*n1" in N by C4a;
        hence thesis by D1;
      end;
      then consider h2 being Element of N such that
      C5: n * h * n" = h2 & h2 in H;

      z is Element of Z by C1;
      then for n0 being Element of N st n0 in H holds z*n0 = n0*z by B60;
      then z*h2 = h2*z by C5;
      then z"*(z * h2) = z" * (h2 * z);
      then (z" * z) * h2 = z" * (h2 * z) by GROUP_1:def 3
                        .= (z" * h2) * z by GROUP_1:def 3;
      then (z" * h2) * z = (z" * z) * h2
                        .= 1_N * h2 by GROUP_1:def 5
                        .= h2 by GROUP_1:def 4;
      then h2 * z" = ((z" * h2) * z) * z"
                  .= (z" * h2) * (z * z") by GROUP_1:def 3
                  .= (z" * h2) * 1_N by GROUP_1:def 5
                  .= z" * h2 by GROUP_1:def 4;
      then C6: z" * h2 = h2 * z";
      h |^ (z |^ n) = (n" * z") * (n * h * n") * (z * n) by C4
                   .= (n" * z") * h2 * (z * n) by C5
                   .= (n" * z") * (h2 * (z * n)) by GROUP_1:def 3
                   .= n" * (z" * (h2 * (z * n))) by GROUP_1:def 3
                   .= n" * ((z" * h2) * (z * n)) by GROUP_1:def 3
                   .= n" * ((h2 * z") * (z * n)) by C6
                   .= n" * (h2 * (z" * (z * n))) by GROUP_1:def 3
                   .= n" * (h2 * ((z" * z) * n)) by GROUP_1:def 3
                   .= n" * (h2 * (1_N * n)) by GROUP_1:def 5
                   .= n" * (h2 * n) by GROUP_1:def 4
                   .= n" * h2 * n by GROUP_1:def 3
                   .= h2 |^ n
                   .= (n * h * n") |^ n by C5
                   .= (h |^ n") |^ n
                   .= h by GROUP_3:25;
      hence h |^ (z |^ n) = h;
    end;


    B4: for z,n,h being Element of N
    st z in Z & h in H
    holds (z |^ n)*h = h*(z |^ n)
    proof
      let z,n,h be Element of N;
      assume C1: z in Z;
      assume C2: h in H;
      n in N;
      then h |^ (z |^ n) = h by C1,C2,B3;
      hence (z |^ n)*h = h*(z |^ n) by GROUP_3:22;
    end;


    B5: for n being Element of N
    for z being Element of N st z in Z holds (z |^ n) in Z
    proof
      let n be Element of N;
      let z be Element of N;
      assume C1: z in Z;
      set g = z |^ n;
      for h being Element of N st h in H holds g*h = h*g by C1,B4;
      then g is Element of Z by B60;
      hence thesis;
    end;

    for n being Element of Normalizer H
    holds Z is Subgroup of Z |^ n
    proof
      let n be Element of Normalizer H;
      for z being Element of N st z in Z holds z in Z |^ n
      proof
        let z be Element of N;
        assume C1: z in Z;
        (z |^ n") in Z by C1,B5;
        then (z |^ n") |^ n in (Z |^ n) by GROUP_3:58;
        hence z in Z |^ n by GROUP_3:25;
      end;

      hence Z is Subgroup of Z |^ n by GROUP_2:58;
    end;
    then Z is normal Subgroup of N by GROUP_3:121;
    hence thesis;
  end;
  hence thesis;
end;
@ %def Th81

\begin{proof}[Proof step ($B_{60}$).]
We prove the analogous result from theorem 60, but when restricting
quantifiers to the normalizer.
\end{proof}

<<Proof: $\forall z\in N_{G}(H), (\forall n\in N_{G}(H), n\in H\implies zn=nz)\iff z\in C_{G}(H)$>>=
proof
  let z be Element of N;
  C0: z in N;
  reconsider z1=z as Element of G by GROUP_2:42;

  C2: z is Element of Z implies (for n being Element of N st n in H holds z*n = n*z)
  proof
    assume Z1: z is Element of Z;
    let n be Element of N;
    assume Z2: n in H;
    reconsider n1=n as Element of G by GROUP_2:42;
    Z3: n1 in H by Z2;
    z in Z by Z1;
    then z in carr(Centralizer H);
    then z1 in Centralizer H;
    then z1 is Element of Centralizer H;
    then z1*n1 = n1*z1 by Z3,Th60
              .= n*z by GROUP_2:43;
    then z*n = n*z by GROUP_2:43;
    hence thesis;
  end;
  not (z is Element of Z) implies not (for n being Element of N st n in H
  holds z*n = n*z)
  proof
    assume Z1: not z is Element of Z;
    then not z in Z;
    then not z1 in Centralizer H;
    then not z1 is Element of Centralizer H;
    then consider g being Element of G such that
    Z2: g in H & g*z1 <> z1 * g by Th60;
    Z3: H is Subgroup of Normalizer H by Th80;
    g in H by Z2;
    then g in Normalizer H by Z3, GROUP_2:41;
    then reconsider n=g as Element of N;
    Z6: n in H by Z2;
    Z4: g*z1 = n*z by GROUP_2:43;
    z1*g = z*n by GROUP_2:43;
    then Z5: n*z <> z*n by Z2,Z4;
    take n;
    thus thesis by Z5,Z6;
  end;
  hence
  (for n being Element of N st n in H holds z*n = n*z) implies z is Element of Z;

  thus z is Element of Z implies (for n being Element of N st n in H
  holds z*n = n*z) by C2;
end;
@
