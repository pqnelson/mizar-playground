% -*- mode: poly-noweb; noweb-code-mode: mizar-mode; -*-
\subsection{Inner Automorphisms}

\M
We can now organize our treatment of inner automorphisms.

<<Results and definition for Inner automorphisms>>=
<<Define [[inner]] for Automorphism>>
<<Theorem: [[Id_G]] is effectively inner>>
<<Register [[inner]] for [[Automorphism]]>>
<<Theorem: Relate [[Automorphism of G]] to elements of [[Aut G]]>>
<<Theorem: $f$ in [[InnAut G]] iff $f$ is [[inner Automorphism of G]]>>
<<Theorem: inner automorphism acting on subgroup is conjugate of argument>>
<<Theorem: Kernel of conjugation as endomorphism>>
<<Theorem: Conjugation by fixed element is an automorphism>>
<<Corollary: conjugation of given element is an \emph{inner} automorphism>>
<<Theorem: constructing inner automorphisms from group elements>>
<<Theorem: inner Automorphisms fix only normal Subgroups>>

@

\N{Definition: Inner Automorphism}\index{Automorphism!inner}\index{inner!Automorphism|see {Automorphism}}
We call a group automorphism $f\in\Aut(G)$ \define{inner} if there is a
group element $g\in G$ such that for all $x\in G$ we have $f(x) = x^{g} = g^{-1}xg$.
That is, $f$ is just conjugation by a fixed group element.

<<Define [[inner]] for Automorphism>>=
definition :: Def5
  let G;
  let IT be Automorphism of G;
  attr IT is inner means
  :Def5:
  ex a being Element of G st
  for x being Element of G holds IT.x = x |^ a;
end;
<<Outer as antonym of inner>>

@ %def Def5 inner

\M\index{Automorphism!outer} We also recall that an automorphism is called \define{Outer} if it is
not inner. Mizar let's us do this with the [[antonym]] construct within
a [[notation]] block.

<<Outer as antonym of inner>>=
notation
  let G be Group, f be Automorphism of G;
  antonym f is outer for f is inner;
end;

@ %def outer

\N{Update our vocabular file}
Before rushing off to prove properties concerning inner and outer
automorphisms, we should add the attributes to our vocabulary file.


<<DICT/CHAR.VOC>>=
Vinner
Vouter

@

\N{Theorem: $\Id_{G}$ is effectively inner}\label{thm:characteristic:id-inner}
We will be registering ``inner'' as an attribute for ``Automorphism
of $G$''. This will require proving that there exists an inner
Automorphism of $G$. I've found the trivial examples are often best for
establishing the existence of such things, so we will prove [[Id G]] is
an inner Automorphism. This uses the fact, if $e\in G$ is the identity
element, then for any $g\in G$ we have conjugation $g^{e} = e^{-1}ge=g$
(proven in Theorem~19 of [[GROUP_3]]).\MizThm{GROUP\_3}{19}

<<Theorem: [[Id_G]] is effectively inner>>=
theorem Th12:
  for x being Element of G holds (Id_G).x = x |^ 1_G
proof
  let x be Element of G;
  (Id_G).x = x by Def3
          .= x |^ 1_G by GROUP_3:19;
  hence thesis;
end;

@ %def Th12

\M Now registering [[inner]] for [[Automorphism]].

<<Register [[inner]] for [[Automorphism]]>>=
registration
  let G;
  cluster inner for Automorphism of G;
  existence
  <<Proof of existence of an inner Automorphism>>
end;

@

\N{Proof} The proof is a two punch knock-out. We take [[Id_G]] to be the
morphism, [[1_g]] the group's identity element to be the element
[[Id_G]] conjugates by, then from earlier (\S\ref{thm:characteristic:id-inner})
we have [[Id_G]] be inner.

<<Proof of existence of an inner Automorphism>>=
proof
  take Id_G;
  take 1_G;
  thus thesis by Th12;
end;

@

\N{Theorem ($f\in\aut(G)\iff f$ is [[Automorphism of G]])}
Mizar has [[AUTGROUP]], an article which defines [[Aut G]] the
collection of functions on the underlying set $U(G)$ of a group $G$. We
can prove that $f\in\aut(G)$ if and only if $f$ is [[Automorphism of G]].

<<Theorem: Relate [[Automorphism of G]] to elements of [[Aut G]]>>=
theorem Th13:
  for G being strict Group, f being object
  holds (f in Aut G) iff (f is Automorphism of G)
proof
  let G be strict Group;
  let f be object;
  thus f in Aut G implies f is Automorphism of G
  <<Proof $f\in\aut(G)\implies f$ is [[Automorphism of G]]>>
  thus f is Automorphism of G implies f in Aut G by AUTGROUP:def 1;
  thus thesis;
end;

@ %def Th13

\N{Proof forwards direction} The forward direction is
straightforward. The only subtlety is, since we didn't assume anything
about $f$, we should establish it's an endomorphism of $G$ along the way,

<<Proof $f\in\aut(G)\implies f$ is [[Automorphism of G]]>>=
proof
  assume A1: f in Aut G;
  then reconsider f as Endomorphism of G by AUTGROUP:def 1;
  f is bijective by A1,AUTGROUP:def 1;
  hence thesis;
end;

@


\N{Inner automorphisms are inner automorphisms}
We can relate the notion of an [[inner Automorphism of G]] with elements
of [[InnAut G]] from [[AUTGROUP]]. The only peculiarity is that
[[AUTGROUP]] requires $G$ to be a \emph{strict} group.

<<Theorem: $f$ in [[InnAut G]] iff $f$ is [[inner Automorphism of G]]>>=
<<Lemma: Elements of [[InnAut G]] are automorphisms>>

theorem Th14:
  for G being strict Group
  for f being object
  holds (f in InnAut G) iff (f is inner Automorphism of G)
proof
  let G be strict Group;
  let f be object;
  A1: f is Automorphism of G implies f is Element of Funcs (the carrier of G, the carrier of G) by FUNCT_2:9;
  thus (f in InnAut G) implies (f is inner Automorphism of G)
  <<Proof $f$ is in [[InnAut G]] $\implies$ ($f$ is inner automorphism)>>
  thus (f is inner Automorphism of G) implies (f in InnAut G)
  <<Proof ($f$ is inner automorphism) $\implies$ $f$ is in [[InnAut G]]>>
  thus thesis;
end;

@ %def Th14

\N{Proof in forwards direction} The proof amounts to unwinding
definitions, but the subtlety is in first reconsidering $f$ as an
Automorphism of $G$ thanks to our handy-dandy lemma.

<<Proof $f$ is in [[InnAut G]] $\implies$ ($f$ is inner automorphism)>>=
proof
  assume B1: f in InnAut G;
  then f is Automorphism of G by Lm4;
  then reconsider f as Automorphism of G;
  consider a being Element of G such that
  B2: for x being Element of G holds f.x = x |^ a
  by A1,B1,AUTGROUP:def 4;
  f is inner Automorphism of G by Def5,B2;
  hence thesis;
end;

@

\N{Proof in backwards direction}
This is again unwinding the definitions. The same subtlety lurks here,
requiring us to reconsider $f$ as an inner automorphism of $G$.

<<Proof ($f$ is inner automorphism) $\implies$ $f$ is in [[InnAut G]]>>=
proof
  assume B1: f is inner Automorphism of G;
  then reconsider f as inner Automorphism of G;
  f is Element of Aut G by B1,Th13;
  then consider a being Element of G such that
  B2: for x being Element of G holds f.x = x |^ a
  by B1,Def5;
  f in InnAut G by A1,B2,AUTGROUP:def 4;
  hence thesis;
end;

@

\M
It's relatively straightforward to show that if $f$ is an element of
[[InnAut G]], then $f$ is an [[Automorphism of G]]. We just unwind the
definitions.

<<Lemma: Elements of [[InnAut G]] are automorphisms>>=
Lm4:
  for G being strict Group
  for f being Element of InnAut G
  holds f is Automorphism of G
proof
  let G be strict Group;
  let f be Element of InnAut G;
  f is Element of Aut G by AUTGROUP:12;
  hence f is Automorphism of G by Th13;
end;

@ %def Lm4

\begin{theorem}
Given any element $a\in G$, and any inner automorphism $f$ of $G$ such
that $\forall x\in G, f(x) = x^{a} = a^{-1}xa$, it follows that the
image of a subgroup under $f$ is the conjugate of that subgroup
$f(H) = H^{a}$.
\end{theorem}

<<Theorem: inner automorphism acting on subgroup is conjugate of argument>>=
theorem Th15:
  for H being Subgroup of G
  for a being Element of G
  for f being inner Automorphism of G
  st (for x being Element of G holds f.x = x |^ a)
  holds Image(f|H) = H |^ a
proof
  let H be Subgroup of G,
      a be Element of G,
      f be inner Automorphism of G;
  assume
A1: for x being Element of G holds f.x = x |^ a;
C1: for h being Element of G st h in H holds (f|H).h = h |^ a
  proof
    let h be Element of G;
    assume h in H;
    hence (f|H).h = f.h by Lm3
                 .= h |^ a by A1;
  end;

C2: for y being Element of G st y in Image(f|H) holds y in H |^ a
  <<Proof $\forall y\in G, y\in f(H)\implies y\in H^{a}$>>
C3: for y being Element of G st y in H |^ a holds y in Image(f|H)
  <<Proof $\forall y\in G, y \in f(H)\impliedby y\in H^{a}$>>
  for y being Element of G holds y in (H |^ a) iff y in Image(f|H) by C2,C3;
  then (H |^ a) = Image(f|H) by GROUP_2:def 6;
  hence thesis;
end;

@ %def Th15

\M The forward direction amounts to unwrapping the definition of $f(h) = h^{a}$.

<<Proof $\forall y\in G, y\in f(H)\implies y\in H^{a}$>>=
proof
  let y be Element of G;
  assume y in Image(f|H);
  then consider h being Element of H such that
  B1: (f|H).h = y by GROUP_6:45;
  reconsider h as Element of G by GROUP_2:42;
  B2: h in H by STRUCT_0:def 5;
  then h |^ a = (f|H).h by B2,C1
             .= y by B1;
  hence y in H |^ a by B2,GROUP_3:58;
end;

@

\M The proof in the backwards direction begins with $y = g^{a}$ for some
$g\in G$, and showing $y = f(g)$, which amounts to ``plug it in''.

<<Proof $\forall y\in G, y \in f(H)\impliedby y\in H^{a}$>>=
proof
  let y be Element of G;
  assume y in H |^ a;
  then consider g being Element of G such that
  B1:    y=g|^a and
  B2:    g in H
  by GROUP_3:58;

  B3: (f|H).g = f.g by Lm3,B2
             .= g |^ a by A1
             .= y by B1;
  g is Element of H by B2,LmSubgroupElt;
  hence y in Image(f|H) by B3,GROUP_6:45;
end;

@

\begin{theorem}
Let $G$ be a group, $f\in\Inn(G)$ be an inner automorphism.
Then $\ker(f)=\trivialSubgroup_{G}$.
\end{theorem}

We will prove something a little
more general, namely, any endomorphism $f\colon G\to G$ defined by
$f(x)=x^{a}$ (for some fixed $a\in G$) will have a trivial kernel.

<<Theorem: Kernel of conjugation as endomorphism>>=
theorem Th16:
  for a being Element of G
  for f being Endomorphism of G
  st (for x being Element of G holds f.x = x |^ a)
  holds Ker f = (1).G
proof
  let a be Element of G;
  let f be Endomorphism of G;
  assume A1: for x being Element of G holds f.x = x |^ a;
  for x being Element of G holds x in Ker f implies x in (1).G
  <<Proof $\ker(f)\subset\trivialSubgroup$>>
  then A2: Ker f is Subgroup of (1).G by GROUP_2:58;

  A3: (1).G is Subgroup of Ker f by GROUP_2:65;
  thus Ker f = (1).G by A2,A3,GROUP_2:55;
end;

@ %def Th16

\M Let $x\in\ker(f)$ be arbitrary, then $f(x)=1_{G}$. But this means $x=1_{G}$
(according to \verb#GROUP_3:18#). Thus $x\in\trivialSubgroup_{G}$ by
definition of the trivial subgroup.

<<Proof $\ker(f)\subset\trivialSubgroup$>>=
proof
  let x be Element of G;
  assume x in Ker f;
  then 1_G = f.x by GROUP_6:41
          .= x |^ a by A1;
  then x = 1_G by GROUP_3:18;
  hence x in (1).G by GROUP_2:46;
end;

@

\begin{theorem}[Conjugation by fixed element is an automorphism]
Let $G$ be a group, $a\in G$ a fixed element.
The endomorphism $f\colon G\to G$ defined by $f(x)=x^{a}$ is, in fact,
an automorphism of $G$.
\end{theorem}

<<Theorem: Conjugation by fixed element is an automorphism>>=
theorem Th17:
  for a being Element of G
  for f being Endomorphism of G
  st (for x being Element of G holds f.x = x |^ a)
  holds f is Automorphism of G
proof
  let a be Element of G;
  let f be Endomorphism of G;
  assume A1: for x being Element of G holds f.x = x |^ a;
  then Ker f = (1).G by Th16;
  then A2: f is one-to-one by GROUP_6:56;
  ex fInv being Endomorphism of G st f*fInv = id (the carrier of G)
  <<Proof an endomorphism $f^{-1}$ exists>>

  then rng f = the carrier of G by FUNCT_2:18;
  then f is onto by GROUP_6:58;
  then f is bijective by A2;
  then f is Automorphism of G;
  hence thesis;
end;

@ %def Th17

\N{Proof an inverse exists}
This is the long part of the proof, its length owing to showing every
detail.

<<Proof an endomorphism $f^{-1}$ exists>>=
proof
  deffunc F(Element of G) = ($1) |^ a";
  consider fInv be Function of the carrier of G, the carrier of G such that
  A3: for g being Element of G holds fInv.g = F(g) from FUNCT_2:sch 4;
  <<Establish $f^{-1}$ is an Endomorphism>>
  <<Establish $f^{-1}$ is the inverse function of $f$>>
  hence thesis;
end;

@

\N{Sub-Proof: $f^{-1}$ is the inverse of $f$}
The proof is straightforward, simply compose the functions together and
show we get the identity function. We just have to unwind a lot of
definitions.

<<Establish $f^{-1}$ is the inverse function of $f$>>=
for x being Element of G holds (f*fInv).x = (id the carrier of G).x
proof
  let x be Element of G;
  (f * fInv).x = f.(fInv.x) by FUNCT_2:15
              .= f.(x |^ a") by A3
              .= (x |^ a") |^ a by A1
              .= x |^ (a" * a) by GROUP_3:24
              .= x |^ 1_G by GROUP_1:def 5
              .= x by GROUP_3:19
              .= (id the carrier of G).x;
  hence thesis;
end;
then f*fInv = id the carrier of G by FUNCT_1:2;

@

\N{Sub-Proof $f^{-1}$ is a group endomorphism}
We need to establish $f^{-1}$ is multiplicative, i.e.,
$f^{-1}(x_{1}x_{2}) = f^{-1}(x_{1})f^{-1}(x_{2})$. The heavy-lifting has
been done in [[GROUP_3]].

<<Establish $f^{-1}$ is an Endomorphism>>=
for x1,x2 being Element of G holds fInv.(x1 * x2) = fInv.x1 * fInv.x2
proof
  let x1,x2 be Element of G;
  A4: fInv.x1 = x1 |^ a" & fInv.x2 = x2 |^ a" by A3;
  fInv.(x1 * x2) = (x1 * x2) |^ a" by A3
                .= (x1 |^ a") * (x2 |^ a") by GROUP_3:23
                .= fInv.x1 * fInv.x2 by A4;
  hence thesis;
end;
then fInv is multiplicative by GROUP_3:23, GROUP_6:def 6;
then fInv is Endomorphism of G;
then reconsider fInv as Endomorphism of G;

@

\begin{corollary}
Given a group element $a\in G$, we can always construct an
inner automorphism $f\in\Inn(G)$ defined by
$\forall x\in G,f(x)=x^{a}=a^{-1}xa$ conjugation by $a$.
\end{corollary}

<<Corollary: conjugation of given element is an \emph{inner} automorphism>>=
theorem Th18:
  for a being Element of G
  for f being Endomorphism of G
  st (for x being Element of G holds f.x = x |^ a)
  holds f is inner Automorphism of G
proof
  let a be Element of G;
  let f be Endomorphism of G;
  assume A1: for x being Element of G holds f.x = x |^ a;
  then f is Automorphism of G by Th17;
  hence f is inner Automorphism of G by A1,Def5;
end;

@ %def Th18

\N{Constructing inner automorphisms from group elements}
Given a group $G$ and suppose we have an element $a\in G$, can we
construct an inner automorphism $f\in\Inn(G)$ such that
$\forall x\in G, f(x) = x^{a}$? Yes, we can do it!
A wiser way to organize these results may be to first show such an $f$
is an Endomorphism and it exists, then use that result in the proof that
it's an automorphism, and so on.

<<Theorem: constructing inner automorphisms from group elements>>=
theorem Th19:
  for a being Element of G
  holds ex f being inner Automorphism of G st (for x being Element of G
  holds f.x = x |^ a)
proof
  let a be Element of G;
  deffunc F(Element of G) = ($1) |^ a;
  consider f be Function of the carrier of G, the carrier of G such that
A1: for g being Element of G holds f.g = F(g) from FUNCT_2:sch 4;
  f is Element of Funcs(the carrier of G, the carrier of G) by FUNCT_2:8;
  for x1,x2 being Element of G holds f.(x1 * x2) = f.x1 * f.x2
  proof
    let x1,x2 be Element of G;
    A2: f.x1 = x1 |^ a & f.x2 = x2 |^ a by A1;
    f.(x1 * x2) = (x1 * x2) |^ a by A1
               .= (x1 |^ a) * (x2 |^ a) by GROUP_3:23
               .= f.x1 * f.x2 by A2;
    hence thesis;
  end;
  then f is multiplicative by GROUP_3:23, GROUP_6:def 6;
  then f is Endomorphism of G;
  then reconsider f as Endomorphism of G;
  for x being Element of G holds f.x = x |^ a & f is inner Automorphism of G
  by A1,Th18;
  hence thesis by C1;
end;

@ %def Th19

\begin{theorem}
  Let $H\subgroup G$ be an arbitrary subgroup.
  Then $\forall\varphi\in\Inn(G),\varphi(H)=H$ if and only if
  $H\normalSubgroup G$.
\end{theorem}

This is another, ``Well, I guess Mizar should have this, but I can't
find it, so here we go!''-type proof. The only quirk is the use of
[[strict Subgroup]], which is necessary because otherwise [[H |^ a]] is
strictly speaking a [[multMagma]], not a subgroup. And to assert two
subgroups are equal, we need a [[strict]] subgroup, which requires
adding a [[strict]] hypothesis.

<<Theorem: inner Automorphisms fix only normal Subgroups>>=
theorem Th20:
  for H being strict Subgroup of G
  holds (H is normal) iff (for f being inner Automorphism of G holds Image(f|H)=H)
proof
  let H be strict Subgroup of G;
  A1: (H is normal) implies (for f being inner Automorphism of G holds Image(f|H)=H)
  <<Proof: normal implies fixed by inner automorphisms>>
  A2: not ((for f being inner Automorphism of G holds Image(f|H)=H) implies H is normal)
  implies contradiction
  <<Proof by contradiction: fixed by inner automorphisms implies normal>>
  thus thesis by A1,A2;
end;

@ %def Th20

\begin{remark}[On \texttt{strict} hypothesis]
The [[strict]] condition is necessary to prove $H\normalSubgroup G$
implies $\forall\varphi\in\Inn(G),\varphi(H)=H$. Otherwise, we end up
with the slightly peculiar situation where $\varphi(H) = $
[[the multMagma of ]]$H$, which isn't terrible, but requires additional
steps later on in proving ``$K\subgroup N$ is characteristic and
$N\normalSubgroup G$ is [[strict]] normal implies $K\normalSubgroup G$.''
\end{remark}

<<Proof: normal implies fixed by inner automorphisms>>=
proof
  assume B1: H is normal;
  let f be inner Automorphism of G;
  consider a being Element of G such that
  B2: for x being Element of G holds f.x = x |^ a
  by Def5;
  Image(f|H) = H |^ a by B2,Th15
            .= the multMagma of H by B1,GROUP_3:def 13
            .= H by GROUP_2:def 6;
  hence Image(f|H)=H;
end;

@

\M Mizar's proof by contradiction is rather curious. If we want to prove
$P\implies Q$ by contradiction, we assert $\neg(P\implies
Q)\implies\bot$. The proof we offer is about as satisfying as any other
proof by contradiction.

<<Proof by contradiction: fixed by inner automorphisms implies normal>>=
proof
  assume B1: for f being inner Automorphism of G holds Image(f|H)=H;
  assume not H is normal;
  then consider a being Element of G such that
  B2: H |^ a <> the multMagma of H
  by GROUP_3:def 13;
  consider f being inner Automorphism of G such that
  B3: (for x being Element of G holds f.x= x |^ a)
  by Th19;
  Image(f|H) = H |^ a by B3, Th15;
  hence contradiction by B1,B2;
end;

@
