\subsection{Characteristic Subgroups}

\M
We now arrive at the meat of the matter: characteristic subgroups!

<<Characteristic subgroup results>>=
<<Define characteristic attribute>>
<<Existence theorem for characteristic subgroups>>
<<Register [[characteristic]] as attribute for Subgroup>>
<<Theorem: characteristic subgroups are normal>>
<<Register characteristic subgroups are normal>>

@

\begin{definition}[{Dummit and Foote~\cite[\S4,4]{dummit-foote}}]
A subgroup $H$ of $G$ is called \define{Characteristic} in $G$, usually
denoted $H~\mathrm{char}~G$, if every Automorphism of $G$ maps $H$ to
itself; i.e., $\sigma(H)=H$ for all $\sigma\in\aut(G)$.
\end{definition}

<<Define characteristic attribute>>=
definition :: Def9
  let G;
  let IT be Subgroup of G;
  attr IT is characteristic means
  :Def9:
  for f being Automorphism of G
  holds Image (f|IT) = IT;
end;

@ %def characteristic Def9

\N{Existence theorem}
We need to prove the existence of a characteristic subgroup. For our
purposes, it suffices to prove that the trivial subgroup is characteristic.

<<Existence theorem for characteristic subgroups>>=
<<Lemma: if $H\subgroup\trivialGroup$, then $H=\trivialGroup$>>

Lm7: ex H being Subgroup of G st H is characteristic
proof
  take H = (1).G;
A1:    for f being Automorphism of G
       holds Image (f|H) is Subgroup of H by Th2;
A2:  for f being Automorphism of G holds Image(f|H) = H
  proof
    let f be Automorphism of G;
    reconsider I = Image(f|H) as Subgroup of H by A1;
    H = I by Lm6;
    then
A3:    Image(f|H) = H by A1;
    thus thesis by A3;
  end;
  then H is characteristic by A2,Def9;
  hence thesis;
  end;

@

\N{Lemma: the only subgroup of the trivial group is trivial}
I couldn't quite find this anywhere in the MML, so I had to prove it
myself.

<<Lemma: if $H\subgroup\trivialGroup$, then $H=\trivialGroup$>>=
Lm6: for H being Subgroup of G st H is Subgroup of (1).G holds the
multMagma of H = the multMagma of (1).G
proof
  let H be Subgroup of G;
  assume
A1:  H is Subgroup of (1).G;
  reconsider H as Subgroup of (1).G by A1;
A2: (1).G is Subgroup of H by GROUP_2:65;
  H is Subgroup of (1).G & (1).G is Subgroup of H by A1,A2;
  hence thesis by GROUP_2:55;
end;

@

\N{Register [[characteristic]] for Subgroups}
Now we can instruct Mizar to recognize [[characteristic]] as an
adjective of [[Subgroup]].

<<Register [[characteristic]] as attribute for Subgroup>>=
registration
  let G;
  cluster characteristic for Subgroup of G;
  existence by Lm7;
end;

@



\N{Characteristic subgroups are normal}
Let $G$ be a group, $K\subgroup G$ a subgroup. If $K$ is a
characteristic subgroup of $G$, then $K\normalSubgroup G$ it is also
normal.

<<Theorem: characteristic subgroups are normal>>=
theorem Th11:
  for G being Group
  for K being characteristic Subgroup of G
  holds K is normal Subgroup of G
proof
  let G be Group;
  let K be characteristic Subgroup of G;
A1:  for a being Element of G holds K |^ a = K
  <<Sub-proof: characteristic subgroups invariant under conjugation>>
  for a being Element of G holds the carrier of K |^ a = the carrier of K by A1;
  then for a being Element of G holds the multMagma of K = K |^ a by GROUP_2:59;
  hence K is normal Subgroup of G by A1,GROUP_3:def 13;
end;

@ %def Th11

\M
Really, proving a characteristic subgroup is normal amounts to proving
invariance under conjugation. Fortunately, we have established this
along the way! We just have to point to our hard work from earlier.

<<Sub-proof: characteristic subgroups invariant under conjugation>>=
proof
  let a be Element of G;
  consider f being inner Automorphism of G such that
A2: for x being Element of G holds f.x = x |^ a
  by ThAlwaysFindInnerAuto;
  A3: Image(f|K) = K by GROUP_6:57,Def9;
  Image(f|K) = K |^ a by A2,Th7;
  then K |^ a = K by A3,Def9;
  hence thesis;
end;

@

\M
Now we can register this fact with Mizar, so it will be automatically
accounted for in future proofs. Since we made this fact a proof, we just
have to tell Mizar where to find the proof.

<<Register characteristic subgroups are normal>>=
registration
  let G be Group;
  cluster characteristic -> normal for Subgroup of G;
  coherence by Th11;
end;

@
