% -*- mode: poly-noweb; noweb-code-mode: mizar-mode; -*-
\subsection{Characteristic Subgroups}

\M
We now arrive at the meat of the matter: characteristic subgroups!

<<Characteristic subgroup results>>=
<<Define [[characteristic]] attribute>>
<<Lemma: trivial subgroup is [[characteristic]]>>
<<Theorem: Existence of characteristic subgroups>>
<<Register [[characteristic]] as attribute for Subgroup>>
<<Register [[strict]] [[characteristic]] for Subgroups>>
<<Theorem: characteristic subgroups are normal>>
<<Register characteristic subgroups are normal>>
<<Theorem: unique subgroup of order $n$ is [[characteristic]]>>
<<Theorem: characteristic subgroup of a normal subgroup is normal>>
<<Theorem: transitivity of characteristic subgroups>>
<<Theorem: $H\subgroup G$ is characteristic iff $\forall\varphi\in\Aut(G),\varphi(H)\subgroup H$>>
<<Theorem: $Z(G)$ is characteristic subgroup>>

@

\begin{definition}[{Dummit and Foote~\cite[\S4.4]{dummit-foote}}]\index{Subgroup!Characteristic}\index{Characteristic!Subgroup}
A subgroup $H$ of $G$ is called \define{Characteristic} in $G$, usually
denoted $H~\mathrm{char}~G$, if every Automorphism of $G$ maps $H$ to
itself; i.e., $\sigma(H)=H$ for all $\sigma\in\aut(G)$.
\end{definition}

<<Define [[characteristic]] attribute>>=
definition :: Def9
  let G;
  let IT be Subgroup of G;
  attr IT is characteristic means
  :Def9:
  for f being Automorphism of G
  holds Image (f|IT) = IT;
end;

@ %def characteristic Def9

\begin{lemma}
  For any group $G$, its trivial subgroup $\trivialSubgroup$ is characteristic.
\end{lemma}

<<Lemma: trivial subgroup is [[characteristic]]>>=
<<Lemma: if $H\subgroup\trivialGroup$, then $H=\trivialGroup$>>

Lm6: (1).G is characteristic
proof
A1:  for f being Automorphism of G
     holds Image (f|(1).G) is Subgroup of (1).G by Th2;
  for f being Automorphism of G holds Image(f|(1).G) = (1).G
  proof
    let f be Automorphism of G;
    reconsider I = Image(f|(1).G) as Subgroup of (1).G by A1;
    (1).G = I by Lm5;
    then Image(f|(1).G) = (1).G by A1;
    hence thesis;
  end;
  then (1).G is characteristic by Def9;
  hence thesis;
end;

@ %def Lm6

\N{Lemma: the only subgroup of the trivial group is trivial}
I couldn't quite find this anywhere in the MML, so I had to prove it
myself.

<<Lemma: if $H\subgroup\trivialGroup$, then $H=\trivialGroup$>>=
Lm5: for H being Subgroup of G st H is Subgroup of (1).G holds the
multMagma of H = the multMagma of (1).G
proof
  let H be Subgroup of G;
  assume A1: H is Subgroup of (1).G;
  then reconsider H as Subgroup of (1).G;
  (1).G is Subgroup of H by GROUP_2:65;
  then H is Subgroup of (1).G & (1).G is Subgroup of H by A1;
  hence thesis by GROUP_2:55;
end;

@ %def Lm5

\N{Existence theorem}
We need to prove the existence of a characteristic subgroup. For our
purposes, it suffices to prove that the trivial subgroup is characteristic.

<<Theorem: Existence of characteristic subgroups>>=

theorem Th21:
  ex H being Subgroup of G st H is characteristic
proof
  take H = (1).G;
  H is characteristic by Lm6;
  hence thesis;
end;

@ %def Th21

\N{Register [[characteristic]] for Subgroups}
Now we can instruct Mizar to recognize [[characteristic]] as an
adjective of [[Subgroup]].

<<Register [[characteristic]] as attribute for Subgroup>>=
registration
  let G;
  cluster characteristic for Subgroup of G;
  existence by Th21;
end;

@

\N{Register [[strict]] [[characteristic]] for Subgroups}
We can also register the cluster [[strict]] [[characteristic]] for
Subgroups, which will come handy later.

<<Register [[strict]] [[characteristic]] for Subgroups>>=
registration
  let G be Group;
  cluster strict characteristic for Subgroup of G;
  existence
  proof
    take (1).G;
    thus thesis by Lm6;
  end;
end;

@


\N{Characteristic subgroups are normal}
Let $G$ be a group, $K\subgroup G$ a subgroup. If $K$ is a
characteristic subgroup of $G$, then $K\normalSubgroup G$ it is also
normal.

<<Theorem: characteristic subgroups are normal>>=
theorem Th22:
  for K being characteristic Subgroup of G
  holds K is normal Subgroup of G
proof
  let K be characteristic Subgroup of G;
A1:  for a being Element of G holds K |^ a = K
  <<Sub-proof: characteristic subgroups invariant under conjugation>>
  for a being Element of G holds the carrier of K |^ a = the carrier of K by A1;
  then for a being Element of G holds the multMagma of K = K |^ a by GROUP_2:59;
  hence K is normal Subgroup of G by A1,GROUP_3:def 13;
end;

@ %def Th22

\M
Really, proving a characteristic subgroup is normal amounts to proving
invariance under conjugation. Fortunately, we have established this
along the way! We just have to point to our hard work from earlier.

<<Sub-proof: characteristic subgroups invariant under conjugation>>=
proof
  let a be Element of G;
  consider f being inner Automorphism of G such that
A2: for x being Element of G holds f.x = x |^ a
  by Th19;
  K = Image(f|K) by GROUP_6:57,Def9
   .= K |^ a by A2,Th15;
  hence thesis;
end;

@

\M
Now we can register this fact with Mizar, so it will be automatically
accounted for in future proofs. Since we made this fact a proof, we just
have to tell Mizar where to find the proof.

<<Register characteristic subgroups are normal>>=
registration
  let G be Group;
  cluster characteristic -> normal for Subgroup of G;
  coherence by Th22;
end;

@

\begin{theorem}
  If $H$ is the unique subgroup of a given order in a group $G$,
  then $H$ is characteristic in $G$.
\end{theorem}

\begin{proof}
Let $H$ be a subgroup of $G$. Assume there are no other subgroups of
order $|H|$. Then for any $\varphi\in\Aut(G)$, we'd have $\varphi(H)=H$
since $\varphi(H)$ has the same order as $H$ by
Theorem~\ref{thm:pure-math:iso-subgroups-have-same-order}, but we
assumed there is only one (namely, $H$).
\end{proof}

<<Theorem: unique subgroup of order $n$ is [[characteristic]]>>=
theorem Th23:
  for H being strict Subgroup of G
  st (for K being strict Subgroup of G
      st card K = card H
      holds H = K)
  holds H is characteristic
proof
  let H be strict Subgroup of G;
  assume A0: for K being strict Subgroup of G st card K = card H holds H = K;
  H is characteristic
  proof
    let phi be Automorphism of G;
    Image(phi|H) = phi .: H by GRSOLV_1:def 3;
    then Image(phi|H),H are_isomorphic by Th8;
    then card H = card Image(phi|H) by GROUP_6:73;
    then Image(phi|H) = H by A0;
    hence thesis;
  end;
  hence thesis;
end;

@ %def Th23

\begin{theorem}
If $K\subgroup H$ is characteristic, and if $H\normalSubgroup G$ is normal,
then $K\normalSubgroup G$ is normal.
\end{theorem}

This required a surprising amount of legwork, even when carving it out
into a helper theorem! The ``architecture'' of the proof follows what we
would find in a textbook. It's just that a textbook will gloss over
facts about morphisms which we'd need to prove.

<<Theorem: characteristic subgroup of a normal subgroup is normal>>=
<<Lemma: two group morphisms which coincide on subgroup have the same image>>

theorem Th25:
  for N being strict normal Subgroup of G
  for K being characteristic Subgroup of N
  holds K is normal Subgroup of G
proof
  let N be strict normal Subgroup of G;
  let K be characteristic Subgroup of N;
  for a being Element of G holds K |^ a = K
  <<Proof: $\forall a\in G, K^{a}=K$>>
  then K is normal Subgroup of G by LmNormalSubgroup;
  hence thesis;
end;

@ %def Th25

\begin{remark}
The [[strict]] hypothesis on [[N]] is necessary, since [[GROUP_2:def 6]]\MizDef{GROUP\_2}[06]{6}
defines equality only on \emph{strict} subgroups.
\end{remark}

<<Proof: $\forall a\in G, K^{a}=K$>>=
proof
  let a be Element of G;
  consider g being inner Automorphism of G such that
  A1: for x being Element of G holds g.x = x |^ a
  by Th19;

  Image(g|N) = N by Th20;
  then g|N is Automorphism of N by Th11;
  then reconsider f = g|N as Automorphism of N;
  A2: Image(f|K) = K by Def9;

  for k being Element of G st k in K holds f.k = g.k
  <<Proof: $\forall k\in G$, $k\in K\implies f(k)=g(k)$>>
  then Image(f|K) = Image(g|K) by Th24;
  then Image(g|K) = K & Image(g|K) = K |^ a by A1,A2,Th15;
  then K = K |^ a by A1,Th15;
  hence thesis;
end;

@

<<Proof: $\forall k\in G$, $k\in K\implies f(k)=g(k)$>>=
proof
  let k be Element of G;
  assume B1: k in K;
  then (g|K).k = g.k by Lm3;
  B2: k in N by B1,GROUP_2:40;
  k is Element of K by LmSubgroupElt,B1;
  then f.k = (g|N).k
          .= g.k by B1, B2, Lm3;
  hence f.k = g.k;
end;

@

\N{Lemma}
The heavy lifting is done by a straightforward and intuitive result: if
we have two morphisms $f\colon G_{1}\to G_{2}$ and $g\colon H_{1}\to H_{2}$,
where $H_{1}\subgroup G_{1}$ and $H_{2}\subgroup G_{2}$, and if we have
a common subgroup $K\subgroup H_{1}\subgroup G_{1}$, then the image of
the morphisms on this shared subgroup should coincide.

Initially, I had a more conservative result: let $\varphi,\psi\in\Aut(G)$
and $H\subgroup G$, if $\forall h\in H,\varphi(h)=\psi(h)$, then
$\varphi(H)=\psi(H)$. Then I realized this didn't describe the situation
I was facing, so I revised it to fit. Then I realized I didn't need the
hypothesis that $\varphi$ and $\psi$ were automorphisms, they could be
generic morphisms.

<<Lemma: two group morphisms which coincide on subgroup have the same image>>=
theorem Th24:
  for G1,G2 being Group
  for H1 being Subgroup of G1
  for K being Subgroup of H1
  for H2 being Subgroup of G2
  for f being Homomorphism of G1,G2
  for g being Homomorphism of H1,H2
  st (for k being Element of G1 st k in K holds f.k=g.k)
  holds Image(f|K) = Image(g|K)
proof
  let G1,G2 be Group;
  let H1 be Subgroup of G1;
  let K be Subgroup of H1;
  let H2 be Subgroup of G2;
  let f be Homomorphism of G1,G2;
  let g be Homomorphism of H1,H2;
  assume A1: for k being Element of G1 st k in K holds f.k=g.k;
  A2: Image(f|K) is strict Subgroup of G2 &
      Image(g|K) is strict Subgroup of G2 by GROUP_2:56;
  for y being object
  holds y in the carrier of Image(f|K) iff y in the carrier of Image(g|K)
  <<Proof: $y\in f(K)\iff y\in g(K)$>>
  then the carrier of Image(f|K) = the carrier of Image(g|K) by TARSKI:2;
  then Image(f|K) = Image(g|K) by A2,GROUP_2:59,GROUP_2:def 6;
  hence thesis;
end;

@ %def Th24



<<Proof: $y\in f(K)\iff y\in g(K)$>>=
proof
  let y be object;
  thus y in the carrier of Image(f|K) implies y in the carrier of Image(g|K)
  <<Proof: $y\in f(K)\implies y\in g(K)$>>
  thus y in the carrier of Image(g|K) implies y in the carrier of Image(f|K)
  <<Proof: $y\in g(K)\implies y\in f(K)$>>
  thus thesis;
end;

@

<<Proof: $y\in f(K)\implies y\in g(K)$>>=
proof
  assume y in the carrier of Image(f|K);
  then consider h being Element of K such that
  B1: (f|K).h = y
  by STRUCT_0:def 5,GROUP_6:45;
  B2: h is Element of G1 & h is Element of H1 & h in K by GROUP_2:42, STRUCT_0:def 5;
  f.h = g.h by A1,B2
     .= (g|K).h by B2, Lm3;
  then (g|K).h = f.h
              .= (f|K).h by B2,Lm3
              .= y by B1;
  then (g|K).h = y & h is Element of K;
  then y in the carrier of Image(g|K) by STRUCT_0:def 5,GROUP_6:45;
  hence thesis;
end;

@

<<Proof: $y\in g(K)\implies y\in f(K)$>>=
proof
  assume y in the carrier of Image(g|K);
  then consider h being Element of K such that
  C1: (g|K).h = y
  by STRUCT_0:def 5,GROUP_6:45;
  C2: h is Element of H1 & h is Element of G1 & h in K by GROUP_2:42, STRUCT_0:def 5;
  g.h = f.h by A1,C2
     .= (f|K).h by C2,Lm3;
  then (f|K).h = g.h
              .= (g|K).h by C2,Lm3
              .= y by C1;
  then (f|K).h = y & h is Element of K;
  then y in the carrier of Image(f|K) by STRUCT_0:def 5,GROUP_6:45;
  hence thesis;
end;

@

\begin{theorem}
If $N\subgroup G$ is characteristic, and if $K\subgroup N$ is
characteristic, then $K\subgroup G$ is characteristic.
\end{theorem}

The proof is remarkably similar to the previous theorem. In fact, we can
re-use exactly the same line of reasoning establishing $\forall k\in G$, $k\in K\implies f(k)=g(k)$.

<<Theorem: transitivity of characteristic subgroups>>=
theorem Th26:
  for N being characteristic Subgroup of G
  for K being characteristic Subgroup of N
  holds K is characteristic Subgroup of G
proof
  let N be characteristic Subgroup of G;
  let K be characteristic Subgroup of N;
  for g being Automorphism of G holds Image(g|K) = K
  proof
    let g be Automorphism of G;
    A1: Image(g|N) = N by Def9;
    then g|N is Automorphism of N by Th11;
    then reconsider f = g|N as Automorphism of N;
    A2: Image(f|K) = K by Def9;

    for k being Element of G st k in K holds f.k = g.k
    <<Proof: $\forall k\in G$, $k\in K\implies f(k)=g(k)$>>
    then Image(g|K) = Image(f|K) by Th24
                   .= K by A2;
    hence Image(g|K) = K;
  end;
  then K is characteristic Subgroup of G by Def9;
  hence thesis;
end;

@ %def Th26

\begin{theorem}
  Let $H\subgroup G$.
  Then $H\charSubgroup G$ if and only if for any automorphism $\varphi$, we have
  $\varphi(H)\subgroup H$.
\end{theorem}

Some texts give this as the definition for $H$ being a characteristic
subgroup, which is fine.

<<Theorem: $H\subgroup G$ is characteristic iff $\forall\varphi\in\Aut(G),\varphi(H)\subgroup H$>>=
<<Lemma: $(\forall\varphi\in\Aut(G),\varphi(H)\subgroup H)\implies\varphi[\varphi^{-1}(H)]\subgroup G$>>
<<Lemma: $(\forall\varphi\in\Aut(G),\varphi(H)\subgroup H)\implies \varphi[\varphi^{-1}(H)]=H$>>

theorem Th27:
  for H being strict Subgroup of G
  holds H is characteristic Subgroup of G iff
  (for phi being Automorphism of G holds Image(phi|H) is Subgroup of H)
proof
  let H be strict Subgroup of G;
  thus H is characteristic Subgroup of G implies
       (for phi being Automorphism of G holds Image(phi|H) is Subgroup of H)
  <<Proof: $H\charSubgroup G\implies\forall\varphi\in\Aut(G),\varphi(H)\subgroup H$>>

  thus (for phi being Automorphism of G holds Image(phi|H) is Subgroup of H)
       implies H is characteristic Subgroup of G
  <<Proof: $H\charSubgroup G\impliedby\forall\varphi\in\Aut(G),\varphi(H)\subgroup H$>>

  thus thesis;
end;

@ %def Th27

\M
The proof in the forward direction boils down to the observation
$H\subgroup H$ then applying the definition of characteristic subgroup.

<<Proof: $H\charSubgroup G\implies\forall\varphi\in\Aut(G),\varphi(H)\subgroup H$>>=
proof
  assume A1: H is characteristic Subgroup of G;
  thus for phi being Automorphism of G holds Image(phi|H) is Subgroup of H
  proof
    let phi be Automorphism of G;
    Image(phi|H) = H & H is Subgroup of H by GROUP_2:54,A1,Def9;
    hence Image(phi|H) is Subgroup of H;
  end;
end;

@

\M
The proof in the backward direction amounts to proving, for arbitrary
automorphisms $\varphi$ of $G$, $\varphi(H)\subgroup H$ and since
$\varphi$ is an Automorphism $H\subgroup \varphi^{-1}(H)$. Then for any
$\varphi\in\Aut(G)$, we have $\varphi(H)\subgroup H$. Taken together,
this implies $\forall\varphi\in\Aut(G)$, $H = \varphi(H)$.

But as discussed earlier (\S\ref{par:introduction:subgroup-equality}),
subgroup equality holds only for strict subgroups. For this reason, we
have the hypothesis that $H$ is a strict subgroup of $G$.

<<Proof: $H\charSubgroup G\impliedby\forall\varphi\in\Aut(G),\varphi(H)\subgroup H$>>=
proof
  assume A1: for phi being Automorphism of G holds Image(phi|H) is Subgroup of H;
  A2: for phi being Automorphism of G
  holds H is Subgroup of Image(phi|H)
  proof
    let phi be Automorphism of G;
    consider psi being Automorphism of G such that
    B1: psi = phi" and
    B2: Image(phi|Image(psi|H)) is Subgroup of Image(phi|H) by A1,Lm39;
    consider psi2 being Automorphism of G such that
    B3: psi2 = phi" and
    B4: the multMagma of H = the multMagma of Image(phi|Image(psi2|H)) by A1,Lm40;
    psi2 = psi by B1,B3;
    hence H is Subgroup of Image(phi|H) by B2,B4;
  end;
  for phi being Automorphism of G
  holds H = Image(phi|H)
  proof
    let phi be Automorphism of G;
    H is strict Subgroup of G & Image(phi|H) is strict Subgroup of G;
    H is Subgroup of Image(phi|H) & Image(phi|H) is Subgroup of H
    by A1,A2;
    then the multMagma of H = the multMagma of Image(phi|H) by GROUP_2:55;
    hence H = Image(phi|H) by GROUP_2:def 5;
  end;
  hence H is characteristic Subgroup of G by Def9;
end;

@

<<Lemma: $(\forall\varphi\in\Aut(G),\varphi(H)\subgroup H)\implies\varphi[\varphi^{-1}(H)]\subgroup G$>>=
Lm39:
for H being Subgroup of G
st (for f being Automorphism of G holds Image(f|H) is Subgroup of H)
for phi being Automorphism of G
holds ex psi being Automorphism of G
st psi = phi" & Image(phi|Image(psi|H)) is Subgroup of Image(phi|H)
proof
  let H be Subgroup of G;
  assume A1: for f being Automorphism of G holds Image(f|H) is Subgroup of H;
  let phi be Automorphism of G;
  reconsider psi = phi" as Automorphism of G by GROUP_6:62;
  take psi;
  thus psi = phi";
  Image(psi|H) is Subgroup of H by A1;
  then phi .: Image(psi|H) is Subgroup of phi .: H by GRSOLV_1:12;
  then Image(phi|Image(psi|H)) is Subgroup of phi .: H by GRSOLV_1:def 3;
  hence Image(phi|Image(psi|H)) is Subgroup of Image(phi|H) by GRSOLV_1:def 3;
end;

@ %def Lm39

<<Lemma: $(\forall\varphi\in\Aut(G),\varphi(H)\subgroup H)\implies \varphi[\varphi^{-1}(H)]=H$>>=
Lm40:
for H being Subgroup of G
st (for f being Automorphism of G holds Image(f|H) is Subgroup of H)
for phi being Automorphism of G
holds ex psi being Automorphism of G
st psi = phi" & the multMagma of Image(phi|Image(psi|H)) = the multMagma of H
proof
  let H be Subgroup of G;
  assume A1: for f being Automorphism of G holds Image(f|H) is Subgroup of H;
  let phi be Automorphism of G;
  reconsider psi = phi" as Automorphism of G by GROUP_6:62;
  take psi;
  thus psi = phi";
  for g being Element of G holds g in Image(phi|Image(psi|H)) iff g in H
  <<Proof: $\forall g\in G$, $g\in\varphi[\varphi^{-1}(H)]\iff g\in H$>>
  hence the multMagma of Image(phi|Image(psi|H)) = the multMagma of H by GROUP_2:60;
end;

@ %def Lm40

\M The bones of the proof for this claim amounts to unfolding the
logical structure of the claim.

<<Proof: $\forall g\in G$, $g\in\varphi[\varphi^{-1}(H)]\iff g\in H$>>=
proof
  let g be Element of G;
  thus g in Image(phi|Image(psi|H)) implies g in H
  <<Step 1: $g\in\varphi[\varphi^{-1}(H)]\implies g\in H$>>

  thus g in H implies g in Image(phi|Image(psi|H))
  <<Step 2: $g\in\varphi[\varphi^{-1}(H)]\impliedby g\in H$>>
  thus thesis;
end;

@

<<Step 1: $g\in\varphi[\varphi^{-1}(H)]\implies g\in H$>>=
proof
  assume B1: g in Image(phi|Image(psi|H));
  then ex a being Element of Image(psi|H) st g = (phi|Image(psi|H)).a by GROUP_6:45;
  then consider a being Element of Image(psi|H) such that
  B2: g = (phi|Image(psi|H)).a by GROUP_6:45;
  B3: a in Image(psi|H) & a is Element of G by STRUCT_0:def 5,GROUP_2:42;
  then B4: phi.a = (phi|Image(psi|H)).a by Lm3
                .= g by B2;
  consider b being Element of H such that
  B5: a = (psi|H).b
  by B3,GROUP_6:45;
  B6: b in H & b is Element of G by STRUCT_0:def 5, GROUP_2:42;
  then psi.b = (psi|H).b by Lm3
            .= a by B5;
  then B7: phi.(psi.b) = phi.a
                      .= g by B4;
  b is Element of G by GROUP_2:42;
  then b = phi.((phi").b) by Lm11
        .= phi.(psi.b)
        .= g by B7;
  hence g in H by B6;
end;

@

\M To prove $g\in H$ implies $g\in\varphi[\varphi^{-1}(H)]$, we simply
let $K=\varphi^{-1}(H)$ and show $a = \varphi^{-1}|_{H}(g)\in K$ and
then show $b=\varphi|_{K}(a)\in\varphi[K]=\varphi[\varphi^{-1}(H)]$. Then
we have $b=\varphi(\varphi^{-1}(g))=g$ due to $\varphi$ being bijective.
The result follows.

<<Step 2: $g\in\varphi[\varphi^{-1}(H)]\impliedby g\in H$>>=
proof
  assume B1: g in H;
  set a = (psi|H).g;
  B2: a in Image(psi|H)
  <<Sub-step 1: $a\in\varphi^{-1}(H)$>>

  set K = Image(psi|H);
  set b = (phi|Image(psi|H)).a;

  B3: b in Image(phi|Image(psi|H))
  <<Sub-step 2: $b\in\varphi[\varphi^{-1}(H)]$>>
  thus g in Image(phi|K)
  <<Sub-step 3: $g\in\varphi[\varphi^{-1}(H)]$>>
end;

@

\M The first step follows by unfolding definitions.

<<Sub-step 1: $a\in\varphi^{-1}(H)$>>=
proof
  dom(psi|H) = the carrier of H by FUNCT_2:def 1;
  then g in dom(psi|H) by B1,STRUCT_0:def 5;
  then (psi|H).g in (psi|H) .: (the carrier of H) by FUNCT_1:def 6;
  then (psi|H).g in the carrier of Image(psi|H) by GROUP_6:def 10;
  then (psi|H).g in Image(psi|H) by STRUCT_0:def 5;
  hence a in Image(psi|H);
end;

@

\M The next step, like the first, follows from definitions.

<<Sub-step 2: $b\in\varphi[\varphi^{-1}(H)]$>>=
proof
  dom(phi|K) = the carrier of K by FUNCT_2:def 1;
  then a in dom(phi|K) by B2,STRUCT_0:def 5;
  then (phi|K).a in (phi|K) .: (the carrier of K) by FUNCT_1:def 6;
  then (phi|K).a in the carrier of Image(phi|K) by GROUP_6:def 10;
  then (phi|K).a in Image(phi|K) by STRUCT_0:def 5;
  hence b in Image(phi|K);
end;

@

\M The last step is a little more involved, because it requires
recalling $\varphi|_{k}(k)=\varphi(k)$ for $k\in K$, and
$\varphi^{-1}|_{H}(h)=\varphi^{-1}(h)$ for $h\in H$. We can then combine
these deductions to show $b=\varphi(\varphi^{-1}(g))$ which proves the claim.

<<Sub-step 3: $g\in\varphi[\varphi^{-1}(H)]$>>=
proof
  H is Subgroup of G & g in H & g is Element of G by B1,GROUP_2:42;
  then B4: psi.g = (psi|H).g by Lm3
                .= a by B2;
  B5: a is Element of K & K is Subgroup of G & a in K & phi is
  Homomorphism of G,G by B2,STRUCT_0:def 5;
  then a is Element of G by GROUP_2:42;
  then B6: (phi|K).a =  phi.a  by B5,Lm3
                    .= phi.(psi.g) by B4
                    .= phi.((phi").g)
                    .= g by Lm11;
  then b = (phi|K).((psi|H).g) by B4
        .= g by B4,B6;
  hence thesis by B3;
end;

@


\begin{theorem}\index{$Z(G)$}\index{Center!Group}\index{Group!Center}
Let $G$ be a group. Its center $Z(G)$ is a characteristic subgroup.
\end{theorem}

This is our first application of
$\forall\varphi\in\Aut(G),\varphi(H)\subgroup H$ implies $H$ is characteristic.
The bulk of the proof amounts to showing $\varphi(Z(G))\subgroup Z(G)$,
which requires two prior steps:
\begin{enumerate}
\item for any $g\in G$ and $z\in Z(G)$, we have $\varphi(z)g =
  g\varphi(z)$ --- i.e., $\varphi(z)$ commutes with every element of
  $G$; then
\item $\varphi(z)\in Z(G)$ for any $z\in Z(G)$.
\end{enumerate}
Then we have $\varphi(Z(G))$ be a subgroup of $Z(G)$, which let's us use
the previous theorem.

\begin{remark}
The [[center]]\mizindex{Center@\texttt{center}} functor is defined in
[[GROUP_5:def 10]]\MizDef{GROUP\_5}{10}.
\end{remark}

<<Theorem: $Z(G)$ is characteristic subgroup>>=

theorem Th28:
  center G is characteristic Subgroup of G
proof
  set Z = center G;
  <<Prove $\forall\varphi\in\Aut(G),\varphi(Z(G))\subgroup Z(G)$>>

  hence Z is characteristic Subgroup of G by Th27;
end;

@ %def Th28

\M The heart of the proof amounts to showing, for any Automorphism
$\varphi$, that $\varphi(Z(G))$ is a subgroup of $Z(G)$.

<<Prove $\forall\varphi\in\Aut(G),\varphi(Z(G))\subgroup Z(G)$>>=
<<Step 1: $\forall\varphi\in\Aut(G)\forall y\in G\forall z\in Z(G), \varphi(z)y = y\varphi(z)$>>
<<Step 2: $\forall\varphi\in\Aut(G)\forall z\in G, z\in Z(G)\implies \varphi(z)\in Z(G)$>>
<<Step 3: $\forall\varphi\in\Aut(G),\varphi(Z(G))\subgroup Z(G)$>>

@

\M Proving $\varphi(z)$ commutes with every element of the group is a
straightforward calculation. In fact, this is usually what textbooks
present, then dismiss the rest of the proof as ``trivial'' or
``obvious''.

<<Step 1: $\forall\varphi\in\Aut(G)\forall y\in G\forall z\in Z(G), \varphi(z)y = y\varphi(z)$>>=
A1: for phi being Automorphism of G
for y,z being Element of G
st z in Z
holds (phi.z)*y = y*phi.z
proof
  let phi be Automorphism of G;
  let y,z be Element of G;
  assume B1: z in Z;
  phi" is Automorphism of G by GROUP_6:62;
  set x = (phi").y;
  B2: z*x = x*z by B1,GROUP_5:77;
  (phi.z)*y = (phi.z)*(phi.x) by Lm11
           .= phi.(z*x) by GROUP_6:def 6
           .= phi.(x*z) by B2
           .= (phi.x)*(phi.z) by GROUP_6:def 6
           .= y*(phi.z) by B2, Lm11;
  hence thesis;
end;

@

\M Establishing $z\in Z(G)$ implies $\varphi(z)\in Z(G)$ amounts to
unfolding definitions.

<<Step 2: $\forall\varphi\in\Aut(G)\forall z\in G, z\in Z(G)\implies \varphi(z)\in Z(G)$>>=
A2: for phi being Automorphism of G
for z being Element of G
st z in Z
holds (phi|Z).z in Z
proof
  let phi be Automorphism of G;
  let z be Element of G;
  assume B1: z in Z;
  then for y being Element of G holds (phi.z)*y=y*(phi.z) by A1;
  then B2: (phi.z) in Z by GROUP_5:77;
  phi.z = (phi|Z).z by B1,Lm3;
  hence ((phi|Z).z) in Z by B2;
end;

@

\M The last step, which is the ``obvious'' part, infers from
$\forall z\in Z(G),\varphi(z)\in Z(G)$ that $\varphi(Z(G))\subgroup Z(G)$.
It's also the ugliest part of the proof which could probably be cleaned
up considerably.

<<Step 3: $\forall\varphi\in\Aut(G),\varphi(Z(G))\subgroup Z(G)$>>=
A3: for phi being Automorphism of G
holds Image(phi|Z) is Subgroup of Z
proof
  let phi be Automorphism of G;
  B1: rng(phi|Z) c= the carrier of G &
  the carrier of Z c= the carrier of G by GROUP_2:def 5;
  for z being Element of G st z in Z holds (phi|Z).z in Z by A2;
  for w being Element of G st w in rng(phi|Z) holds w in Z
  proof
    let w be Element of G;
    assume w in rng(phi|Z);
    then consider z being object such that
    C1: z in dom(phi|Z) and
    C2: (phi|Z).z = w by FUNCT_1:def 3;
    reconsider z as Element of Z by C1;
    C3: z in Z by STRUCT_0:def 5;
    z is Element of G by GROUP_2:42;
    then (phi|Z).z in Z by A2,C3;
    hence w in Z by C2;
  end;
  then for w being Element of G holds w in rng(phi|Z) implies w in the carrier of Z
  by STRUCT_0:def 5;
  then rng(phi|Z) c= the carrier of Z by B1,SUBSET_1:2;
  then the carrier of Image(phi|Z) c= the carrier of Z by GROUP_6:44;
  hence Image(phi|Z) is Subgroup of Z by GROUP_2:57;
end;

@
