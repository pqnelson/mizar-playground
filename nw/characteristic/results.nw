% -*- mode: poly-noweb; noweb-code-mode: mizar-mode; -*-
\subsection{Characteristic Subgroups}

\M
We now arrive at the meat of the matter: characteristic subgroups!

<<Characteristic subgroup results>>=
<<Define [[characteristic]] attribute>>
<<Lemma: trivial subgroup is [[characteristic]]>>
<<Theorem: Existence of characteristic subgroups>>
<<Register [[characteristic]] as attribute for Subgroup>>
<<Register [[strict]] [[characteristic]] for Subgroups>>
<<Theorem: characteristic subgroups are normal>>
<<Register characteristic subgroups are normal>>
<<Theorem: unique subgroup of order $n$ is [[characteristic]]>>
<<Theorem: characteristic subgroup of a normal subgroup is normal>>

@

\begin{definition}[{Dummit and Foote~\cite[\S4,4]{dummit-foote}}]\index{Subgroup!Characteristic}\index{Characteristic!Subgroup}
A subgroup $H$ of $G$ is called \define{Characteristic} in $G$, usually
denoted $H~\mathrm{char}~G$, if every Automorphism of $G$ maps $H$ to
itself; i.e., $\sigma(H)=H$ for all $\sigma\in\aut(G)$.
\end{definition}

<<Define [[characteristic]] attribute>>=
definition :: Def9
  let G;
  let IT be Subgroup of G;
  attr IT is characteristic means
  :Def9:
  for f being Automorphism of G
  holds Image (f|IT) = IT;
end;

@ %def characteristic Def9

\begin{lemma}
  For any group $G$, its trivial subgroup $\trivialSubgroup$ is characteristic.
\end{lemma}

<<Lemma: trivial subgroup is [[characteristic]]>>=
<<Lemma: if $H\subgroup\trivialGroup$, then $H=\trivialGroup$>>

Lm6: (1).G is characteristic
proof
A1:  for f being Automorphism of G
     holds Image (f|(1).G) is Subgroup of (1).G by Th2;
  for f being Automorphism of G holds Image(f|(1).G) = (1).G
  proof
    let f be Automorphism of G;
    reconsider I = Image(f|(1).G) as Subgroup of (1).G by A1;
    (1).G = I by Lm5;
    then Image(f|(1).G) = (1).G by A1;
    hence thesis;
  end;
  then (1).G is characteristic by Def9;
  hence thesis;
end;

@ %def Lm6

\N{Lemma: the only subgroup of the trivial group is trivial}
I couldn't quite find this anywhere in the MML, so I had to prove it
myself.

<<Lemma: if $H\subgroup\trivialGroup$, then $H=\trivialGroup$>>=
Lm5: for H being Subgroup of G st H is Subgroup of (1).G holds the
multMagma of H = the multMagma of (1).G
proof
  let H be Subgroup of G;
  assume
A1:  H is Subgroup of (1).G;
  reconsider H as Subgroup of (1).G by A1;
A2: (1).G is Subgroup of H by GROUP_2:65;
  H is Subgroup of (1).G & (1).G is Subgroup of H by A1,A2;
  hence thesis by GROUP_2:55;
end;

@ %def Lm5

\N{Existence theorem}
We need to prove the existence of a characteristic subgroup. For our
purposes, it suffices to prove that the trivial subgroup is characteristic.

<<Theorem: Existence of characteristic subgroups>>=

theorem Th11:
  ex H being Subgroup of G st H is characteristic
proof
  take H = (1).G;
  H is characteristic by Lm6;
  hence thesis;
end;

@ %def Th11

\N{Register [[characteristic]] for Subgroups}
Now we can instruct Mizar to recognize [[characteristic]] as an
adjective of [[Subgroup]].

<<Register [[characteristic]] as attribute for Subgroup>>=
registration
  let G;
  cluster characteristic for Subgroup of G;
  existence by Th11;
end;

@

\N{Register [[strict]] [[characteristic]] for Subgroups}
We can also register the cluster [[strict]] [[characteristic]] for
Subgroups, which will come handy later.

<<Register [[strict]] [[characteristic]] for Subgroups>>=
registration
  let G be Group;
  cluster strict characteristic for Subgroup of G;
  existence
  proof
    take (1).G;
    thus thesis by Lm6;
  end;
end;

@


\N{Characteristic subgroups are normal}
Let $G$ be a group, $K\subgroup G$ a subgroup. If $K$ is a
characteristic subgroup of $G$, then $K\normalSubgroup G$ it is also
normal.

<<Theorem: characteristic subgroups are normal>>=
theorem Th12:
  for G being Group
  for K being characteristic Subgroup of G
  holds K is normal Subgroup of G
proof
  let G be Group;
  let K be characteristic Subgroup of G;
A1:  for a being Element of G holds K |^ a = K
  <<Sub-proof: characteristic subgroups invariant under conjugation>>
  for a being Element of G holds the carrier of K |^ a = the carrier of K by A1;
  then for a being Element of G holds the multMagma of K = K |^ a by GROUP_2:59;
  hence K is normal Subgroup of G by A1,GROUP_3:def 13;
end;

@ %def Th12

\M
Really, proving a characteristic subgroup is normal amounts to proving
invariance under conjugation. Fortunately, we have established this
along the way! We just have to point to our hard work from earlier.

<<Sub-proof: characteristic subgroups invariant under conjugation>>=
proof
  let a be Element of G;
  consider f being inner Automorphism of G such that
A2: for x being Element of G holds f.x = x |^ a
  by ThAlwaysFindInnerAuto;
  A3: Image(f|K) = K by GROUP_6:57,Def9;
  Image(f|K) = K |^ a by A2,Th7;
  then K |^ a = K by A3,Def9;
  hence thesis;
end;

@

\M
Now we can register this fact with Mizar, so it will be automatically
accounted for in future proofs. Since we made this fact a proof, we just
have to tell Mizar where to find the proof.

<<Register characteristic subgroups are normal>>=
registration
  let G be Group;
  cluster characteristic -> normal for Subgroup of G;
  coherence by Th12;
end;

@

\begin{theorem}
  If $H$ is the unique subgroup of a given order in a group $G$,
  then $H$ is characteristic in $G$.
\end{theorem}

\begin{proof}
Let $H$ be a subgroup of $G$. Assume there are no other subgroups of
order $|H|$. Then for any $\varphi\in\Aut(G)$, we'd have $\varphi(H)=H$
since $\varphi(H)$ has the same order as $H$ by
Theorem~\ref{thm:pure-math:iso-subgroups-have-same-order}, but we
assumed there is only one (namely, $H$).
\end{proof}

<<Theorem: unique subgroup of order $n$ is [[characteristic]]>>=
theorem Th13:
  for G being Group
  for H being strict Subgroup of G
  st (for K being strict Subgroup of G
      st card K = card H
      holds H = K)
  holds H is characteristic
proof
  let G be Group;
  let H be strict Subgroup of G;
  assume A0: for K being strict Subgroup of G st card K = card H holds H = K;
  H is characteristic
  proof
    let phi be Automorphism of G;
    Image(phi|H) = phi .: H by GRSOLV_1:def 3;
    then Image(phi|H),H are_isomorphic by Th14;
    then card H = card Image(phi|H) by GROUP_6:73;
    then Image(phi|H) = H by A0;
    hence thesis;
  end;
  hence thesis;
end;

@

\begin{theorem}
If $K\subgroup H$ is characteristic, and if $H\normalSubgroup G$ is normal,
then $K\normalSubgroup G$ is normal.
\end{theorem}

This required a surprising amount of legwork.

<<Theorem: characteristic subgroup of a normal subgroup is normal>>=
theorem
for G being Group
for N being strict normal Subgroup of G
for K being characteristic Subgroup of N
holds K is normal Subgroup of G
proof
  let G be Group;
  let N be strict normal Subgroup of G;
  let K be characteristic Subgroup of N;
  for a being Element of G holds K |^ a = K
  proof
    let a be Element of G;
    consider g being inner Automorphism of G such that
    A1: for x being Element of G holds g.x = x |^ a
    by ThAlwaysFindInnerAuto;

    N is Subgroup of G & g is Automorphism of G & Image(g|N) = N by Th16;
    then A2: g|N is Automorphism of N by LmMinor;

    K2: Image((g|N)|K) = Image(g|K) by LmRestrictCollapse;
    N is Subgroup of G & g is Automorphism of G & Image(g|N) = N by Th16;
    then A2: g|N is Automorphism of N by LmMinor;
    reconsider f = g|N as Automorphism of N by A2;
    f .: K = K by ThAltChar;
    A3: Image(f|K) = K by Def9;

    for k being Element of G st k in K holds f.k = g.k
    <<Proof: $\forall k\in G$, $k\in K\implies f(k)=g(k)$>>
    then Image(f|K) = Image(g|K) by ThImgsEqRel;
    then Image(g|K) = K & Image(g|K) = K |^ a by A3,A1,Th7;
    then K = K |^ a by A1,Th7;
    hence thesis;
  end;
  then K is normal Subgroup of G by LmNormalSubgroup;
  hence thesis;
end;
  
@

<<Proof: $\forall k\in G$, $k\in K\implies f(k)=g(k)$>>=
proof
  let k be Element of G;
  assume Z97: k in K;
  then (g|K).k = g.k by Lm3;
  K is Subgroup of N;
  Z98: k in N by Z97,GROUP_2:40;
  k is Element of K by LmSubgroupElt,Z97;
  then f.k = (g|N).k
  .= g.k by Z98, Z97, Lm3;
  hence f.k = g.k;
end;
    
@

\N{Lemma}
The heavy lifting is done by a straightforward and intuitive result: if
we have two morphisms $f\colon G_{1}\to G_{2}$ and $g\colon H_{1}\to H_{2}$,
where $H_{1}\subgroup G_{1}$ and $H_{2}\subgroup G_{2}$, and if we have
a common subgroup $K\subgroup H_{1}\subgroup G_{1}$, then the image of
the morphisms on this shared subgroup should coincide.

<<Lemma: two group morphisms which coincide on subgroup have the same image>>=
theorem ThImgsEqRel:
  for G1,G2 being Group
  for H1 being Subgroup of G1
  for K being Subgroup of H1
  for H2 being Subgroup of G2
  for f being Homomorphism of G1,G2
  for g being Homomorphism of H1,H2
  st (for k being Element of G1 st k in K holds f.k=g.k)
  holds Image(f|K) = Image(g|K)
proof
  let G1,G2 be Group;
  let H1 be Subgroup of G1;
  let K be Subgroup of H1;
  let H2 be Subgroup of G2;
  let f be Homomorphism of G1,G2;
  let g be Homomorphism of H1,H2;
  assume Z90: for k being Element of G1 st k in K holds f.k=g.k;
  Z91: Image(f|K) is strict Subgroup of G2 &
  Image(g|K) is strict Subgroup of G2 by GROUP_2:56;
  for y being object
  holds y in the carrier of Image(f|K) iff y in the carrier of Image(g|K)
  proof
    let y be object;
    thus y in the carrier of Image(f|K) implies y in the carrier of Image(g|K)
    <<Proof: $y\in f(K)\implies y\in g(K)$>>
    thus y in the carrier of Image(g|K) implies y in the carrier of Image(f|K)
    <<Proof: $y\in g(K)\implies y\in f(K)$>>
    thus thesis;
  end;
  then the carrier of Image(f|K) = the carrier of Image(g|K) by TARSKI:2;
  then the multMagma of Image(f|K) = the multMagma of Image(g|K) by Z91,GROUP_2:59;
  then Image(f|K) = Image(g|K) by GROUP_2:def 6;
  hence thesis;
end;

@ 

<<Proof: $y\in g(K)\implies y\in f(K)$>>=
proof
  assume Z1: y in the carrier of Image(g|K);
  then consider h being Element of K such that
  A2: (g|K).h = y
  by STRUCT_0:def 5,GROUP_6:45;
  A3: h is Element of H1 & h is Element of G1 & h in K by GROUP_2:42, STRUCT_0:def 5;
  then A4: f.h = g.h by Z90;
  then f.h = (f|K).h by A3,Lm3;
  then (f|K).h = g.h by A4
              .= (g|K).h by A3,Lm3
              .= y by A2;
  then A5: (f|K).h = y & h is Element of K;
  then y in the carrier of Image(f|K) by A5,STRUCT_0:def 5,GROUP_6:45;
  hence thesis;
end;

@ 

<<Proof: $y\in f(K)\implies y\in g(K)$>>=
proof
  assume A1: y in the carrier of Image(f|K);
  then consider h being Element of K such that
  A2: (f|K).h = y
  by STRUCT_0:def 5,GROUP_6:45;
  A3: h is Element of G1 & h is Element of H1 & h in K by GROUP_2:42, STRUCT_0:def 5;
  then A4: f.h = g.h by Z90;
  then g.h = (g|K).h by A3,Lm3;
  then (g|K).h = f.h by A4
              .= (f|K).h by A3,Lm3
              .= y by A2;
  then A5: (g|K).h = y & h is Element of K;
  then y in the carrier of Image(g|K) by A5,STRUCT_0:def 5,GROUP_6:45;
  hence thesis;
end;
@ 