% -*- mode: poly-noweb; noweb-code-mode: mizar-mode; -*-
\section{Characteristic Subgroups}\label{sec:characteristic:results}
We now arrive at the meat of the matter: characteristic subgroups!

<<Characteristic subgroups>>=
<<Definition: [[characteristic]] attribute>>

<<Lemma: trivial subgroup is [[characteristic]]>>

<<Theorem: Existence of characteristic subgroups>>

<<Register [[characteristic]] as attribute for Subgroup>>
<<Register [[strict]] [[characteristic]] for Subgroups>>

<<Theorem: characteristic subgroups are normal>>

<<Register characteristic subgroups are normal>>

<<Theorem: two group morphisms which coincide on subgroup have the same image>>

<<Theorem: unique subgroup of order $n$ is [[characteristic]]>>

<<Theorem: characteristic subgroup of a normal subgroup is normal>>

<<Theorem: transitivity of characteristic subgroups>>

<<Theorem: $H\subgroup G$ is characteristic iff $\forall\varphi\in\Aut(G),\varphi(H)\subgroup H$>>

<<Theorem: $Z(G)$ is characteristic subgroup>>

<<Scheme: if $H\subgroup G\land P[H]$ and $\forall\varphi\in\Aut(G),P[\varphi(H)]$, then $\bigcap\{H\subgroup G\mid P[H]\}$ is $\Aut(G)$-invariant>>

<<Scheme: $\bigcap\{A\subset G\mid\exists H\subgroup G, A=H, P[H]\}$ is characteristic>>

<<Theorem: $\FrattiniSubgroup(G)$ is characteristic>>

<<Theorem: $\forall\varphi\in G,\varphi(\Commutators(G))=\Commutators(G)$>>

<<Theorem: $\forall h\in H,\varphi(h)\in H$ implies $\varphi(H)\subgroup H$>>

<<Theorem: $A\subset G$ s.t. $\forall\varphi\in\Aut(G),\varphi(A)=A$, then $\langle A\rangle$ is characteristic>>

<<Theorem: The derived subgroup is characteristic>>

<<Theorem: $H\subgroup G, a\in G$, $\varphi(aH)=\varphi(a)\varphi(H)$>>

<<Theorem: $H\subgroup G, a\in G$, $\varphi(Ha)=\varphi(H)\varphi(a)$>>

<<Theorem: $N\normalSubgroup G$, $\varphi\in\Aut(G)$ implies $\varphi(N)\normalSubgroup G$>>

<<Theorem: $H\subgroup G$ characteristic $\iff\forall\varphi\in\Aut(G)\forall x\in H,\varphi(x)\in H$>>

<<Theorem: $H,K\subgroup G$ characteristic implies $H\cap K$ characteristic>>

<<Theorem: $H,K\subgroup G$ characteristic implies $\langle H, K\rangle$ is characteristic>>

<<Theorem: $H,K\subgroup G$ characteristic implies ${\rm Commutators}(H,K)$ is stable>>

<<Theorem: $H,K\subgroup G$ characteristic implies $[H,K]$ is characteristic>>
@

\begin{definition}[{Dummit and Foote~\cite[\S4.4]{dummit-foote}}]\index{Subgroup!Characteristic}\index{Characteristic!Subgroup}\label{defn:characteristic:results:characteristic-subgroup}
A subgroup $H$ of $G$ is called \define{Characteristic} in $G$, usually
denoted $H~\mathrm{char}~G$, if every Automorphism of $G$ maps $H$ to
itself; i.e., $\sigma(H)=H$ for all $\sigma\in\aut(G)$.
\end{definition}

\begin{def-remark}
The other definition which is routinely given is that $H$ is a
characteristic subgroup of $G$ if for any $\varphi\in\Aut(G)$ we have
$\varphi(H)\subgroup H$. We prove this later as equivalent in Theorem~\ref{thm:results:alt-def-of-char-subgroup}.
\end{def-remark}

<<Definition: [[characteristic]] attribute>>=
:: Dummit and Foote, Abstract Algebra, ch.4 section 4
definition
  let G;
  let IT be Subgroup of G;
  attr IT is characteristic means
  :Def3:
  for f being Automorphism of G
  holds Image (f|IT) = IT;
end;
@ %def characteristic Def3

\begin{voc}
Before rushing off to prove properties concerning characteristic
subgroups, we have to tell Mizar that \lstinline|characteristic| is now
a token that should be associated with Definition~\ref{defn:characteristic:results:characteristic-subgroup}.
\end{voc}

<<DICT/CHAR.VOC>>=
Vcharacteristic
@



\begin{lemma}
  For any group $G$, its trivial subgroup $\trivialSubgroup$ is characteristic.
\end{lemma}

<<Lemma: trivial subgroup is [[characteristic]]>>=
<<Lemma: if $H\subgroup\trivialGroup$, then $H=\trivialGroup$>>

Lm7: (1).G is characteristic
proof
  for f being Automorphism of G holds Image(f|(1).G) = (1).G
  proof
    let f be Automorphism of G;
    reconsider I = Image(f|(1).G) as Subgroup of (1).G by Th14;
    (1).G = I by Lm8;
    hence Image(f|(1).G) = (1).G;
  end;
  hence (1).G is characteristic;
end;
@ %def Lm7

\begin{lemma}
For any subgroup $H\subgroup G$, if $\trivialSubgroup_{G}\subgroup H$
and $H\subgroup\trivialSubgroup_{G}$, then $H=\trivialSubgroup_{G}$.
\end{lemma}

\begin{lem-remark}
I couldn't quite find this anywhere in the MML, so I had to prove it
myself.
\end{lem-remark}
\begin{lem-remark}[To do]
I think I prove this result several times, I should refactor my code accordingly.
\end{lem-remark}

<<Lemma: if $H\subgroup\trivialGroup$, then $H=\trivialGroup$>>=
Lm8: H is Subgroup of (1).G implies the multMagma of H = the multMagma of (1).G
proof
  assume H is Subgroup of (1).G;
  then reconsider H as Subgroup of (1).G;
  H is Subgroup of (1).G & (1).G is Subgroup of H by GROUP_2:65;
  hence thesis by GROUP_2:55;
end;
@ %def Lm8

\begin{theorem}[Existence of a characteristic subgroup]
For any group $G$, there exists a subgroup $H\subgroup G$ which is characteristic.
\end{theorem}
\begin{proof}[Proof outline]
The trivial subgroup is a subgroup of any group. And it is
characteristic. Thus the result.
\end{proof}

<<Theorem: Existence of characteristic subgroups>>=

theorem Th34:
  ex H st H is characteristic
proof
  take H = (1).G;
  thus H is characteristic by Lm7;
end;
@ %def Th34

\begin{registration}
Now we can instruct Mizar to recognize [[characteristic]] as an
adjective of [[Subgroup]].
\end{registration}

<<Register [[characteristic]] as attribute for Subgroup>>=
registration
  let G;
  cluster characteristic for Subgroup of G;
  existence by Th34;
end;
@

\begin{reserve}
We will henceforth use the symbol $K$ to refer to characteristic
subgroups of $G$, unless otherwise stated.
\end{reserve}


<<Register [[characteristic]] as attribute for Subgroup>>=
reserve K for characteristic Subgroup of G;
@

\begin{registration}
We can also register the cluster [[strict]] [[characteristic]] for
Subgroups, which will come handy later.
\end{registration}

<<Register [[strict]] [[characteristic]] for Subgroups>>=
registration
  let G be Group;
  cluster strict characteristic for Subgroup of G;
  existence
  proof
    take (1).G;
    thus thesis by Lm7;
  end;
end;
@

\begin{theorem}[Characteristic subgroups are normal]
Let $G$ be a group, $K\subgroup G$ a subgroup. If $K$ is a
characteristic subgroup of $G$, then $K\normalSubgroup G$ it is also
normal.
\end{theorem}

\begin{proof}[Proof outline]
We show $K$ is invariant under conjugation, i.e., for any $a\in G$ we
have $K^{a}=K$. Then $K\normalSubgroup G$.
\end{proof}

<<Theorem: characteristic subgroups are normal>>=
theorem Th35:
  K is normal Subgroup of G
proof
A1:  for a being Element of G holds K |^ a = K
  <<Sub-proof: characteristic subgroups invariant under conjugation>>
  for a being Element of G holds the carrier of K |^ a = the carrier of K by A1;
  then for a being Element of G holds the multMagma of K = K |^ a by GROUP_2:59;
  hence K is normal Subgroup of G by GROUP_3:def 13;
end;
@ %def Th35

\begin{proof}[Sub-proof (characteristic subgroups invariant under conjugation)]
Really, proving a characteristic subgroup is normal amounts to proving
invariance under conjugation. Fortunately, we have established this
along the way! We just have to point to our hard work from earlier.
\end{proof}

<<Sub-proof: characteristic subgroups invariant under conjugation>>=
proof
  let a be Element of G;
  consider f being inner Automorphism of G such that
A2: for x being Element of G holds f.x = x |^ a
  by Th32;
  K = Image(f|K) by Def3
   .= K |^ a by A2,Th28;
  hence thesis;
end;
@

\begin{registration}
Now we can register this fact with Mizar, so it will be automatically
accounted for in future proofs. Since we made this fact a proof, we just
have to tell Mizar where to find the proof.
\end{registration}

<<Register characteristic subgroups are normal>>=
registration
  let G be Group;
  cluster characteristic -> normal for Subgroup of G;
  coherence by Th35;
end;
@

% Theorem 38
\begin{theorem}
If we have two morphisms $f\colon G_{1}\to G_{2}$ and $g\colon H_{1}\to
H_{2}$, where $H_{1}\subgroup G_{1}$ and $H_{2}\subgroup G_{2}$, and if
we have a common subgroup $K\subgroup H_{1}\subgroup G_{1}$, then the
image of the morphisms on this shared subgroup should coincide.
\end{theorem}

\begin{proof}[Proof outline]
  Let $f\colon G_{1}\to G_{2}$ be a group morphism, let $H_{1}\subgroup G_{1}$
  and $H_{2}\subgroup G_{2}$ be subgroups, let $g\colon H_{1}\to H_{2}$.
  If $K\subgroup H_{1}$ is a subgroup for which $g|_{K}=f|_{K}$, then $f(K)=g(K)$.
\end{proof}


\begin{thm-remark}
The heavy lifting is done by a straightforward and intuitive result.

Initially, I had a more conservative result: let $\varphi,\psi\in\Aut(G)$
and $H\subgroup G$, if $\forall h\in H,\varphi(h)=\psi(h)$, then
$\varphi(H)=\psi(H)$. Then I realized this didn't describe the situation
I was facing, so I revised it to fit. Then I realized I didn't need the
hypothesis that $\varphi$ and $\psi$ were automorphisms, they could be
generic morphisms.
\end{thm-remark}

<<Theorem: two group morphisms which coincide on subgroup have the same image>>=
theorem Th36:
  for G1,G2 being Group
  for H1 being Subgroup of G1
  for K being Subgroup of H1
  for H2 being Subgroup of G2
  for f being Homomorphism of G1,G2
  for g being Homomorphism of H1,H2
  st (for k being Element of G1 st k in K holds f.k=g.k)
  holds Image(f|K) = Image(g|K)
proof
  let G1,G2 be Group;
  let H1 be Subgroup of G1;
  let K be Subgroup of H1;
  let H2 be Subgroup of G2;
  let f be Homomorphism of G1,G2;
  let g be Homomorphism of H1,H2;
  assume A1: for k being Element of G1 st k in K holds f.k=g.k;
  A2: Image(f|K) is strict Subgroup of G2 &
      Image(g|K) is strict Subgroup of G2 by GROUP_2:56;
  for y being object
  holds y in the carrier of Image(f|K) iff y in the carrier of Image(g|K)
  <<Proof: $y\in f(K)\iff y\in g(K)$>>
  hence Image(f|K) = Image(g|K) by A2,GROUP_2:59,TARSKI:2;
end;
@ %def Th36

\begin{proof}[Sub-proof ($\forall y,y\in f(K)\iff y\in g(K)$)]
  We show, for any $y$, that both $y\in f(K)\implies y\in g(K)$ and
  $y\in f(K)\impliedby y\in g(K)$. Taken together, this gives us
  $y\in f(K)\iff y\in g(K)$.
\end{proof}

<<Proof: $y\in f(K)\iff y\in g(K)$>>=
proof
  let y be object;
  thus y in the carrier of Image(f|K) implies y in the carrier of Image(g|K)
  <<Proof: $y\in f(K)\implies y\in g(K)$>>
  thus y in the carrier of Image(g|K) implies y in the carrier of Image(f|K)
  <<Proof: $y\in g(K)\implies y\in f(K)$>>
  thus thesis;
end;
@

\begin{proof}[Sub-proof step 1 ($y\in f(K)\implies y\in g(K)$)]
We have $y\in f(K)$ if there is some $h\in K$ such that $y=f|_{K}(h)$.
But by hypothesis, $f|_{K}(h)=g|_{K}(h)$, and thus $y=g|_{k}(h)\in g(K)$.
\end{proof}

<<Proof: $y\in f(K)\implies y\in g(K)$>>=
proof
  assume y in the carrier of Image(f|K);
  then consider h being Element of K such that
  B1: (f|K).h = y
  by STRUCT_0:def 5,GROUP_6:45;
  B2: h is Element of G1 & h is Element of H1 & h in K by GROUP_2:42;
  f.h = g.h by A1,B2
     .= (g|K).h by B2, Th1;
  then (g|K).h = f.h
              .= (f|K).h by B2,Th1
              .= y by B1;
  hence y in the carrier of Image(g|K) by STRUCT_0:def 5,GROUP_6:45;
end;
@

\begin{proof}[Sub-proof step 2 ($y\in g(K)\implies y\in f(K)$)]
  We have $y\in g(K)$ if there is some $h\in K$ such that $y=g|_{K}(h)$.
  But by hypothesis $g|_{K}(h) = f|_{K}(h)$, and thus $y = f|_{K}(h)\in f(K)$.
\end{proof}

<<Proof: $y\in g(K)\implies y\in f(K)$>>=
proof
  assume y in the carrier of Image(g|K);
  then consider h being Element of K such that
  C1: (g|K).h = y
  by STRUCT_0:def 5,GROUP_6:45;
  C2: h is Element of H1 & h is Element of G1 & h in K by GROUP_2:42;
  g.h = f.h by A1,C2
     .= (f|K).h by C2,Th1;
  then (f|K).h = g.h
              .= (g|K).h by C2,Th1
              .= y by C1;
  hence y in the carrier of Image(f|K) by STRUCT_0:def 5,GROUP_6:45;
end;
@


\begin{theorem}
  If $H$ is the unique subgroup of a given order in a group $G$,
  then $H$ is characteristic in $G$.
\end{theorem}

\begin{proof}
Let $H$ be a subgroup of $G$. Assume there are no other subgroups of
order $|H|$. Then for any $\varphi\in\Aut(G)$, we'd have $\varphi(H)=H$
since $\varphi(H)$ has the same order as $H$ by
Theorem~\ref{thm:pure-math:iso-subgroups-have-same-order}, but we
assumed there is only one (namely, $H$).
\end{proof}

<<Theorem: unique subgroup of order $n$ is [[characteristic]]>>=
theorem Th37:
  for H being strict Subgroup of G
  st (for K being strict Subgroup of G
      st card K = card H
      holds H = K)
  holds H is characteristic
proof
  let H be strict Subgroup of G;
  assume A1: for K being strict Subgroup of G st card K = card H holds H = K;
  H is characteristic
  proof
    let phi be Automorphism of G;
    Image(phi|H) = phi .: H by GRSOLV_1:def 3;
    then card H = card Image(phi|H) by Th19,GROUP_6:73;
    hence Image(phi|H) = H by A1;
  end;
  hence thesis;
end;
@ %def Th37

\begin{theorem}
If $K\subgroup H$ is characteristic, and if $H\normalSubgroup G$ is normal,
then $K\normalSubgroup G$ is normal.
\end{theorem}

This required a surprising amount of legwork, even when carving it out
into a helper theorem! The ``architecture'' of the proof follows what we
would find in a textbook. It's just that a textbook will gloss over
facts about morphisms which we'd need to prove.

\begin{proof}
Let $K$ be a characteristic subgroup of $N$ and $N\normalSubgroup G$ be
a normal subgroup. Then consider conjugation of $N$ by any element $a\in G$.
This corresponds to an inner Automorphism of $G$, $\varphi\in\Inn(G)$, and an
automorphism $\varphi|_{N}\in\Aut(N)$ of $N$. But since $K$ is
characteristic in $N$, it follows $\varphi|_{N}(K)=K$. Then $K$,
considered as a subgroup of $G$, must be invariant under $\varphi$ and
therefore a normal subgroup of $G$ by Theorem~\ref{thm:characteristic:preparatory:normal-subgroup}.
\end{proof}

\begin{thm-remark}
The [[strict]] hypothesis on [[N]] is necessary, since Definition~\mml[def6]{group2}%
\MizDef{GROUP\_2}[06]{6} defines equality only on \emph{strict} subgroups.
\end{thm-remark}

<<Theorem: characteristic subgroup of a normal subgroup is normal>>=
theorem Th38:
  for N being strict normal Subgroup of G
  for K being characteristic Subgroup of N
  holds K is normal Subgroup of G
proof
  let N be strict normal Subgroup of G;
  let K be characteristic Subgroup of N;
  for a being Element of G holds K |^ a = K
  <<Proof: $\forall a\in G, K^{a}=K$>>
  hence K is normal Subgroup of G by Th2;
end;
@ %def Th38

\begin{proof}[Sub-proof ($\forall a\in G, K^{a}=K$)]
Given an arbitrary $a\in G$, we construct an inner automorphism
$\varphi\in\Inn(G)$ defined by $\varphi(x)=x^{a}$. Then $\varphi(N)=N$
by Theorem~\ref{thm:characteristic:inner:subgroup-is-normal-iff-invariant-under-every-inner-automorphism}
which implies $\varphi|_{N}\in\Aut(N)$ by Theorem~\ref{thm:characteristic:automorphism:automorphism-which-leaves-subgroup-invariant-is-automorphism-of-subgroup}.
Viewed
as an automorphism of $N$, $\varphi|_{N}=:\psi\in\Aut(N)$, we must have
$\psi(K)=K$ by virtue of $K$ being characteristic subgroup of $N$. But
$\psi(K)=\varphi(K)$ due to a sub-sub-proof that $\forall k\in K,\psi(k)=\varphi(k)$.
Then the result follows.
\end{proof}

<<Proof: $\forall a\in G, K^{a}=K$>>=
proof
  let a be Element of G;
  consider g being inner Automorphism of G such that
  A1: for x being Element of G holds g.x = x |^ a
  by Th32;

  Image(g|N) = N by Th33;
  then reconsider f = g|N as Automorphism of N by Th22;
  A2: Image(f|K) = K by Def3;

  for k being Element of G st k in K holds f.k = g.k by Th1,GROUP_2:40;
  then Image(g|K) = K & Image(g|K) = K |^ a by A1,A2,Th28,Th36;
  hence thesis;
end;
@



\begin{theorem}
If $N\subgroup G$ is characteristic, and if $K\subgroup N$ is
characteristic, then $K\subgroup G$ is characteristic.
\end{theorem}

The proof is remarkably similar to the previous theorem. In fact, we can
re-use exactly the same line of reasoning establishing
$\forall k\in G$, $k\in K\implies f(k)=g(k)$.

\begin{proof}[Proof outline]
For any automorphism $g\in\Aut(G)$ we have $g(N)=N$ by virtue of $N$ is
a characteristic subgroup of $G$. We can then consider $f=g|_{N}$ as an
automorphism of $N$. Then $f(K)=K$ since $K$ is a characteristic
subgroup of $N$.

We have $\forall k\in K, f(k)=g(k)$. Thus $f(K)=g(K)$, and we have
established $f(K)=K$, therefore $g(K)=K$.
\end{proof}

<<Theorem: transitivity of characteristic subgroups>>=
theorem Th39:
  for N being characteristic Subgroup of G
  for K being characteristic Subgroup of N
  holds K is characteristic Subgroup of G
proof
  let N be characteristic Subgroup of G;
  let K be characteristic Subgroup of N;
  for g being Automorphism of G holds Image(g|K) = K
  proof
    let g be Automorphism of G;
    Image(g|N) = N by Def3;
    then reconsider f = g|N as Automorphism of N by Th22;
    A1: Image(f|K) = K by Def3;

    for k being Element of G st k in K holds f.k = g.k by Th1, GROUP_2:40;
    hence Image(g|K) = K by A1,Th36;
  end;
  hence K is characteristic Subgroup of G by Def3;
end;
@ %def Th39

\begin{theorem}\label{thm:results:alt-def-of-char-subgroup}
  Let $H\subgroup G$.
  Then $H\charSubgroup G$ if and only if for any automorphism $\varphi$, we have
  $\varphi(H)\subgroup H$.
\end{theorem}

Some texts give this as the definition for $H$ being a characteristic
subgroup, which is fine.

\begin{proof}[Proof outline]
  Given a subgroup $H\subgroup G$. We have two halfs to our proof.

We prove $H$ is a characteristic subgroup of $G$ implies
$\forall\varphi\in\Aut(G),\varphi(H)\subgroup H$. This uses the facts
$\varphi(H)=H\subgroup H$. This establishes the first half of the proof.

Now, the other direction, assuming
$\forall\varphi\in\Aut(G),\varphi(H)\subgroup H$ we find $H$ is a
characteristic subgroup of $G$.
\end{proof}

<<Theorem: $H\subgroup G$ is characteristic iff $\forall\varphi\in\Aut(G),\varphi(H)\subgroup H$>>=
theorem Th40:
  for G being Group
  for H being strict Subgroup of G
  holds H is characteristic Subgroup of G iff
  (for phi being Automorphism of G holds Image(phi|H) is Subgroup of H)
proof
  let G be Group;
  let H be strict Subgroup of G;
  thus H is characteristic Subgroup of G implies
       (for phi being Automorphism of G holds Image(phi|H) is Subgroup of H)
  <<Proof: $H\charSubgroup G\implies\forall\varphi\in\Aut(G),\varphi(H)\subgroup H$>>

  thus (for phi being Automorphism of G holds Image(phi|H) is Subgroup of H)
       implies H is characteristic Subgroup of G
  <<Proof: $H\charSubgroup G\impliedby\forall\varphi\in\Aut(G),\varphi(H)\subgroup H$>>

  thus thesis;
end;
@ %def Th40

\begin{proof}[Sub-proof ($H\charSubgroup G\implies\forall\varphi\in\Aut(G),\varphi(H)\subgroup H$).]
The proof in the forward direction boils down to the observation
$H\subgroup H$ then applying the definition of characteristic subgroup.
\end{proof}


<<Proof: $H\charSubgroup G\implies\forall\varphi\in\Aut(G),\varphi(H)\subgroup H$>>=
proof
  assume B1: H is characteristic Subgroup of G;
  let phi be Automorphism of G;
  Image(phi|H) = H & H is Subgroup of H by B1,GROUP_2:54,Def3;
  hence Image(phi|H) is Subgroup of H;
end;
@

\begin{proof}[Sub-proof ($H\charSubgroup G\impliedby\forall\varphi\in\Aut(G),\varphi(H)\subgroup H$).]
The proof in the backward direction amounts to proving, for arbitrary
automorphisms $\varphi$ of $G$, $\varphi(H)\subgroup H$ and since
$\varphi$ is an Automorphism $H\subgroup \varphi^{-1}(H)$. Then for any
$\varphi\in\Aut(G)$, we have $\varphi(H)\subgroup H$. Taken together,
this implies $\forall\varphi\in\Aut(G)$, $H = \varphi(H)$.

But as discussed earlier (\S\ref{par:introduction:subgroup-equality}),
subgroup equality holds only for strict subgroups. For this reason, we
have the hypothesis that $H$ is a strict subgroup of $G$.
\end{proof}

<<Proof: $H\charSubgroup G\impliedby\forall\varphi\in\Aut(G),\varphi(H)\subgroup H$>>=
proof
  assume A1: for phi being Automorphism of G holds Image(phi|H) is Subgroup of H;
  A2: for phi being Automorphism of G holds H is Subgroup of Image(phi|H)
  <<Proof: $\forall\varphi\in\Aut(G),H\subgroup\varphi(H)$>>
  for phi being Automorphism of G holds H = Image(phi|H)
  proof
    let phi be Automorphism of G;
    H is Subgroup of Image(phi|H) & Image(phi|H) is Subgroup of H
    by A1,A2;
    hence H = Image(phi|H) by GROUP_2:55;
  end;
  hence H is characteristic Subgroup of G by Def3;
end;
@

\begin{proof}[Sub-sub-proof ($\forall\varphi\in\Aut(G),H\subgroup\varphi(H)$).]
This is a slick argument, which is confusing until one realizes
what's going on.  We use the facts that, for any $\varphi,\psi\in\Aut(G)$,
\begin{itemize}
\item Hypothesis [[A1]]: $\psi(H)\subgroup H$;
\item Theorem~\ref{thm:characteristic:automorphism:f-fInv-H-is-subgroup-of-H}: for $\psi=\varphi^{-1}$, we have $\varphi[\psi(H)]\subgroup\varphi(H)$; and
\item Theorem~\ref{thm:characteristic:automorphism:phi-phiInv-H-is-H}: for $\psi=\varphi^{-1}$, the underlying magmas of $H$ and $\varphi[\psi(H)]$ are equal.
\end{itemize}
This suffices to infer $H\subgroup\varphi(H)$.
\end{proof}

<<Proof: $\forall\varphi\in\Aut(G),H\subgroup\varphi(H)$>>=
proof
  let phi be Automorphism of G;
  consider psi being Automorphism of G such that
  B1: psi = phi" and
  B2: Image(phi|Image(psi|H)) is Subgroup of Image(phi|H) by A1,Th16;
  consider psi2 being Automorphism of G such that
  B3: psi2 = phi" and
  B4: the multMagma of H = the multMagma of Image(phi|Image(psi2|H)) by Th17;
  thus H is Subgroup of Image(phi|H) by B1,B2,B3,B4;
end;
@


\begin{theorem}\index{$Z(G)$}\index{Center!Group}\index{Group!Center}
Let $G$ be a group. Its center $Z(G)$ is a characteristic subgroup.
\end{theorem}

\begin{proof}[Proof outline]
This is our first application of
$\forall\varphi\in\Aut(G),\varphi(H)\subgroup H$ implies $H$ is characteristic.
The bulk of the proof amounts to showing $\varphi(Z(G))\subgroup Z(G)$,
which requires two prior steps:
\begin{enumerate}
\item for any $g\in G$ and $z\in Z(G)$, we have $\varphi(z)g =
  g\varphi(z)$ --- i.e., $\varphi(z)$ commutes with every element of
  $G$; then
\item $\varphi(z)\in Z(G)$ for any $z\in Z(G)$.
\end{enumerate}
Then we have $\varphi(Z(G))$ be a subgroup of $Z(G)$, which let's us use
the previous theorem.
\end{proof}

\begin{thm-remark}
The [[center]]\mizindex{Center@\texttt{center}} functor is defined in
Definition~\mml[def10]{group5}\MizDef{GROUP\_5}{10}.
\end{thm-remark}

<<Theorem: $Z(G)$ is characteristic subgroup>>=

theorem Th41:
  center G is characteristic Subgroup of G
proof
  set Z = center G;
  <<Prove $\forall\varphi\in\Aut(G),\varphi(Z(G))\subgroup Z(G)$>>

  hence Z is characteristic Subgroup of G by Th40;
end;
@ %def Th41

\begin{proof}[Proof branch ($\forall\varphi\in\Aut(G),\varphi(Z(G))\subgroup Z(G)$)]
The heart of the proof amounts to showing, for any Automorphism
$\varphi$, that $\varphi(Z(G))$ is a subgroup of $Z(G)$.
\end{proof}

<<Prove $\forall\varphi\in\Aut(G),\varphi(Z(G))\subgroup Z(G)$>>=
<<Step 1: $\forall\varphi\in\Aut(G)\forall y\in G\forall z\in Z(G), \varphi(z)y = y\varphi(z)$>>
<<Step 2: $\forall\varphi\in\Aut(G)\forall z\in G, z\in Z(G)\implies \varphi(z)\in Z(G)$>>
<<Step 3: $\forall\varphi\in\Aut(G),\varphi(Z(G))\subgroup Z(G)$>>
@

\begin{proof}[Proof step 1 ($\forall\varphi\in\Aut(G)\forall y\in G\forall z\in Z(G), \varphi(z)y = y\varphi(z)$).]
Proving $\varphi(z)$ commutes with every element of the group is a
straightforward calculation. In fact, this is usually what textbooks
present, then dismiss the rest of the proof as ``trivial'' or
``obvious''.
\end{proof}


<<Step 1: $\forall\varphi\in\Aut(G)\forall y\in G\forall z\in Z(G), \varphi(z)y = y\varphi(z)$>>=
A1: for y,z being Element of G st z in Z
holds (phi.z)*y = y*phi.z
proof
  let y,z be Element of G;
  assume B1: z in Z;
  set x = (phi").y;
  (phi.z)*y = (phi.z)*(phi.x) by Th4
           .= phi.(z*x) by GROUP_6:def 6
           .= phi.(x*z) by B1,GROUP_5:77
           .= (phi.x)*(phi.z) by GROUP_6:def 6
           .= y*(phi.z) by Th4;
  hence thesis;
end;
@

\begin{proof}[Proof step 2 ($\forall\varphi\in\Aut(G)\forall z\in G, z\in Z(G)\implies \varphi(z)\in Z(G)$).]
Establishing $z\in Z(G)$ implies $\varphi(z)\in Z(G)$ amounts to
unfolding definitions.
\end{proof}

<<Step 2: $\forall\varphi\in\Aut(G)\forall z\in G, z\in Z(G)\implies \varphi(z)\in Z(G)$>>=
A2: for z being Element of G st z in Z
holds (phi|Z).z in Z
proof
  let z be Element of G;
  assume B1: z in Z;
  then for y being Element of G holds (phi.z)*y=y*(phi.z) by A1;
  then (phi.z) in Z by GROUP_5:77;
  hence ((phi|Z).z) in Z by B1,Th1;
end;
@

\begin{proof}[Proof step 3 ($\forall\varphi\in\Aut(G),\varphi(Z(G))\subgroup Z(G)$).]
The last step, which is the ``obvious'' part, infers from
$\forall z\in Z(G),\varphi(z)\in Z(G)$ that $\varphi(Z(G))\subgroup Z(G)$.
It's also the ugliest part of the proof which could probably be cleaned
up considerably. This is only due to the sub-step establishing
$w\in\rng(\varphi|_{Z(G)})\implies w\in Z(G)$.
\end{proof}

<<Step 3: $\forall\varphi\in\Aut(G),\varphi(Z(G))\subgroup Z(G)$>>=
Image(phi|Z) is Subgroup of Z
proof
  for w being Element of G st w in rng(phi|Z) holds w in Z
  <<Sub-step 3.1: $\forall w\in G,w\in\rng(\varphi|_{Z(G)})\implies w\in Z(G)$>>
  then rng(phi|Z) c= the carrier of Z by STRUCT_0:def 5;
  then the carrier of Image(phi|Z) c= the carrier of Z by GROUP_6:44;
  hence Image(phi|Z) is Subgroup of Z by GROUP_2:57;
end;
@

\begin{proof}[Proof sub-step 3.1 ($\forall w\in G,w\in\rng(\varphi|_{Z(G)})\implies w\in Z(G)$).]
This substep is ugly, and I offer no apology for it. There's probably
a more elegant solution, but I cannot think of one. We explicitely
walkthrough showing $w\in\rng(\varphi|_{Z(G)})$, which means there is
some $z$ such that $z\in\dom(\varphi|_{Z(G)})$ and $\varphi|_{Z(G)}(z) = w$.
Since $\dom(\varphi|_{Z(G)})=Z(G)$, it follows $\varphi|_{Z(G)}(z)\in Z(G)$
from step 2. Thus the result follows.
\end{proof}

<<Sub-step 3.1: $\forall w\in G,w\in\rng(\varphi|_{Z(G)})\implies w\in Z(G)$>>=
proof
  let w be Element of G;
  assume w in rng(phi|Z);
  then consider z being object such that
  C1: z in dom(phi|Z) and
  C2: (phi|Z).z = w by FUNCT_1:def 3;
  reconsider z as Element of Z by C1;
  z is Element of G by GROUP_2:42;
  hence w in Z by C2,A2,STRUCT_0:def 5;
end;
@

\begin{scheme}\label{scheme:characteristic:results:intersection-invariant-under-automorphism}
Let $P[-]$ be a predicate on subgroups of $G$ for which (1) there is at
least one subgroup $H\subgroup G$ satisfying $P[H]$ and (2) if
$H\subgroup G$ satisfies $P[H]$, then for any automorphism
$\varphi\in\Aut(G)$ we have $P[\varphi(H)]$.

Let $\mathcal{F} = \{A\subset G\mid \exists H\subgroup G, P[H]\land A=U(H)\}$
be the family of sets underlying all subgroups of $G$ satisfying $P[-]$.
Then $\varphi(\bigcap\mathcal{F})=\bigcap\mathcal{F}$.
\end{scheme}

\begin{proof}[Proof outline]
Let $\mathcal{F} = \{A\subset G\mid H\subgroup G\land P[H]\}$ be the
collection of sets underlying subgroups $H\subgroup G$ which satisfy the
property $P[H]$. First, we need to prove $\mathcal{F}\neq\emptyset$. But
since we have, by hypothesis, there is at least one subgroup $H\subgroup G$
which satisfies $P[H]$\dots so $\mathcal{F}$ is nonempty. Then to prove
$\varphi(\bigcap\mathcal{F})=\bigcap\mathcal{F}$, we establish
$\varphi(\bigcap\mathcal{F})\subset\bigcap\mathcal{F}$ and
$\varphi(\bigcap\mathcal{F})\supset\bigcap\mathcal{F}$. The result then follows.
\end{proof}

<<Scheme: if $H\subgroup G\land P[H]$ and $\forall\varphi\in\Aut(G),P[\varphi(H)]$, then $\bigcap\{H\subgroup G\mid P[H]\}$ is $\Aut(G)$-invariant>>=
reserve A1,A2 for set;

scheme :: sch 1
  CharMeet{G() -> Group, P[set]} :
  for phi being Automorphism of G()
  holds phi .: meet{A where A is Subset of G() : ex K being strict Subgroup
  of G() st A = the carrier of K & P[K]} = meet{A where A is Subset of G() :
  ex K being strict Subgroup of G() st A = the carrier of K & P[K]}
provided
A1: for phi being Automorphism of G()
    for H being strict Subgroup of G()
    st P[H]
    holds P[Image(phi|H)] and
A2: ex H being strict Subgroup of G() st P[H]
proof
  let phi be Automorphism of G();
  set UG = the carrier of G();
  set Fam = {A where A is Subset of G() : ex K being strict Subgroup
  of G() st A = the carrier of K & P[K]};
  consider H being strict Subgroup of G() such that
A3: P[H]
  by A2;
  A4: Fam <> {}
  <<Proof step: $\mathcal{F}\neq\emptyset$>>

  A5: for phi0 being Automorphism of G()
  for x being object st x in meet Fam
  holds phi0.x in meet Fam
  <<Proof step: $\forall\varphi_{0}\in\Aut(G),\forall x, x\in\bigcap\mathcal{F}\implies\varphi_{0}(x)\in\bigcap\mathcal{F}$>>

  for x being object st x in meet Fam
  holds x in phi .: meet Fam
  <<Proof step: $x\in\bigcap\mathcal{F}\implies x\in\varphi(\bigcap\mathcal{F})$>>
  then P1: meet Fam c= phi .: meet Fam;

  for y being object st y in phi .: meet Fam
  holds y in meet Fam
  <<Proof step: $y\in\varphi(\bigcap\mathcal{F})\implies y\in\bigcap\mathcal{F}$>>
  then P2: phi .: meet Fam c= meet Fam;

  thus thesis by P1,P2,XBOOLE_0:def 10;

end;
@ %def CharMeet

\begin{proof}[Proof step: $\mathcal{F}$ is non-empty]
Since we assumed there exists a subgroup $H\subgroup G$ such that $P[H]$
holds, there is at least one element of $\mathcal{F}$, namely
$H\in\mathcal{F}$. Hence $\bigcap\mathcal{F}\neq\emptyset$.
\end{proof}

<<Proof step: $\mathcal{F}\neq\emptyset$>>=
proof
  consider A being set such that
  B1: A = the carrier of H;
  the carrier of H is Subset of G() by GROUP_2:def 5;
  then A in Fam by A3, B1;
  hence thesis;
end;
@

\begin{proof}[Proof step ($\forall\varphi_{0}\in\Aut(G),\forall x, x\in\bigcap\mathcal{F}\implies\varphi_{0}(x)\in\bigcap\mathcal{F}$).]
Suppose $x\in\bigcap\mathcal{F}$. Then $\forall A\in\mathcal{F},x\in A$.
Let $\varphi_{0}$ be an arbitrary automorphism of $G$.
From the hypothesis $P[H]\implies \forall\varphi\in\Aut(G),
P[\varphi(H)]$, we apply it to $\psi=\varphi_{0}^{-1}$. But this means $x\in\psi(A)$.
Then $\varphi_{0}(x)\in\varphi_{0}(\psi(A))$, and $\varphi_{0}(\psi(A))=A$. Thus the
result follows.
\end{proof}

<<Proof step: $\forall\varphi_{0}\in\Aut(G),\forall x, x\in\bigcap\mathcal{F}\implies\varphi_{0}(x)\in\bigcap\mathcal{F}$>>=
proof
  let phi0 be Automorphism of G();
  let x be object;
  assume x in meet Fam;
  then B1: for A1 holds A1 in Fam implies x in A1 by SETFAM_1:def 1;
  for A being set holds A in Fam implies phi0.x in A
  proof
    let A be set;
    assume C1: A in Fam;
    consider A0 being Subset of G() such that
    C2: A0=A & ex H being strict Subgroup of G() st A0 = the carrier of H & P[H]
    by C1;
    consider K being strict Subgroup of G() such that
    C3: A = the carrier of K & P[K]
    by C2;
    consider psi being Automorphism of G() such that
    C4: psi = phi0" & the multMagma of Image(phi0|Image(psi|K)) = the multMagma of K
    by Th17;
    x in K by C1,B1,C3;
    then C5: x in G() & dom phi0 = the carrier of G() by GROUP_2:40,FUNCT_2:def 1;
    P[Image(psi|K)] by C3,A1;
    then carr Image(psi|K) in Fam;
    then x in carr Image(psi|K) by B1;
    then phi0.x in phi0 .: (carr Image(psi|K)) by C5, FUNCT_1:def 6;
    then phi0.x in carr (phi0 .: Image(psi|K)) by GRSOLV_1:8;
    hence phi0.x in A by C3,C4,GRSOLV_1:def 3;
  end;
  hence thesis by A4, SETFAM_1:def 1;
end;
@

\begin{proof}[Proof step ($x\in\bigcap\mathcal{F}\implies x\in\varphi(\bigcap\mathcal{F})$).]
Suppose $x\in\bigcap\mathcal{F}$. Let $\varphi\in\Aut(G)$ be completely
arbitrary, and $\psi=\varphi^{-1}$. From $x\in\bigcap\mathcal{F}$, it
follows $\forall A\in\mathcal{F}, x \in A$. It follows from a previous
step that $\forall A\in\mathcal{F},\psi(x)\in A$. Then applying
$\varphi$ we find $\forall A\in\mathcal{F},\varphi(\psi(x))=x\in\varphi(A)$.
Hence $x\in\varphi(\bigcap\mathcal{F})$.
\end{proof}

<<Proof step: $x\in\bigcap\mathcal{F}\implies x\in\varphi(\bigcap\mathcal{F})$>>=
proof
  let x be object;
  assume B1: x in meet Fam;
  then carr H in Fam & for A1 holds A1 in Fam implies x in A1 by A3,SETFAM_1:def 1;
  then B2: x in H;
  then B3: x is Element of G() by GROUP_2:42;
  reconsider psi = phi" as Automorphism of G() by GROUP_6:62;
  B4: dom phi = the carrier of G() by FUNCT_2:def 1;
  B5: psi.x in meet Fam by B1,A5;
  B6: phi.(psi.x) = x by B3,Th4;
  psi is bijective & x in G() by B2, GROUP_2:40;
  then psi.x in dom phi by B4, FUNCT_2:5;
  hence x in phi .: meet Fam by B6,B5,FUNCT_1:def 6;
end;
@

\begin{proof}[Proof step ($y\in\varphi(\bigcap\mathcal{F})\implies y\in\bigcap\mathcal{F}$).]
  Let $\varphi\in\Aut(G)$ be arbitrary.
Suppose $y\in\varphi(\bigcap\mathcal{F})$. Then there is an
$x\in\bigcap\mathcal{F}$ such that $y=\varphi(x)$. Then
$\varphi(x)\in\bigcap\mathcal{F}$ from our first proof step. Hence
$y\in\bigcap\mathcal{F}$.
\end{proof}

<<Proof step: $y\in\varphi(\bigcap\mathcal{F})\implies y\in\bigcap\mathcal{F}$>>=
proof
  let y be object;
  assume y in phi .: meet Fam;
  then consider x being object such that
  B1: x in dom phi & x in meet Fam & y = phi.x
  by FUNCT_1:def 6;
  thus y in meet Fam by B1,A5;
end;
@

\begin{scheme}\label{scheme:characteristic:results:meet-is-characteristic}
Let $P[-]$ be a predicate on subgroups of $G$ for which (1) there is at
least one subgroup $H\subgroup G$ satisfying $P[H]$ and (2) if
$H\subgroup G$ satisfies $P[H]$, then for any automorphism
$\varphi\in\Aut(G)$ we have $P[\varphi(H)]$.

Let $\mathcal{F} = \{A\subset G\mid \exists H\subgroup G, P[H]\land A=U(H)\}$
be the family of sets underlying all subgroups of $G$ satisfying $P[-]$.
There exists a subgroup $K\subgroup G$ whose underling set is
$U(K) = \bigcap\mathcal{F}$ such that $K$ is characteristic.
\end{scheme}

\begin{proof}[Proof outline]
The proof boils down to what we would find in a textbook. For any
Automorphism $\varphi\in\Aut(G)$, we have the collection $\mathcal{F}$
of subgroups of $G$ satisfying $P[H]$, then we know there exists a
subgroup $K\subgroup G$ such that its underlying set $U(K)$ is
\begin{equation}
U(K) := \bigcap\mathcal{F}.
\end{equation}
Now to prove it is characteristic, we reason as follows:
\begin{equation}
\varphi(K)=\varphi(\bigcap\mathcal{F})=\bigcap\varphi(\mathcal{F})=\bigcap
\mathcal{F} = K.
\end{equation}
We take advantage of Scheme~\ref{scheme:characteristic:results:intersection-invariant-under-automorphism} to prove
\begin{equation}
\varphi(\bigcap\mathcal{F})=\bigcap\varphi(\mathcal{F})=\bigcap \mathcal{F}.
\end{equation}
We also use the hypothesis that subgroups $H\subgroup G$ satisfying $P[H]$ are
mapped to subgroups $\varphi(H)$ satisfying $P[\varphi(H)]$ under
automorphisms $\varphi\in G$.
\end{proof}

<<Scheme: $\bigcap\{A\subset G\mid\exists H\subgroup G, A=H, P[H]\}$ is characteristic>>=
scheme :: sch 2
  MeetIsChar{G() -> Group, P[set]} :
  ex K being strict Subgroup of G() st
  the carrier of K = meet {A where A is Subset of G() :
  ex H being strict Subgroup of G() st A = the carrier of H & P[H]} &
  K is characteristic
provided
A1: for phi being Automorphism of G()
    for H being strict Subgroup of G()
    st P[H]
    holds P[Image(phi|H)] and
A2: ex H being strict Subgroup of G() st P[H]
proof
  set Fam = {A where A is Subset of G() :
             ex H being strict Subgroup of G()
             st A = the carrier of H & P[H]};

  A3: for phi being Automorphism of G()
  holds phi .: meet Fam = meet Fam from CharMeet(A1,A2);

  consider K being strict Subgroup of G() such that
  A4: the carrier of K = meet Fam
  from GROUP_4:sch 1(A2);
  take K;

  for phi being Automorphism of G()
  holds Image(phi|K) = K
  proof
    let phi be Automorphism of G();
    the carrier of Image(phi|K)
     = the carrier of phi .: K by GRSOLV_1:def 3
    .= phi .: (the carrier of K) by GRSOLV_1:8
    .= phi .: meet Fam by A4
    .= meet Fam by A3
    .= the carrier of K by A4;
    hence Image(phi|K) = K by GROUP_2:59;
  end;
  hence thesis by A4;
end;
@ %def MeetIsChar

\begin{proposition}[{\mml[Th38]{group4}}]
  Let $G$ be a group. Suppose $G$ has a maximal subgroup.
  Then for any group element $a\in G$, we have
  $a\in\FrattiniSubgroup(G)$ if and only if for every maximal subgroup
  $H\properSubgroup G$ we have $a\in H$.
\end{proposition}

\begin{theorem}
The Frattini subgroup $\FrattiniSubgroup(G)$ is a characteristic subgroup of $G$.
\end{theorem}

\begin{proof}[Proof outline]
The proof boils down to what we would find in a textbook.
We have abstracted away the argument to form Scheme~\ref{scheme:characteristic:results:meet-is-characteristic},
which gives us the results.
\end{proof}

\begin{thm-remark}
Observe the key property required to prove that $\Phi(G)$ is
characteristic is the observation we have a family $\mathcal{F}$ of
subgroups of $G$ for which any automorphism $\varphi\in\Aut(G)$ acts
like a permutation of $\mathcal{F}$. If this is true, then
$\bigcap\mathcal{F}$ is characteristic. We can formulate this argument
as a scheme.
\end{thm-remark}

\begin{thm-remark}
The proof is an extra step, because I believe it worth the cost to
stress at a human-readable level that $K=\Phi(G)$.
\end{thm-remark}
<<Theorem: $\FrattiniSubgroup(G)$ is characteristic>>=
theorem Th42:
  for G being non trivial Group
  holds (ex H being strict Subgroup of G st H is maximal) implies
  Phi(G) is characteristic Subgroup of G
proof
  let G be non trivial Group;
  defpred P[Subgroup of G] means $1 is maximal;
  assume A1: ex H being strict Subgroup of G st P[H];
  set MaxSubCarrs = {A where A is Subset of G :
                     ex H being strict Subgroup of G
                     st A = the carrier of H & P[H]};

  A2: for phi being Automorphism of G
  for H being strict Subgroup of G
  st P[H]
  holds P[Image(phi|H)] by Th24;

  consider K being strict Subgroup of G such that
  A3: the carrier of K = meet {A where A is Subset of G :
      ex H being strict Subgroup of G st A = the carrier of H & P[H]} and
  A4: K is characteristic
  from MeetIsChar(A2,A1);
  K = Phi(G) by A1,A3,GROUP_4:def 7; :: sic
  hence thesis by A4;
end;
@ %def Th42


\subsection{Derived Subgroup is Characteristic}
We will denote the \emph{set} of all commutators of elements of $G$ by
\begin{equation}
\Commutators(G) := \{[a,b]\in G\mid a,b\in G\}.
\end{equation}
The derived subgroup is the \emph{subgroup} generated by this
\begin{equation}
G' = [G,G] := \langle\Commutators(G)\rangle.
\end{equation}
We will prove the derived subgroup is characteristic.

\begin{theorem}
Let $G$ be a group, let $C=\Commutators(G)$ be the set of
commutators of any pair of group elements. Then for any automorphism
$\varphi\in\Aut(G)$, we have $\varphi(C) = C$.
\end{theorem}

\begin{proof}[Proof outline]
We will prove $\varphi(C)\subset C$ and $\varphi(C)\supset C$.
\end{proof}

<<Theorem: $\forall\varphi\in G,\varphi(\Commutators(G))=\Commutators(G)$>>=
theorem Th43:
  for phi being Automorphism of G
  holds phi .: commutators G = commutators G
proof
  let phi be Automorphism of G;
  for g being object
  st g in commutators G
  holds g in phi .: commutators G
  <<Proof: $\varphi(\Commutators(G))\supset\Commutators(G)$>>
  then P1: commutators G c= phi .: commutators G;
  for h being object
  st h in phi .: commutators G
  holds h in commutators G
  <<Proof: $\varphi(\Commutators(G))\subset\Commutators(G)$>>
  then phi .: commutators G c= commutators G;
  hence commutators G = phi .: commutators G by P1,XBOOLE_0:def 10;
end;
@ %def Th43

\begin{proof}[{Proof step [$\varphi(\Commutators(G))\supset\Commutators(G)$]}.]
  Let $g\in\Commutators(G)$. Then consider $a,b\in G$ such that
  \begin{equation}
g = [a,b]
  \end{equation}
  by Theorem~\mml[Th58]{group5}. Take $x=\varphi^{-1}(a)$ and
  $y=\varphi^{-1}(b)$. These are elements of $G$, so $[x,y]\in\Commutators(G)$.
  Then $\varphi([x,y])\in\varphi(\Commutators(G))$ and
  \begin{equation}
\varphi([x,y])=[a,b]=g
  \end{equation}
  thus $g\in\varphi(\Commutators(G))$. Since we let $g$ be arbitrary,
  this implies $\varphi(\Commutators(G))\supset\Commutators(G)$ by Theorem~\mml[def3]{tarski}.
\end{proof}

<<Proof: $\varphi(\Commutators(G))\supset\Commutators(G)$>>=
proof
  let g be object;
  assume B1: g in commutators G;
  then reconsider g as Element of G;
  consider a,b being Element of G such that
  B2: g = [.a,b.]
  by B1, GROUP_5:58;
  reconsider psi = phi" as Automorphism of G by GROUP_6:62;
  set x = psi.a;
  set y = psi.b;
  set h = [.x,y.];
  dom phi = the carrier of G by FUNCT_2:def 1;
  then B3: h in dom phi & h in commutators G & phi.((phi").g) = g by Th4;
  psi.g = psi.([.a,b.]) by B2
       .= [.psi.a,psi.b.] by GROUP_6:34
       .= h;
  hence thesis by B3, FUNCT_1:def 6;
end;
@

\begin{proof}[{Proof step [$\varphi(\Commutators(G))\subset\Commutators(G)$]}.]
We consider an arbitrary $h\in\varphi(\Commutators(G))$. Then there is
some $g\in\Commutators(G)$ such that $\varphi(g)=h$. Consider $a,b\in G$
such that
\begin{equation}
g=[a,b].
\end{equation}
Then by Theorem~\mml[Th34]{group6},
\begin{equation}
\varphi([a,b])
= [\varphi(a),\varphi(b).
\end{equation}
But look, this is a commutator! Combining all this together, this means
$\varphi(g)\in\Commutators(G)$. And since we let $h$ be arbitrary, this
means $\varphi(\Commutators(G))\subset\Commutators(G)$ by Theorem~\mml[def3]{tarski}.
\end{proof}

<<Proof: $\varphi(\Commutators(G))\subset\Commutators(G)$>>=
proof
  let h be object;
  assume B1: h in phi .: commutators G;
  consider g being object such that
      g in dom phi and
  B2: g in commutators G and
  B3: h = phi.g
  by B1,FUNCT_1:def 6;
  consider a,b be Element of G such that
  B4: g = [.a,b.]
  by B2,GROUP_5:58;
  h = phi.g by B3
   .= phi.([.a,b.]) by B4
   .= [.phi.a, phi.b.] by GROUP_6:34;
  hence h in commutators G;
end;
@

\begin{theorem}
Let $H\subgroup G$ be a subgroup, $\varphi\in\Aut(G)$ be an automorphism.
If every $h\in H$ satisfies $\varphi(h)\in H$, then $\varphi(H)\subgroup H$.
\end{theorem}

\begin{proof}
We have $\varphi(H)\subgroup H$ follow from the underlying set
inclusions, and unfolding the definitions.
\end{proof}

<<Theorem: $\forall h\in H,\varphi(h)\in H$ implies $\varphi(H)\subgroup H$>>=
theorem Th44:
  for G being Group
  for phi being Automorphism of G
  for H being Subgroup of G
  st (for h being Element of H
      holds phi.h in H)
  holds Image(phi|H) is Subgroup of H
proof
  let G be Group;
  let phi be Automorphism of G;
  let H be Subgroup of G;
  assume A1: for h being Element of H holds phi.h in H;
  for y being object st y in rng(phi|H) holds y in the carrier of H
  proof
    let y be object;
    assume y in rng(phi|H);
    then consider x being object such that
    B1: x in dom(phi|H) and
    B2: y = (phi|H).x
    by FUNCT_1:def 3;
    B3: x in H by B1;
    reconsider x as Element of H by B1;
    phi.x in H & x is Element of G by A1,GROUP_2:42;
    hence y in the carrier of H by B2,B3,Th1;
  end;
  then rng(phi|H) c= the carrier of H;
  then the carrier of Image(phi|H) c= the carrier of H by GROUP_6:44;
  hence Image(phi|H) is Subgroup of H by GROUP_2:57;
end;
@ %def Th44

\begin{theorem}
If $A\subset G$ is a nonempty subset such that, for any automorphism
$\varphi\in\Aut(G)$ we have $\varphi(A) = A$, then the generated
subgroup $\langle A\rangle$ is a characteristic subgroup.
\end{theorem}

\begin{proof}[Proof sketch]
We should recall that a generic element of $\langle A\rangle$ is given
by the product of finitely many elements of $A$. The first thing we
prove is $(A_{2})$ for any automorphism $\varphi\in\Aut(G)$ and for any
$a\in A$, we have $\varphi(a)\in A$. Then we prove
$\varphi(\langle A\rangle)=\langle A\rangle$, which establishes the result.
\end{proof}

<<Theorem: $A\subset G$ s.t. $\forall\varphi\in\Aut(G),\varphi(A)=A$, then $\langle A\rangle$ is characteristic>>=
theorem Th45:
  for G being Group
  for A being non empty Subset of G
  st (for phi being Automorphism of G
      holds phi .: A = A)
  holds gr A is characteristic
proof
  let G be Group;
  let A be non empty Subset of G;
  assume A1: for phi being Automorphism of G holds phi .: A = A;
  A2: for phi being Automorphism of G for a being Element of A holds phi.a in A
  proof
    let phi be Automorphism of G;
    let a be Element of A;
    dom phi = the carrier of G by FUNCT_2:def 1;
    then phi.a in phi .: A by FUNCT_1:def 6;
    hence phi.a in A by A1;
  end;
  set H = gr A;
  A3: for phi being Automorphism of G holds Image(phi|H) is Subgroup of H
  proof
    let phi be Automorphism of G;
    B2: for h being Element of G st h in H holds phi.h in H
    <<Proof: $\forall h\in G, h\in H\implies \varphi(h)\in H$>>
    for h being Element of H holds phi.h in H
    proof
      let h be Element of H;
      C1: h in H;
      h is Element of G by GROUP_2:42;
      hence phi.h in H by B2,C1;
    end;
    hence Image(phi|H) is Subgroup of H by Th44;
  end;
  thus gr A is characteristic by A3,Th40;
end;
@ %def Th45

\begin{proof}[Proof step ($\forall h\in G, h\in H\implies \varphi(h)\in H$).]
Taking two finite sequences $F^{(1)}=(a_{1},\dots,a_{n})$ and $F^{(2)}_{j}=\varphi(F^{(1)}_{j})$,
we prove $(C_{8})$ that $F^{(2)}$ is a sequence of elements of $A$, then $\varphi(\prod_{j}F^{(1)}_{j})=\prod_{j}\varphi(F^{(1)}_{j})=\prod_{j}F^{(2)}_{j}$
implies $\prod_{j}F^{(2)}_{j}\in H$.
\end{proof}

<<Proof: $\forall h\in G, h\in H\implies \varphi(h)\in H$>>=
proof
  let h be Element of G;
  assume h in H;
  then consider F1 being FinSequence of the carrier of G,
                I being FinSequence of INT such that
  C2: len F1 = len I and
  C3: rng F1 c= A and
  C4: Product(F1 |^ I) = h
  by GROUP_4:28;
  deffunc F(Nat) = phi.(F1/.$1);
  consider F2 being FinSequence such that
  C5: len(F2) = len F1 and
  C6: for k being Nat st k in dom F2 holds F2.k = F(k)
  from FINSEQ_1:sch 2;
  C7: dom F2 = dom F1 by C5,FINSEQ_3:29;
  C8: F2 is FinSequence of the carrier of G & rng F2 c= A
  proof
    D1: for y being object st y in rng F2 holds y in A
    proof
      let y be object;
      assume y in rng F2;
      then consider k being object such that
      E2: k in dom F2 and
      E3: y = F2.k by FUNCT_1:def 3;
      reconsider k as Element of NAT by E2;
      set x = F1.k;
      x in rng F1 by FUNCT_1:def 3, E2, C7;
      then reconsider x as Element of A by C3;
      E4: x = F1/.k by E2,C7,PARTFUN1:def 6;
      y = F(k) by E2,E3,C6
       .= phi.(F1/.k);
      hence y in A by A2,E4;
    end;
    for y being object st y in rng F2 holds y in the carrier of G
    proof
      let y be object;
      assume y in rng F2;
      then y in A by D1;
      hence y in the carrier of G;
    end;
    hence rng F2 c= the carrier of G;
    thus rng F2 c= A by D1;
  end;
  then reconsider F2 as FinSequence of the carrier of G;
  set h2 = Product(F2 |^ I);
  C9: (for k being Nat st k in dom F1
       holds F2.k = phi.(F1.k)) & len F1 = len I & len F2 = len I
  proof
    thus (for k being Nat st k in dom F1 holds F2.k = phi.(F1.k))
    proof
      let k be Nat;
      assume D1: k in dom F1;
      then k in dom F2 by C5,FINSEQ_3:29;
      then F2.k = F(k) by C6
               .= phi.(F1/.k);
      hence F2.k = phi.(F1.k) by D1,PARTFUN1:def 6;
    end;
    thus len F1 = len I by C2;
    thus len F2 = len I by C2,C5;
  end;
  then len F2 = len I & rng F2 c= A & Product(F2 |^ I) = phi.h
  by C4,C8,GROUP_9:125;
  hence phi.h in H by GROUP_4:28;
end;
@

\begin{theorem}
The derived subgroup $G'=[G,G]$ is a characteristic subgroup.
\end{theorem}

\begin{proof}[Proof sketch.]
We simply use the fact that, for any automorphism (indeed, any
endomorphism) $\varphi\in\Aut(G)$, we have
$\varphi(\{[x,y]\mid x,y\in G\})=\{[x,y]\mid x,y\in G\})$. Then from the
previous theorem, we have $\varphi([G,G])=[G,G]$.
\end{proof}

<<Theorem: The derived subgroup is characteristic>>=
theorem Th46:
  G` is characteristic
proof
  A1: [.1_G,1_G.] in commutators G;
  for phi being Automorphism of G holds phi .: commutators G = commutators G
  by Th43;
  hence thesis by A1,Th45;
end;
@ %def Th46

\begin{theorem}
If $H\subgroup G$ is any subgroup, $a\in G$ is any group element, and
$\varphi\in\Aut(G)$, then $\varphi(aH)=\varphi(a)\varphi(H)$.
\end{theorem}

\begin{proof}[Proof sketch]
We prove set equality by showing $\varphi(aH)\subset\varphi(a)\varphi(H)$ and then
$\varphi(a)\varphi(H)\subset\varphi(aH)$, which then proves the result.
\end{proof}

<<Theorem: $H\subgroup G, a\in G$, $\varphi(aH)=\varphi(a)\varphi(H)$>>=
theorem Th47:
  for G1,G2 being Group
  for H being Subgroup of G1
  for a being Element of G1
  for f being Homomorphism of G1,G2
  holds f.:(a * H) = (f.a) * (f .: H)
proof
  let G1,G2 be Group;
  let H be Subgroup of G1;
  let a be Element of G1;
  let f be Homomorphism of G1,G2;
  A1: dom f = the carrier of G1 by FUNCT_2:def 1;
  for y being object st y in f.:(a * H) holds y in (f.a)*(f.:H)
  proof
    let y be object;
    assume y in f .: (a * H);
    then consider x being object such that
    B1: x in the carrier of G1 & x in (a * H) and
    B2: y = f.x
    by A1,FUNCT_1:def 6;
    consider h being Element of G1 such that
    B3: x = a*h & h in H
    by B1,GROUP_2:103;
    B4: y = f.(a*h) by B2,B3
         .= (f.a)*(f.h) by GROUP_6:def 6;
    dom f = the carrier of G1 & h in H & h in G1 by B3,FUNCT_2:def 1;
    then f.h in f.:(the carrier of H) by FUNCT_1:def 6;
    then f.h in f.:H by GRSOLV_1:8;
    hence y in (f.a) * (f .: H) by B4,GROUP_2:103;
  end;
  then A1: f.:(a * H) c= (f.a) * (f .: H);
  for y being object st y in (f.a)*(f.:H) holds y in f.:(a * H)
  proof
    let y be object;
    assume y in (f.a)*(f.:H);
    then consider g being Element of G2 such that
    B1: y = (f.a)*g and
    B2: g in (f.:H)
    by GROUP_2:103;
    g in Image(f|H) by B2,GRSOLV_1:def 3;
    then consider x being Element of H such that
    B3: g = (f|H).x
    by GROUP_6:45;
    B4: x in H & x is Element of G1 by GROUP_2:42;
    reconsider x as Element of G1 by GROUP_2:42;
    B5: y = (f.a)*g by B1
         .= (f.a)*(f.x) by B3,B4,Th1
         .= f.(a*x) by GROUP_6:def 6;
    a*x in the carrier of G1 & dom f = the carrier of G1 by FUNCT_2:def 1;
    then (a*x) in dom f & (a*x) in a*H & y=f.(a*x) by B4,B5,GROUP_2:103;
    hence y in f.:(a * H) by FUNCT_1:def 6;
  end;
  then A2: (f.a) * (f .: H) c= f.:(a * H);
  thus f.:(a * H) = (f.a) * (f .: H) by A1,A2,XBOOLE_0:def 10;
end;
@ %def Th47

\begin{theorem}
If $H\subgroup G$ is any subgroup, $a\in G$ is any group element, and
$\varphi\in\Aut(G)$, then $\varphi(Ha)=\varphi(H)\varphi(a)$.
\end{theorem}

The proof boils down to the same steps as the previous one.

<<Theorem: $H\subgroup G, a\in G$, $\varphi(Ha)=\varphi(H)\varphi(a)$>>=
theorem Th48:
  for G1,G2 being Group
  for H being Subgroup of G1
  for a being Element of G1
  for f being Homomorphism of G1,G2
  holds f.:(H * a) = (f .: H) * (f.a)
proof
  let G1,G2 be Group;
  let H be Subgroup of G1;
  let a be Element of G1;
  let f be Homomorphism of G1,G2;
  A1: dom f = the carrier of G1 by FUNCT_2:def 1;
  for y being object st y in f.:(H * a) holds y in (f.:H)*(f.a)
  proof
    let y be object;
    assume y in f .: (H * a);
    then consider x being object such that
    B1: x in the carrier of G1 & x in (H * a) and
    B2: y = f.x
    by A1,FUNCT_1:def 6;
    consider h being Element of G1 such that
    B3: x = h*a & h in H
    by B1,GROUP_2:104;
    dom f = the carrier of G1 & h in H & h in G1 by FUNCT_2:def 1, B3;
    then f.h in f.:(the carrier of H) by FUNCT_1:def 6;
    then f.h in f.:H by GRSOLV_1:8;
    then (f.h)*(f.a) in (f.:H)*(f.a) by GROUP_2:104;
    hence thesis by B2,B3,GROUP_6:def 6;
  end;
  then A1: f.:(H * a) c= (f .: H) * (f.a);
  for y being object st y in (f.:H)*(f.a) holds y in f.:(H * a)
  proof
    let y be object;
    assume y in (f.:H)*(f.a);
    then consider g being Element of G2 such that
    B1: y = g*(f.a) and
    B2: g in (f.:H)
    by GROUP_2:104;
    g in Image(f|H) by B2,GRSOLV_1:def 3;
    then consider x being Element of H such that
    B3: g = (f|H).x
    by GROUP_6:45;
    B4: x in H & x is Element of G1 by GROUP_2:42;
    reconsider x as Element of G1 by GROUP_2:42;
    B5: y = g*(f.a) by B1
         .= (f.x)*(f.a) by B3,B4,Th1
         .= f.(x*a) by GROUP_6:def 6;
    x*a in the carrier of G1 & dom f = the carrier of G1 by FUNCT_2:def 1;
    then (x*a) in dom f & (x*a) in H*a & y=f.(x*a) by B4,B5,GROUP_2:104;
    hence y in f.:(H * a) by FUNCT_1:def 6;
  end;
  then A2: (f .: H) * (f.a) c= f.:(H * a);
  thus f.:(H * a) = (f .: H)*(f.a) by A1,A2,XBOOLE_0:def 10;
end;
@ %def Th48

\begin{theorem}\label{thm:characteristic:automorphisms-preserve-normal-subgroups}
If $N\normalSubgroup G$, then given any automorphism $\varphi\in\Aut(G)$
of $G$ we have our automorphism map $N$ to another normal subgroup
$\varphi(N)\normalSubgroup G$.
\end{theorem}

\begin{proof}[Proof sketch]
We recall $gN=Ng$ for any $g\in G$ and normal subgroup $N\normalSubgroup
G$.
Then
\begin{subequations}
  \begin{align}
    \varphi(g)\varphi(N) &= \varphi(gN)\\
    &=\varphi(Ng)\\
    &=\varphi(N)\varphi(g).\qedhere
  \end{align}
\end{subequations}
\end{proof}

<<Theorem: $N\normalSubgroup G$, $\varphi\in\Aut(G)$ implies $\varphi(N)\normalSubgroup G$>>=
theorem Th49:
  for G being Group
  for N being strict normal Subgroup of G
  for phi being Automorphism of G
  holds Image(phi|N) is normal Subgroup of G
proof
  let G be Group;
  let N be strict normal Subgroup of G;
  let phi be Automorphism of G;
  set H = Image(phi|N);
  for g being Element of G holds g * H = H * g
  proof
    let g be Element of G;
    set f = (phi").g;
    B1: phi.f = g by Th4;
    B2: phi .: (f * N) = (phi.f) * (phi .: N) by Th47
                      .= g * H by B1,GRSOLV_1:def 3;
    phi .: (N * f) = (phi .: N)*(phi.f) by Th48
                  .= (phi .: N)*g by Th4
                  .= H*g by GRSOLV_1:def 3;
    hence g * H = H * g by B2,GROUP_3:117;
  end;
  hence H is normal Subgroup of G by GROUP_3:117;
end;
@ %def Th49

\begin{theorem}
Let $H\subgroup G$. Then $H$ is characteristic if and only if for any
automorphism $\varphi\in\Aut(G)$ and every $x\in H$ we have
$\varphi(x)\in H$.
\end{theorem}

\begin{thm-remark}
We need to have $H$ be a strict subgroup since the definition of a
characteristic subgroup requires $\forall\varphi\in\Aut(G),\varphi(H)=H$.
Without strictness, we cannot have subgroup equality.
\end{thm-remark}

\begin{proof}[Proof sketch.]
There are two key moments to this proof:
\begin{enumerate}
\item $H$ is characteristic implies $\forall\varphi\in\Aut(G),\forall x\in H,\varphi(x)\in H$;
\item $\forall\varphi\in\Aut(G),\forall x\in H,\varphi(x)\in H$ implies $H$ is characteristic.
\end{enumerate}
The result follows immediately.
\end{proof}

<<Theorem: $H\subgroup G$ characteristic $\iff\forall\varphi\in\Aut(G)\forall x\in H,\varphi(x)\in H$>>=
theorem Th50:
  for G being Group
  for H being strict Subgroup of G
  holds H is characteristic iff
        (for phi being Automorphism of G
         for x being Element of G
         st x in H
         holds phi.x in H)
proof
  let G be Group;
  let H be strict Subgroup of G;
  thus H is characteristic implies (for phi being Automorphism of G
                                    for x being Element of G
                                    st x in H
                                    holds phi.x in H)
  proof
    assume B1: H is characteristic;
    let phi be Automorphism of G;
    let x be Element of G;
    assume B2: x in H;
    B3: H = Image(phi|H) by B1
         .= phi .: H by GRSOLV_1:def 3;
    dom phi = the carrier of G by FUNCT_2:def 1;
    then phi.x in phi .: (the carrier of H) by B2,FUNCT_1:def 6;
    hence thesis by B3,GRSOLV_1:8;
  end;
  thus (for phi being Automorphism of G
        for x being Element of G
        st x in H
        holds phi.x in H)
       implies H is characteristic
  proof
    assume B1: for phi being Automorphism of G for x being Element of G st x in H holds phi.x in H;
    for phi being Automorphism of G holds Image(phi|H) is Subgroup of H
    proof
      let phi be Automorphism of G;
      for x being Element of H holds phi.x in H
      proof
        let x be Element of H;
        reconsider g=x as Element of G by GROUP_2:42;
        g in H;
        hence thesis by B1;
      end;
      hence Image(phi|H) is Subgroup of H by Th44;
    end;
    hence H is characteristic by Th40;
  end;
end;
@ %def Th50

\begin{theorem}
If $H\subgroup G$ and $K\subgroup G$ are strict characteristic subgroups,
then $H\cap K$ is a characteristic subgroup.
\end{theorem}

\begin{thm-remark}
Although we don't use this result in this article, it is important in
other settings.
\end{thm-remark}

\begin{proof}[Proof sketch]
For any $x\in H\cap K$ and automorphism $\varphi\in\Aut(G)$, we have
$\varphi(x)\in H$ and $\varphi(x)\in K$, hence $\varphi(x)\in H\cap K$.
Since $x$ was arbitrary, this establishes $\varphi(H\cap K)\subgroup H\cap K$,
which implies $H\cap K$ is characteristic.
\end{proof}

<<Theorem: $H,K\subgroup G$ characteristic implies $H\cap K$ characteristic>>=
theorem Th51:
  for G being Group
  for H,K being strict characteristic Subgroup of G
  holds H /\ K is characteristic Subgroup of G
proof
  let G be Group;
  let H,K be strict characteristic Subgroup of G;
  for phi being Automorphism of G
  for x being Element of G st x in H /\ K
  holds phi.x in H /\ K
  proof
    let phi be Automorphism of G;
    let x be Element of G;
    assume x in H /\ K;
    then B1: x in H & x in K by GROUP_2:82;
    then B2: phi.x in H by Th50;
    phi.x in K by B1,Th50;
    hence phi.x in H /\ K by B2, GROUP_2:82;
  end;
  hence H /\ K is characteristic Subgroup of G by Th50;
end;
@ %def Th51

\begin{theorem}
  If $H\subgroup G$ and $K\subgroup G$ are characteristic subgroups,
  then $\langle H, K\rangle$ is a characteristic subgroup of $G$.
\end{theorem}

\begin{thm-remark}
More generally, if $\{K_{i}\}_{i\in I}$ is any family of characteristic subgroups
of $G$, then their join $\langle K_{i}\rangle_{i\in I}$.
\end{thm-remark}

\begin{proof}[Proof sketch]
This amounts to showing the product of subsets $U(H)U(K)$ is stable
under automorphisms of $G$, then it generates a characteristic subgroup
of $G$.
\end{proof}

<<Theorem: $H,K\subgroup G$ characteristic implies $\langle H, K\rangle$ is characteristic>>=
theorem Th52:
  for G being Group
  for H,K being strict characteristic Subgroup of G
  holds H "\/" K is characteristic Subgroup of G
proof
  let G be Group;
  let H,K be strict characteristic Subgroup of G;

  for phi being Automorphism of G
  for g being Element of G st g in H "\/" K
  holds phi.g in H "\/" K
  proof
    let phi be Automorphism of G;
    let g be Element of G;
    assume g in H "\/" K;
    then g in H*K by GROUP_4:53;
    then consider h, k being Element of G such that
    B1: g = h*k and
    B2: h in carr H and
    B3: k in carr K;
    h in H by B2;
    then B4: phi.h in H by Th50;
    k in K by B3;
    then phi.k in K by Th50;
    then phi.h * phi.k in carr H*carr K by B4;
    then phi.g in H*K by B1,GROUP_6:def 6;
    hence phi.g in H "\/" K by GROUP_4:53;
  end;

  hence H "\/" K is characteristic Subgroup of G by Th50;
end;
@ %def Th52

\begin{theorem}
If $H\subgroup G$ and $K\subgroup G$ are characteristic, then the set of
commutators $\{[h,k]\in G\mid h\in H, k\in K\}$ is invariant under
automorphisms of $G$.
\end{theorem}

<<Theorem: $H,K\subgroup G$ characteristic implies ${\rm Commutators}(H,K)$ is stable>>=
theorem Th53:
  for G being Group
  for H,K being strict characteristic Subgroup of G
  for phi being Automorphism of G
  holds phi .: commutators(H,K) = commutators(H,K)
proof
  let G be Group;
  let H,K be strict characteristic Subgroup of G;
  let phi be Automorphism of G;
  A1: dom phi = the carrier of G by FUNCT_2:def 1;

  for x being object st x in commutators(H,K) holds x in phi .: commutators(H,K)
  proof
    let x be object;
    assume B0: x in commutators(H,K);
    then reconsider g=x as Element of G;
    consider h,k being Element of G such that
    B1: x = [.h,k.] and
    B2: h in H & k in K by B0,GROUP_5:52;
    reconsider psi = phi" as Automorphism of G by GROUP_6:62;
    set a = psi.h;
    set b = psi.k;
    B3: a in H & b in K by B2,Th50;
    B4: psi.x = psi.([.h,k.]) by B1
             .= [.psi.h,psi.k.] by GROUP_6:34
             .= [.a,b.];
    B5: phi.([. a,b .]) = phi.(psi.x) by B4
                       .= g by Th4;
    [. a, b .] in commutators(H,K) by B3;
    hence x in phi .: commutators(H,K) by B5,A1,FUNCT_1:def 6;
  end;
  then A2: commutators(H,K) c= phi .: commutators(H,K);

  for y being object st y in phi .: commutators(H,K) holds y in commutators(H,K)
  proof
    let y be object;
    assume y in phi .: commutators(H,K);
    then consider x being object such that
    B2: x in dom phi & x in commutators(H,K) & y = phi.x
    by FUNCT_1:def 6;
    consider h,k being Element of G such that
    B3: x = [.h,k.] and
    B4: h in H & k in K by B2,GROUP_5:52;
    B5: phi.h in H & phi.k in K by B4,Th50;
    phi.x = phi.([. h,k .]) by B3
         .= [. phi.h, phi.k .] by GROUP_6:34;
    hence y in commutators(H,K) by B2,B5;
  end;
  then phi .: commutators(H,K) c= commutators(H,K);
  hence phi .: commutators(H,K) = commutators(H,K) by A2,XBOOLE_0:def 10;
end;
@ %def Th53

\begin{theorem}
If $H\subgroup G$ and $K\subgroup G$ are characteristic, then the commutator
subgroup $[H,K]$ is a characteristic subgroup.
\end{theorem}

\begin{proof}[Proof sketch]
We use the fact $\varphi([H,K]) = [\varphi(H),\varphi(K)]$, then since
$H$ and $K$ are characteristic the result follows immediately.
\end{proof}

<<Theorem: $H,K\subgroup G$ characteristic implies $[H,K]$ is characteristic>>=
theorem Th54:
  for G being Group
  for H,K being strict characteristic Subgroup of G
  holds [.H,K.] is characteristic Subgroup of G
proof
  let G be Group;
  let H,K be strict characteristic Subgroup of G;
  set A = commutators(H,K);
  reconsider A as non empty Subset of G by GROUP_5:53;
  for phi being Automorphism of G holds phi .: A = A by Th53;
  hence [.H,K.] is characteristic Subgroup of G by Th45;
end;
@ %def Th54
