% -*- mode: poly-noweb; noweb-code-mode: mizar-mode; -*-
\subsection{Automorphisms}

\M Now, we have a section for defining inner and outer group
automorphisms. A \emph{group automorphism} is just a bijective endomorphism on
a group, and an \emph{endomorphism} is a group morphism whose codomain
is its domain.

Remember (\S\ref{par:characteristic:vocabularies}), although we are
defining new terms [[Endomorphism]] and [[Automorphism]], we do not need
to add them to our [[DICT/CHAR.VOC]] file. Why not? Because the tokens
are already included from [[MOD_4]].

<<Inner and outer automorphisms>>=
begin

<<Reserve symbols for inner and outer automorphisms>>
<<Define [[Endomorphism]]>>
<<Register [[bijective]] for [[Endomorphism]]>>
<<Define [[Automorphism]]>>
<<Theorem: Endomorphisms preserve the trivial subgroup>>
<<Theorem: Automorphisms map trivial subgroups to themselves>>
<<Theorem: for $\varphi\in\Aut(G)$ and $H\subgroup G$, we have $\ker(\varphi|H)\subgroup\ker(\varphi)$>>
<<Lemma: for any $\varphi\in\Aut(G)$ and $H\subgroup G$ we have monomorphism $\varphi|_{H}$>>
<<Theorem: for any $\varphi\in\Aut(G)$ and $H\subgroup G$ we have $H\iso\varphi(H)$>>
<<Define $\Id_{G}$>>
<<Theorem: [[Id_G]] is the same as [[id the carrier of G]]>>
<<Register [[Id_G]] is bijective>>
<<Theorem: $\varphi\in\Aut(G)$ and $H\subgroup G$ such that $\varphi(H)=H$ implies $\varphi|H\in\Aut(H)$>>
<<Results and definition for Inner automorphisms>>

@

\M Note we just used the symbol [[G]] for a generic group, so we add to
the reserved symbols this association.

<<Reserve symbols for inner and outer automorphisms>>=
reserve G for Group;

@

\M\index{Endomorphism} A group endomorphism of $G$ is just a homomorphism $f\colon G\to G$.

<<Define [[Endomorphism]]>>=
definition :: Def1
  let G;
  mode Endomorphism of G is Homomorphism of G,G;
end;

@ %def Endomorphism

\M\label{par:characteristic:register-bijective-for-endomorphism}
We begin by registering the attribute [[bijective]] for group
endomorphisms. This will effectively create a subtype of
[[Endomorphism of G]], the aptly named [[bijective Endomorphism of G]].
Most of our work has been done in article [[GROUP_6]] by
Theorem~38 (which effectively states the function on the underlying set
[[id (the carrier of G)]] is a multiplicative function and so nearly a
group morphism that we can reconsider it as an [[Endomorphism]]), and
the fact that [[id X]] is bijective.

<<Register [[bijective]] for [[Endomorphism]]>>=
registration
  let G;
  cluster bijective for Endomorphism of G;
  existence
  proof
    reconsider i = id the carrier of G as Homomorphism of G,G by GROUP_6:38;
    i is bijective;
    hence thesis;
  end;
end;

@

\N{Definition (Automorphism)}\index{Automorphism}
Now we have an [[Automorphism of G]] be just a bijective endomorphism.

<<Define [[Automorphism]]>>=
definition :: Def2
  let G;
  mode Automorphism of G is bijective Endomorphism of G;
end;

@ %def Automorphism

\begin{theorem}
For any automorphism $\varphi\colon G\to G$, its inverse $\varphi^{-1}$
is also an automorphism.
\end{theorem}

This is proven in \verb#GROUP_6:62#. We have, for example, the following
accepted by Mizar:

\begin{mizar}
for G being Group
for phi being Automorphism of G
holds phi" is Automorphism of G by GROUP_6:62;
\end{mizar}

\M For any group $G$ and endomorphism $f\in\End(G)$, we have the trivial
subgroup $\trivialSubgroup\subgroup G$ be preserved under $f$; i.e.,
$f(\trivialSubgroup)=\trivialSubgroup$.

<<Theorem: Endomorphisms preserve the trivial subgroup>>=
theorem Th1:
  Image (f|(1).G) = (1).G
proof
  Image(f|(1).G) = f .: ((1).G) by GRSOLV_1:def 3
                .= (1).G by GRSOLV_1:11;
  hence thesis;
end;

@ %def Th1

\M Now we need to tell Mizar that $f$ is an endomorphism of $G$.

<<Define [[Endomorphism]]>>=
reserve f for Endomorphism of G;

@

\M If $\phi$ is an automorphism of a group $G$, then
the image of the trivial subgroup under $\phi$ is a subgroup of itself
$\phi(\trivialSubgroup)\subgroup\trivialSubgroup$. We have, from
Theorem~[[Th1]], that $\phi(\trivialSubgroup)=\trivialSubgroup$ since
$\phi$ (being an automorphism) is also an endomorphism. And [[GROUP_2]]
proves that $G$ is a subgroup of itself in Theorem~[[Th54]].

<<Theorem: Automorphisms map trivial subgroups to themselves>>=
:: In particular, the trivial proper subgroup (1).G of G is invariant
:: under inner automorphisms, and thus is a characteristic subgroup.
theorem Th2:
  for phi being Automorphism of G
  holds Image(phi|(1).G) is Subgroup of (1).G
proof
  let phi be Automorphism of G;
  (1).G is Subgroup of (1).G by GROUP_2:54;
  hence Image(phi|(1).G) is Subgroup of (1).G by Th1;
end;

@ %def Th2

<<Lemma: $H\subgroup G$ and $\trivialSubgroup\subgroup H$ implies $H=\trivialSubgroup$>>=
Lm8: for H being Subgroup of G st H is Subgroup of (1).G holds the
multMagma of H = the multMagma of (1).G
proof
  let H be Subgroup of G;
  assume A1: H is Subgroup of (1).G;
  then reconsider H as Subgroup of (1).G;
  (1).G is Subgroup of H by GROUP_2:65;
  then H is Subgroup of (1).G & (1).G is Subgroup of H by A1;
  hence thesis by GROUP_2:55;
end;

@ %def Lm8

<<Lemma: for any $\varphi\in\Aut(G)$ and $H\subgroup G$ we have monomorphism $\varphi|_{H}$>>=
<<Lemma: $H\subgroup G$ and $\trivialSubgroup\subgroup H$ implies $H=\trivialSubgroup$>>
Lm9:
for H being Subgroup of G
for phi being Automorphism of G
holds (phi|H) is Homomorphism of H,Image(phi|H) & (phi|H) is one-to-one
proof
  let H be Subgroup of G;
  let phi be Automorphism of G;
::  thus (phi|H) is one-to-one Homomorphism of H,Image(phi|H);
  thus phi|H is Homomorphism of H,Image(phi|H) by GRSOLV_1:def 2, GRSOLV_1:def 3, GROUP_6:49;
  phi is one-to-one;
  then Ker(phi) = (1).G by GROUP_6:56;
  then Ker(phi|H) is Subgroup of (1).G by Th9;
  then Ker(phi|H) = (1).G by Lm8
                 .= (1).H by GROUP_2:63;
  then (phi|H) is one-to-one by GROUP_6:56;
  hence thesis;
end;

@ %def Lm9

\begin{theorem}
For any automorphism $\varphi\in\Aut(G)$, its inverse is also an
automorphism $\varphi^{-1}\in\Aut(G)$.
\end{theorem}

<<Theorem: if $\varphi\in\Aut(G)$, then $\varphi^{-1}\in\Aut(G)$>>=
theorem Th3:
  for f being Automorphism of G
  holds f" is Automorphism of G
proof
  let f be Automorphism of G;
  set UG = the carrier of G;
  A1: f is bijective;
  then A2: f" is Function of UG,UG & f" is bijective by GROUP_6:63;
  for a,b being Element of G holds (f").(a * b) = (f").a * (f").b
  <<Proof: $\varphi^{-1}$ is multiplicative>>
  then f" is multiplicative by GROUP_6:def 6;
  then f" is Automorphism of G by A2;
  hence thesis;
end;

@ %def Th3

<<Proof: $\varphi^{-1}$ is multiplicative>>=
proof
  let a,b be Element of G;
  consider x1 being Element of G such that
  B1: a = f.x1
  by GROUP_6:58,A1;
  consider x2 being Element of G such that
  B2: b = f.x2
  by GROUP_6:58,A1;
  B3: f".a = x1 & f".b = x2 by B1,B2,FUNCT_2:26;
  (f").(a*b) = (f").((f.x1)*(f.x2)) by B1,B2
            .= (f").(f.(x1 * x2)) by A1,FUNCT_2:26,GROUP_6:def 6
            .= x1*x2 by A1,FUNCT_2:26
            .= (f").a * (f").b by B3;
  then (f").(a * b) = (f").a * (f").b;
  hence thesis;
end;

@ 

\begin{theorem}
Let $G$ be a group, $H\subgroup G$ be any subgroup, and
$\varphi\in\Aut(G)$ be any automorphism. Then the image of $H$ under
$\varphi$ is isomorphic to $H$ itself, i.e., $H\iso\varphi(H)$.
\end{theorem}

<<Theorem: for any $\varphi\in\Aut(G)$ and $H\subgroup G$ we have $H\iso\varphi(H)$>>=
theorem Th8:
  for H being Subgroup of G
  for phi being Automorphism of G
  holds H,phi .: H are_isomorphic
proof
  let H be Subgroup of G;
  let phi be Automorphism of G;
  reconsider H2 = phi .: H as Subgroup of G;
  Image(phi|H) = H2 by GRSOLV_1:def 3;
  phi|H is one-to-one by Lm9;
  then H,Image(phi|H) are_isomorphic by GROUP_6:68;
  then H,phi .: H are_isomorphic by GRSOLV_1:def 3;
  hence thesis;
end;

@ %def Th8

\begin{theorem}
Let $G$ be a group, $H\subgroup G$ any subgroup, and $\varphi\in\aut(G)$
any automorphism.
Then $\ker(\varphi|H)\subgroup\ker(\varphi)$.
\end{theorem}

<<Theorem: for $\varphi\in\Aut(G)$ and $H\subgroup G$, we have $\ker(\varphi|H)\subgroup\ker(\varphi)$>>=
theorem Th9:
  for G1,G2 being Group
  for f being Homomorphism of G1,G2
  for H being Subgroup of G1
  holds Ker(f|H) is Subgroup of Ker(f)
proof
  let G1,G2 be Group;
  let f be Homomorphism of G1,G2;
  let H be Subgroup of G1;
  A1: Ker(f|H) is Subgroup of G1 by GROUP_2:56;
  Ker(f) is Subgroup of G1;
  for g being Element of G1
  st g in Ker(f|H)
  holds g in Ker(f)
  <<Proof: $\forall g\in G, g\in\ker(\varphi|H)\implies g\in\ker(H)$>>
  then Ker(f|H) is Subgroup of Ker(f) by A1,GROUP_2:58;
  hence thesis;
end;

@ %def Th9

<<Proof: $\forall g\in G, g\in\ker(\varphi|H)\implies g\in\ker(H)$>>=
proof
  let g be Element of G1;
  assume A2: g in Ker(f|H);
  Ker(f|H) is Subgroup of H;
  then A3: g in H by A2, LmSubgroupEltTrans;
  then A4: (f|H).g = f.g by Lm3;
  g is Element of H by A3, LmSubgroupElt;
  then 1_G2 = (f|H).g by A2,GROUP_6:41
           .= f.g by A4;
  then g in Ker(f) by GROUP_6:41;
  hence thesis;
end;

@

\N{Identity Group Endomorphism}\index{$\Id_{G}$}\index{Endomorphism!Identity}
I found it tedious to write [[id (the carrier of G)]] over and over
again, so I wrote a shortcut [[Id_G]] for $\Id_{G}$.

<<Define $\Id_{G}$>>=
definition :: Def3
  let G;
  func Id_G -> Endomorphism of G means
  :Def3:
  for x being Element of G holds it.x=x;
  existence
  <<Proof that $\Id_{G}$ exists>>
  uniqueness
  <<Proof $\Id_{G}$ is unique>>
end;

@ %def Id_G Id_ Def3

\M We need to add [[Id_]] to our vocabulary.

<<DICT/CHAR.VOC>>=
OId_

@

\N{Proof ($\Id_{G}$ exists)}
Similar to the proof (\S\ref{par:characteristic:register-bijective-for-endomorphism})
that there exists a bijective endomorphism, the proof that $\Id_{G}$
exists amounts to reconsidering [[id]] as an endomorphism. Earlier work
in [[FUNCT_1]] proved (in Theorem~17) the identity function satisfies
$\forall x\in X, \id_{X}(x)=x$.

<<Proof that $\Id_{G}$ exists>>=
proof
  reconsider i = id (the carrier of G) as Homomorphism of G,G by GROUP_6:38;
  for x being Element of G holds i.x = x by FUNCT_1:17;
  hence thesis;
end;

@

\N{Proof (Uniqueness of $\Id_{G}$)}
The usual strategy is to consider two arbitrary endomorphisms
$\Id^{(1)}_{G}$ and $\Id^{(2)}_{G}$ satisfying the definition of
[[Id_]], then prove $\Id^{(1)}_{G} = \Id^{(2)}_{G}$.

If we wanted to be completely pedantic, we could cite Theorem~12 from
[[FUNCT_2]] which states for any functions of sets
$f_{1}$, $f_{2}\colon X\to Y$ we have $\forall x\in X, f_{1}(x)=f_{2}(x)$
implies $f_{1}=f_{2}$.

<<Proof $\Id_{G}$ is unique>>=
proof
  let Id1, Id2 be Endomorphism of G such that
  A1: for x being Element of G holds Id1.x=x and
  A2: for x being Element of G holds Id2.x=x;
  let x be Element of G;
  thus Id1.x = x by A1
            .= Id2.x by A2;
  thus thesis;
end;

@

\M\label{thm:characteristic:identity-endomorphism-is-identity-function}
We should now have some sanity check that [[Id_G]] really is
the identity group morphism $\Id_{G}$ that \emph{we} think it is.

<<Theorem: [[Id_G]] is the same as [[id the carrier of G]]>>=
theorem Th10:
  Id_G = id (the carrier of G)
proof
  let x be Element of G;
  thus (Id_G).x = x by Def3
               .= (id (the carrier of G)).x;
  thus thesis;
end;

@ %def Th10

\M
Now we should automatically associate $\Id_{G}$ is bijective. This
requires proving a couple of helper lemmas establishing injectivity and
surjectivity.

<<Register [[Id_G]] is bijective>>=
<<[[Id_G]] is injective>>
<<[[Id_G]] is surjective>>

registration let G;
  cluster Id_G -> bijective;
  coherence by Lm1,Lm2;
end;

@

\begin{lemma}[Identity is injective]
For any group $G$, the identity morphism $\Id_{G}$ is injective.
\end{lemma}

The proof is straightforward since we've established (\S\ref{thm:characteristic:identity-endomorphism-is-identity-function}) the group
endomorphism $\Id_{G}$ coincides with the set-theoretic function $\id_{U(G)}$
on the underlying set $U(G)$ of the group $G$.

<<[[Id_G]] is injective>>=
Lm1: Id_G is one-to-one
proof
  id (the carrier of G) is one-to-one;
  hence thesis by Th10;
end;

@ %def Lm1

\begin{lemma}[Identity is surjective]
For any group $G$, the identity morphism $\Id_{G}$ is surjective.
\end{lemma}

As with establishing injectivity, it's straightforward.

<<[[Id_G]] is surjective>>=
Lm2: Id_G is onto
proof
  id (the carrier of G) is onto;
  hence thesis by Th10;
end;

@ %def Lm2

\begin{theorem}
  Let $H\subgroup G$ be any subgroup.
  If $\varphi\in\Aut(G)$ is an automorphism such that $\varphi(H)=H$ it
  leaves $H$ invariant, then its restriction to $H$ is an automorphism
  $\varphi|_{H}\in\Aut(H)$.
\end{theorem}

This result isn't surprising, but proving surjectivity was surprisingly
(and agonizingly) hard.

<<Theorem: $\varphi\in\Aut(G)$ and $H\subgroup G$ such that $\varphi(H)=H$ implies $\varphi|H\in\Aut(H)$>>=
theorem Th11:
  for H being strict Subgroup of G
  for f being Automorphism of G
  st Image(f|H) = H
  holds f|H is Automorphism of H
proof
  let H be strict Subgroup of G;
  let f be Automorphism of G;
  assume A1: Image(f|H) = H;
  then C1: f|H is Homomorphism of H,H by GROUP_6:49;
  C2: f|H is one-to-one by A1, Lm9;

  C3: f|H is onto Endomorphism of H
  <<Proof: $\varphi|_{H}$ is surjective>>
  then f|H is bijective Homomorphism of H,H by C2;
  then f|H is Automorphism of H;
  hence thesis;
end;

@ %def Th11

\N{Proof of surjectivity}
For reasons I do not adequately understand, I could not simply handle
[[f|H]] as a [[Homomorphism of H,H]] (despite having established this
fact).

I could not use any theorem concerning surjectivity of group morphisms,
so I just ``forgot'' it was a morphism, proved surjectivity, then
``remembered'' it was a group morphism after all. I am starting to think
that the claim [[f is onto]] is different than [[f is onto Homomorphism of G1,G2]].

<<Proof: $\varphi|_{H}$ is surjective>>=
proof
  set UH = the carrier of H;
  B1: f|H is Function of UH,UH by C1;
  rng(f|H) = the carrier of H by GROUP_6:44, A1;
  then f|H is onto Function of UH,UH by B1,FUNCT_2:def 3;
  then f|H is onto Homomorphism of H,H by C1;
  hence thesis;
end;

@
