% -*- mode: poly-noweb; noweb-code-mode: mizar-mode; -*-
\section{Automorphisms}
Now, we have a section for defining inner and outer group
automorphisms. A \emph{group automorphism} is just a bijective endomorphism on
a group, and an \emph{endomorphism} is a group morphism whose codomain
is its domain.

Remember (\S\ref{par:characteristic:vocabularies}), although we are
defining new terms [[Endomorphism]] and [[Automorphism]], we do not need
to add them to our [[DICT/CHAR.VOC]] file. Why not? Because the tokens
are already included from \mml{mod4}.

<<Inner and outer automorphisms>>=
begin :: Automorphisms

<<Reserve symbols for inner and outer automorphisms>>
<<Define [[Endomorphism]]>>
<<Register [[bijective]] for [[Endomorphism]]>>
<<Define [[Automorphism]]>>
<<Theorem: Endomorphisms preserve the trivial subgroup>>
<<Theorem: Automorphisms map trivial subgroups to themselves>>
<<Theorem: for $\varphi\in\Aut(G)$ and $H\subgroup G$, we have $\ker(\varphi|H)\subgroup\ker(\varphi)$>>
<<Lemma: for any $\varphi\in\Aut(G)$ and $H\subgroup G$ we have monomorphism $\varphi|_{H}$>>
<<Theorem: $(\forall\varphi\in\Aut(G),\varphi(H)\subgroup H)\implies\varphi[\varphi^{-1}(H)]\subgroup \varphi(H)$>>
<<Theorem: $\forall\varphi\in\Aut(G), \varphi[\varphi^{-1}(H)]=H$>>
<<Theorem: $\varphi(H)\subgroup K\implies H\subgroup\varphi^{-1}(K)$>>
<<Theorem: for any $\varphi\in\Aut(G)$ and $H\subgroup G$ we have $H\iso\varphi(H)$>>
<<Define $\Id_{G}$>>
<<Theorem: [[Id_G]] is the same as [[id the carrier of G]]>>
<<Register [[Id_G]] is bijective>>
<<Theorem: $\varphi\in\Aut(G)$ and $H\subgroup G$ such that $\varphi(H)=H$ implies $\varphi|H\in\Aut(H)$>>
<<Theorem: $\varphi\in\Aut(G)$ and $H\properSubgroup G$ implies $\varphi(H)\properSubgroup G$>>
<<Theorem: Automorphisms map maximal subgroups to maximal subgroups>>
<<Results and definition for Inner automorphisms>>

@

\begin{reserve}
Note we just used the symbol [[G]] for a generic group, so we add to
the reserved symbols this association. Most of our theorems will no
longer state ``Let $G$ be a group'', it will be implicitly assumed
unless otherwise stated.

Likewise, we reserve [[H]] for a subgroup of [[G]].
\end{reserve}

<<Reserve symbols for inner and outer automorphisms>>=
reserve G for Group;
reserve H for Subgroup of G;

@

\begin{definition}\index{Endomorphism|textbf}
Let $G$ be a group. An \define{Endomorphism} of $G$ is a group morphism
$f\colon G\to G$.
\end{definition}

\begin{def-remark}\index{$\End(G)$}
We denote the collection of endomorphisms of $G$ as $\End(G)$.
\end{def-remark}

\begin{def-remark}
Mizar uses the archaic word ``homomorphism'' instead of the more modern
conventional term ``morphism''. I will use the two interchangeably. And,
unless stated otherwise, it is understood these are group morphisms.
\end{def-remark}

<<Define [[Endomorphism]]>>=
definition :: Def1
  let G;
  mode Endomorphism of G is Homomorphism of G,G;
end;

@ %def Endomorphism

\begin{registration}\label{par:characteristic:register-bijective-for-endomorphism}
We begin by registering the attribute [[bijective]] for group
endomorphisms. This will effectively create a subtype of [[Endomorphism of G]],
the aptly named [[bijective Endomorphism of G]]. Most of our work has
been done in Theorem~\mml[Th38]{group6}\MizThm{GROUP\_6}{38}
(which effectively states the function on the underlying set
[[id (the carrier of G)]] is a multiplicative function and so nearly a
group morphism that we can reconsider it as an [[Endomorphism]]), and
the fact that [[id X]] is bijective.
\end{registration}

<<Register [[bijective]] for [[Endomorphism]]>>=
registration
  let G;
  cluster bijective for Endomorphism of G;
  existence
  proof
    reconsider i = id the carrier of G as Homomorphism of G,G by GROUP_6:38;
    i is bijective;
    hence thesis;
  end;
end;

@

\begin{definition}\index{Automorphism|textbf}\index{Endomorphism}
Let $G$ be a group. We define an \define{Automorphism} of $G$ to be a
bijective endomorphism $f\colon G\to G$. In particular, an inverse
$f^{-1}\colon G\to G$ exists and is a group morphism.
\end{definition}

\begin{def-remark}\index{$\Aut(G)$}
We denote the collection of automorphisms of $G$ as $\Aut(G)$.
\end{def-remark}

<<Define [[Automorphism]]>>=
definition :: Def2
  let G;
  mode Automorphism of G is bijective Endomorphism of G;
end;

@ %def Automorphism

\begin{reserve}
  We will henceforth generically use $\varphi$ as an Automorphism of $G$
  unless otherwise stated. This means, for most theorems, we can omit
  explicitly stating, ``For any automorphism $\varphi$ of $G$, \dots'';
  and for most proofs, we can omit the line, ``Let $\varphi$ be an
  Automorphism of $G$''.
\end{reserve}

<<Define [[Automorphism]]>>=
reserve phi for Automorphism of G;

@

\begin{proposition}
For any automorphism $\varphi\colon G\to G$, its inverse $\varphi^{-1}$
is also an automorphism.
\end{proposition}

This is proven in Theorem~\mml[Th62]{group6}.\MizThm{GROUP\_6}{62} We have, for
example, the following accepted by Mizar:

\begin{mizar}
for G being Group
for phi being Automorphism of G
holds phi" is Automorphism of G by GROUP_6:62;
\end{mizar}

\begin{theorem}\label{thm:characteristic:automorphism:trivial-subgroup-invariant-under-automorphisms}
For any group $G$ and endomorphism $f\in\End(G)$, we have the trivial
subgroup $\trivialSubgroup\subgroup G$ be preserved under $f$; i.e.,
$f(\trivialSubgroup)=\trivialSubgroup$.
\end{theorem}

The proof is simply ``follow your nose''.

\begin{proof}
\emph{Trivial}.
\end{proof}

<<Theorem: Endomorphisms preserve the trivial subgroup>>=
theorem Th1:
  Image (f|(1).G) = (1).G
proof
  Image(f|(1).G) = f .: ((1).G) by GRSOLV_1:def 3
                .= (1).G by GRSOLV_1:11;
  hence thesis;
end;

@ %def Th1

\begin{reserve}
Now we need to tell Mizar that $f$ is an endomorphism of $G$.
\end{reserve}


<<Define [[Endomorphism]]>>=
reserve f for Endomorphism of G;

@

\begin{theorem}
For any automorphism $\varphi\in\Aut(G)$, we have $\varphi(\trivialSubgroup_{G})=\trivialSubgroup_{G}$.
\end{theorem}

\begin{proof}[Proof outline]
If $\phi$ is an automorphism of a group $G$, then
the image of the trivial subgroup under $\phi$ is a subgroup of itself
$\phi(\trivialSubgroup)\subgroup\trivialSubgroup$. We have, from
Theorem~\ref{thm:characteristic:automorphism:trivial-subgroup-invariant-under-automorphisms}, that $\phi(\trivialSubgroup)=\trivialSubgroup$ since
$\phi$ (being an automorphism) is also an endomorphism. And
Theorem~\mml[Th54]{group2}\MizThm{GROUP\_2}{54}
proves that $G$ is a subgroup of itself.
\end{proof}

<<Theorem: Automorphisms map trivial subgroups to themselves>>=
:: In particular, the trivial proper subgroup (1).G of G is invariant
:: under inner automorphisms, and thus is a characteristic subgroup.
theorem Th2:
  Image(phi|(1).G) is Subgroup of (1).G
proof
  (1).G is Subgroup of (1).G by GROUP_2:54;
  hence Image(phi|(1).G) is Subgroup of (1).G by Th1;
end;

@ %def Th2

\begin{lemma}
If $H\subgroup\trivialSubgroup_{G}\subgroup G$, then $H=\trivialSubgroup$.
\end{lemma}

\begin{proof}
Assume $H\subgroup\trivialSubgroup_{G}$. We know
$\trivialSubgroup_{G}\subgroup H$, and taken together, the result follows.
\end{proof}

<<Lemma: $H\subgroup G$ and $\trivialSubgroup\subgroup H$ implies $H=\trivialSubgroup$>>=
Lm8: H is Subgroup of (1).G implies the multMagma of H = the multMagma of (1).G
proof
  assume H is Subgroup of (1).G;
  then H is Subgroup of (1).G & (1).G is Subgroup of H by GROUP_2:65;
  hence thesis by GROUP_2:55;
end;

@ %def Lm8

\begin{lemma}\label{lemma:characteristic:automorphism:restrict-automorphism-gives-monomorphism}
For any subgroup $H\subgroup G$ and automorphism $\varphi\in\Aut(G)$ of
$G$, we have $\varphi|_{H}\colon H\into\varphi(H)$ be an injective group morphism.
\end{lemma}

\begin{proof}[Proof sketch]
Given a subgroup $H\subgroup G$ and automorphism $\varphi\in\Aut(G)$, we
know $\varphi|_{H}\colon H\to \varphi|_{H}(H)$ is a group morphism.
Since $\varphi$ is an automorphism, it is injective, and in particular
$\ker(\varphi)=\trivialSubgroup_{G}$. Then
$\ker(\varphi|_{H})\subgroup\ker(\varphi)$ and
$\trivialSubgroup_{G}\subgroup\ker(\varphi|_{H})$ implies
$\ker(\varphi|_H)$ is trivial, and thus $\varphi|_{H}$ is injective.
\end{proof}

<<Lemma: for any $\varphi\in\Aut(G)$ and $H\subgroup G$ we have monomorphism $\varphi|_{H}$>>=
<<Lemma: $H\subgroup G$ and $\trivialSubgroup\subgroup H$ implies $H=\trivialSubgroup$>>
Lm9:
(phi|H) is Homomorphism of H,Image(phi|H) & (phi|H) is one-to-one
proof
  thus phi|H is Homomorphism of H,Image(phi|H) by GROUP_6:49;
  Ker(phi) = (1).G by GROUP_6:56;
  then Ker(phi|H) is Subgroup of (1).G by Th9;
  then Ker(phi|H) = (1).G by Lm8
                 .= (1).H by GROUP_2:63;
  hence (phi|H) is one-to-one by GROUP_6:56;
end;

@ %def Lm9

\begin{theorem}\label{thm:characteristic:automorphism:f-fInv-H-is-subgroup-of-H}
Let $H\subgroup G$ be a subgroup such that
$\forall\varphi\in\Aut(G),\varphi(H)\subgroup H$.
Then any automorphism $\varphi$ of $G$ has an inverse which satisfies
$\varphi[\varphi^{-1}(H)]\subgroup\varphi(H)$.
\end{theorem}

\begin{proof}
Let $\psi=\varphi^{-1}\in\Aut(G)$ be an automorphism (which follows from
Theorem~\mml[Th62]{group6}\MizThm{GROUP\_6}{62}). We have
$\psi(H)=\psi|_{H}(H)\subgroup H$ since the image of a morphism is a subgroup of
the codomain. Then $\phi(\psi(H))\subgroup\phi(H)$.
\end{proof}

<<Theorem: $(\forall\varphi\in\Aut(G),\varphi(H)\subgroup H)\implies\varphi[\varphi^{-1}(H)]\subgroup \varphi(H)$>>=
theorem Th6:
  (for f being Automorphism of G holds Image(f|H) is Subgroup of H) implies
  ex psi being Automorphism of G
  st psi = phi" & Image(phi|Image(psi|H)) is Subgroup of Image(phi|H)
proof
  assume A1: for f being Automorphism of G holds Image(f|H) is Subgroup of H;
  reconsider psi = phi" as Automorphism of G by GROUP_6:62;
  take psi;
  thus psi = phi";
  Image(psi|H) is Subgroup of H by A1;
  then phi .: Image(psi|H) is Subgroup of phi .: H by GRSOLV_1:12;
  then Image(phi|Image(psi|H)) is Subgroup of phi .: H by GRSOLV_1:def 3;
  hence Image(phi|Image(psi|H)) is Subgroup of Image(phi|H) by GRSOLV_1:def 3;
end;

@ %def Th6

\begin{theorem}\label{thm:characteristic:automorphism:phi-phiInv-H-is-H}
  Let $G$ be a group, and $H\subgroup G$ be a subgroup.
  Then for any automorphism $\varphi\in\Aut(G)$, we have
  $\varphi[\varphi^{-1}(H)]=H$.
\end{theorem}

This is an obvious result which is usually taken for granted.

\begin{proof}[Proof outline]
Let $\psi=\varphi^{-1}\in\Aut(G)$ (which follows from
Theorem~\mml[Th62]{group6}\MizThm{GROUP\_6}{62}). Then we establish any
group element $g$ is a member of $\varphi(\psi(H))$ if and only if $g$
is a member of $H$.
The result then follows that they are equal as groups using Theorem~\mml[Th60]{group2}\MizThm{GROUP\_2}{60}.
\end{proof}

<<Theorem: $\forall\varphi\in\Aut(G), \varphi[\varphi^{-1}(H)]=H$>>=
theorem Th7:
  ex psi being Automorphism of G
  st psi = phi" & the multMagma of Image(phi|Image(psi|H)) = the multMagma of H
proof
  reconsider psi = phi" as Automorphism of G by GROUP_6:62;
  take psi;
  thus psi = phi";
  for g being Element of G holds g in Image(phi|Image(psi|H)) iff g in H
  <<Proof: $\forall g\in G$, $g\in\varphi[\varphi^{-1}(H)]\iff g\in H$>>
  hence the multMagma of Image(phi|Image(psi|H)) = the multMagma of H by GROUP_2:60;
end;

@ %def Th7

\begin{proof}[{Sub-proof ($\forall g\in G, g\in\varphi[\varphi^{-1}(H)]\implies g\in H$)}.]
The bones of the proof for this claim amounts to unfolding the
logical structure of the claim.
\end{proof}

<<Proof: $\forall g\in G$, $g\in\varphi[\varphi^{-1}(H)]\iff g\in H$>>=
proof
  let g be Element of G;
  thus g in Image(phi|Image(psi|H)) implies g in H
  <<Step 1: $g\in\varphi[\varphi^{-1}(H)]\implies g\in H$>>

  thus g in H implies g in Image(phi|Image(psi|H))
  <<Step 2: $g\in\varphi[\varphi^{-1}(H)]\impliedby g\in H$>>
  thus thesis;
end;

@

\begin{proof}[{Proof step 1 ($g\in\varphi[\varphi^{-1}(H)]\implies g\in H$)}.]
We begin by showing, if $g\in\varphi[\varphi^{-1}(H)]$, then there is
some [[M1:]] $a\in\varphi^{-1}(H)$ such that [[B2:]] $g=\varphi(a)$. And
then we have, by the same line of reasoning applied to $a$, there is
some [[M2:]] $b\in H$ such that [[B3:]] $\varphi^{-1}|_{H}(b)=a$. It
follows that $g = \varphi(\varphi^{-1}(b))$, and by Lemma~\ref{lemma:characteristic:preparatory:f-finv-y-is-y} we have
$g = b$ which proves the claim.
\end{proof}

<<Step 1: $g\in\varphi[\varphi^{-1}(H)]\implies g\in H$>>=
proof
  assume g in Image(phi|Image(psi|H));
  <<$\exists a\in\varphi^{-1}(H),g=\varphi(a)$>>
  <<$\exists b\in H, a = \varphi^{-1}(b)$>>
  then b = phi.(psi.b) by Lm11
        .= phi.((psi|H).b) by M2,Lm3
        .= phi.a by B3
        .= g by B2;
  hence g in H;
end;

@

\begin{proof}[Proof sub-step 1]
Showing $a\in\varphi^{-1}(H)$ and $g=\varphi(a)$ follow from basic results.
\end{proof}

<<$\exists a\in\varphi^{-1}(H),g=\varphi(a)$>>=
then consider a being Element of Image(psi|H) such that
B1: g = (phi|Image(psi|H)).a by GROUP_6:45;
M1: a in Image(psi|H) & a is Element of G by GROUP_2:42;
then B2: phi.a = g by B1, Lm3;

@

\begin{proof}[Proof sub-step 2]
Almost the same reasoning applies to $b\in H$ satisfying
$a=\varphi^{-1}(b)$.
\end{proof}

<<$\exists b\in H, a = \varphi^{-1}(b)$>>=
consider b being Element of H such that
B3: a = (psi|H).b
by M1,GROUP_6:45;
M2: b in H & b is Element of G by GROUP_2:42;

@

\begin{proof}[{Proof step 2 ($g\in\varphi[\varphi^{-1}(H)]\impliedby g\in H$)}.]
To prove $g\in H$ implies $g\in\varphi[\varphi^{-1}(H)]$, we simply
let $K=\varphi^{-1}(H)$ and show $a = \varphi^{-1}|_{H}(g)\in K$ and
then show $b=\varphi|_{K}(a)\in\varphi[K]=\varphi[\varphi^{-1}(H)]$. Then
we have $b=\varphi(\varphi^{-1}(g))=g$ due to $\varphi$ being bijective.
The result follows.
\end{proof}

<<Step 2: $g\in\varphi[\varphi^{-1}(H)]\impliedby g\in H$>>=
proof
  assume B1: g in H;
  set a = (psi|H).g;
  B2: a in Image(psi|H)
  <<Sub-step 1: $a\in\varphi^{-1}(H)$>>

  set K = Image(psi|H);
  set b = (phi|Image(psi|H)).a;

  B3: b in Image(phi|Image(psi|H))
  <<Sub-step 2: $b\in\varphi[\varphi^{-1}(H)]$>>
  thus g in Image(phi|K)
  <<Sub-step 3: $g\in\varphi[\varphi^{-1}(H)]$>>
end;

@

\begin{proof}[Proof sub-step 1 ($a\in\varphi^{-1}(H)$).]
The first step follows by unfolding definitions.
\end{proof}


<<Sub-step 1: $a\in\varphi^{-1}(H)$>>=
proof
  dom(psi|H) = the carrier of H by FUNCT_2:def 1;
  then g in dom(psi|H) by B1;
  then (psi|H).g in (psi|H) .: (the carrier of H) by FUNCT_1:def 6;
  then (psi|H).g in the carrier of Image(psi|H) by GROUP_6:def 10;
  then (psi|H).g in Image(psi|H);
  hence a in Image(psi|H);
end;

@

\begin{proof}[{Proof sub-step 2 ($b\in\varphi[\varphi^{-1}(H)]$)}.]
The next step, like the first, follows from definitions.
\end{proof}

<<Sub-step 2: $b\in\varphi[\varphi^{-1}(H)]$>>=
proof
  dom(phi|K) = the carrier of K by FUNCT_2:def 1;
  then a in dom(phi|K) by B2;
  then (phi|K).a in (phi|K) .: (the carrier of K) by FUNCT_1:def 6;
  then (phi|K).a in the carrier of Image(phi|K) by GROUP_6:def 10;
  then (phi|K).a in Image(phi|K);
  hence b in Image(phi|K);
end;

@

\begin{proof}[{Proof sub-step 3 ($g\in\varphi[\varphi^{-1}(H)]$)}.]
The last step is a little more involved, because it requires
recalling $\varphi|_{k}(k)=\varphi(k)$ for $k\in K$, and
$\varphi^{-1}|_{H}(h)=\varphi^{-1}(h)$ for $h\in H$. We can then combine
these deductions to show $b=\varphi(\varphi^{-1}(g))$ which proves the claim.
\end{proof}

<<Sub-step 3: $g\in\varphi[\varphi^{-1}(H)]$>>=
proof
  H is Subgroup of G & g in H & g is Element of G by B1;
  then B4: psi.g = (psi|H).g by Lm3
                .= a;
  B5: a is Element of K & K is Subgroup of G & a in K & phi is
  Homomorphism of G,G by B2;
  then a is Element of G by GROUP_2:42;
  then B6: (phi|K).a =  phi.a  by B5,Lm3
                    .= phi.(psi.g) by B4
                    .= phi.((phi").g)
                    .= g by Lm11;
  b = (phi|K).((psi|H).g)
   .= g by B6;
  hence thesis by B3;
end;

@

\begin{theorem}
Let $H\subgroup G$ and $K\subgroup G$ be subgroups, let $\varphi\in\Aut(G)$
be an automorphism of G. If $\varphi(H)\subgroup K$, then $H\subgroup\varphi^{-1}(K)$.
\end{theorem}


\begin{proof}[Proof sketch]
The argument basically applies Theorem~\ref{thm:characteristic:automorphism:phi-phiInv-H-is-H} to $\varphi^{-1}$, then unfolds
definitions.
\end{proof}

\begin{thm-remark}
I need to work on my numbering scheme\dots
\end{thm-remark}

<<Theorem: $\varphi(H)\subgroup K\implies H\subgroup\varphi^{-1}(K)$>>=
theorem Th7b:
  for H being strict Subgroup of G
  for K being Subgroup of G
  st Image(phi|H) is Subgroup of K
  holds ex psi being Automorphism of G
  st psi = phi" & H is Subgroup of Image(psi|K)
proof
  let H be strict Subgroup of G;
  let K be Subgroup of G;
  assume A1: Image(phi|H) is Subgroup of K;
  reconsider psi = phi" as Automorphism of G by GROUP_6:62;
  take psi;
  thus psi = phi";
  A2: psi" = phi by Lm10;
  consider phi0 being Automorphism of G such that
  A3: phi0 = psi" and
  A4: the multMagma of Image(psi|Image(phi0|H)) = the multMagma of H
  by Th7;
  phi = phi0 by A2,A3;
  then A5: Image(psi|Image(phi|H)) = H by A4;
  psi .: Image(phi|H) is Subgroup of psi .: K by A1,GRSOLV_1:12;
  then Image(psi|Image(phi|H)) is Subgroup of psi .: K by GRSOLV_1:def 3;
  then Image(psi|Image(phi|H)) is Subgroup of Image(psi|K) by GRSOLV_1:def 3;
  hence H is Subgroup of Image(psi|K) by A5;
end;

@

\begin{theorem}
Let $G$ be a group, $H\subgroup G$ be any subgroup, and
$\varphi\in\Aut(G)$ be any automorphism. Then the image of $H$ under
$\varphi$ is isomorphic to $H$ itself, i.e., $H\iso\varphi(H)$.
\end{theorem}

\begin{proof}[Proof outline]
Let $H_{2}=\varphi(H)$ be a subgroup of $G$. We know $\varphi|_{H}$ is
injective by Lemma~\ref{lemma:characteristic:automorphism:restrict-automorphism-gives-monomorphism}. This gives us our result.
\end{proof}

<<Theorem: for any $\varphi\in\Aut(G)$ and $H\subgroup G$ we have $H\iso\varphi(H)$>>=
theorem Th8:
  H,phi .: H are_isomorphic
proof
  reconsider H2 = phi .: H as Subgroup of G;
  phi|H is one-to-one by Lm9;
  then H,Image(phi|H) are_isomorphic by GROUP_6:68;
  hence H,phi .: H are_isomorphic by GRSOLV_1:def 3;
end;

@ %def Th8

\begin{theorem}
Let $G$ be a group, $H\subgroup G$ any subgroup, and $\varphi\in\aut(G)$
any automorphism.
Then $\ker(\varphi|H)\subgroup\ker(\varphi)$.
\end{theorem}

\begin{proof}[Proof outline]
  We begin by recognizing $\ker(\varphi|_{H})\subgroup G_{1}$ and
  $\ker(\varphi)\subgroup G_{1}$. Then any $g\in G$ such that
  $g\in\ker(\varphi|_{H})$ is also a member of $\ker(\varphi)$. The
  result follows.
\end{proof}

<<Theorem: for $\varphi\in\Aut(G)$ and $H\subgroup G$, we have $\ker(\varphi|H)\subgroup\ker(\varphi)$>>=
theorem Th9:
  for G1,G2 being Group
  for f being Homomorphism of G1,G2
  for H being Subgroup of G1
  holds Ker(f|H) is Subgroup of Ker(f)
proof
  let G1,G2 be Group;
  let f be Homomorphism of G1,G2;
  let H be Subgroup of G1;
  A1: Ker(f|H) is Subgroup of G1 by GROUP_2:56;
  for g being Element of G1
  st g in Ker(f|H)
  holds g in Ker(f)
  <<Proof: $\forall g\in G, g\in\ker(\varphi|H)\implies g\in\ker(H)$>>
  then Ker(f|H) is Subgroup of Ker(f) by A1,GROUP_2:58;
  hence thesis;
end;

@ %def Th9

\begin{proof}[Sub-proof ($\forall g\in G, g\in\ker(\varphi|H)\implies g\in\ker(H)$).]
Any $g\in\ker(\varphi|_{H})$ is defined to be $\varphi|_{H}(g)=1_{G_{2}}$.
But $\varphi|_{H}(g)=\varphi(g)$ by Lemma~\ref{lemma:characteristic:preparatory:restrict-group-morphism-to-subgroups}. So we have
$\varphi(g)=1_{G_{2}}$, which implies $g\in\ker(\varphi)$ by Theorem~\mml[Th41]{group6}.\MizThm{GROUP\_6}{41}
\end{proof}

<<Proof: $\forall g\in G, g\in\ker(\varphi|H)\implies g\in\ker(H)$>>=
proof
  let g be Element of G1;
  assume A2: g in Ker(f|H);
  Ker(f|H) is Subgroup of H;
  then A3: g in H by A2, LmSubgroupEltTrans;
  then A4: (f|H).g = f.g by Lm3;
  g is Element of H by A3;
  then 1_G2 = f.g by A2,A4,GROUP_6:41;
  hence g in Ker(f) by GROUP_6:41;
end;

@
\begin{definition}\index{$\Id_{G}$}\index{Endomorphism!Identity}
Let $G$ be a group. The \define{Identity endomorphism} of $G$ is defined
to be $\Id_{G}\in\End(G)$ such that for any $x\in G$, $\Id_{G}(x)=x$.
\end{definition}

\begin{def-remark}
I found it tedious to write [[id (the carrier of G)]] over and over
again, so I wrote a shortcut [[Id_G]] for $\Id_{G}$.
\end{def-remark}

<<Define $\Id_{G}$>>=
definition :: Def3
  let G;
  func Id_G -> Endomorphism of G means
  :Def3:
  for x being Element of G holds it.x=x;
  existence
  <<Proof that $\Id_{G}$ exists>>
  uniqueness
  <<Proof $\Id_{G}$ is unique>>
end;

@ %def Id_G Id_ Def3

\begin{voc}
We need to add [[Id_]] to our vocabulary.
\end{voc}

<<DICT/CHAR.VOC>>=
OId_

@

\begin{proof}[Proof sketch (Existence of $\Id_{G}$)]
Similar to the proof of existence in Registration~\ref{par:characteristic:register-bijective-for-endomorphism}, the proof that $\Id_{G}$
exists amounts to reconsidering [[id]] as an endomorphism. Using Theorem~\mml[Th17]{funct1}\MizThm{FUNCT\_1}{17} the identity
function satisfies $\forall x\in X, \id_{X}(x)=x$.
\end{proof}


<<Proof that $\Id_{G}$ exists>>=
proof
  reconsider i = id (the carrier of G) as Homomorphism of G,G by GROUP_6:38;
  for x being Element of G holds i.x = x;
  hence thesis;
end;

@

\begin{proof}[Proof sketch (Uniqueness of $\Id_{G}$)]
The usual strategy is to consider two arbitrary endomorphisms
$\Id^{(1)}_{G}$ and $\Id^{(2)}_{G}$ satisfying the definition of
[[Id_]], then prove $\Id^{(1)}_{G} = \Id^{(2)}_{G}$.

If we wanted to be completely pedantic, we could cite Theorem~\mml[Th12]{funct2}%
\MizThm{FUNCT\_2}{12} which states for any functions of sets
$f_{1}$, $f_{2}\colon X\to Y$ we have $\forall x\in X, f_{1}(x)=f_{2}(x)$
implies $f_{1}=f_{2}$.
\end{proof}

<<Proof $\Id_{G}$ is unique>>=
proof
  let Id1, Id2 be Endomorphism of G such that
  A1: for x being Element of G holds Id1.x=x and
  A2: for x being Element of G holds Id2.x=x;
  let x be Element of G;
  thus Id1.x = x by A1
            .= Id2.x by A2;
  thus thesis;
end;

@

\begin{theorem}\label{thm:characteristic:identity-endomorphism-is-identity-function}
For any group $G$, the identity endomorphism coincides with the identity
function on the underlying set.
\end{theorem}

We should now have some sanity check that [[Id_G]] really is
the identity group morphism $\Id_{G}$ that \emph{we} think it is.

<<Theorem: [[Id_G]] is the same as [[id the carrier of G]]>>=
theorem Th10:
  Id_G = id (the carrier of G) by Def3;

@ %def Th10

\begin{registration}
Now we should automatically associate $\Id_{G}$ is bijective. This
requires proving a couple of helper lemmas establishing injectivity and
surjectivity.
\end{registration}

<<Register [[Id_G]] is bijective>>=
<<[[Id_G]] is injective>>
<<[[Id_G]] is surjective>>

registration let G;
  cluster Id_G -> bijective;
  coherence by Lm1,Lm2;
end;

@

\begin{lemma}[Identity is injective]
For any group $G$, the identity morphism $\Id_{G}$ is injective.
\end{lemma}

\begin{proof}[Proof sketch]
The proof is straightforward since we've established in Theorem~\ref{thm:characteristic:identity-endomorphism-is-identity-function} the group
endomorphism $\Id_{G}$ coincides with the set-theoretic function $\id_{U(G)}$
on the underlying set $U(G)$ of the group $G$.
\end{proof}

<<[[Id_G]] is injective>>=
Lm1: Id_G is one-to-one
proof
  id (the carrier of G) is one-to-one;
  hence thesis by Th10;
end;

@ %def Lm1

\begin{lemma}[Identity is surjective]
For any group $G$, the identity morphism $\Id_{G}$ is surjective.
\end{lemma}

As with establishing injectivity, it's straightforward.

<<[[Id_G]] is surjective>>=
Lm2: Id_G is onto
proof
  id (the carrier of G) is onto;
  hence thesis by Th10;
end;

@ %def Lm2

\begin{theorem}\label{thm:characteristic:automorphism:automorphism-which-leaves-subgroup-invariant-is-automorphism-of-subgroup}
  Let $H\subgroup G$ be any subgroup.
  If $\varphi\in\Aut(G)$ is an automorphism such that $\varphi(H)=H$ it
  leaves $H$ invariant, then its restriction to $H$ is an automorphism
  $\varphi|_{H}\in\Aut(H)$.
\end{theorem}

This result isn't surprising, but proving surjectivity was surprisingly
(and agonizingly) hard.

\begin{proof}[Proof outline]
  We have for any $\varphi\in\Aut(G)$ such that
  \begin{equation*}
    \tag{$A_{1}$} \varphi|_{H}(H)=H
  \end{equation*}
  be a morphism $\varphi|_{H}\colon H\to H$ by Theorem~\mml[Th49]{group6}%
\MizThm{GROUP\_6}{49}. This is our first claim $C_{1}$.
  Our next claim $C_{2}$ is that $\varphi|_{H}$ is injective by Lemma~\ref{lemma:characteristic:automorphism:restrict-automorphism-gives-monomorphism}
  and hypothesis $A_{1}$.

  The nontrivial part of the proof is establishing $\varphi|_{H}$ is
  surjective. Once established, it follows $\varphi|_{H}$ is bijective
  and thus an automorphism.
\end{proof}

<<Theorem: $\varphi\in\Aut(G)$ and $H\subgroup G$ such that $\varphi(H)=H$ implies $\varphi|H\in\Aut(H)$>>=
theorem Th11:
  for f being Automorphism of G
  st Image(f|H) = H
  holds f|H is Automorphism of H
proof
  let f be Automorphism of G;
  assume A1: Image(f|H) = H;
  then C1: f|H is Homomorphism of H,H by GROUP_6:49;
  C2: f|H is one-to-one by Lm9;

  f|H is onto Endomorphism of H
  <<Proof: $\varphi|_{H}$ is surjective>>
  then f|H is bijective Homomorphism of H,H by C2;
  then f|H is Automorphism of H;
  hence thesis;
end;

@ %def Th11

\begin{proof}[Proof sketch (Surjectivity)]
For reasons I do not adequately understand, I could not simply handle
[[f|H]] as a [[Homomorphism of H,H]] (despite having established this
fact).

I could not use any theorem concerning surjectivity of group morphisms,
so I just ``forgot'' it was a morphism, proved surjectivity, then
``remembered'' it was a group morphism after all.
\end{proof}

\begin{thm-remark}
I am starting to think that the claim [[f is onto]] is different than
[[f is onto Homomorphism of G1,G2]].
\end{thm-remark}

<<Proof: $\varphi|_{H}$ is surjective>>=
proof
  set UH = the carrier of H;
  f|H is Function of UH,UH & rng(f|H) = the carrier of H by C1, GROUP_6:44, A1;
  then f|H is onto Function of UH,UH by FUNCT_2:def 3;
  then f|H is onto Homomorphism of H,H by C1;
  hence thesis;
end;

@

\begin{theorem}
Let $H\properSubgroup G$ be a proper subgroup. Then its image under any
automorphism $\varphi\in\Aut(G)$ is another proper subgroup
$\varphi(H)\properSubgroup G$.
\end{theorem}

<<Theorem: $\varphi\in\Aut(G)$ and $H\properSubgroup G$ implies $\varphi(H)\properSubgroup G$>>=
theorem ThAutoProper:
  for G being non trivial Group
  for H being Subgroup of G
  for phi being Automorphism of G
  st H is proper Subgroup of G
  holds Image(phi|H) is proper Subgroup of G
proof
  let G be non trivial Group;
  let H be Subgroup of G;
  let phi be Automorphism of G;
  set UH = the carrier of H;
  set UG = the carrier of G;
  A1: phi is one-to-one & phi is onto & UH is non empty Subset of UG &
      phi is Function of UG,UG by GROUP_2:def 5;
  assume H is proper Subgroup of G;
  then UG \ UH is non empty by Lm100;
  then consider x such that
  A2: x in UG \ UH by XBOOLE_0:def 1;
  A3: x in G & not x in H by A2,XBOOLE_0:def 5;
  A4: <<$\varphi(x)\notin\varphi(H)$>>
  <<$\varphi(x)\in G$>>
  then ex y being Element of G st not y in phi .: H by A4;
  then phi .: H is proper;
  hence Image(phi|H) is proper Subgroup of G by GRSOLV_1:def 3;
end;

@ %def ThAutoProper

\begin{proof}[Proof step ($\varphi(x)\notin\varphi(H)$)]
Since $x\in G$ and $x\notin H$, it follows that $\varphi(x)\notin\varphi(H)$
thanks to Lemma~\ref{lemma:characteristic:preparatory:x-notin-A-implies-fx-notin-fA}.
We also need an extra step since $\varphi$ is considered first as a
function on the underlying set $U(H)$ of $H$, then we need to remember
that this is the same as $\varphi$ applied to the subgroup $H$.
\end{proof}
<<$\varphi(x)\notin\varphi(H)$>>=
not (phi.x in phi .: H)
proof
  not (phi.x in (phi .: UH)) by A1, A3, Lm18;
  hence not (phi.x in (phi .: H)) by GRSOLV_1:8;
end;

@

\begin{proof}[Proof step ($\varphi(x)\in G)$.]
We need to make explicit that $\varphi(x)$ is not just ``some object'',
but an element of the group $G$. This follows from the fact $\varphi(x)$
is in the range of $\varphi$ by definition of the range of a function
(i.e., \mml[def3]{funct1}\MizDef{FUNCT\_1}[03]{3}). Since $\varphi$ is
an automorphism, in particular surjective, it follows that the set
underlying $\varphi(G)$ is the set underlying $G$, i.e., $U(G)$.
Then $\varphi(x)\in G$.
\end{proof}

<<$\varphi(x)\in G$>>=
phi.x is Element of G
proof
  dom phi = UG & rng phi = UG by A1, FUNCT_2:def 1;
  then phi.x in UG by A3, FUNCT_1:def 3;
  hence phi.x is Element of G;
end;

@

\begin{theorem}\label{thm:characteristic:automorphism:image-of-maximal-subgroup-under-automorphism-is-maximal}
  Let $G$ be a group, $\varphi\in\Aut(G)$ an arbitrary automorphism.
  If $H\properSubgroup G$ is a maximal subgroup, then
  $\varphi(H)\properSubgroup G$ is also maximal.
\end{theorem}

\begin{proof}[Proof outline]
  Since $H\properSubgroup G$ is maximal, if $\varphi(H)$ were not
  maximal, there would be a subgroup $K\properSubgroup G$ such that
  $\varphi(H)\properSubgroup K$. In that case, $\varphi^{-1}(K)=L$ would
  be a proper subgroup which contains $H$ as a proper subgroup, which is
  impossible. Thus $\varphi(H)$ must be maximal.
\end{proof}

<<Theorem: Automorphisms map maximal subgroups to maximal subgroups>>=
theorem Th31:
  for G being non trivial Group
  for H being strict Subgroup of G
  for phi being Automorphism of G
  st H is maximal
  holds Image(phi|H) is maximal
proof
  let G be non trivial Group;
  let H be strict Subgroup of G;
  let phi be Automorphism of G;
  assume A1: H is maximal;
  A2: Image(phi|H) is proper Subgroup of G by A1,ThAutoProper;
  then P1: the multMagma of Image(phi|H) <> the multMagma of G by Def10;
  set UG = the carrier of G;
  set UH = the carrier of H;
  P2: for K being strict Subgroup of G
  st Image(phi|H) <> K & Image(phi|H) is Subgroup of K
  holds K = the multMagma of G
  <<Proof: $H$ is maximal implies $K=G$>>
  thus Image(phi|H) is maximal by P1,P2,GROUP_4:def 6;
end;

@ %def Th31

\begin{proof}[Sub-proof outline ($H$ is maximal implies $K=G$).]
Let $K$ be an arbitrary subgroup of $G$ such that $\varphi(H)\properSubgroup K$.
We can consider $\psi\in\Aut(G)$ defined by $\psi(x)=\varphi^{-1}(x)$
for all $x\in G$. Since $H\properSubgroup K\properSubgroup G$, we can
find some $k\in K$ but $k\notin K$. Then $\psi(k)\in\psi(K)$. Since $H$
is maximal, $\varphi(\psi(K))=G$. But also $\varphi(\psi(K))=K$. Hence $K=G$.
\end{proof}

<<Proof: $H$ is maximal implies $K=G$>>=
proof
  let K be strict Subgroup of G;
  assume B1: Image(phi|H) <> K;
  assume B2: Image(phi|H) is Subgroup of K;
  then consider psi being Automorphism of G such that
  B3: psi = phi" and
  B4: H is Subgroup of Image(psi|K)
  by Th7b;
  set UK = the carrier of K;
  reconsider K as non trivial strict Subgroup of G by A2,B1,B2,Lm110;
  UK \ (the carrier of Image(phi|H)) is non empty by B1,B2,Def10,Lm100;
  then consider k being object such that
  B6: k in UK \ (the carrier of Image(phi|H))
  by XBOOLE_0:def 1;
  reconsider k as Element of K by B6;
  set L = Image(psi|K);
  B8: psi.k in L
  <<Proof: $\psi(k)\in L$>>
  B9: the multMagma of H <> L
  <<Proof: $H\neq L$>>
  B10: Image(phi|L) = the multMagma of G
  <<Proof: $\varphi(L)=G$>>
  Image(phi|L) = K
  <<Proof: $\varphi(L) = K$>>
  hence thesis by B10;
end;

@

\begin{proof}[Proof step ($\psi(k)\in L$).]
Since $k\in K$ and $L=\psi(K)$, the result follows from unfolding definitions.
\end{proof}

<<Proof: $\psi(k)\in L$>>=
proof
  C1: k in G by GROUP_2:41;
  consider l being object such that
  C2: l = psi.k;
  dom psi = the carrier of G by FUNCT_2:def 1;
  then l in psi .: (the carrier of K) by C1,C2,FUNCT_1:def 6;
  then l in the carrier of (psi .: K) by GRSOLV_1:8;
  hence psi.k in Image(psi|K) by C2,GRSOLV_1:def 3;
end;

@

\begin{proof}[Proof step ($H\neq L$).]
Since $k\in K\setminus\varphi(H)$, it follows
$\psi(k)\in\psi(K)\setminus H$.
\end{proof}

<<Proof: $H\neq L$>>=
proof
  set UPH = the carrier of Image(phi|H);
  C1: phi is one-to-one & phi is onto & UPH is non empty Subset of UG &
  phi is Function of UG,UG by GROUP_2:def 5;
  k in G & not k in Image(phi|H) by B6, XBOOLE_0:def 5, GROUP_2:41;
  then C2: not psi.k in (psi .: UPH) by C1,Lm18;
  consider phi2 being Automorphism of G such that
  C3: phi2 = psi" and
  C4: the multMagma of Image(psi|Image(phi2|H)) = the multMagma of H
  by Th7;
  phi2=phi by C3,B3, Lm10;
  then C5: the multMagma of Image(psi|Image(phi|H)) = the multMagma of H by C4;
  set UPH = the carrier of Image(phi|H);
  psi .: UPH = the carrier of (psi .: Image(phi|H)) by GRSOLV_1:8
            .= the carrier of Image(psi|Image(phi|H)) by GRSOLV_1:def 3;
  then the carrier of H <> the carrier of L by B8,C2,C5;
  hence thesis;
end;

@

\begin{proof}[Proof step ($\varphi(L)=G$).]
Since $H\neq L$, and $H$ is maximal, it follows that $L=G$. Then
$\varphi(L)=\varphi(G)$ and $\varphi(G)=G$ gives the result.
\end{proof}

<<Proof: $\varphi(L)=G$>>=
proof
  L = the multMagma of G by A1,B4,B9,GROUP_4:def 6;
  then phi is Function of UG,UG & phi is onto & the carrier of L = UG;
  then phi .: the carrier of L = phi .: UG
                              .= rng phi by RELSET_1:22
                              .= UG by FUNCT_2:def 3;
  then UG = phi .: (the carrier of L)
         .= the carrier of (phi .: L) by GRSOLV_1:8
         .= the carrier of Image(phi|L) by GRSOLV_1:def 3;
  then the carrier of Image(phi|L) = the carrier of G;
  then the multMagma of Image(phi|L) = the multMagma of G by GROUP_2:61;
  hence thesis;
end;

@

\begin{proof}[Proof step ($\varphi(L)=K$).]
From $L=\psi(K)$, it follows $\varphi(L)=K$.
\end{proof}

<<Proof: $\varphi(L) = K$>>=
proof
  consider psi2 being Automorphism of G such that
  C1: psi2 = phi" and
  C2: the multMagma of Image(phi|Image(psi2|K)) = the multMagma of K
  by Th7;
  thus Image(phi|Image(psi|K)) = K by B3,C1,C2;
end;

@
