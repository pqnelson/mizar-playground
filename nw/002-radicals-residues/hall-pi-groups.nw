% -*- mode: poly-noweb; noweb-code-mode: mizar-mode; -*-

\section{Hall $\pi$-Groups}

We can generalize the notions surrounding Sylow's theorems from
$p$-groups to $\pi$-groups. Towards this end, we will consciously follow
the conventions laid down in the Mizar article concerning Sylow
theorems~\mml{group10}.

There is an obscure notion of ``Sylow $\pi$-subgroups''.
However, in general, they are not conjugate to one another. For
this reason, they are not very popular, and fallen by the
wayside.\footnote{In fact, but for Robinson~\cite{robinson1995course} and the
bibliography for the entry ``Sylow subgroup'' in the
\emph{Encyclopedia of Mathematics} (\url{https://encyclopediaofmath.org/wiki/Sylow_subgroup}),
I wouldn't have known of them. For more on the history behind
generalizing Sylow's theorems to $\pi$-groups, see Rozelle Wright's
``The Sylow theorems and their generalizations'' (1963)~\cite{wright1963sylow}.}

Instead people usually prefer working with Hall $\pi$-subgroups.
And if $G$ is finite, then these two notions (Hall $\pi$-subgroups and
Sylow $\pi$-subgroups) coincide. However, unlike Sylow $p$-subgroups,
existence of Hall subgroups is not guaranteed. For a concrete example of
this, the alternating group $A_{5}$ has no Hall $\{2,5\}$-subgroup, nor
does $A_{5}$ have a Hall $\{3,5\}$-subgroup.

Also be warned: unlike the Sylow subgroups, there is no guarantee of
conjugacy among the Hall $\pi$-subgroups.

This motivates the natural objective: establish criteria for when
analogous results hold for Hall subgroups as the Sylow theorems. In the
literature, we find the results referred to by the following names:
\begin{itemize}
  \item[{$(E_{\pi})$}] A Hall $\pi$-subgroup of $G$ exists
  \item[{$(C_{\pi})$}] Any two Hall $\pi$-subgroups of $G$ are conjugate to
    each other
  \item[{$(D_{\pi})$}] If $P$ is a $\pi$-subgroup of $G$, then there exists
    a Hall $\pi$-subgroup $H\subgroup G$ such that $P\subgroup H$.
\end{itemize}

<<Hall $\pi$-Groups>>=
<<Theorem: existence of $\pi$-restricted prime factorization>>

<<Definition: $\pi$-part of $n$>>

<<Definition: predicate ``is Hall $\pi$-subgroup of primes [$\pi$]''>>

<<Theorem: for any $G$ finite and $\pi(G)\subset\pi$, $G$ is a Hall $\pi$-subgroup of itself>>

<<Theorem: for any finite $G$ and set of primes $\pi$, $\trivialSubgroup$ is a Hall $\pi$-subgroup of $G$>>

<<Theorem: $p\divides ab\implies [\gcd(a,b)=1\iff (p\divides a\land p\ndivides b)\lor(p\ndivides a\land p\divides b)]$>>

<<Theorem: $H$ is a Hall $\pi$-subgroup iff $|H|$ is coprime to $\Index{G}{H}$>>

<<Definition: $\HallSubgroups{\pi}{G}$ the collection of Hall $\pi$-Subgroups>>

<<Theorem: $2\not\in\pi$ and $H\in\HallSubgroups{\pi}{G}$ implies $\forall g\in G, gHg^{-1}\in\HallSubgroups{\pi}{G}$>>

@

\begin{theorem}
For any nonzero number $n$ and set of primes $\pi$, there exists a
sequence of exponents $\{A_{p}\}_{p\in\pi}$ such that if
$n=\prod_{p}p^{e_{p}}$ is the prime factorization, then $A_{p}=e_{p}$
when $p\in\pi$ and $A_{p}=0$ if $p\notin\pi$.
\end{theorem}

<<Theorem: existence of $\pi$-restricted prime factorization>>=
theorem Th32:
  for n being non zero Nat
  ex A being bag of SetPrimes
  st (for p being object st p in Pi /\ support(ppf n) holds A.p = (ppf n).p) &
  (for p being object st not (p in Pi /\ support(ppf n)) holds A.p = 0);
@ %def Th32

\begin{definition}
Let $\pi$ be a set of primes, $n\in\NN$. We define the
\define{$\pi$-part} of $n$ to be the number
\begin{equation}
n_{\pi} = \prod_{p\in\pi}p^{e_{p}}
\end{equation}
where $n = \prod_{p}p^{e_{p}}$ is the prime factorization of $n$. If
$n=0$, we define $0_{\pi}=0$ by convention.
\end{definition}

\begin{todo}
Refactor these proofs (of existence and uniqueness).
\end{todo}

<<Definition: $\pi$-part of $n$>>=
definition
  let Pi be Subset of SetPrimes;
  let n be Nat;
  func Pi -part n -> Nat means
  :Def4:
  it = 0 if n = 0
  otherwise ex n1 being non zero Nat st n1 = n & ex A being bag of SetPrimes
  st support A = Pi /\ support (ppf n1) &
  (for p being Prime st p in support A holds A.p = (ppf n1).p) &
  it = Product A;
  existence
  proof
    thus n=0 implies ex k being Nat st k=0;
    assume n <> 0;
    then reconsider n as non zero Nat;
    consider A being bag of SetPrimes such that
    A1: for p being object st p in Pi /\ support(ppf n) holds A.p = (ppf n).p and
    A2: for p being object st not (p in Pi /\ support(ppf n)) holds A.p = 0
    by Th32;

    A3: for p being object holds p in Pi /\ support(ppf n) iff A.p <> 0
    proof
      let p be object;
      thus p in Pi /\ support(ppf n) implies A.p <> 0
      proof
        assume B1: p in Pi /\ support(ppf n);
        then B2: p in support(ppf n) by XBOOLE_0:def 4;
        then p in support(pfexp n) by NAT_3:def 9;
        then B3: p is Prime by TARSKI:1,NAT_3:34;
        (ppf n).p <> 0 by B2,PRE_POLY:def 7;
        hence A.p <> 0 by B1,B2,B3,A1;
      end;
      B1: (not p in Pi /\ support(ppf n) implies A.p = 0) implies
      (not A.p = 0 implies p in Pi /\ support(ppf n));
      not p in Pi /\ support(ppf n) implies A.p = 0 by A2;
      hence thesis by B1;
    end;

    A4: for x being object holds x in support A iff x in Pi /\ support(ppf n)
    by A3,PRE_POLY:def 7;
    then
    A5: support A = Pi /\ support(ppf n) by TARSKI:2;
    A6: for p being Prime st p in support A holds A.p = (ppf n).p
    proof
      let p be Prime;
      assume B1: p in support A;
      reconsider x=p as object;
      B2: x in support A by B1;
      then x in Pi /\ support(ppf n) by A4;
      then A.x = (ppf n).x by A1;
      hence A.p = (ppf n).p;
    end;

    take k = Product A;
    thus thesis by A5,A6;
  end;
  uniqueness
  proof
    defpred P[Nat] means ex n1 being non zero Nat st n=n1 &
    ex A being bag of SetPrimes
    st support A = Pi /\ support (ppf n1) &
    (for p being Prime st p in support A holds A.p = (ppf n1).p) &
    $1 = Product A;

    let k1,k2 be Nat;
    thus n=0 & k1=0 & k2=0 implies k1=k2;

    assume n <> 0;
    then reconsider n as non zero Nat;
    
    assume A1: P[k1];
    assume A2: P[k2];
    consider A1 being bag of SetPrimes such that
    A3: support A1 = Pi /\ support(ppf n) and
    A4: for p being Prime st p in support A1 holds A1.p = (ppf n).p and
    A5: k1 = Product A1
    by A1;

    consider A2 being bag of SetPrimes such that
    A6: support A2 = Pi /\ support(ppf n) and
    A7: for p being Prime st p in support A2 holds A2.p = (ppf n).p and
    A8: k2 = Product A2
    by A2;

    for x being object st x in SetPrimes
    holds A1.x = A2.x
    proof
      let x be object;
      assume B1: x in SetPrimes;
      then reconsider x as Prime by NEWTON:def 6;
      x in Pi /\ support (ppf n) or not x in Pi /\ support (ppf n) by B1;
      then per cases;
      suppose B2: x in Pi /\ support (ppf n);
        then C1: x in support A1 by A3;
        C2: x in support A2 by A6,B2;
        A2.x = (ppf n).x by A7,C2
            .= A1.x by A4,C1;
        hence thesis;
      end;
      suppose B3: not x in Pi /\ support (ppf n);
        then C1: not x in support A1 by A3;
        not x in support A2 by B3,A6;
        then A2.x = 0 by PRE_POLY:def 7
                 .= A1.x by C1,PRE_POLY:def 7;
        hence thesis;
      end;
    end;

    then A1 = A2 by PBOOLE:3;
    hence k1 = k2 by A5,A8;
  end;
  consistency;
end;
@ 

\begin{theorem}
The $\pi$-part of a non-zero $n\in\NN$ is given by a sequence of
non-zero exponents $\{A_{p}=p^{e_{p}}\mid e_{p}\neq0\}_{p\in\pi}$ from the
prime-factorization of $n=\prod_{p}p^{e_{p}}$.
\end{theorem}

<<Theorem: $n\neq 0$ has a sequence of $\pi$-exponents>>=
theorem Th33:
  for n being non zero Nat
  ex A being bag of SetPrimes
  st support A = Pi /\ support (ppf n) &
  (for p being Prime st p in support A holds A.p = (ppf n).p) &
  Pi-part n = Product A
proof
  let n be non zero Nat;
  A1: not n = 0;
  not n = 0 implies ex n1 being non zero Nat st n1 = n & ex A being bag
  of SetPrimes st support A = Pi /\ support (ppf n1) &
  (for p being Prime st p in support A holds A.p = (ppf n1).p) &
  Pi-part n = Product A by Def4;
  hence thesis by A1;
end;
@ %def Th33

\begin{theorem}
Let $n\in\NN$ and $\pi$ be a set of primes. If no prime $p\in\pi$
divides $n$ (i.e., $n$ is a $\pi'$-number),
then $n_{\pi} = 1$.
\end{theorem}

\begin{thm-remark}
Arguably this follows from Theorem~\mml[Th57]{nat3}, or similar
reasoning along the lines of the proof for that theorem.
\end{thm-remark}

<<Theorem: $\pi^{\prime}$-numbers have trivial $\pi$-parts>>=
theorem Th34:
  for n being non zero Nat
  st (for p being Prime st p in Pi holds not p divides n)
  holds Pi-part n = 1
@ %def Th34

\begin{registration}
The $\pi$-part of a non-zero number is positive.
\end{registration}

\begin{remark}
  The obligation we want to establish is
  ``\lstinline{for k being Nat} \lstinline{st k = Pi-part n}
  \lstinline{holds k is pos}''.
\end{remark}

<<Register: $n\neq0$ has $n_{\pi}\gt0$>>=
registration
  let n be non zero Nat;
  cluster Pi-part n -> pos Nat;
  coherence
  <<Proof: coherence that $n\neq0$ has $n_{\pi}\gt0$>>
end;
@

\begin{proof}[Proof sketch]
Let $k\in\NN$ be such that $k = n_{\pi}$. We will show $k>0$.
\end{proof}

<<Proof: coherence that $n\neq0$ has $n_{\pi}\gt0$>>=
proof
  let k be Nat;
  assume A1: k = Pi-part n;
  thus k is pos;
end;
@ 

\begin{theorem}
For every nonzero $n\in\NN$ and set of primes $\pi$,
we necessarily have $n_{\pi}\divides n$.
\end{theorem}

<<Theorem: $\pi$-part of a number is also a divisor>>=
theorem Th35:
  for n being non zero Nat
  holds Pi-part n divides n
@ %def Th35

\begin{theorem}
For every nonzero $n\in\NN$ and set of primes $\pi$, we have
$n$ is the product of its $\pi$-part and $\pi'$-part
$n = n_{\pi}\cdot n_{\pi'}$.
%% their quotient $n/n_{\pi}$ is a natural number and moreover it is a
%% $\pi'$-number. Its $\pi$-part is unity $(n/n_{\pi})_{\pi}=1$.
\end{theorem}

<<Theorem: $n=n_{\pi}\cdot n_{\pi^{\prime}}$>>=
theorem Th36:
  for n being non zero Nat
  holds n = (Pi`-part n)*(Pi-part n)
@ %def Th36

\begin{corollary}
The $\pi'$-part of a number is coprime to the $\pi$-part of the number.
More explicitly, for any $n\in\NN$ and set of primes $\pi$, we have
$n_{\pi}$ be coprime to $n_{\pi'}$.
\end{corollary}

<<Corollary: $n_{\pi}$, $n_{\pi^{\prime}}$ are coprime>>=
theorem Th37:
  for n being non zero Nat
  holds (Pi`-part n), (Pi-part n) are_coprime
@ %def Th37

\begin{theorem}
  For every $n\in\NN$ and set of primes $\pi_{1}$, $\pi_{2}$, we have
  $(n_{\pi_{1}})_{\pi_{2}} = n_{\pi_{1}\cap\pi_{2}}$.
\end{theorem}

\begin{definition}
Let $G$ be a finite group, $\pi$ a set of primes, and $H\subgroup G$
some subgroup. We call $H$ a \define{Hall $\pi$-Subgroup} if its index
$\Index{G}{H}$ is coprime to every prime $p\in\pi$:
\begin{equation}
  \forall p\in\pi, \gcd(p, \Index{G}{H})=1.
\end{equation}
\end{definition}

\begin{def-remark}
Not every finite group has a Hall subgroup, so we cannot make an
attribute ``Hall'' for \lstinline{Subgroup of G}. Fortunately,
Definition~\mml[def18]{group10} defines a predicate for Sylow subgroup,
and we follow suite.
\end{def-remark}

<<Definition: predicate ``is Hall $\pi$-subgroup of primes [$\pi$]''>>=
definition
  let Pi be Subset of SetPrimes;
  let G be finite Group;
  let IT be Subgroup of G;
  pred IT is_Hall_pi-subgroup_of_primes Pi means
  :Def3:
  IT is Pi-group &
  for p being prime Nat
  st p in Pi
  holds p, index IT are_coprime;
end;
@ %def is_Hall_pi-subgroup_of_primes

\begin{voc}
We need to add \lstinline{is_Hall_pi-subgroup_of_primes} to our
vocabulary.
\end{voc}

<<DICT/GROUP-23.VOC>>=
Ris_Hall_pi-subgroup_of_primes
@

\begin{theorem}[{\cite[pg.71]{aschbacher2000finite}}]
Let $\pi$ be a set of primes, $G$ a finite group.
Then $H\subgroup G$ is a Hall $\pi$-subgroup if and only if the order of
$H$ is $|G|_{\pi}$.
\end{theorem}

<<Theorem: $H\subgroup G$ is Hall subgroup iff $|H|=|G|_{\pi}$>>=
theorem
  for G being finite Group
  for H being Subgroup of G
  holds H is_Hall_pi-subgroup_of_primes Pi iff
  card H = Product ((prime_factorization card G)|Pi);
@ 

\begin{theorem}
Let $\pi$ be a set of primes. Let $G$ be a finite group,
let $\pi(G)\subset \pi$ be the set of prime divisors of $|G|$.
Then $G$ is a Hall $\pi$-subgroup of itself.
\end{theorem}

<<Theorem: for any $G$ finite and $\pi(G)\subset\pi$, $G$ is a Hall $\pi$-subgroup of itself>>=
theorem
  for G being finite Group
  for Pi being Subset of SetPrimes
  st support (prime_factorization card G) c= Pi
  holds (Omega).G is_Hall_pi-subgroup_of_primes Pi;
@

\begin{theorem}
Let $\pi$ be a set of primes.
Let $G$ be a finite group. Then $\trivialSubgroup\subgroup G$ is a Hall
$\pi$-subgroup of $G$.
\end{theorem}

<<Theorem: for any finite $G$ and set of primes $\pi$, $\trivialSubgroup$ is a Hall $\pi$-subgroup of $G$>>=
theorem
  for G being finite Group
  holds (1).G is_Hall_pi-subgroup_of_primes Pi;
@

\begin{theorem}
Let $a,b\in\NN$ and let $p\in\NN$ be prime.
Suppose $p\divides ab$.
Then $a$, $b$ are coprime if and only if
$p$ divides $a$ or $p$ divides $b$ but $p$ does not divide both.
\end{theorem}

<<Theorem: $p\divides ab\implies [\gcd(a,b)=1\iff (p\divides a\land p\ndivides b)\lor(p\ndivides a\land p\divides b)]$>>=
theorem
  for a,b being Nat
  for p being prime Nat
  st p divides a*b
  holds (a,b are_coprime) iff
        ((p divides a & not p divides b) or
         (not p divides a & p divides b));
@

\begin{theorem}
Let $\pi$ be a set of primes, $G$ be a finite group, $H\subgroup G$ be
a subgroup.
Then $H$ is a Hall $\pi$-subgroup if and only if
every prime divisor $p\divides|G|$ divides exactly one of $|H|$ or $\Index{G}{H}$.
\end{theorem}

<<Theorem: $H$ is a Hall $\pi$-subgroup iff $|H|$ is coprime to $\Index{G}{H}$>>=

theorem
  for G being finite Group
  for H being Subgroup of G
  st H is Pi-group
  holds H is_Hall_pi-subgroup_of_primes Pi iff
        (for p being prime Nat st p divides card G
        holds not (p divides index H & p divides card H))
proof
  let G be finite Group;
  let H be Subgroup of G;
  assume A1: H is Pi-group;
  thus H is_Hall_pi-subgroup_of_primes Pi implies
       (for p being prime Nat st p divides card G
        holds not (p divides index H & p divides card H))
  proof
    assume B1: H is_Hall_pi-subgroup_of_primes Pi;
    let p be prime Nat;
    assume B2: p divides card G;
    thus not (p divides index H & p divides card H) by B1,B2,PYTHTRIP:def 2;
  end;
  thus (for p being prime Nat st p divides card G
        holds not (p divides index H & p divides card H)) implies
       H is_Hall_pi-subgroup_of_primes Pi
  proof
    assume B1: for p being prime Nat st p divides card G
               holds not (p divides index H & p divides card H);
    for p being prime Nat
    holds not p divides index H or not p divides card H
    proof
      let p be prime Nat;
      assume C1: p divides index H;
      then p divides (index H)*(card H) by INT_2:2;
      then p divides card G by GROUP_2:147;
      then not (p divides index H & p divides card H) by B1;
      hence not p divides card H by C1;
    end;
    then index H,card H are_coprime by PYTHTRIP:def 2;
    hence H is_Hall_pi-subgroup_of_primes Pi by A1,Def3;
  end;
end;
@

\begin{lemma}
If $\pi$ is a set of primes and $2\notin\pi$, then every $p\in\pi$ is odd.
\end{lemma}

<<Lemma: $2\notin\pi$ implies every prime $p\in\pi$ is odd>>=
Lm5:
  for Pi being Subset of SetPrimes
  st not 2 in Pi
  for p being prime Nat
  holds p is odd;
@


\begin{definition}
Let $G$ be a finite group, $\pi$ a set of prime numbers.
We define the set of Hall $\pi$-subgroups of $G$ as $\HallSubgroups{\pi}{G}$.
\end{definition}

\begin{def-remark}
This defines a new Mizar functor, which will require proving the set
$\HallSubgroups{\pi}{G}$ exists and is unique.
\end{def-remark}

\begin{def-remark}
We should also register the fact that if we have
``\lstinline[breaklines=true]{H is Element of the_hall_pi-subgroups_of_primes (Pi,G)}'',
then ``\lstinline[breaklines=true]{H is Pi-group Subgroup of G}''.
\end{def-remark}

\begin{def-remark}
This is analogous to definition~\mml[def19]{group10} for the set of
Sylow $p$-subgroups of $G$. In fact, the analogy guides our definition.
\end{def-remark}

<<Definition: $\HallSubgroups{\pi}{G}$ the collection of Hall $\pi$-Subgroups>>=
definition
  let Pi be Subset of SetPrimes;
  let G be finite Group;
  func the_hall_pi-subgroups_of_primes (Pi,G) -> Subset of (Subgroups G) equals
  :Def4:
  { H where H is Element of Subgroups G : ex P being strict Subgroup of G st
   P = H & P is_Hall_pi-subgroup_of_primes Pi };
  correctness
  proof
    set X = {H where H is Element of Subgroups G: ex P being strict Subgroup
    of G st P = H & P is_Hall_pi-subgroup_of_primes Pi};
    now
      let x be object;
      assume x in X;
      then ex H being Element of Subgroups G st x = H & ex P being strict
      Subgroup of G st P = H & P is_Hall_pi-subgroup_of_primes Pi;
      hence x in Subgroups G;
    end;
    hence thesis by TARSKI:def 3;
  end;
end;
@ %def the_hall_pi-subgroups_of_primes

\begin{voc}
We need to add \lstinline{the_hall_pi-subgroups_of_primes} to our
vocabulary.
\end{voc}

<<DICT/GROUP-23.VOC>>=
Othe_hall_pi-subgroups_of_primes
@

\begin{theorem}[{Gross~\cite{gross1987conjugacy}}]
Let $G$ be a finite group, $\pi$ be a set of odd primes. Then the Hall
$\pi$-subgroups of $G$ (if any) are all conjugate.
\end{theorem}

<<Theorem: $2\not\in\pi$ and $H\in\HallSubgroups{\pi}{G}$ implies $\forall g\in G, gHg^{-1}\in\HallSubgroups{\pi}{G}$>>=
:: Fletcher Gross, "Conjugacy of Odd order Hall Subgroups".
:: Bull. London Math. Soc. 19, no.4 (1987) 311-319
theorem
  not 2 in Pi implies
  for G being finite Group
  for H1,H2 being Subgroup of G
  st H1 is_Hall_pi-subgroup_of_primes Pi &
     H2 is_Hall_pi-subgroup_of_primes Pi
  holds H1,H2 are_conjugated;
@
