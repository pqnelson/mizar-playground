% -*- mode: poly-noweb; noweb-code-mode: mizar-mode; -*-

\section{$\pi$-Groups and $\pi$-Subgroups}

We can generalize the notion of a $p$-group to a $\pi$-group, where
$\pi$ is a set of prime numbers. This motivates analogous notions like
Sylow $\pi$-subgroups, and Hall $\pi$-subgroups (which coincide for
finite groups, but may be different in general).

<<$\pi$-Groups and $\pi$-Subgroups>>=
<<Reserve: $\pi$ for subset of primes>>

<<Register: positive for [[Nat]]>>

<<Definition: $\pi$-element of Group>>

<<Theorem: $1_{G}$ is a $\pi$-element>>

<<Theorem: if $|a|=|b|$ and $a$ is a $\pi$-element, then $b$ is a $\pi$-element>>

<<Theorem: $a$ is a $\pi$-element and $b$ arbitrary, then $b^{-1}ab$ is a $\pi$-element>>

<<Theorem: $a\in G$ is a $\pi$-element implies $a^{-1}$ is a $\pi$-element>>

<<Theorem: if $g$ is a $\pi$-element and $|h|$ divides $|g|$, then $h$ is a $\pi$-element>>

<<Definition: $\pi$-Group>>

<<Theorem: the trivial group is $\pi$-group>>

<<Registration: trivial group is $\pi$-group>>

<<Theorem: criteria for finite group to be $\pi$-group>>

<<Theorem: if $\pi_{1}\subset\pi_{2}$ and $G$ is $\pi_{1}$-group, then $G$ is $\pi_{2}$-group>>

<<Register: $\pi$-group for groups and finite groups>>

<<Register: Elements of a $\pi$-group are $\pi$-elements>>

<<Register: $\pi$-group for normal Subgroups>>

<<Redefine: if $p$ is prime, then $\{p\}$ is subset of prime numbers>>

<<Theorem: If $G$ is a $\{p\}$-group, then it is Sylow $p$-group>>

<<Theorem: $G$ is a $p$-group iff $G$ is a $\{p\}$-group>>

<<Theorem: if $g\in G$ and $h\in H\subgroup G$ such that $g=h$, then for any $n\in\NN$ we have $g^{n}=h^{n}$>>

<<Lemma: $g=h$ and $|g|\neq0$ implies $|g|=|h|$>>

<<Theorem: for $g\in G$ and $h\in H\subgroup G$, if $g=h$, then $|g|=|h|$>>

<<Lemma: $H\subgroup G$ and $H$ is nontrivial implies $G$ is nontrivial>>

<<Theorem: if $G$ is a $\pi$-group, then $H\subgroup G$ is a $\pi$-group>>

<<Register: subgroup of $\pi$-group is $\pi$-group>>

<<Register: quotient group of finite groups is finite>>

<<Theorem: $x\in G/N$, $x=gN$, and $n\in\NN$ implies $x^{n}=g^{n}N$>>

<<Theorem: $x\in G/N$ and $g\in G$ such that $x=gN$, then $|x|\divides|g|$>>

<<Theorem: $G\neq\trivialGroup\implies\exists g\in G,g\neq\idElt{G}$>>

<<Theorem: if $G$ is a $\pi$-group and $N\normalSubgroup G$, then $G/N$ is a $\pi$-group>>

<<Theorem: isomorphic group elements have same order>>

<<Theorem: $N$ and $G/N$ are $\pi$-groups $\implies$ $G$ is a $\pi$-group>>

<<Theorem: $\varphi\colon G_{1}\iso G_{2}$ and $g\in G_{1}$ is a $\pi$-element, then $\varphi(g)\in G_{2}$ is a $\pi$-element>>

<<Theorem: if $G_{1}$ is a $\pi$-group and $G_{1}\iso G_{2}$, then $G_{2}$ is a $\pi$-group>>

<<Theorem: $H\subgroup G$ and $N\normalSubgroup G$ both $\pi$-groups implies $H\cap N$ is a $\pi$-subgroup of $G$>>

<<Theorem: $H\subgroup G$ and $N\normalSubgroup G$ both $\pi$-groups implies $HN/N$ is a $\pi$-subgroup of $G$>>

<<Corollary: $H\subgroup G$ and $N\normalSubgroup G$ both $\pi$-groups implies $HN$ is a $\pi$-subgroup of $G$>>

<<Theorem: $\forall H_{1},H_{2}\subgroup G,\quad U(H_{1})=U(H_{2})\implies H_{1}\iso H_{2}$>>

<<Theorem: $H\subgroup B\subgroup G$ implies the underlying magma of $H$ is a strict subgroup of $B$>>

<<Theorem: $(N)_{B}\iso N$>>

<<Theorem: $\SymmetricGroup{2}$ is finite>>

<<Theorem: $\SymmetricGroup{2}$ is simple>>

<<Register: strict finite simple group>>
@


\begin{reserve}
We will reserve \lstinline{Pi} for some subset of prime numbers.
\end{reserve}

<<Reserve: $\pi$ for subset of primes>>=
reserve Pi for Subset of SetPrimes;
@

\begin{registration}
We now want to register the adjectives [[zero]], [[non zero]],
[[positive]] for [[Nat]]. Further, we want to register the fact that a
nonzero natural number is positive.
\end{registration}

<<Register: positive for [[Nat]]>>=
registration
  cluster positive for Nat;
  existence
  proof
    take 1;
    thus thesis;
  end;
  cluster zero for natural number;
  existence
  proof
    take 0;
    thus thesis;
  end;
  cluster non zero for Nat; :: natural number;
  existence
  proof
    take 1;
    thus thesis;
  end;
end;

registration
  cluster non zero -> positive for Nat;
  coherence
  proof
    for x being Nat st x is non zero holds x is positive
    proof
      let x be Nat;
      assume A1: x is non zero;
      then x <> 0;
      then x > 0;
      hence x is positive;
    end;
    hence thesis;
  end;
end;
@

Recall, the notion of a $p$-group is defined as:

\begin{mizar}
definition
  let p be Nat;
  let G be Group;
  attr G is p-group means :: GROUP_10:def 17
  ex r being Nat st card G = p |^ r;
end;
\end{mizar}

\begin{definition}\label{defn:002:pi-groups:pi-element}
Let $\pi$ be a set of primes, $G$ a group.
We call $g\in G$ a \define{$\pi$-Element} if
\begin{enumerate}
\item the element's order is nonzero $|g|\neq0$, and
\item every prime divisors $p\divides|g|$ belongs to $p\in\pi$.
\end{enumerate}
\end{definition}

\begin{def-remark}
In the literature, we often find $\pi'$ for the set of primes
\emph{except} those found in $\pi$. In Mizar, we would write
``\lstinline{Pi `}''. For this reason, I will not be defining an
``\lstinline{Pi'-element}''.
\end{def-remark}

%% \begin{theorem}[Sylow C {Issacs 1.12}]
%%   If $S$ and $T$ are Sylow $p$-subgroups of a finite group $G$,
%%   then there exists some $g\in G$ such that $T = S^{g} = g^{-1}Sg$.
%% \end{theorem}

%% \begin{theorem}[Sylow D (Isaacs 1.14)]
%% Let $P$ be a $p$-subgroup of a finite group $G$. Then $P$ is contained
%% in some Sylow $p$-subgroup of $G$.
%% \end{theorem}

<<Definition: $\pi$-element of Group>>=
:: Derek J.S. Rose, _A Course in the Theory of Groups_, note before
:: theorem 5.2.7
definition
  let Pi be Subset of SetPrimes;
  let G be Group;
  let IT be Element of G;
  attr IT is Pi-element means
  :Def1:
  (for p being prime Nat st p divides (ord IT) holds p in Pi) &
  (ord IT <> 0);
end;
@ %def Def1 Pi-element


\begin{theorem}
For any group $G$, its identity element is a $\pi$-element.
\end{theorem}

\begin{proof}[Proof sketch]
This is because the set of prime divisors to $|\idElt{G}|=1$ is the empty
set, which is a subset of every set. In particular, it's a subset to any
$\pi$.
\end{proof}

<<Theorem: $1_{G}$ is a $\pi$-element>>=
theorem Th1:
  for G being Group
  holds 1_G is Pi-element
proof
  let G be Group;
  A1: ord 1_G = 1 by GROUP_1:42;
  for p being prime Nat st p divides (ord 1_G) holds p in Pi
  proof
    let p be prime Nat;
    assume B1: p divides (ord 1_G);
    then p divides 1 by GROUP_1:42;
    then B2: p = 1 by INT_2:13;
    p > 1 by INT_2:def 4;
    then contradiction by B2;
    hence p in Pi;
  end;
  hence 1_G is Pi-element by A1,Def1;
end;
@

\begin{theorem}
  Let $G$ be a group, let $a,b\in G$. If $a$ is a $\pi$-element, and if
  $|a|=|b|$, then $b$ is a $\pi$-element.
\end{theorem}

<<Theorem: if $|a|=|b|$ and $a$ is a $\pi$-element, then $b$ is a $\pi$-element>>=
theorem Th2:
  for G being Group
  for a,b being Element of G
  st a is Pi-element & ord a = ord b
  holds b is Pi-element
proof
  let G be Group;
  let a,b be Element of G;
  assume A1: a is Pi-element;
  assume A2: ord a = ord b;
  A3: ord b <> 0 by A1,A2,Def1;
  for p being prime Nat st p divides (ord b) holds p in Pi by A1,A2,Def1;
  hence b is Pi-element by A3;
end;
@ %def Th2

\begin{theorem}
Let $G$ be a group, $a\in G$ be a $\pi$-element, and $b\in G$ arbitrary.
Then $b^{-1}ab$ is a $\pi$-element.
\end{theorem}

<<Theorem: $a$ is a $\pi$-element and $b$ arbitrary, then $b^{-1}ab$ is a $\pi$-element>>=
theorem
  for G being Group
  for a,b being Element of G
  st a is Pi-element
  holds a |^ b is Pi-element
proof
  let G be Group;
  let a,b be Element of G;
  assume A1: a is Pi-element;
  A2: ord (a |^ b) = ord a by GROUPP_1:6;
  hence thesis by A1,Th2;
end;
@

\begin{theorem}
If $a\in G$ is a $\pi$-element, then $a^{-1}$ is a $\pi$-element.
\end{theorem}

<<Theorem: $a\in G$ is a $\pi$-element implies $a^{-1}$ is a $\pi$-element>>=
theorem
  for G being Group
  for a,b being Element of G
  st a is Pi-element
  holds a" is Pi-element
proof
  let G be Group;
  let a,b be Element of G;
  assume A1: a is Pi-element;
  A2: ord (a") = ord a by GROUPP_1:5;
  hence thesis by A1,Th2;
end;
@



\begin{theorem}
Let $G$ be a group, $\pi$ a set of primes.
If $g\in G$ is a $\pi$-element and $h\in G$ such that $|h|$ divides $|g|$,
then $h$ is a $\pi$-element.
\end{theorem}

\begin{proof}[Proof sketch.]
Every prime divisor $p$ of $|h|$ is also a prime divisor of $|g|$.
But since $g$ is a $\pi$-element, it follows that $p\in\pi$ by Definition~\ref{defn:002:pi-groups:pi-element}.
Hence $h$ is a $\pi$-element.
\end{proof}

<<Theorem: if $g$ is a $\pi$-element and $|h|$ divides $|g|$, then $h$ is a $\pi$-element>>=
theorem
  for G being Group
  for g,h being Element of G
  st g is Pi-element
  holds ord h divides ord g implies h is Pi-element
proof
  let G be Group;
  let g,h be Element of G;
  assume A1: g is Pi-element;
  assume A2: ord h divides ord g;
  then A3: ord h <> 0 by A1,Def1,INT_2:3;
  for p being prime Nat st p divides ord h holds p in Pi
  proof
    let p be prime Nat;
    assume B1: p divides ord h;
    then p divides ord g by A2,INT_2:9;
    hence p in Pi by A1,Def1;
  end;
  hence h is Pi-element by A3,Def1;
end;
@

\begin{definition}[{\cite[ch.1\S1]{kurzweil2004theory}}]
Let $G$ be a finite group, $\pi$ be a set of prime numbers. We call $G$
a \define{$\pi$-Group} if every prime divisor of the group's order $p\divides|G|$
also belongs to $\pi\ni p$, or if the group is trivial $G=\trivialGroup$.
\end{definition}

\begin{def-remark}
We need to make this definition explicitly include the possibility of
the group being trivial, since Mizar defines ``prime''~\mml[def4]{int2}
as being greater than 1 \emph{and} its only divisors are 1 and itself.
\end{def-remark}

<<Definition: $\pi$-Group>>=
:: See, e.g., Kurzweil & Stellmacher, "Theory of finite groups", Ch.1 section 1, pg 9
definition
  let Pi be Subset of SetPrimes;
  let IT be Group;
  attr IT is Pi-group means
  :Def2:
  for g being Element of IT holds g is Pi-element;
end;
@ %def Def2 Pi-group

<<Register: Elements of a $\pi$-group are $\pi$-elements>>=
registration
  let Pi;
  let G be Pi-group Group;
  cluster -> Pi-element for Element of G;
  coherence by Def2;
end;
@

\begin{theorem}
  Let $\pi$ be any set of prime numbers.
  The trivial group is a $\pi$-group.
\end{theorem}

\begin{proof}[Proof sketch]
Since the only element in the trivial group is the identity element
$\idElt{G}$, which we proved to be a $\pi$-element. Thus the trivial
group is trivially a $\pi$-group.
\end{proof}

<<Theorem: the trivial group is $\pi$-group>>=
theorem Th2:
  for G being trivial Group
  holds G is Pi-group
proof
  let G be trivial Group;
  consider x being object such that
  A1: the carrier of G = {x} by GROUP_6:def 2;
  1_G in G by GROUP_2:def 7;
  then 1_G in {x} by A1;
  then A2: x = 1_G by TARSKI:def 1;
  A3: for g being Element of G holds g=1_G
  proof
    let g be Element of G;
    g in {1_G} by A1,A2;
    hence g=1_G by TARSKI:def 1;
  end;
  1_G is Pi-element by Th1;
  hence G is Pi-group by A3,Def2;
end;
@

\begin{registration}
We can now speak of the trivial $\pi$-group, and register this with Mizar.
\end{registration}

<<Registration: trivial group is $\pi$-group>>=
registration
  let Pi be Subset of SetPrimes;
  cluster Pi-group for trivial Group;
  existence
  proof
    take the trivial Group;
    thus thesis by Th2;
  end;
end;
@

\begin{theorem}
Let $G$ be a finite group, $\pi$ be a set of prime numbers.
Then $G$ is a $\pi$-group iff every prime divisor $p$ of $|G|$ belongs
to $\pi$.
\end{theorem}

<<Theorem: criteria for finite group to be $\pi$-group>>=
theorem Th7:
  for G being finite Group
  holds G is Pi-group iff (for p being prime Nat st p divides card G
                           holds p in Pi)
proof
  let G be finite Group;
  thus G is Pi-group implies (for p being prime Nat st p divides card G
                              holds p in Pi)
  proof
    assume B1: G is Pi-group;
    let p be prime Nat;
    assume B2: p divides card G;
    not p in Pi implies contradiction
    proof
      assume C1: not p in Pi;
      consider g being Element of G such that
      C2: ord g = p by B2,GROUP_10:11;
      g is Pi-element by B1,Def2;
      then p in Pi by C2,Def1;
      hence contradiction by C1;
    end;
    hence p in Pi;
  end;
  thus (for p being prime Nat st p divides card G holds p in Pi) implies
  G is Pi-group
  proof
    assume B1: for p being prime Nat st p divides card G holds p in Pi;
    for g being Element of G holds g is Pi-element
    proof
      let g be Element of G;
      not g is being_of_order_0 by GR_CY_1:6;
      then C1: ord g <> 0 by GROUP_1:def 11;
      ord g divides card G by GR_CY_1:8;
      then for p being prime Nat st p divides (ord g) holds p divides
      (card G) by NAT_D:4;
      then for p being prime Nat st p divides (ord g) holds p in Pi by B1;
      hence thesis by C1,Def1;
    end;
    hence G is Pi-group by Def2;
  end;
end;
@ %def Th7

\begin{theorem}
Let $G$ be a finite group, $\pi_{1}\subset\pi_{2}$ be sets of prime numbers.
If $G$ is a $\pi_{1}$-group, then $G$ is a $\pi_{2}$-group.
\end{theorem}

<<Theorem: if $\pi_{1}\subset\pi_{2}$ and $G$ is $\pi_{1}$-group, then $G$ is $\pi_{2}$-group>>=
theorem Th8:
  for Pi1,Pi2 being Subset of SetPrimes
  st Pi1 c= Pi2
  for G being finite Group
  st G is Pi1-group
  holds G is Pi2-group
proof
  let Pi1,Pi2 be Subset of SetPrimes;
  assume A1: Pi1 c= Pi2;
  let G be finite Group;
  assume A2: G is Pi1-group;
  A3: G is trivial or G is non trivial;
  per cases by A3;
  suppose G is trivial;
    hence thesis by Th2;
  end;
  suppose A4: G is non trivial;
    then for p being prime Nat st p divides card G holds p in Pi1 by A2,Th7;
    then for p being prime Nat st p divides card G holds p in Pi2 by A1;
    hence thesis by A4,Th7;
  end;
end;
@ %def Th8

<<Register: $\pi$-group for groups and finite groups>>=
registration
  let Pi be Subset of SetPrimes;
  cluster Pi-group for Group;
  existence
  proof
    take the trivial Group;
    thus thesis by Th2;
  end;
  cluster Pi-group for finite Group;
  existence
  proof
    take the trivial Group;
    thus thesis by Th2;
  end;
end;
@

\begin{registration}
We may sensibly talk about normal $\pi$-subgroups.
\end{registration}

<<Register: $\pi$-group for normal Subgroups>>=
registration
  let Pi be Subset of SetPrimes;
  let G be Group;
  cluster Pi-group for normal Subgroup of G;
  existence
  proof
    take N = (1).G;
    thus thesis by Th2;
  end;
  cluster Pi-group for Subgroup of G;
  existence
  proof
    take N = (1).G;
    thus thesis by Th2;
  end;
  cluster Pi-group for strict Subgroup of G;
  existence
  proof
    take N = (1).G;
    thus thesis by Th2;
  end;
end;
registration
  let Pi be Subset of SetPrimes;
  cluster trivial Pi-group for Group;
  existence
  proof
    take N = the trivial Group;
    thus thesis by Th2;
  end;
end;
registration
  let Pi be Subset of SetPrimes;
  let G be Group;
  cluster trivial Pi-group for Subgroup of G;
  existence
  proof
    take N = (1).G;
    thus thesis by Th2;
  end;
end;
@

\begin{theorem}
If $p\in\NN$ is prime, then $\{p\}$ is a subset of the collection of
prime numbers.
\end{theorem}

\begin{thm-remark}
We introduce this theorem for the purpose of a \emph{redefinition}\index{Redefine}
of Mizar's functor \lstinline|{p}| to have as its type \lstinline{Subset of SetPrimes}
rather than the generic \lstinline{set}.
\end{thm-remark}

<<Redefine: if $p$ is prime, then $\{p\}$ is subset of prime numbers>>=
theorem Th9:
  for p being prime Nat
  holds {p} is Subset of SetPrimes
proof
  let p be prime Nat;
  for x being object st x in {p} holds x in SetPrimes
  proof
    let x be object;
    assume A1: x in {p};
    then A2: x = p by TARSKI:def 1;
    p in SetPrimes by NEWTON:def 6;
    hence thesis by A2;
  end;
  then {p} c= SetPrimes by TARSKI:def 3;
  hence thesis;
end;

definition
  let p be prime Nat;
  redefine func {p} -> Subset of SetPrimes;
  coherence by Th9;
end;
@ %def Th9

\begin{theorem}
If $p\in\NN$ is prime and $G$ is a $\{p\}$-group, then $G$ is a Sylow $p$-group.
\end{theorem}

<<Theorem: If $G$ is a $\{p\}$-group, then it is Sylow $p$-group>>=
theorem Th10:
  for p being prime Nat
  for G being finite Group
  st G is {p}-group & G is non trivial
  holds (Omega).G is_Sylow_p-subgroup_of_prime p
proof
  let p be prime Nat;
  let G be finite Group;
  assume A1: G is {p}-group;
  assume A2: G is non trivial;
  A3: for q being prime Nat st q divides card G holds q=p
  proof
    let q be prime Nat;
    assume B1: q divides card G;
    then q in {p} by A1,Th7;
    hence q = p by TARSKI:def 1;
  end;
  A5: p divides card G
  proof
    B1: card G >= 1 by GROUP_1:45;
    G is non trivial implies card G <> 1 by GROUP_6:11;
    then card G <> 1 by A2;
    then B2: card G > 1 by B1,GROUP_6:11,NAT_2:19;
    then card G is non trivial by NAT_2:def 1;
    then card G >= 2 by NAT_2:29;
    then consider q being Element of NAT such that
    B3: q is prime and
    B4: q divides card G by INT_2:31;
    reconsider q as prime Nat by B3;
    q = p by A3,B4;
    hence thesis by B4;
  end;
  then consider P being strict Subgroup of G such that
  A4: P is_Sylow_p-subgroup_of_prime p by GROUP_10:10;
  A5: index P = 1
  proof
    B1: card G = (card P)*(index P) by GROUP_2:147;
    B2: not p divides index P by A4,GROUP_10:def 18;
    (ex q being Element of NAT st q is prime & q divides index P) implies contradiction
    proof
      given q being Element of NAT such that
      C1: q is prime & q divides index P;
      reconsider q1=q as prime Nat by C1;
      q1 divides (card P)*(index P) by C1,INT_2:2;
      then q1 divides (card G) by B1,C1;
      then C2: q1=p by A3;
      hence contradiction by B2,C1;
    end;
    then index P < 2 by INT_2:31;
    then not index P >= 2;
    then index P is trivial Nat by NAT_2:29;
    then B3: index P = 0 or index P = 1 by NAT_2:def 1;
    card G >= 1 by GROUP_1:45;
    then (card P)*(index P) >= 1 by B1;
    then index P >= 1 by B3,XREAL_1:64;
    hence index P = 1 by B3;
  end;
  A6: card G = (card P)*(index P) by GROUP_2:147
            .= card P by A5;
  P is p-group by A4,GROUP_10:def 18;
  then consider r being Nat such that
  A7: card P = p |^ r by GROUP_10:def 17;
  card G = p |^ r by A6,A7;
  then A8: card (Omega).G = p |^ r;
  then A9: (Omega).G is p-group by GROUP_10:def 17;
  not p divides index P by A4,GROUP_10:def 18;
  then not p divides 1 by A5;
  then A10: not p divides index (Omega).G by GROUP_2:150;
  hence (Omega).G is_Sylow_p-subgroup_of_prime p by A9,GROUP_10:def 18;
end;
@ %def Th10

\begin{theorem}
Let $p$ be a prime number, $G$ a finite group.
Then $G$ is a $p$-group if and only if $G$ is a $\{p\}$-group.
\end{theorem}

<<Theorem: $G$ is a $p$-group iff $G$ is a $\{p\}$-group>>=
theorem Th11:
  for p being prime Nat
  for G being finite Group
  holds G is p-group iff G is {p}-group
proof
  let p be prime Nat;
  let G be finite Group;

  thus G is p-group implies G is {p}-group
  proof
    assume B1: G is p-group;
    for g being Element of G holds g is {p}-element
    proof
      let g be Element of G;
      g is p-power by B1;
      then consider r being Nat such that
      C1: ord g = p |^ r by GROUPP_1:def 1;
      C2: for q being prime Nat st q divides ord g holds q in {p}
      proof
        let q be prime Nat;
        assume D1: q divides ord g;
        then q divides p |^ r by C1;
        then q = p by D1,NAT_3:6;
        hence q in {p} by TARSKI:def 1;
      end;
      not g is being_of_order_0 by GR_CY_1:6;
      then ord g <> 0 by GROUP_1:def 11;
      hence g is {p}-element by C1,C2,Def1;
    end;
    hence G is {p}-group by Def2;
  end;
  assume A1: G is {p}-group;
  A2: G is trivial or G is non trivial;
  per cases by A2;
  suppose A3: G is trivial;
    A4: card G = 1 by A3,GROUP_6:11
              .= p |^ 0 by NEWTON:4;
    hence thesis by GROUP_10:def 17;
  end;
  suppose A5: G is non trivial;
    then (Omega).G is_Sylow_p-subgroup_of_prime p by A1,Th10;
    then (Omega).G is p-group by GROUP_10:def 18;
    then consider r being Nat such that
    A6: card (Omega).G = p |^ r by GROUP_10:def 17;
    card (Omega).G = card (the carrier of G)
                  .= card G;
    then card G = p |^ r by A6;
    hence thesis by GROUP_10:def 17;
  end;
end;
@ %def Th11

\begin{theorem}
Let $H\subgroup G$ bea subgroup, $g\in G$ and $h\in H$.
If $g=h$, then for any $n\in\NN_{0}$ we have $g^{n}=h^{n}$.
\end{theorem}

<<Theorem: if $g\in G$ and $h\in H\subgroup G$ such that $g=h$, then for any $n\in\NN$ we have $g^{n}=h^{n}$>>=
theorem Th12:
  for G being Group
  for g being Element of G
  for H being Subgroup of G
  st g in H
  for h being Element of H st g = h
  for n being Nat holds h |^ n = g |^ n
proof
  let G be Group;
  let g be Element of G;
  let H be Subgroup of G;
  assume A1: g in H;
  let h be Element of H;
  assume A2: g = h;
  let n be Nat;
  defpred P[Nat] means h |^ $1 = g |^ $1;
  A3: P[0]
  proof
    g |^ 0 = 1_G by GROUP_1:25
          .= 1_H by GROUP_2:44
          .= h |^ 0 by GROUP_1:25;
    hence thesis;
  end;
  A4: for k being Nat st P[k] holds P[k+1]
  proof
    let k be Nat;
    assume B1: P[k];
    then g |^ k = h |^ k;
    then B2: g * (g |^ k) = h * (h |^ k) by A2,GROUP_2:43;
    g * (g |^ k) = g |^ (k+1) & h * (h |^ k) = h |^ (k+1) by GROUP_1:34;
    hence P[k+1] by B2;
  end;
  A5: for k being Nat holds P[k] from NAT_1:sch 2(A3,A4);
  hence h |^ n = g |^ n;
end;
@ %def Th12

\begin{lemma}
If $g\in G$, $h\in H\subgroup H$, $h=g$ and $|g|\neq0$, then $|g|=|h|$.
\end{lemma}

<<Lemma: $g=h$ and $|g|\neq0$ implies $|g|=|h|$>>=
Lm1:
  for G being Group
  for H being Subgroup of G
  for g being Element of G
  st not g is being_of_order_0 & g in H
  for h being Element of H
  st h = g
  holds ord g = ord h
proof
  let G be Group;
  let H be Subgroup of G;
  let g be Element of G;
  assume A1: not g is being_of_order_0;
  assume A2: g in H;
  let h be Element of H;
  assume A3: h = g;
  consider n being Nat such that
  A4: n = ord g
  by A1,GROUP_1:def 11;
  A5: n <> 0 & g |^ n = 1_G &
      for m being Nat st g |^ m = 1_G & m <> 0 holds n <= m
  by A1,A4,GROUP_1:def 11;
  A6: h |^ n = 1_H
  proof
    h |^ n = g |^ n by A2,A3,Th12
    .= 1_G by A5;
    hence thesis by GROUP_2:44;
  end;
  A7: for m being Nat st h |^ m = 1_H & m <> 0 holds n <= m
  proof
    let m be Nat;
    assume B1: h |^ m = 1_H;
    assume B2: m <> 0;
    g |^ m = h |^ m by A2,A3,Th12
          .= 1_H by B1
          .= 1_G by GROUP_2:44;
    hence thesis by A1,A4,B2,GROUP_1:def 11;
  end;
  A8: n <> 0 by A5;
  A9: not h is being_of_order_0 by A5,A6,GROUP_1:def 10;
  consider k being Nat such that
  A10: k = ord h by A9,GROUP_1:def 11;
  h |^ k = 1_H & k <> 0 & (for m being Nat st h |^ m = 1_H & m <> 0
  holds k <= m)
  by A9,A10,GROUP_1:def 11;
  then n <= k & k <= n by A6,A7,A8;
  then n = k by XXREAL_0:1;
  hence ord g = ord h by A4,A10;
end;
@ %def Lm1

\begin{theorem}
Let $g\in G$, $H\subgroup G$, and $h\in H$
If $g=h$, then $|g|=|h|$.
\end{theorem}

<<Theorem: for $g\in G$ and $h\in H\subgroup G$, if $g=h$, then $|g|=|h|$>>=
theorem Th13:
  for G being Group
  for H being Subgroup of G
  for g being Element of G
  st g in H
  for h being Element of H
  st h = g
  holds ord g = ord h
proof
  let G be Group;
  let H be Subgroup of G;
  let g be Element of G;
  assume A1: g in H;
  let h be Element of H;
  assume A2: h = g;
  A3: g is being_of_order_0 or not (g is being_of_order_0);
  per cases by A3;
  suppose not (g is being_of_order_0);
    hence thesis by A1,A2,A3,Lm1;
  end;
  suppose A4: g is being_of_order_0;
    (ex n being Nat st n <> 0 & h |^ n = 1_H) implies contradiction
    proof
      given n being Nat such that
      B1: n <> 0 & h |^ n = 1_H;
      g |^ n = h |^ n by A1,A2,Th12
            .= 1_H by B1
            .= 1_G by GROUP_2:44;
      hence contradiction by A4,B1,GROUP_1:def 10;
    end;
    then A5: h is being_of_order_0 by GROUP_1:def 10;
    then ord h = 0 & ord g = 0 by A4,GROUP_1:def 11;
    hence ord g = ord h;
  end;
end;
@ %def Th13

\begin{lemma}
If $H\subgroup G$ and $H$ is nontrivial, then $G$ is nontrivial.
\end{lemma}

<<Lemma: $H\subgroup G$ and $H$ is nontrivial implies $G$ is nontrivial>>=
Lm2:
for G being Group
for H being Subgroup of G st H is non trivial
holds G is non trivial
proof
  let G be Group;
  let H be Subgroup of G;
  assume A1: H is non trivial;
  A2: the carrier of H c= the carrier of G by GROUP_2:def 5;
  the multMagma of H <> (1).H by A1,LmGROUP226;
  then the carrier of H <> the carrier of (1).H by GROUP_2:61;
  then the carrier of H <> {1_H} by GROUP_2:def 7;
  then A3: the carrier of H <> {1_G} by GROUP_2:44;
  the multMagma of G <> (1).G
  proof
    (1).G is Subgroup of H by GROUP_2:65;
    then the carrier of (1).G c= the carrier of H by GROUP_2:def 5;
    then {1_G} c= the carrier of H by GROUP_2:def 7;
    then not (the carrier of H c= {1_G}) by A3,XBOOLE_0:def 10;
    then consider x being object such that
    B1: x in the carrier of H & not x in {1_G}
    by A1,A3,GROUP_2:65;
    B2: x in H by B1;
    B3: x in G by B2,GROUP_2:40;
    B5: x <> 1_G by B1,TARSKI:def 1;
    then B4: x in (the carrier of G) \ {1_G} by B1,B3,XBOOLE_0:def 5;
    then not (the carrier of G) c= {1_G} by B1,TARSKI:def 3;
    not x in {1_G} by B1;
    then not x in (1).G by GROUP_2:def 7;
    then ex y being object st y in G & not y in (1).G by B3;
    hence thesis by B3,B5,GROUP_2:def 6;
  end;
  hence G is non trivial by LmGROUP226;
end;
@

\begin{theorem}[{\cite[Th7.42]{rose1994course}}]
Let $G$ be a finite $\pi$-group. Then any subgroup $H\subgroup G$ is a
$\pi$-group.
\end{theorem}

<<Theorem: if $G$ is a $\pi$-group, then $H\subgroup G$ is a $\pi$-group>>=
theorem Th14:
  for G being Pi-group Group
  for H being Subgroup of G
  holds H is Pi-group
proof
  let G be Pi-group Group;
  let H be Subgroup of G;
  A1: H is trivial or H is non trivial;
  per cases by A1;
  suppose H is trivial;
    hence thesis by Th2;
  end;
  suppose A2: H is non trivial;
    then A3: G is non trivial by Lm2;
    for h being Element of H holds h is Pi-element
    proof
      let h be Element of H;
      B1: h in H;
      reconsider g=h as Element of G by GROUP_2:42;
      g in H by B1;
      then B2: ord g = ord h by Th13;
      B3: g is Pi-element by A3,Def2;
      then B4: ord g <> 0 by GR_CY_1:6,GROUP_1:def 11;
      for p being prime Nat st p divides (ord g) holds p in Pi by B3,Def1;
      then for p being prime Nat st p divides (ord h) holds p in Pi by B2;
      hence h is Pi-element by B2,B4,Def1;
    end;
    hence thesis by Def2;
  end;
end;
@ %def Th14

<<Register: subgroup of $\pi$-group is $\pi$-group>>=
registration
  let Pi;
  let G be Pi-group Group;
  cluster -> Pi-group for Subgroup of G;
  coherence by Th14;
end;
@

<<Register: quotient group of finite groups is finite>>=
registration
  let G be finite Group;
  let N be normal Subgroup of G;
  cluster G./.N -> finite;
  coherence
  proof
    for G being finite Group
    for N being normal Subgroup of G
    holds G./.N is finite;
    hence thesis;
  end;
end;
@

\begin{theorem}
  If $N\normalSubgroup G$ and $x\in N$ is $x=gN$ for some $g\in G$,
  then for any $n\in\NN$ we have $x^{n} = g^{n}N$.
\end{theorem}

<<Theorem: $x\in G/N$, $x=gN$, and $n\in\NN$ implies $x^{n}=g^{n}N$>>=
theorem Th15:
  for G being Group
  for N being normal Subgroup of G
  for x being Element of G./.N
  for n being Nat
  for g being Element of G
  st x = g*N
  holds x |^ n = (g |^ n)*N & x |^ n = N*(g |^ n)
proof
  let G be Group;
  let N be normal Subgroup of G;
  let x be Element of G./.N;
  let n be Nat;
  let g be Element of G;
  assume A1: x = g*N;
  defpred P[Nat] means x |^ $1 = (g |^ $1)*N;
  A2: P[0]
  proof
    x |^ 0 = 1_(G./.N) by GROUP_1:25
    .= carr N by GROUP_6:24
    .= (1_G)*N by GROUP_2:37
    .= (g |^ 0)*N by GROUP_1:25;
    then x |^ 0 = (g |^ 0)*N;
    hence thesis;
  end;
  A3: for k be Nat st P[k] holds P[k + 1]
  proof
    let k be Nat;
    assume P[k];
    then B1: x |^ k = (g |^ k)*N;
    then B2: x |^ (k + 1) = x * (x |^ k) by GROUP_1:34
    .= @x * ((g |^ k)*N) by B1,GROUP_6:def 3
    .= (g*N) * ((g |^ k)*N) by A1;
    B3: (g * N) * ((g |^ k)*N) = g * N * (g |^ k)*N by GROUP_2:30,31
    .= g * (N * (g |^ k)) * N by GROUP_2:31
    .= g * ((g |^ k) * N) * N by GROUP_3:117
    .= g * (((g |^ k) * N) * N) by GROUP_2:29
    .= g * ((g |^ k) * (N * N)) by GROUP_4:45
    .= g * ((g |^ k) * N) by GROUP_2:76
    .= (g * (g |^ k)) * N by GROUP_2:32
    .= (g |^ (k + 1)) * N by GROUP_1:34;
    then (g |^ (k + 1))*N = x |^ (k + 1) by B2;
    hence thesis by A1,B2;
  end;

  A4: for k being Nat holds P[k] from NAT_1:sch 2(A2,A3);
  thus x |^ n = (g |^ n)*N by A4;
  (g |^ n)*N = N*(g |^ n) by GROUP_3:117;
  hence x |^ n = N*(g |^ n) by A4;
end;
@ %def Th15

\begin{theorem}
Let $N\normalSubgroup G$. If $x\in G/N$ and $g\in G$ such that $x=gN$,
then $|x|\divides|g|$.
\end{theorem}

<<Theorem: $x\in G/N$ and $g\in G$ such that $x=gN$, then $|x|\divides|g|$>>=
theorem Th16:
  for G being Group
  for N being normal Subgroup of G
  for x being Element of G./.N
  for g being Element of G st x = g*N
  holds ord x divides ord g
proof
  let G be Group;
  let N be normal Subgroup of G;
  let x be Element of G./.N;
  let g be Element of G;
  assume A1: x = g*N;
  set n = ord g;
  A2: x |^ n = (g |^ n)*N by A1,Th15
            .= (1_G)*N by GROUP_1:41
            .= carr N by GROUP_2:37
            .= 1_(G./.N) by GROUP_6:24;
  hence ord x divides n by GROUP_1:44;
end;
@ %def Th16

\begin{lemma}
The finite cyclic group $\cyclicGroup{2}$ is nontrivial.
\end{lemma}

<<Lemma: existence of finite nontrivial group>>=
LmFiniteNontrivial:
  not INT.Group(2) is trivial
proof
  set G = INT.Group(2);
  G is finite;
  then G is trivial implies card G = 1 by GROUP_6:11;
  then A1: not card G = 1 implies not G is trivial;
  card G = card (Segm 2) by GR_CY_1:def 5
  .= card 2 by ORDINAL1:def 17
  .= 2;
  hence not INT.Group(2) is trivial by A1;
end;
@

\begin{registration}
We can use ``\lstinline{non trivial}'' as an adjective for finite groups.
\end{registration}

<<Register: non-trivial for finite group>>=
<<Lemma: existence of finite nontrivial group>>
registration
  cluster non trivial for finite Group;
  existence by LmFiniteNontrivial;
end;

@

\begin{theorem}
If $G\neq\trivialGroup$, then there is a $g\in G$ which is not the
identity element.
\end{theorem}

<<Theorem: $G\neq\trivialGroup\implies\exists g\in G,g\neq\idElt{G}$>>=
<<Register: non-trivial for finite group>>

theorem Th17:
  for G being non trivial Group
  ex g being Element of G st g <> 1_G
proof
  let G be non trivial Group;
  A1: the carrier of G <> {1_G} by GROUP_6:def 2;
  (1).G is Subgroup of G;
  then the carrier of (1).G c= the carrier of G by GROUP_2:def 5;
  then {1_G} c= the carrier of G by GROUP_2:def 7;
  then not (the carrier of G c= {1_G}) by A1,XBOOLE_0:def 10;
  then consider x being object such that
  A2: x in the carrier of G & not x in {1_G};
  x in G by A2;
  then reconsider g=x as Element of G;
  take g;
  thus g <> 1_G by A2,TARSKI:def 1;
end;
@ %def Th17

\begin{lemma}
Let $N\normalSubgroup G$. If $G/N$ is nontrivial, then $G$ is nontrivial.
\end{lemma}

<<Lemma: if $G/N$ is nontrivial, then $G$ is nontrivial>>=
Lm3:
  for G being Group
  for N being normal Subgroup of G
  st G./.N is non trivial
  holds G is non trivial
proof
  let G be Group;
  let N be normal Subgroup of G;

  assume A1: G./.N is non trivial;
  then consider S being Element of G./.N such that
  A2: S <> 1_(G./.N) by Th17;
  consider g being Element of G such that
  A3: S = g*N & S = N*g by GROUP_6:21;
  g*N <> 1_(G./.N) by A2,A3;
  then g*N <> carr N by GROUP_6:24;
  then A4: not g in N by GROUP_2:113;
  1_G in N by GROUP_2:46;
  then g <> 1_G by A4;
  hence G is non trivial by GROUP_6:def 2;
end;

@

\begin{theorem}[{\cite[Th7.42]{rose1994course}}]\label{thm:002:quotient-of-pi-group-is-pi-group}
Let $G$ be a $\pi$-group. If $N\normalSubgroup G$, then $G/N$ is
a $\pi$-group.
\end{theorem}

<<Theorem: if $G$ is a $\pi$-group and $N\normalSubgroup G$, then $G/N$ is a $\pi$-group>>=
<<Lemma: if $G/N$ is nontrivial, then $G$ is nontrivial>>
theorem Th18:
  for G being Pi-group Group
  for N being normal Subgroup of G
  holds G./.N is Pi-group
proof
  let G be Pi-group Group;
  let N be normal Subgroup of G;
  for x being Element of G./.N
  holds x is Pi-element
  proof
    let x be Element of G./.N;
    x in G./.N;
    then consider g being Element of G such that
    B1: x = g*N & x = N*g by GROUP_6:23;
    B2: g is Pi-element by Def2;
    B3: ord x divides ord g by B1,Th16;
    then B4: ord x <> 0 by B2,B3,Def1,INT_2:3;
    for p being prime Nat st p divides (ord x) holds p in Pi
    proof
      let p be prime Nat;
      assume C1: p divides (ord x);
      then p divides (ord g) by B3,NAT_D:4;
      hence p in Pi by B2,Def1;
    end;
    hence x is Pi-element by B4,Def1;
  end;
  hence G./.N is Pi-group by Def2;
end;
@ %def Th18

\begin{theorem}
  Let $G_{1}$, $G_{2}$ be groups and $g\in G$.
  If $\varphi\colon G_{1}\iso G_{2}$ is an isomorphism, then
  $|g| = |\varphi(g)|$.
\end{theorem}

<<Theorem: isomorphic group elements have same order>>=
theorem Th19:
  for G1,G2 being Group
  for phi being Homomorphism of G1,G2
  st phi is bijective
  for g being Element of G1
  holds ord g = ord (phi.g)
proof
  let G1,G2 be Group;
  let phi be Homomorphism of G1,G2;
  assume A1: phi is bijective;
  let g be Element of G1;
  A2: phi" is Homomorphism of G2,G1 by A1,GROUP_6:62;
  then reconsider psi=phi" as Homomorphism of G2,G1;
  set n = ord g;
  A3: g |^ n = 1_G1 by GROUP_1:41;
  A4: (phi.g) |^ n = phi.(g |^ n) by GROUP_6:36
              .= phi.(1_G1) by A3
              .= 1_G2 by GROUP_6:31;
  then A5: ord (phi.g) divides n by GROUP_1:44;
  set n2 = ord (phi.g);
  set g2 = phi.g;
  A6: g = (phi").(phi.g) by A1,A2,FUNCT_2:26;
  g |^ n2 = psi.g2 |^ n2 by A1,A2,A6
         .= (phi").((phi.g) |^ n2) by GROUP_6:36
         .= (phi").(1_G2) by GROUP_1:41
         .= 1_G1 by A2,GROUP_6:31;
  then n divides ord (phi.g) by A4,GROUP_1:44;
  hence ord g = ord (phi.g) by A5,NAT_D:5;
end;
@ %def Th19

\begin{theorem}
Let $N\normalSubgroup G$ and $\pi$ be a set of primes.
If $G/N$ is a $\pi$-group, then $G$ is a $\pi$-group.
\end{theorem}

<<Theorem: $N$ and $G/N$ are $\pi$-groups $\implies$ $G$ is a $\pi$-group>>=
theorem
  for G being finite Group
  for N being normal Subgroup of G
  st N is Pi-group
  holds G./.N is Pi-group implies G is Pi-group
proof
  let G be finite Group;
  let N be normal Subgroup of G;
  assume A1: N is Pi-group;
  assume A2: G./.N is Pi-group;
  A3: card G = (card N)*(index N) by GROUP_2:147
            .= (card N)*(card (G./.N)) by GROUP_6:27;
  for p being prime Nat
  st p divides card G
  holds p in Pi
  proof
    let p be prime Nat;
    assume B1: p divides card G;
    then p divides card N or p divides card (G./.N) by A3,NEWTON:80;
    hence thesis by A1,A2,Th7;
  end;
  hence G is Pi-group by Th7;
end;
@

\begin{theorem}
If $\varphi\colon G_{1}\to G_{2}$ is an isomorphism and $g\in G_{1}$
is a $\pi$-element, then $\varphi(g)\in G_{2}$ is a $\pi$-element.
\end{theorem}

<<Theorem: $\varphi\colon G_{1}\iso G_{2}$ and $g\in G_{1}$ is a $\pi$-element, then $\varphi(g)\in G_{2}$ is a $\pi$-element>>=
theorem Th21:
  for G1,G2 being Group
  for g being Element of G1 st g is Pi-element
  for phi being Homomorphism of G1,G2
  st phi is bijective
  holds phi.g is Pi-element
proof
  let G1,G2 be Group;
  let g be Element of G1;
  assume A1: g is Pi-element;
  let phi be Homomorphism of G1,G2;
  assume A2: phi is bijective;
  then A3: ord g = ord (phi.g) by Th19;
  A4: ord g <> 0 by A1,Def1;
  for p being prime Nat st p divides ord (phi.g) holds p in Pi
  proof
    let p be prime Nat;
    assume B1: p divides ord (phi.g);
    then p divides ord g by A3;
    hence p in Pi by A1,Def1;
  end;
  hence phi.g is Pi-element by A3,A4,Def1;
end;
@ %def Th21

\begin{theorem}
Let $G_{1}$ be a $\pi$-group and $G_{1}\iso G_{2}$ be isomorphic groups.
Then $G_{2}$ is a $\pi$-group.
\end{theorem}

\begin{proof}[Proof sketch]
We consider some isomorphism $\varphi\colon G_{1}\to G_{2}$.
Since $G_{1}$ is a $\pi$-group, every element $g\in G_{1}$ is a
$\pi$-element. But we just proved this implies $\varphi(g)\in G_{2}$
is a $\pi$-element. Since $\varphi$ is a bijection, that establishes
every element of $G_{2}$ is a $\pi$-element, thus $G_{2}$ is a
$\pi$-group by definition.
\end{proof}

<<Theorem: if $G_{1}$ is a $\pi$-group and $G_{1}\iso G_{2}$, then $G_{2}$ is a $\pi$-group>>=
theorem Th22:
  for G1,G2 being Group
  st G1,G2 are_isomorphic & G1 is Pi-group
  holds G2 is Pi-group
proof
  let G1,G2 be Group;
  assume A1: G1,G2 are_isomorphic;
  assume A2: G1 is Pi-group;
  consider phi being Homomorphism of G1,G2 such that
  A3: phi is bijective by A1,GROUP_6:def 11;
  reconsider psi=phi" as Homomorphism of G2,G1 by A3,GROUP_6:62;
  A4: psi is bijective by A3,GROUP_6:63;
  phi is onto & phi is one-to-one by A3;
  then A5: the carrier of G1 <> {} & the carrier of G2 <> {} &
    rng phi = the carrier of G2 & phi is one-to-one &
    rng psi = the carrier of G1 & psi is one-to-one
    by A4,FUNCT_2:def 3;
  for g2 being Element of G2 holds g2 is Pi-element
  proof
    let g2 be Element of G2;
    psi is bijective by A3,GROUP_6:63;
    then phi * phi" = id (the carrier of G2) by A3,A4,A5,FUNCT_2:29;
    then phi * psi = id (the carrier of G2);
    then phi = psi" by A5,FUNCT_2:30,FUNCT_2:def 3;
    then B1: phi.(psi.g2) = g2 by A3,FUNCT_2:26;
    psi.g2 is Element of G1;
    psi.g2 is Pi-element by A4,Th21,A2,Def2;
    then phi.(psi.g2) is Pi-element by A3,Th21;
    hence g2 is Pi-element by B1;
  end;
  hence G2 is Pi-group by Def2;
end;
@ %def Th22

\begin{theorem}
If $H$, $N$ are $\pi$-subgroups of $G$, and $N\normalSubgroup G$,
then $H\cap N$ is a $\pi$-subgroup of $G$.
\end{theorem}

<<Theorem: $H\subgroup G$ and $N\normalSubgroup G$ both $\pi$-groups implies $H\cap N$ is a $\pi$-subgroup of $G$>>=
:: Derek J.S. Rose, _A Course in the Theory of Groups_, section 9.1,
:: in passim
theorem Th23:
  for G being Group
  for H being Subgroup of G
  for N being normal Subgroup of G
  st H is Pi-group & N is Pi-group
  holds H /\ N is Pi-group
proof
  let G be Group;
  let H be Subgroup of G;
  let N be normal Subgroup of G;
  assume A1: H is Pi-group;
  assume A2: N is Pi-group;
  H /\ N is Subgroup of H by GROUP_6:9;
  hence H /\ N is Pi-group by A1,Th14;
end;
@ %def Th23

\begin{theorem}
If $H$, $N$ are $\pi$-subgroups of $G$, and $N\normalSubgroup G$,
then $HN/N$ is a $\pi$-subgroup of $G$.
\end{theorem}

\begin{proof}[Proof sketch]
We know that $HN/N\iso H/(H\cap N)$ by the second isomorphism theorem.
We know $H$ is a $\pi$-group and $H\cap N\normalSubgroup H$, which means
we know from Theorem~\ref{thm:002:quotient-of-pi-group-is-pi-group}
$H/(H\cap N)$ is a $\pi$-group. Hence the result.
\end{proof}

<<Theorem: $H\subgroup G$ and $N\normalSubgroup G$ both $\pi$-groups implies $HN/N$ is a $\pi$-subgroup of $G$>>=
:: Derek J.S. Rose, _A Course in the Theory of Groups_, section 9.1,
:: in passim
theorem Th24:
  for G being Group
  for H being Subgroup of G
  for N being strict normal Subgroup of G
  st H is Pi-group & N is Pi-group
  holds (H "\/" N)./.((H "\/" N,N)`*`) is Pi-group
proof
  let G be Group;
  let H be Subgroup of G;
  let N be strict normal Subgroup of G;
  assume A1: H is Pi-group;
  assume A2: N is Pi-group;
  A3: (H "\/" N)./.((H "\/" N,N)`*`), H./.(H /\ N) are_isomorphic
  by GROUP_6:81;
  H./.(H /\ N) is Pi-group by A1,Th18;
  hence (H "\/" N)./.((H "\/" N,N)`*`) is Pi-group by A3,Th22;
end;
@ %def Th24

\begin{corollary}
If $H$, $N$ are $\pi$-subgroups of $G$, and $N\normalSubgroup G$,
then $HN$ is a $\pi$-subgroup of $G$.
\end{corollary}

<<Corollary: $H\subgroup G$ and $N\normalSubgroup G$ both $\pi$-groups implies $HN$ is a $\pi$-subgroup of $G$>>=
:: Derek J.S. Rose, _A Course in the Theory of Groups_, section 9.1,
:: in passim
theorem Th25:
  for G being finite Group
  for H being Subgroup of G
  for N being strict normal Subgroup of G
  st H is Pi-group & N is Pi-group
  holds H "\/" N is Pi-group
proof
  let G be finite Group;
  let H be Subgroup of G;
  let N be strict normal Subgroup of G;
  assume A1: H is Pi-group;
  assume A2: N is Pi-group;
  A3: (H "\/" N)./.((H "\/" N,N)`*`) is Pi-group by A1,A2,Th24;

  A4: card(H "\/" N)
   = index((H "\/" N,N)`*`) * card((H "\/" N,N)`*`) by GROUP_2:147
  .= card((H "\/" N)./.((H "\/" N,N)`*`)) * card((H "\/" N,N)`*`) by GROUP_6:27;
  A5: N is Subgroup of H "\/" N by GROUP_4:60;

  A6: card ((H "\/" N,N)`*`)
  = card (the carrier of ((H "\/" N,N)`*`)) by STRUCT_0:def 16
  .= card (the carrier of N) by A5,GROUP_6:def 1
  .= card N by STRUCT_0:def 16;

  for p being prime Nat
  st p divides card(H "\/" N)
  holds p in Pi
  proof
    let p be prime Nat;
    assume B1: p divides card(H "\/" N);
    then p divides card((H "\/" N)./.((H "\/" N,N)`*`)) * card((H "\/" N,N)`*`)
    by A4;
    then p divides card((H "\/" N)./.((H "\/" N,N)`*`)) or
    p divides card((H "\/" N,N)`*`) by NEWTON:80;
    then per cases;
    suppose p divides card((H "\/" N)./.((H "\/" N,N)`*`));
      hence p in Pi by A3,Th7;
    end;
    suppose B2: p divides card((H "\/" N,N)`*`);
      then p divides card N by A6;
      hence p in Pi by A2,Th7;
    end;
  end;

  hence H "\/" N is Pi-group by Th7;
end;
@ %def Th25

\begin{theorem}
If $H_{1}\subgroup G$ and $H_{2}\subgroup G$ have the same underlying
sets $U(H_{1})=U(H_{2})$, then $H_{1}\iso H_{2}$.
\end{theorem}

\begin{thm-remark}
This theorems seems silly, but we really care more about isomorphic
groups than strict equality. And it's a bit trickier to prove formally
than one would expect!
\end{thm-remark}

\begin{proof}[Proof outline]
We establish that $f\colon H_{1}\to H_{2}$ defined by $f(x)=x$ for every
$x\in U(H_{1})$ is a homomorphism. Then we prove it is
bijective. Injectivity is easy. But surjectivity requires a separate
subproof. Thus we establish an isomorphism $f$ exists, which proves the
theorem.
\end{proof}

<<Theorem: $\forall H_{1},H_{2}\subgroup G,\quad U(H_{1})=U(H_{2})\implies H_{1}\iso H_{2}$>>=
theorem Th26:
  for G being Group
  for H1,H2 being Subgroup of G
  st the carrier of H1 = the carrier of H2
  holds H1,H2 are_isomorphic
proof
  let G be Group;
  let H1,H2 be Subgroup of G;
  assume A1: the carrier of H1 = the carrier of H2;
  A2: id H1 is Function of H1,H2 by A1;
  reconsider f=id H1 as Function of H1,H2 by A1;
  A3: for x being Element of H1 holds f.x = (id H1).x;
  A5: the multF of H1 = (the multF of G)||the carrier of H1 by GROUP_2:def 5
                     .= (the multF of G)||the carrier of H2 by A1
                     .= the multF of H2 by GROUP_2:def 5;
  for a, b being Element of H1 holds f.(a * b) = f.a * f.b
  proof
    let a, b be Element of H1;
    f.(a * b) = (id H1).(a * b) by A3
             .= (id H1).a * (id H1).b by GROUP_6:38
             .= (the multF of H2).((id H1).a, (id H1).b) by A5
             .= (the multF of H2).((id H1).a, f.b) by A3
             .= (the multF of H2).(f.a, f.b) by A3
             .= f.a * f.b by A3;
    hence f.(a * b) = f.a * f.b;
  end;
  then A6: f is Homomorphism of H1,H2 by A2,GROUP_6:def 6;
  then reconsider f as Homomorphism of H1,H2;
  A7: f is one-to-one;
  f is onto
  <<Proof: $\id\colon H_{1}\to H_{2}$ is onto>>
  then f is bijective by A7;
  hence H1,H2 are_isomorphic by A3,GROUP_6:def 11;
end;
@ %def Th26

\begin{proof}[Proof step ($\id\colon H_{1}\to H_{2}$ is onto)]
We need to prove that $\id_{H_{1}}(H_{1})=H_{2}$, which we do by
appealing directly to the axiom of extensionality: every element in the
image is also an element of $H_{2}$.
\end{proof}

<<Proof: $\id\colon H_{1}\to H_{2}$ is onto>>=
proof
  B1: rng f = the carrier of (Image f) by GROUP_6:44;
  B2: for y being object
  holds y in H2 iff ex a being Element of H1 st y = f.a
  proof
    let y be object;
    thus y in H2 implies ex a being Element of H1 st y = f.a
    proof
      assume y in H2;
      then reconsider y as Element of H2;
      consider a being Element of H1 such that
      Z1: a = y by A1;
      take a;
      f.a = (id H1).a
         .= a
         .= y by Z1;
      hence thesis;
    end;
    given a being Element of H1 such that
    Z2: y = f.a;
    thus y in H2 by Z2;
  end;
  for x being object
  holds x in the carrier of Image f iff x in the carrier of H2
  proof
    let x be object;
    thus x in the carrier of Image f implies x in the carrier of H2
    proof
      assume x in the carrier of Image f;
      then x in Image f;
      then consider a being Element of H1 such that
      Z1: x = f.a by GROUP_6:45;
      x in H2 by Z1,B2;
      hence thesis;
    end;
    assume x in the carrier of H2;
    then x in H2;
    then consider a being Element of H1 such that
    Z2: x = f.a by B2;
    x in Image f by Z2,GROUP_6:45;
    hence thesis;
    end;
  then the carrier of Image f = the carrier of H2 by TARSKI:2;
  hence thesis by B1;
end;
@

\begin{theorem}
Let $H\subgroup G$, $B\subgroup G$.
If $H\subgroup B$, then the underlying magma of $H$ is a strict subgroup of $B$.
\end{theorem}

<<Theorem: $H\subgroup B\subgroup G$ implies the underlying magma of $H$ is a strict subgroup of $B$>>=
theorem Th27:
  for G being Group
  for H,B being Subgroup of G
  st H is Subgroup of B
  holds the multMagma of H is strict Subgroup of B
proof
  let G be Group;
  let H,B be Subgroup of G;
  assume A1: H is Subgroup of B;
  then reconsider H1=H as Subgroup of B;
  A2: H1 is Group-like non empty multMagma;
  H1 is Subgroup of B;
  then A3: the carrier of H1 c= the carrier of B &
       the multF of H1 = (the multF of B)||the carrier of H1 by GROUP_2:def 5;

  A4: multMagma(# the carrier of H1, the multF of H1 #) is Subgroup of B
  proof
    the carrier of multMagma(# the carrier of H1, the multF of H1 #)
    = the carrier of H1;
    then B1: the carrier of multMagma(# the carrier of H1, the multF of H1 #)
    c= the carrier of B by A3;
    the multF of multMagma(# the carrier of H1, the multF of H1 #)
    = the multF of H1;
    then B2: the multF of multMagma(# the carrier of H1, the multF of H1 #)
    = (the multF of B)||(the carrier of multMagma(# the carrier of H1, the multF of H1 #)) by A3;
    multMagma(# the carrier of H1, the multF of H1 #) is Group-like non
    empty multMagma;
    hence thesis by B1,B2,GROUP_2:def 5;
  end;
  hence thesis;
end;

@ %def Th27

\begin{theorem}
Let $N\normalSubgroup G$ and $B\subgroup G$. If $N\subgroup B$, then
$(N)_{B}\iso N$.
\end{theorem}

<<Theorem: $(N)_{B}\iso N$>>=
theorem
  for G being Group
  for B being Subgroup of G
  for N being normal Subgroup of G
  st N is Subgroup of B
  holds (B,N)`*`, N are_isomorphic
proof
  let G be Group;
  let B be Subgroup of G;
  let N be normal Subgroup of G;
  assume A1: N is Subgroup of B;
  then reconsider N1=the multMagma of N as strict Subgroup of B by Th27;
  N1 = (B,N)`*` by GROUP_6:def 1;
  hence thesis by Th26;
end;

@

\begin{theorem}
The symmetric group $\SymmetricGroup{2}$ is finite.
\end{theorem}

<<Theorem: $\SymmetricGroup{2}$ is finite>>=
theorem Th29:
  Group_of_Perm 2 is strict finite Group
proof
  set G = Group_of_Perm 2;
  A1: G is strict Group;
  Permutations 2 is finite;
  then the carrier of G is finite by MATRIX_1:def 13;
  hence thesis;
end;
@ %def Th29

\begin{theorem}
The symmetric group $\SymmetricGroup{2}$ is simple.
\end{theorem}

<<Theorem: $\SymmetricGroup{2}$ is simple>>=
theorem Th30:
  Group_of_Perm 2 is simple
proof
  set G = Group_of_Perm 2;
A1: for H being strict normal Subgroup of G
  st H <> (Omega).G & H <> (1).G
  holds contradiction
  proof
    let H be strict normal Subgroup of G;
    assume
A2: H <> (Omega).G;
    assume
A3: H <> (1).G;
    1_G in H by GROUP_2:46;
    then 1_G in the carrier of H by STRUCT_0:def 5;
    then {1_G} c= the carrier of H by ZFMISC_1:31;
    then {<*1,2*>} c= the carrier of H by FINSEQ_2:52,MATRIX_1:15;
    then
A4: <*1,2*> in the carrier of H by ZFMISC_1:31;
    the carrier of H c= the carrier of G by GROUP_2:def 5;
    then
A5: the carrier of H c= {<*1,2*>,<*2,1*>} by MATRIX_1:def 13,MATRIX_7:3;
    per cases by A5,ZFMISC_1:36;
    suppose
      the carrier of H = {};
      hence contradiction;
    end;
    suppose
      the carrier of H = {<*1,2*>};
      then {1_G} = the carrier of H by FINSEQ_2:52,MATRIX_1:15;
      hence contradiction by A3,GROUP_2:def 7;
    end;
    suppose
      the carrier of H = {<*2,1*>};
      then <*2,1*>.1 = <*1,2*>.1 by A4,TARSKI:def 1;
      then 2 = <*1,2*>.1 by FINSEQ_1:44;
      hence contradiction by FINSEQ_1:44;
    end;
    suppose
      the carrier of H = {<*1,2*>,<*2,1*>};
      then the carrier of H = the carrier of G by MATRIX_1:def 13,MATRIX_7:3;
      hence contradiction by A2,GROUP_2:61;
    end;
  end;
  now
    assume G is trivial;
    then consider e be object such that
A6: the carrier of G = {e} by GROUP_6:def 2, GROUP_2:def 7;
    Permutations 2 = {e} by A6,MATRIX_1:def 13;
    then <*2,1*> = <*1,2*> by MATRIX_7:3,ZFMISC_1:5;
    then 2 = <*1,2*>.1 by FINSEQ_1:44;
    hence contradiction by FINSEQ_1:44;
  end;
  hence thesis by A1,GROUP_9:def 12;
end;
@ %def Th30

\begin{registration}
We have ``\lstinline{strict finite simple}'' for ``\lstinline{Group}''
be a sensible string of adjectives.
\end{registration}

<<Register: strict finite simple group>>=
registration
  cluster strict finite simple for Group;
  existence by Th29,Th30;
end;
@

\begin{theorem}
Let $n\in\NN$. Since the collection of even elements of $\SymmetricGroup{n}$
is closed under multiplication and inversion, they form a subgroup of $\SymmetricGroup{n}$.
\end{theorem}

<<Theorem: existence of alternating group>>=
theorem Th31:
  for n being Nat
  for G being Group_of_Perm n
  for A being Subset of G st A <> {} &
  (for g being Element of G st g is even Permutation of (Seg n) holds g in A)
  holds (for g1, g2 being Element of G st g1 in A & g2 in A holds
  g1 * g2 in A) & (for g being Element of G st g in A holds g" in A);
@ %def Th31

\begin{definition}
Let $n\in\NN$. We define the \define{Alternating Group} of $n$ elements
to be the subgroup of $\SymmetricGroup{n}$ consisting of even permutations.
\end{definition}

<<Definition: Alternating Group>>=
definition
  let n be Nat;
  func AlternatingGroup(n) -> strict Subgroup of Group_of_Perm n means :Def3:
  for g being Element of Group_of_Perm n holds
  g in it iff g is even;
  existence by Th31,GROUP_2:52;
  uniqueness;
end;
@
