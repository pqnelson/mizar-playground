% -*- mode: poly-noweb; noweb-code-mode: mizar-mode; -*-

\section{Sylow $\pi$-Groups}

We can generalize the notions surrounding Sylow's theorems from
$p$-groups to $\pi$-groups. Towards this end, we will consciously follow
the conventions laid down in the Mizar article concerning Sylow
theorems~\mml{group10}.

Note that, while Sylow $\pi$-subgroups always exist, they usually are
not conjugate to one another (unless $\pi=\{p\}$ is a singleton). For
this reason, people usually prefer working with Hall $\pi$-subgroups.
And if $G$ is finite, then these two notions (Hall $\pi$-subgroups and
Sylow $\pi$-subgroups) coincide.

<<Sylow $\pi$-Groups>>=
<<Definition: predicate ``is Sylow $\pi$-subgroup of primes [$\pi$]''>>

<<Theorem: Existence of Sylow $\pi$-subgroup>>

<<Definition: $\SylowSubgroups{\pi}{G}$ the collection of Sylow $\pi$-Subgroups>>

<<Register: $\SylowSubgroups{\pi}{G}$ is non empty set>>
@

% Derek J.S. Robinson, _A Course in the Theory of Groups_, section 9.1
\begin{definition}
Let $G$ be a group and $\pi$ be a set of primes. We define a
\define{Sylow $\pi$-Subgroup} to be a $\pi$-subgroup $H\subgroup G$ such
that it is a maximal $\pi$-subgroup, i.e., for any $\pi$-subgroup
$K\subgroup G$ such that $H\subgroup K$ has $K=H$ or $K=G$.
\end{definition}

\begin{def-remark}
This is defined in direct analogy to Definition~\mml[def18]{group10}.
\end{def-remark}

\begin{def-remark}
We will follow the formalization of maximal subgroups in
Definition~\mml[def6]{group4}:
\begin{mizar}
definition
  let G, H;
  attr H is maximal means
  :: GROUP_4:def 6
  the multMagma of H <> the multMagma of G &
for K being strict Subgroup of G
  st the multMagma of H <> K & H is Subgroup of K
  holds K = the multMagma of G;
end;
\end{mizar}
Thus, we need to assert being proper and any other distinct subgroup
satisfying the desired property must be a $\pi$-subgroup.
\end{def-remark}

\begin{def-remark}
This seems to be a relatively ``obscure'' definition, presumably because
for finite groups it coincides with Hall $\pi$-subgroups. For further
information about this, see Robinson~\cite{robinson1995course} and the
bibliography for the entry ``Sylow subgroup'' in the
\emph{Encyclopedia of Mathematics}\footnote{\url{https://encyclopediaofmath.org/wiki/Sylow_subgroup}}.
\end{def-remark}

<<Definition: predicate ``is Sylow $\pi$-subgroup of primes [$\pi$]''>>=
:: Derek J.S. Robinson, _A Course in the Theory of Groups_, section 9.1
definition
  let Pi be Subset of SetPrimes;
  let G be Group;
  let IT be Subgroup of G;
  pred IT is_Sylow_pi-subgroup_of_primes Pi means
  :Def3:
  IT is Pi-group & :: the multMagma of IT <> the multMagma of G &
  for P being strict Subgroup of G
  st P is Pi-group & P <> the multMagma of G & IT is Subgroup of P
  holds P = the multMagma of IT;
end;
@ %def is_Sylow_pi-subgroup_of_primes

\begin{voc}
We need to add \lstinline{is_Sylow_pi-subgroup_of_primes} to our
vocabulary.
\end{voc}

<<DICT/GROUP-23.VOC>>=
Ris_Sylow_pi-subgroup_of_primes
@

\begin{theorem}[First Sylow $\pi$ Theorem]
For any group $G$ and set of primes $\pi$, there exists a Sylow
$\pi$-subgroup of $G$.
\end{theorem}

\begin{thm-remark}
Again, we consciously model our exact phrasing off of Theorem~\mml[Th10]{group10},
the formalization of the first Sylow theorem.
\end{thm-remark}

<<Theorem: Existence of Sylow $\pi$-subgroup>>=
theorem Th2001:
  for G being Group
  ex P being strict Subgroup of G
  st P is_Sylow_pi-subgroup_of_primes Pi;
@


\begin{definition}
Let $G$ be a group, $\pi$ be a set of prime numbers. We define
$\SylowSubgroups{\pi}{G}$ to be the set of $\pi$-subgroups of $G$.
\end{definition}

<<Definition: $\SylowSubgroups{\pi}{G}$ the collection of Sylow $\pi$-Subgroups>>=
definition
  let Pi be Subset of SetPrimes;
  let G be Group;
  func the_sylow_pi-subgroups_of_primes (Pi,G) -> Subset of (Subgroups G) equals
  :Def4:
  { H where H is Element of Subgroups G : ex P being strict Subgroup of G st
   P = H & P is_Sylow_pi-subgroup_of_primes Pi };
  correctness
  proof
    set X = { H where H is Element of Subgroups G :
    ex P being strict Subgroup of G
    st P = H & P is_Sylow_pi-subgroup_of_primes Pi };
    for x being object st x in X holds x in Subgroups G
    proof
      let x be object;
      assume x in X;
      then ex H being Element of Subgroups G st x = H & ex P being strict
      Subgroup of G st P = H & P is_Sylow_pi-subgroup_of_primes Pi;
      hence x in Subgroups G;
    end;
    hence thesis by TARSKI:def 3;
  end;
end;
@

\begin{voc}
We need to add \lstinline{the_sylow_pi-subgroups_of_primes} to our
vocabulary.
\end{voc}

<<DICT/GROUP-23.VOC>>=
Othe_sylow_pi-subgroups_of_primes
@

<<Register: $\SylowSubgroups{\pi}{G}$ is non empty set>>=
registration
  let G be Group;
  let Pi be Subset of SetPrimes;
  cluster the_sylow_pi-subgroups_of_primes (Pi,G) -> non empty;
  correctness
  proof
    consider P being strict Subgroup of G such that
    A1: P is_Sylow_pi-subgroup_of_primes Pi by Th2001;
    reconsider H9=(Omega).P as Element of Subgroups G by GROUP_3:def 1;
    H9 in the_sylow_pi-subgroups_of_primes (Pi,G) by A1;
    hence thesis;
  end;
end;
@
