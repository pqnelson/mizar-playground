% -*- mode: poly-noweb; noweb-code-mode: mizar-mode; -*-

\section{Inclusion Morphisms}

When $H\subgroup G$, we have an obvious inclusion morphism $\iota_{H}\colon H\into G$
such that for any $h\in H$, $\iota_{H}(h)=h$. This has the nifty benefit
that, if $\varphi G\to K$ is a group morphism, then its restriction to
$H$ satisfies $\varphi|_{H}=\varphi\circ\iota_{H}$.

<<Inclusion Morphisms>>=
begin :: Inclusion Morphism
<<Definition: inclusion morphism (with explicit codomain)>>

<<Definition: inclusion morphism (with implicit codomain)>>

<<Theorem: $H\subgroup G\implies\forall h\in{H},\iota(h)=h$>>

<<Theorem: inclusion $\iota\colon H\into G$ is monomorphism and such that $\iota(H)=H$>>

<<Register: inclusion morphism is injective>>

<<Theorem: Restriction of group morphism is pre-composition by inclusion morphism>>
@ 

\begin{definition}
Let $H$, $G$ be groups.
If $H\subgroup G$,
We define the \define{Inclusion Morphism} of $H$ into $G$ to be
$\iota\colon H\to G$, $\iota(h)=h$.
\end{definition}

<<Definition: inclusion morphism (with explicit codomain)>>=
definition
  let G, H be Group;
  assume A1: H is Subgroup of G;
  func incl (H, G) -> Homomorphism of H, G
  equals :DefIncl: id (the carrier of H);
  coherence
  proof
    set f = id (the carrier of H);
    f is Function of the carrier of H, the carrier of G
    proof
      B1: dom f = the carrier of H;
      rng f c= the carrier of G by A1, GROUP_2:def 5;
      hence f is Function of the carrier of H, the carrier of G
      by B1, FUNCT_2:2;
    end;
    then reconsider f as Function of H, G;
    for g,h being Element of H
    holds f.(g * h) = (f.g) * (f.h) by A1, GROUP_2:43;
    hence id (the carrier of H) is Homomorphism of H, G by GROUP_6:def 6;
  end;
end;
@ %def incl DefIncl

\begin{definition}
  Let $H\subgroup G$ be a subgroup.
  Then we can construct the inclusion morphism $H\into G$ with the
  codomain implicit.
\end{definition}

<<Definition: inclusion morphism (with implicit codomain)>>=
definition
  let G be Group, H be Subgroup of G;
  func incl H -> Homomorphism of H,G equals incl (H, G);
  coherence;
end;
@

\begin{theorem}
Let $H$, $G$ be groups.
If $H\subgroup G$, then the inclusion morphism $\iota\colon H\to G$ such that
for any $h\in H$ we have $\iota(h)=h$.
\end{theorem}

<<Theorem: $H\subgroup G\implies\forall h\in{H},\iota(h)=h$>>=
theorem Th8:
  for H being Group
  for h being Element of H
  st H is Subgroup of G
  holds (incl (H, G)).h = h
proof
  let H be Group;
  let h be Element of H;
  assume H is Subgroup of G;
  hence (incl (H, G)).h = (id the carrier of H).h by DefIncl
                       .= h;
end;
@ %def Th8

\begin{theorem}
For any $H\subgroup G$, there is a monomorphism $\varphi\colon H\into G$
such that $\varphi(H)=H$.
\end{theorem}

<<Theorem: inclusion $\iota\colon H\into G$ is monomorphism and such that $\iota(H)=H$>>=
theorem Th9:
  for H being Subgroup of G
  holds incl (H, G) is one-to-one
  & Image (incl (H, G)) = the multMagma of H
proof
  let H be Subgroup of G;
  set f = incl (H, G);
  A1: f = id (the carrier of H) by DefIncl;
  the carrier of H = rng f by A1
                  .= the carrier of (Image f) by GROUP_6:44;
  then A2: Image f = the multMagma of H by GROUP_2:59;
  
  Ker f = (1).H
  <<Proof: $\ker\iota=\trivialSubgroup_{H}$>>
  hence f is one-to-one by GROUP_6:56;
  thus Image f = the multMagma of H by A2;
end;
@ %def Th9

\begin{proof}
We see for any $h\in H$ that $h\in\ker(\iota)$ iff $h\in\trivialSubgroup_{H}$
iff $h=1_{H}$, thanks to $\iota(h)=h$.
\end{proof}

<<Proof: $\ker\iota=\trivialSubgroup_{H}$>>=
proof
  for h being Element of H holds h in Ker f iff h in (1).H
  proof
    let h be Element of H;
    hereby 
      assume h in Ker f;
      then f.h = 1_G by GROUP_6:41
              .= 1_H by GROUP_2:44;
      then h in {1_H} by A1, TARSKI:def 1;
      hence h in (1).H by GROUP_2:def 7;
    end;
    assume h in (1).H;
    then h in {1_H} by GROUP_2:def 7;
    then h = 1_H by TARSKI:def 1;
    then f.h = 1_G by GROUP_6:31;
    hence h in Ker f by GROUP_6:41;
  end;
  hence thesis;
end;
@

\begin{registration}
The inclusion morphism $\iota\colon H\into G$ is injective.
\end{registration}

<<Register: inclusion morphism is injective>>=
registration
  let G be Group, H be Subgroup of G;
  cluster incl (H, G) -> one-to-one;
  correctness by Th9;
end;
@ 

\begin{theorem}
Let $G$, $H$, $K$ be groups, let $\varphi\colon G\to K$ be a group morphism.
If $H\subgroup G$, then $\varphi|_{H} = \varphi\circ\iota_{H}$.
\end{theorem}

<<Theorem: Restriction of group morphism is pre-composition by inclusion morphism>>=
theorem
  for H,K being Group st H is Subgroup of G
  for phi being Homomorphism of G,K
  holds phi|(the carrier of H) = phi * (incl (H, G))
proof
  let H,K be Group;
  assume A1: H is Subgroup of G;
  let phi be Homomorphism of G,K;
  A2: dom (phi|(the carrier of H)) = the carrier of H
  proof
    dom phi = the carrier of G by FUNCT_2:def 1;
    hence dom (phi|(the carrier of H)) = the carrier of H
    by A1, GROUP_2:def 5, RELAT_1:62;
  end;
  for x being object st x in dom (phi|(the carrier of H))
  holds (phi|(the carrier of H)).x = (phi * (incl (H, G))).x
  proof
    let x be object;
    assume B1: x in dom (phi|(the carrier of H));
    thus (phi * (incl (H, G))).x
     = phi.((incl (H, G)).x) by B1, A2, FUNCT_2:15
    .= phi.x by A1, A2, B1, Th8
    .= (phi|(the carrier of H)).x by B1, FUNCT_1:47;
  end;

  hence phi|(the carrier of H) = phi * (incl (H, G))
  by A2, FUNCT_2:def 1;
end;
@ 




