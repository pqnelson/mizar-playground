% -*- mode: poly-noweb; noweb-code-mode: mizar-mode; -*-

\section*{Appendix: Null Object for Group}

We recall that the null object for a category is one which is both
initial and terminal. Mizar defines the initial object~\mml[def 19]{cat1}
as
\begin{mizar}
definition
  let C be Category;
  let a be Object of C;
  attr a is initial means :: CAT_1:def 19
  for b being Object of C holds
  (Hom (a,b) <> {} & ex f being Morphism of a,b st
   for g being Morphism of a,b holds f = g);
end;
\end{mizar}
Similarly, the criteria for a terminal object~\mml[def 18]{cat1} as:
\begin{mizar}
definition
  let C be Category;
  let a be Object of C;
  attr a is terminal means :: CAT_1:def 18
  for b being Object of C holds
  (Hom (b,a) <> {} & ex f being Morphism of b,a st
  for g being Morphism of b,a holds f = g);
end;
\end{mizar}
We will take for granted there is always a morphism $\trivialGroup\to G$
and similarly $G\to\trivialGroup$. We establish in Theorem~\ref{thm:002-universal-properties:null-object:trivial-group-is-terminal}
that any trivial group is terminal, and Theorem~\ref{thm:002-universal-properties:null-object:trivial-group-is-initial}
establishes any trivial group is initial.

<<Appendix: Null object for groups>>=
begin :: Appendix: Universal Property of Null Group
<<Theorem: uniqueness of morphisms to trivial group>>

<<Theorem: Trivial Group is the Terminal Object>>

<<Theorem: if $f\colon G\to\trivialGroup$ and $g\in G$, then $f(g)=1$>>

<<Theorem: Properties of the constant morphism $\trivialGroup\to G$>>

<<Theorem: uniqueness of morphisms from trivial groups>>

<<Theorem: Trivial Group is an Initial Object>>

<<Theorem: extend codomain for group morphisms>>
@ 

\begin{theorem}
For any group $G$, if $f_{1},f_{2}\colon G\to\trivialGroup$, then $f_{1}=f_{2}$.
\end{theorem}

<<Theorem: uniqueness of morphisms to trivial group>>=
:: Uniqueness of morphisms to the trivial group
theorem Th69:
  for H being trivial Group
  for f1,f2 being Homomorphism of G,H
  holds f1=f2
proof
  let H be trivial Group;
  let f1,f2 be Homomorphism of G,H;
  A1: the carrier of H = {1_H}
  proof
    consider x being object such that
    B1: the carrier of H = {x} by GROUP_6:def 2;
    thus thesis by B1, TARSKI:def 1;
  end;

  for g being object st g in the carrier of G holds f1.g = f2.g
  proof
    let g be Element of G;
    f1.g = 1_H by A1, TARSKI:def 1;
    hence f1.g = f2.g by A1, TARSKI:def 1;
  end;
  hence f1=f2 by FUNCT_2:def 8;
end;
@ %def Th69


\begin{theorem}\label{thm:002-universal-properties:null-object:trivial-group-is-terminal}
For any group $G$, there is exactly one morphism $G\to\trivialGroup$.
\end{theorem}

<<Theorem: Trivial Group is the Terminal Object>>=
:: Universal property of terminal object for the trivial group
theorem
  for H being trivial Group
  ex h being Homomorphism of G,H 
  st (for f being Homomorphism of G,H holds h=f)
proof
  let H be trivial Group;
  take h = 1:(G,H);
  thus thesis by Th69;
end;
@

\begin{theorem}
Explicitly, if $f\colon G\to\trivialGroup$ is a group morphism and $g\in G$,
then $f(g)=1$ is the identity element.
\end{theorem}

<<Theorem: if $f\colon G\to\trivialGroup$ and $g\in G$, then $f(g)=1$>>=
theorem
  for H being trivial Group
  for f being Homomorphism of G,H
  for g being Element of G
  holds f.g = 1_H
proof
  let H be trivial Group;
  let f be Homomorphism of G,H;
  let g be Element of G;
  f = 1:(G,H) by Th69;
  hence f.g = 1_H;
end;
@ 

\begin{theorem}
Let $H$ be a trivial group, let $G$ be any group.
The constant morphism $\varphi\colon H\to G$ sending everything to the
identity element of $G$ is injective and its image is the trivial
subgroup of $G$.
\end{theorem}

<<Theorem: Properties of the constant morphism $\trivialGroup\to G$>>=
theorem
  for H being trivial Group
  holds Image (1:(H,G)) = (1).G & (1:(H,G)) is one-to-one
proof
  let H be trivial Group;
  thus Image (1:(H,G)) = (1).G by GROUP_6:47;
  the multMagma of H = (1).H by GROUP_22:6;
  then Ker (1:(H,G)) is Subgroup of (1).H
  by GROUP_2:57, GROUP_2:def 5;
  then Ker (1:(H,G)) is trivial Subgroup of H by GROUP_22:9;
  then Ker (1:(H,G)) = (1).H by GROUP_22:8;
  hence (1:(H,G)) is one-to-one by GROUP_6:56;
end;
@ 

\begin{theorem}
The trivial group is universally initial, i.e., there is exactly one
homomorphism from a trivial group to any other group.
\end{theorem}

<<Theorem: uniqueness of morphisms from trivial groups>>=
theorem Th73:
  for H being trivial Group
  for f1, f2 being Homomorphism of H, G
  holds f1 = f2
proof
  let H be trivial Group;
  let f1, f2 be Homomorphism of H, G;
  for h being Element of H holds f1.h = f2.h
  proof
    let h be Element of H;
    the multMagma of H = (1).H by GROUP_22:6;
    then the carrier of H = {1_H} by GROUP_2:def 7;
    then B1: h = 1_H by TARSKI:def 1;
    hence f1.h = 1_G by GROUP_6:31
              .= f2.h by B1, GROUP_6:31;
  end;
  hence f1 = f2 by FUNCT_2:def 8;
end;
@ %def Th73

\begin{theorem}\label{thm:002-universal-properties:null-object:trivial-group-is-initial}
For any group $G$, there is exactly one morphism $\trivialGroup\to G$;
i.e., any trivial group is an initial object.
\end{theorem}

<<Theorem: Trivial Group is an Initial Object>>=
:: Universal property of initial object for the trivial group
theorem
  for H being trivial Group
  ex h being Homomorphism of H, G
  st (for f being Homomorphism of H, G holds h=f)
proof
  let H be trivial Group;
  take h = 1:(H,G);
  thus thesis by Th73;
end;
@

\begin{theorem}
Let $H\subgroup G_{2}$ and $\varphi\colon G_{1}\to H$ be a group
morphism. Then $\varphi$ is also a group morphism $G_{1}\to G_{2}$.
\end{theorem}

<<Theorem: extend codomain for group morphisms>>=
theorem Th75:
  for G1, G2 being Group
  for H being Subgroup of G2
  for f being Homomorphism of G1, H
  holds f is Homomorphism of G1, G2 by GROUP_19:6;
@ %def Th75

