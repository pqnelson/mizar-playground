% -*- mode: poly-noweb; noweb-code-mode: mizar-mode; -*-

\section{Results Concerning Product Group}

<<Results Concerning Product Groups>>=
begin :: Universal Property of Direct Products of Groups
<<Theorem: Universal Property of Group Products (Explicit Version)>>

<<Theorem: Universal Property of Group Products (Implicit Version)>>

<<Theorem: Uniqueness of group products>>

<<Definition: product of homomorphism-family>>

<<Theorem: product morphism maps unit element componentwise to the unit element>>

<<Theorem: $\forall g\in G,g\in\ker(\prod f)\iff\forall i\in I,g\in\ker(f_{i})$>>

<<Theorem: $g\in\ker(\varphi_{2}\circ\varphi_{1})\iff\varphi_{1}(g)\in\ker(\varphi_{2})$>>

<<Theorem: $U(\ker(\varphi_{2}\circ\varphi_{1}))=\varphi_{1}^{-1}[\ker(\varphi_{2})]$>>

<<Theorem: $\ker(\prod\varphi)=\bigcap\ker(\varphi)$>>

<<Theorem: $\forall i\in I, f_{i}\in\mathcal{F}_{i}\implies f\in\prod\mathcal{F}$>>

<<Theorem: Product Group is a Subgroup iff it is componentwise>>

<<Theorem: Product of a Subgroup-Family is a Subgroup of the Parent's Product>>

<<Definition: product of subgroup-family>>

<<Theorem: $g\in\ker(\nu_{i}\circ\pi_{i})\iff\pi_{i}(g)\in[\mathcal{F}_{i},\mathcal{F}_{i}]$>>

<<Theorem: image of projection morphism>>

<<Theorem: if $\pi_{i}(\prod\mathcal{F}^{(1)})\subgroup\pi_{i}(\prod\mathcal{F}^{(2)})$, then $\prod\mathcal{F}^{(1)}\subgroup\prod\mathcal{F}^{(2)}$>>

<<Theorem: if $G\subgroup\prod\mathcal{F}$, then $\prod(\pi_{i}\circ\iota_{G})=\id_{G}$>>

<<Theorem: images of equal morphisms are equal (as subgroups)>>

<<Theorem: criteria for group morphism being surjective>>

<<Theorem: product of normal subgroup-family is a normal subgroup of the product group>>

<<Register: product of normal subgroup-family is a normal subgroup>>
@

\begin{theorem}
For any family of morphisms $f_{i}\colon G\to\mathcal{F}_{i}$,
there is a morphism $f\colon G\to\prod\mathcal{F}$ such that $f(g)_{i}=f_{i}(g)$.
\end{theorem}

<<Theorem: Universal Property of Group Products (Explicit Version)>>=
:: Universal property of group product, explicit version.
theorem Th42:
  for f being Homomorphism-Family of G, F
  ex phi being Homomorphism of G, product F
  st for g being Element of G
     holds (for j being Element of I holds (f.j).g = (proj (F,j)) . (phi.g))
proof
  let f be Homomorphism-Family of G, F;
  <<Proof step: construct the underlying function for the product morphism>>

  A3: for g being Element of G
  for j being Element of I
  holds (phi.g).j = (f.j).g
  proof
    let g be Element of G;
    let j be Element of I;
    P[g, phi.g] by A2;
    hence (phi.g).j = (f.j).g;
  end;

  for a, b being Element of G holds phi.(a * b) = (phi.a) * (phi.b)
  <<Proof: $\varphi$ is multiplicative>>
  then reconsider phi as Homomorphism of G, product F by GROUP_6:def 6;
  take phi;
  let g be Element of G;
  for j being Element of I holds (f.j).g = (proj (F, j)) . (phi.g)
  proof
    let j be Element of I;
    (f.j).g = (phi.g).j by A3;
    hence (f.j).g = (proj (F, j)).(phi.g) by Def16;
  end;
  hence thesis;
end;
@ %def Th42

<<Proof step: construct the underlying function for the product morphism>>=
defpred P[object, object] means ex g0 being Element of product F
st $2 = g0 & (for j being Element of I holds (f.j).$1 = g0.j);
deffunc F1() = the carrier of G;

A1: for x being object st x in F1()
ex y being object st y in the carrier of product F &
P[x,y]
<<Proof: $\forall x\in G\exists y\in\prod\mathcal{F},P[x,y]$>>

consider phi being Function of F1(), the carrier of (product F)
such that
A2: for x being object st x in F1() holds P[x, phi.x]
from FUNCT_2:sch 1(A1);
reconsider phi as Function of G, product F;
@ 

<<Proof: $\forall x\in G\exists y\in\prod\mathcal{F},P[x,y]$>>=
proof
  let x be object;
  assume x in F1();
  then reconsider xx=x as Element of G;
  defpred P1[object, object] means ex i being Element of I st
  i = $1 & $2 = ((f.i).xx);
  B1: for i, y1, y2 being object st i in I & P1[i,y1] & P1[i,y2]
      holds y1 = y2;
  B2: for i being object st i in I holds ex y being object st P1[i,y]
  proof
    let i be object;
    assume i in I;
    then reconsider ii=i as Element of I;
    consider y being object such that
    C1: y = ((f.ii).xx);
    take y;
    thus P1[i,y] by C1;
  end;
  
  consider y being Function such that
  B3: dom y = I and
  B4: for j being object st j in I holds P1[j, y.j]
  from FUNCT_1:sch 2(B1, B2);
  take y;
  thus y in the carrier of product F
  proof
    C1: dom y = dom (Carrier F) by B3, PARTFUN1:def 2;
    for i being Element of I holds y.i in (Carrier F).i
    proof
      let i be Element of I;
      P1[i, y.i] by B4;
      then consider ii being Element of I such that
      D1: ii=i & y.i = ((f.i).xx);
      (Carrier F).i = the carrier of F.i by Th20; 
      hence thesis by D1;
    end;
    then for i being object st i in dom (Carrier F)
         holds y.i in (Carrier F).i;
    then y in product (Carrier F) by C1, CARD_3:9;
    hence thesis by GROUP_7:def 2;
  end;
  then consider g0 being Element of product F such that
  B5: g0 = y;
  take g0;
  thus y = g0 by B5;
  let j be Element of I;
  P1[j, g0.j] by B4, B5;
  hence (f.j).x = g0.j;
end;
@ 

<<Proof: $\varphi$ is multiplicative>>=
proof
  let a, b be Element of G;
  (phi.(a * b)) is Element of product (Carrier F)
  & (phi.a)*(phi.b) is Element of product (Carrier F)
  by GROUP_7:def 2;
  then B1: dom (phi.(a * b)) = I & dom ((phi.a)*(phi.b)) = I
  by PARTFUN1:def 2;
  for j being Element of I holds (phi.(a * b)).j = ((phi.a) * (phi.b)).j
  proof
    let j be Element of I;
    reconsider fj = f.j as Homomorphism of G, F.j;
    (phi.(a * b)).j = fj.(a * b) by A3
                   .= (fj.a) * (fj.b) by GROUP_6:def 6
                   .= ((phi.a)/.j) * (fj.b) by A3
                   .= ((phi.a)/.j) * ((phi.b)/.j) by A3
                   .= ((phi.a) * (phi.b))/.j by GROUP_7:1;
    hence (phi.(a * b)).j = ((phi.a) * (phi.b)).j;
  end;
  hence phi.(a * b) = (phi.a) * (phi.b) by B1;
end;
@ 

\begin{theorem}
For $f$ being a family of Homomorphisms $f_{i}\colon G\to\mathcal{F}_{i}$,
for $i\in I$, there exists a morphism $\varphi\colon G\to\prod\mathcal{F}$
such that $f_{i} = \pi_{i}\circ\varphi$.
\end{theorem}

<<Theorem: Universal Property of Group Products (Implicit Version)>>=
:: Universal property of group product, as most people would recognize it.
theorem
  for f being Homomorphism-Family of G, F
  ex phi being Homomorphism of G, product F
  st (for i being Element of I holds f.i = (proj (F,i)) * phi)
proof
  let f be Homomorphism-Family of G, F;
  consider phi being Homomorphism of G, product F such that
  A1: for g being Element of G
      holds (for j being Element of I holds (f.j).g = (proj (F,j)) . (phi.g))
  by Th42;
  take phi;
  let i be Element of I;
  for g being Element of G holds ((proj (F,i)) * phi).g = (f.i).g
  proof
    let g be Element of G;
    (f.i).g = (proj (F,i)) . (phi.g) by A1
           .= ((proj (F,i)) * phi).g by FUNCT_2:15;
    hence (f.i).g = ((proj (F,i)) * phi).g;
  end;
  hence f.i = (proj (F,i)) * phi;
end;
@ 

\begin{theorem}
The morphism formed by the universal property of the group product is
unique.
\end{theorem}

<<Theorem: Uniqueness of group products>>=
theorem
  for f being Homomorphism-Family of G, F
  for phi1, phi2 being Homomorphism of G, product F
  st (for i being Element of I holds f.i = (proj (F,i)) * phi1)
     & (for i being Element of I holds f.i = (proj (F,i)) * phi2)
  holds phi1 = phi2
proof
  let f be Homomorphism-Family of G, F;
  let phi1, phi2 be Homomorphism of G, product F;
  assume A1: for i being Element of I holds f.i = (proj (F,i)) * phi1;
  assume A2: for i being Element of I holds f.i = (proj (F,i)) * phi2;
  for g being Element of G holds phi1.g = phi2.g
  proof
    let g be Element of G;
    phi1.g is Element of product (Carrier F) 
    & phi2.g is Element of product (Carrier F) by GROUP_7:def 2;
    then B1: dom (phi1.g) = I & dom (phi2.g) = I by PARTFUN1:def 2;
    for j being Element of I holds (phi1.g).j = (phi2.g).j
    proof
      let j be Element of I;
      C2: (f.j).g = ((proj (F, j)) * phi1).g by A1
                 .= (proj (F, j)) . (phi1.g) by FUNCT_2:15
                 .= (phi1.g).j by Def16;
      (f.j).g = ((proj (F, j)) * phi2).g by A2
             .= (proj (F, j)) . (phi2.g) by FUNCT_2:15
             .= (phi2.g).j by Def16;
      hence (phi1.g).j = (phi2.g).j by C2;
    end;
    hence phi1.g = phi2.g by B1;
  end;
  hence phi1 = phi2;
end;
@ 

\begin{definition}
If $\{f_{i}\colon G\to\mathcal{F}_{i}\}_{i\in I}$ is a family of
homomorphisms, then its \define{Product Morphism} $f\colon G\to\prod\mathcal{F}$
is the unique morphism satisfying the universal property of group products.
\end{definition}

<<Definition: product of homomorphism-family>>=
definition
  let G be Group;
  let I be non empty set;
  let F be Group-Family of I;
  let f be Homomorphism-Family of G, F;
  func product f -> Homomorphism of G, product F means
  :Def17:
  for g being Element of G
  for i being Element of I
  holds (f.i).g = (it.g).i;
  existence
  <<Proof: existence of products for homomorphism-families>>
  uniqueness
  <<Proof: uniqueness of products for homomorphism-families>>
end;
@ %def Def17

<<Proof: existence of products for homomorphism-families>>=
proof
  consider phi being Homomorphism of G, product F such that
  A1: for g being Element of G
      holds (for j being Element of I holds (f.j).g = (proj (F,j)) . (phi.g))
  by Th42;
  take phi;
  for g being Element of G
  for j being Element of I holds (f.j).g = (phi.g).j
  proof
    let g be Element of G;
    let j be Element of I;
    (f.j).g = (proj (F,j)) . (phi.g) by A1
           .= (phi.g).j by Def16;
    hence thesis;
  end;
  hence thesis;
end;
@ 

<<Proof: uniqueness of products for homomorphism-families>>=
proof
  for phi1, phi2 being Homomorphism of G,product F
  st (for g being Element of G
      for i being Element of I
      holds (f.i).g = (phi1.g).i)
  & (for g being Element of G
     for i being Element of I
     holds (f.i).g = (phi2.g).i)
  holds phi1 = phi2
  proof
    let phi1,phi2 be Homomorphism of G,product F;
    assume A1: for g being Element of G
               for i being Element of I
               holds (f.i).g = (phi1.g).i;
    assume A2: for g being Element of G for i being Element of I
               holds (f.i).g = (phi2.g).i;
    for g being Element of G
    holds phi1.g = phi2.g
    proof
      let g be Element of G;
      phi1.g is Element of product (Carrier F) 
      & phi2.g is Element of product (Carrier F) by GROUP_7:def 2;
      then A3: dom (phi1.g) = I & dom (phi2.g) = I by PARTFUN1:def 2;
      for j being Element of I holds (phi1.g).j = (phi2.g).j
      proof
        let j be Element of I;
        (phi1.g).j = (f.j).g by A1
                  .= (phi2.g).j by A2;
        hence (phi1.g).j = (phi2.g).j;
      end;
      hence phi1.g = phi2.g by A3;
    end;
    hence phi1 = phi2;
  end;
  hence thesis;
end;
@ 

\begin{theorem}
The product morphism maps unit elements to products of unit elements.
\end{theorem}

<<Theorem: product morphism maps unit element componentwise to the unit element>>=
theorem Th44:
  for g being Element of G
  holds (for i being Element of I holds ((product f).g).i = 1_(F.i))
  iff (product f).g = 1_(product F)
proof
  let g be Element of G;
  thus (for i being Element of I holds ((product f).g).i = 1_(F.i))
  implies (product f).g = 1_(product F)
  proof
    assume A1: for i being Element of I holds ((product f).g).i = 1_(F.i);
    set s = (product f).g;
    A2: for i being set
        st i in I
        holds (ex FG being non empty Group-like multMagma
               st FG = F.i & s.i = (1_FG))
    proof
      let i be set;
      assume i in I;
      then reconsider ii=i as Element of I;
      take FG = F.ii;
      thus thesis by A1;
    end;
    s is ManySortedSet of I
    proof
      s is Element of product (Carrier F) by GROUP_7:def 2;
      hence s is ManySortedSet of I;
    end;
    hence thesis by A2, GROUP_7:5;
  end;
  thus (product f).g = 1_(product F) implies
  (for i being Element of I holds ((product f).g).i = 1_(F.i)) by GROUP_7:6;
end;
@ %def Th44

\begin{theorem}
  For any $g\in G$, we have $g\in\ker(\prod f)$ if and only if
  for each $i\in I$ we have $g\in\ker(f_{i})$.
\end{theorem}

<<Theorem: $\forall g\in G,g\in\ker(\prod f)\iff\forall i\in I,g\in\ker(f_{i})$>>=
theorem Th45:
  for g being Element of G
  holds g in Ker (product f)
        iff (for i being Element of I holds g in Ker (f.i))
proof
  let g be Element of G;
  thus g in Ker (product f)
       implies (for i being Element of I holds g in Ker (f.i))
  proof
    assume A1: g in Ker (product f);
    let i be Element of I;
    (product f).g = 1_(product F) by A1, GROUP_6:41;
    then 1_(F.i) = ((product f).g).i by Th44
                .= (f.i).g by Def17;
    hence g in Ker (f.i) by GROUP_6:41;
  end;
  thus (for i being Element of I holds g in Ker (f.i))
       implies g in Ker (product f)
  proof
    assume A1: for i being Element of I holds g in Ker (f.i);
    A2: for i being Element of I holds (f.i).g = 1_(F.i)
    proof
      let i be Element of I;
      g in Ker (f.i) by A1;
      hence (f.i).g = 1_(F.i) by GROUP_6:41;
    end;
    for i being Element of I holds ((product f).g).i = 1_(F.i)
    proof
      let i be Element of I;
      ((product f).g).i = (f.i).g by Def17
                       .= 1_(F.i) by A2;
      hence thesis;
    end;
    then (product f).g = 1_(product F) by Th44;
    hence g in Ker (product f) by GROUP_6:41;
  end;
end;
@ %def Th45

\begin{theorem}
Let $\varphi_{1}\colon G_{1}\to G_{2}$ and $\varphi_{2}\colon G_{2}\to G_{3}$
be group morphisms. Then $g\in\ker(\varphi_{2}\circ\varphi_{1})$ if and
only if $\varphi_{1}(g)\in\ker(\varphi_{2})$.
\end{theorem}

<<Theorem: $g\in\ker(\varphi_{2}\circ\varphi_{1})\iff\varphi_{1}(g)\in\ker(\varphi_{2})$>>=
theorem Th46:
  for G1, G2, G3 being Group
  for f1 being Homomorphism of G1, G2
  for f2 being Homomorphism of G2, G3
  for g being Element of G1
  holds g in Ker (f2 * f1) iff f1.g in Ker(f2)
proof
  let G1, G2, G3 be Group;
  let f1 be Homomorphism of G1, G2;
  let f2 be Homomorphism of G2, G3;
  let g be Element of G1;
  thus g in Ker (f2 * f1) implies f1.g in Ker(f2)
  proof
    assume g in Ker (f2 * f1);
    then 1_(G3) = (f2 * f1).g by GROUP_6:41
               .= f2.(f1.g) by FUNCT_2:15;
    hence f1.g in Ker(f2) by GROUP_6:41;
  end;
  thus f1.g in Ker(f2) implies g in Ker (f2 * f1)
  proof
    assume f1.g in Ker(f2);
    then 1_G3 = f2.(f1.g) by GROUP_6:41
             .= (f2 * f1).g by FUNCT_2:15;
    hence g in Ker (f2 * f1) by GROUP_6:41;
  end;
end;
@ %def Th46

\begin{theorem}
Let $\varphi_{1}\colon G_{1}\to G_{2}$ and $\varphi_{2}\colon G_{2}\to G_{3}$
be group morphisms. Then the underlying set
$U(\ker(\varphi_{2}\circ\varphi_{1}))=\varphi_{1}^{-1}[\ker(\varphi_{2})]$. 
\end{theorem}

<<Theorem: $U(\ker(\varphi_{2}\circ\varphi_{1}))=\varphi_{1}^{-1}[\ker(\varphi_{2})]$>>=
theorem
  for G1, G2, G3 being Group
  for f1 being Homomorphism of G1, G2
  for f2 being Homomorphism of G2, G3
  holds the carrier of Ker (f2 * f1) = f1" (the carrier of Ker f2)
proof
  let G1, G2, G3 be Group;
  let f1 be Homomorphism of G1, G2;
  let f2 be Homomorphism of G2, G3;
  A1: f1 "(the carrier of Ker f2) is Subset of G1
      & the carrier of Ker (f2 * f1) is Subset of G1
  by GROUP_2:def 5, FUNCT_2:39;
  for g being Element of G1
  st g in the carrier of Ker (f2 * f1)
  holds g in f1 " (the carrier of (Ker f2))
  proof
    let g be Element of G1;
    assume g in the carrier of Ker (f2 * f1);
    then g in Ker (f2 * f1);
    then f1.g in Ker f2 by Th46;
    hence g in f1 " (the carrier of (Ker f2)) by FUNCT_2:38;
  end;
  then
  A2: the carrier of Ker (f2 * f1) c= f1" (the carrier of Ker f2)
  by A1;
  for g being Element of G1
  st g in f1 " (the carrier of (Ker f2))
  holds g in the carrier of Ker (f2 * f1)
  proof
    let g be Element of G1;
    assume g in f1 " (the carrier of (Ker f2));
    then f1.g in Ker f2 by FUNCT_2:38;
    then g in Ker (f2 * f1) by Th46;
    hence g in the carrier of Ker (f2 * f1);
  end;
  then
  A3: f1" (the carrier of Ker f2) c= the carrier of Ker (f2 * f1) by A1;
  thus the carrier of Ker (f2 * f1) = f1" (the carrier of Ker f2)
  by A2,A3,XBOOLE_0:def 10;
end;
@ 

\begin{theorem}
Let $\varphi_{i}\colon G\to\mathcal{F}_{i}$ be a family of group
morphisms.
Then $\ker(\prod\varphi)=\bigcap\ker(\varphi_{i})$ the kernel of the
product morphism is the intersection of kernels of its components.
\end{theorem}

<<Theorem: $\ker(\prod\varphi)=\bigcap\ker(\varphi)$>>=
theorem
  the carrier of Ker (product f) = meet { the carrier of Ker (f.i) where
  i is Element of I : not contradiction }
proof
  set Fam = { the carrier of Ker (f.i) where i is Element of I
              : not contradiction };
  A1: Fam <> {}
  proof
    set i = the Element of I;
    the carrier of Ker (f.i) in Fam;
    hence thesis;
  end;
  A3: for g being object
  holds g in Ker (product f)
        iff (for A being set st A in Fam holds g in A)
  proof
    let g be object;
    hereby 
      assume B1: g in Ker (product f);
      let A be set;
      assume A in Fam;
      then consider i1 being Element of I such that
      B2: A = the carrier of Ker(f.i1);
      g in G & g in Ker (product f) by B1, GROUP_2:40;
      then g in Ker (f.i1) by Th45;
      hence g in A by B2;
    end;
    assume B1: for A being set st A in Fam holds g in A;
    g in G & for i being Element of I holds g in Ker (f.i)
    proof
      ex x being object st x in Fam by A1, XBOOLE_0:def 1;
      then consider A0 being set such that
      B2: A0 in Fam; 
      consider i0 being Element of I such that
      B3: A0 = the carrier of Ker (f.i0) by B2;
      g in Ker (f.i0) by B1, B2, B3;
      hence g in G by GROUP_2:40;
      
      let i be Element of I;
      the carrier of Ker (f.i) in Fam;
      hence g in Ker (f.i) by B1;
    end;

    hence g in Ker (product f) by Th45;
  end;
  A4: for g being object
  holds g in Ker (product f)
        iff g in (meet Fam)
  proof
    let g be object;
    g in Ker (product f) iff (for A being set st A in Fam holds g in A) by A3;
    hence thesis by A1, SETFAM_1:def 1;
  end;
  for g being object
  holds g in the carrier of Ker (product f) iff g in meet Fam
  proof
    let g be object;
    g in the carrier of Ker (product f) iff g in Ker (product f);
    hence thesis by A4;
  end;
  hence thesis by TARSKI:2;
end;
@ 

\begin{theorem}
For any function $f$ such that $\dom(f)=I$ if, for each $i\in I$, we
have $f_{i}\in\mathcal{F}_{i}$, then $f\in\prod\mathcal{F}$.
\end{theorem}

<<Theorem: $\forall i\in I, f_{i}\in\mathcal{F}_{i}\implies f\in\prod\mathcal{F}$>>=
theorem Th49:
  for f being Function
  st dom f = I & (for i being Element of I holds f.i in F.i)
  holds f in product F
proof
  let f be Function;
  assume A1: dom f = I;
  assume A2: for i being Element of I holds f.i in F.i;
  A3: dom (Carrier F) = I by PARTFUN1:def 2;
  for i being object st i in dom (Carrier F) holds f.i in (Carrier F).i
  proof
    let i be object;
    assume B1: i in dom (Carrier F);
    then reconsider ii=i as Element of I;
    f.i in F.ii by A2;
    then f.i in the carrier of (F.ii);
    hence f.i in (Carrier F).i by Th20;
  end;
  then f in product (Carrier F) by A1, A3, CARD_3:9;
  hence f in product F by GROUP_7:def 2;
end;
@ %def Th49

\begin{theorem}
Let $\mathcal{S}$ and $\mathcal{F}$ are group-families indexed by $I$,
let $g\in\prod\mathcal{F}$.
Then $g\in\prod\mathcal{S}$ if and only if
for each $i\in I$ we have $\pi_{i}(g)\in\mathcal{S}_{i}$.
\end{theorem}

<<Theorem: Product Group is a Subgroup iff it is componentwise>>=
theorem
  for S being Group-Family of I
  for g being Element of product F
  holds g in product S iff (for i being Element of I
                            holds (proj (F,i)).g in S.i)
proof
  let S be Group-Family of I;
  let g be Element of product F;
  hereby 
    assume A1: g in product S;
    let i be Element of I;
    g.i in S.i by A1, GROUP_19:5;
    hence (proj (F,i)).g in S.i by Def16;
  end;
  assume Z2: for i being Element of I holds (proj (F,i)).g in S.i;
  Z3: dom g = I by GROUP_19:3;
  for i being Element of I holds g.i in S.i
  proof
    let i be Element of I;
    (proj (F, i)).g in S.i by Z2;
    hence g.i in S.i by Def16;
  end;
  hence g in product S by Z3, Th49;
end;
@

\begin{theorem}
Let $g\in\prod\mathcal{F}$, $\nu_{i}\colon\mathcal{F}_{i}\onto\mathcal{F}_{i}/[\mathcal{F}_{i},\mathcal{F}_{i}]$
be the natural homomorphism of the component $i\in I$.
Then $g\in\ker(\nu_{i}\circ\pi_{i})$ if and only if $\pi_{i}(g)\in[\mathcal{F}_{i},\mathcal{F}_{i}]$.
\end{theorem}

<<Theorem: $g\in\ker(\nu_{i}\circ\pi_{i})\iff\pi_{i}(g)\in[\mathcal{F}_{i},\mathcal{F}_{i}]$>>=
theorem
  for g being Element of product F
  holds g in Ker ((nat_hom ((F.i)`)) * (proj (F, i)))
        iff (proj (F,i)).g in (F.i)`
proof
  let g be Element of product F;
  set H=(F.i)./.((F.i)`);
  hereby
    assume A1: g in Ker ((nat_hom ((F.i)`)) * (proj (F, i)));
    (nat_hom ((F.i)`)).((proj (F,i)).g)
     = ((nat_hom ((F.i)`)) * (proj (F, i))).g by FUNCT_2:15
    .= 1_H by A1, GROUP_6:41;
    then (proj (F,i)).g in Ker (nat_hom ((F.i)`)) by GROUP_6:41;
    hence (proj (F,i)).g in (F.i)` by GROUP_6:43;
  end;
  assume (proj (F,i)).g in (F.i)`;
  then (proj (F,i)).g in Ker (nat_hom ((F.i)`)) by GROUP_6:43;
  then 1_H = (nat_hom ((F.i)`)).((proj (F,i)).g) by GROUP_6:41
          .= ((nat_hom ((F.i)`)) * (proj (F, i))).g by FUNCT_2:15;
  hence g in Ker ((nat_hom ((F.i)`)) * (proj (F, i))) by GROUP_6:41;
end;
@ 

\begin{theorem}
Let $I\neq\emptyset$ be a nonempty set, let $\mathcal{F}$ 
and $\mathcal{G}$ be families of groups indexed by $I$.
If for each $i\in I$ we have $\mathcal{F}_{i}\subgroup\mathcal{G}_{i}$,
then $\prod\mathcal{F}\subgroup\prod\mathcal{G}$.
\end{theorem}

<<Theorem: Product of a Subgroup-Family is a Subgroup of the Parent's Product>>=
theorem Th56:
  for F1,F2 being Group-Family of I
  st (for i being Element of I holds F1.i is Subgroup of F2.i)
  holds product F1 is Subgroup of product F2
proof
  let F1,F2 be Group-Family of I;
  assume A1: for i being Element of I holds F1.i is Subgroup of F2.i;
  <<Proof step: construct endomorphism family $f_{i}=\pi_{i}\circ\iota$>>
  A4: for g being Element of product F1
  for i being Element of I
  holds (f.i).g = g.i
  <<Proof step: $f_{i}(g)=g_{i}$>>
  consider phi being Homomorphism of product F1, product F2 such
  that
  A5: for g being Element of product F1
  holds (for i being Element of I holds (f.i).g = (proj (F2,i)) . (phi.g))
  by Th42;
  for g being Element of product F1
  holds phi.g = g
  <<Proof step: $\varphi$ is the inclusion morphism>>
  hence product F1 is Subgroup of product F2 by InclByAnyOtherName;
end;
@ %def Th56

\begin{proof}[Proof step]
We construct a homomorphism $f=\pi_{i}\circ\iota\colon\mathcal{F}_{i}\to\mathcal{F}_{i}$.
\end{proof}

<<Proof step: construct endomorphism family $f_{i}=\pi_{i}\circ\iota$>>=
deffunc Fun(Element of I) = (incl (F1.$1, F2.$1)) * (proj (F1, $1));
A2: for i being Element of I
holds Fun(i) is Homomorphism of product F1, F2.i
proof
  let i be Element of I;
  B1: (proj (F1, i)) is Homomorphism of product F1, F1.i by Def16;
  incl (F1.i, F2.i) is Homomorphism of F1.i, F2.i;
  thus thesis by B1,GROUP_6:39;
end;
consider f being Homomorphism-Family of product F1, F2 such that
A3: for i being Element of I holds f.i = Fun(i)
from RHomFamSch(A2);
@

\begin{proof}[Proof step]
We establish that $f_{i}(g)=g_{i}$ for any $g\in\prod\mathcal{F}^{(1)}$.
\end{proof}

<<Proof step: $f_{i}(g)=g_{i}$>>=
proof
  let g be Element of product F1;
  let i be Element of I;
  B1: F1.i is Subgroup of F2.i by A1;
  ((proj (F1, i)).g) in F1.i by Def16;
  then (g.i) is Element of F1.i by Def16;
  then B2: (incl (F1.i, F2.i)).(g.i) = g.i by B1, Th8;
  (f.i).g = ((incl (F1.i, F2.i)) * (proj (F1, i))).g by A3
         .= (incl (F1.i, F2.i)).((proj (F1, i)).g) by FUNCT_2:15
         .= (incl (F1.i, F2.i)).(g.i) by Def16
         .= (g.i) by B2;
  hence (f.i).g = g.i;
end;
@

\begin{proof}[Proof step]
We lastly establish $\varphi=\prod_{i}f_{i}$ is the identity morphism,
which makes it an inclusion.
\end{proof}

<<Proof step: $\varphi$ is the inclusion morphism>>=
proof
  let g be Element of product F1;
  B1: for i being object st i in I holds (phi.g).i = g.i
  proof
    let i be object;
    assume C1: i in I;
    then reconsider j=i as Element of I;
    g.j = (f.j).g by A4
       .= (proj (F2,j)) . (phi.g) by A5
       .= (phi.g).j by Def16;
    hence thesis;
  end;
  B2: dom g = I by GROUP_19:3;
  dom (phi.g) = I by GROUP_19:3;
  then dom g = dom (phi.g) by B2;
  hence g = phi.g by B1, B2, FUNCT_1:2;
end;
@

\begin{definition}
If $\mathcal{S}$ is a subgroup family of $\mathcal{F}$,
then $\prod\mathcal{S}$ is a subgroup of $\mathcal{F}$.
\end{definition}

<<Definition: product of subgroup-family>>=
definition
  let I be non empty set;
  let F be Group-Family of I;
  let S be Subgroup-Family of F;
  func product S -> strict Subgroup of product F equals product S;
  coherence
  proof
    for i being Element of I holds S.i is Subgroup of F.i;
    hence product S is strict Subgroup of product F by Th56;
  end;
end;
@

%% \begin{theorem}
%% Let $G_{1}$ and $G_{2}$ be groups, let $\varphi\colon G_{1}\to G_{2}$ be
%% a group morphism. For any finite sequence $\mathcal{F}_{1}$ of elements
%% in $G_{1}$, there is a corresponding finite sequence $\mathcal{F}_{2}$
%% of elements of $G_{2}$ obtained by applying $\varphi$ componentwise.
%% \end{theorem}

%% \begin{thm-remark}
%% I have not seen a standard name for the product of elements in a finite
%% sequence for a group $\prod_{g\in\mathcal{F}}g$, but I have seen it
%% called the Frobenius product. I guess I will keep that terminology, at
%% least for the moment.
%% \end{thm-remark}
\begin{theorem}
The image of the projection morphism is the direct factor (as a strict group).
\end{theorem}

<<Theorem: image of projection morphism>>=
theorem Th59:
  Image (proj (F, i)) = the multMagma of F.i
proof
  for x being Element of F.i st x in the carrier of F.i
  holds x in the carrier of Image (proj (F, i))
  proof
    let x be Element of F.i;
    assume A1: x in the carrier of F.i;
    ex g being Element of product F st x = (proj (F, i)).g
    proof
      dom (1_(product F)) = I by GROUP_19:3;
      then B1: ((1_(product F)) +* (i,x)).i = x by FUNCT_7:31;
      (1_(product F)) +* (i,x) in ProjSet (F, i) by A1, GROUP_12:def 1;
      then reconsider g = ((1_(product F)) +* (i,x)) as Element of product F;
      take g;
      (proj (F, i)).((1_(product F)) +* (i,x))
       = (proj (F, i)).g
      .= ((1_(product F)) +* (i,x)).i by Def16
      .= x by B1;
      hence thesis;
    end;
    then x in Image (proj (F, i)) by GROUP_6:45;
    hence x in the carrier of Image (proj (F, i)) by GROUP_6:def 10;
  end;
  then the carrier of F.i c= the carrier of Image (proj (F, i)) by SUBSET_1:2;
  hence thesis by GROUP_2:61;
end;
@ %def Th59

\begin{theorem}
If we have two families of subgroups $\mathcal{F}^{(1)}$ and
$\mathcal{F}^{(2)}$ such that
$\pi_{i}(\prod\mathcal{F}^{(1)})\subgroup\pi_{i}(\prod\mathcal{F}^{(2)})$,
then $\prod\mathcal{F}^{(1)}\subgroup\prod\mathcal{F}^{(2)}$.
\end{theorem}

<<Theorem: if $\pi_{i}(\prod\mathcal{F}^{(1)})\subgroup\pi_{i}(\prod\mathcal{F}^{(2)})$, then $\prod\mathcal{F}^{(1)}\subgroup\prod\mathcal{F}^{(2)}$>>=
theorem Th60:
  for F1,F2 being componentwise_strict Subgroup-Family of F
  st (for i being Element of I
      holds Image (proj (F1, i)) is Subgroup of Image (proj (F2, i)))
  holds (product F1) is strict Subgroup of (product F2)
proof
  let F1,F2 be componentwise_strict Subgroup-Family of F;
  assume A1: for i being Element of I
  holds Image (proj (F1, i)) is Subgroup of Image (proj (F2, i));
  for i being Element of I holds F1.i is Subgroup of F2.i
  proof
    let i be Element of I;
    Image (proj (F1, i)) is Subgroup of Image (proj (F2, i)) by A1;
    then F1.i is Subgroup of Image (proj (F2, i)) by Th59;
    hence F1.i is Subgroup of F2.i by Th59;
  end;
  hence thesis by Th56;
end;
@ %def Th60

\begin{theorem}
Let $G\subgroup\prod\mathcal{F}$ be a subgroup,
let $\mathcal{S}$ be a subgroup-family of $\mathcal{F}$ defined by
$\mathcal{S}_{i} = (\pi_{i}\circ\iota)(G)$, and let
$\varphi_{i}\colon G\to\mathcal{S}_{i}$ be a homomorphism family
defined by $\varphi_{i} = \pi_{i}\circ\iota$.
Then we have $\prod\varphi=\id_{G}$.
\end{theorem}

<<Theorem: if $G\subgroup\prod\mathcal{F}$, then $\prod(\pi_{i}\circ\iota_{G})=\id_{G}$>>=
theorem Th64c:
  for G being strict Subgroup of product F
  for S st (for i being Element of I holds S.i = Image ((proj (F,i)) * (incl G)))
  for f being Homomorphism-Family of G, S
  st (for i being Element of I holds f.i = ((proj (F,i)) * (incl G)))
  holds product f = id the carrier of G
proof
  let G be strict Subgroup of product F;
  let S;
  assume A1: for i being Element of I
  holds S.i = Image ((proj (F,i)) * (incl G));
  let f be Homomorphism-Family of G, S;
  assume A2: for i being Element of I holds f.i = ((proj (F,i)) * (incl G));
  A3: for g being Element of G
  for i being Element of I
  holds ((proj (F,i)) * (incl G)).g = ((proj (F, i)) * (product f)).g
  proof
    let g be Element of G;
    let i be Element of I;
    for j being Element of I holds S.j is Subgroup of F.j; 
    then B1: (product S) is Subgroup of (product F) by Th56;
    ((product f).g) in (product S);
    then B2: ((product f).g) in (product F) by B1, GROUP_2:40;
    B3: ((product f).g).i = (proj (F, i)).((product f).g) by B2, Def16
                         .= ((proj (F, i)) * (product f)).g by FUNCT_2:15;
    ((proj (F,i)) * (incl G)).g = (f.i).g by A2
                               .= ((product f).g).i by Def17;
    hence ((proj (F,i)) * (incl G)).g = ((proj (F, i)) * (product f)).g by B3;
  end;
  A4: for g being Element of product F st g in G
  holds (product f).g = g
  proof
    let g be Element of product F;
    assume B1: g in G;
    ((product f).g) in (product F)
    proof
      for j being Element of I holds S.j is Subgroup of F.j;
      then C1: (product S) is Subgroup of (product F) by Th56;
      ((product f).g) in (product S) by B1, FUNCT_2:def 1, FUNCT_2:5;
      hence ((product f).g) in (product F) by C1, GROUP_2:40;
    end;
    then reconsider foo=(product f).g as Element of product F;
    B2: for i being Element of I
    holds ((proj (F, i)) * (incl G)).g = g.i
    proof
      let i be Element of I;
      ((proj (F,i)) * (incl G)).g
       = (proj (F, i)). ((incl G).g) by B1, FUNCT_2:15
      .= (proj (F, i)).((id the carrier of G).g) by DefIncl
      .= (proj (F, i)).g by B1, FUNCT_1:18
      .= g.i by Def16;
      hence thesis;
    end;
    B3: dom g = I & dom foo = I by GROUP_19:3;
    for i being Element of I holds foo.i = g.i
    proof
      let i be Element of I;
      (proj (F, i)).foo = ((proj (F,i)) * (product f)).g by B1, FUNCT_2:15
                       .= ((proj (F, i)) * (incl G)).g by B1, A3
                       .= g.i by B2;
      hence thesis by Def16;
    end;
    then foo = g by B3;
    hence (product f).g = g;
  end;
  for x being object st x in the carrier of G holds (product f).x = x
  proof
    let x be object;
    assume x in the carrier of G;
    then B1: x in G;
    then x in product F by GROUP_2:40;
    hence (product f).x = x by B1, A4;
  end;
  hence product f = id the carrier of G;
end;
@ 

\begin{theorem}
If $H\subgroup G_{2}$ is a subgroup, if $\psi\colon G_{1}\to G_{2}$ and $\varphi\colon G_{1}\to H$
are group morphisms such that $\psi=\varphi$,
then $\Im(\psi)=\Im(\varphi)$.
\end{theorem}

<<Theorem: images of equal morphisms are equal (as subgroups)>>=
theorem Th64d:
  for G1,G2 being Group
  for H being Subgroup of G2
  for f1 being Homomorphism of G1,G2
  for f2 being Homomorphism of G1,H
  st f1 = f2
  holds Image f1 = Image f2
proof
  let G1,G2 be Group;
  let H be Subgroup of G2;
  let f1 be Homomorphism of G1,G2;
  let f2 be Homomorphism of G1,H;
  assume A1: f1 = f2;
  A2: Image f2 is strict Subgroup of G2 by GROUP_2:56;
  for g being Element of G2 holds g in Image f1 iff g in Image f2
  proof
    let g be Element of G2;
    hereby 
      assume g in Image f1;
      then ex a being Element of G1 st g = f1.a by GROUP_6:45;
      hence g in Image f2 by A1, GROUP_6:45;
    end;
    assume g in Image f2;
    then ex a being Element of G1 st g = f2.a by GROUP_6:45;
    hence g in Image f1 by A1, GROUP_6:45;
  end;
  hence Image f1 = Image f2 by A2, GROUP_2:def 6;
end;
@ 

\begin{theorem}
Let $\varphi\colon G\to H$ be a group morphism.
If for each $y\in H$ there is an $x\in G$ such that $y=\varphi(x)$,
then $\varphi$ is surjective.
\end{theorem}

<<Theorem: criteria for group morphism being surjective>>=
theorem Th64e:
  for G1,G2 being Group
  for phi being Homomorphism of G1,G2
  st (for y being Element of G2 ex x being Element of G1 st phi.x=y)
  holds phi is onto
proof
  let G1,G2 be Group;
  let phi be Homomorphism of G1,G2;
  assume Z1: for y being Element of G2 ex x being Element of G1 st phi.x=y;
  for y being object st y in the carrier of G2
  ex x being object st (x in the carrier of G1 & y = phi.x)
  proof
    let y be object;
    assume B1: y in the carrier of G2;
    then reconsider yy=y as Element of G2;
    consider x being Element of G1 such that
    B2: phi.x = yy by Z1;
    take x;
    thus x in the carrier of G1;
    thus y = phi.x by B2;
  end;
  then rng phi = the carrier of G2 by FUNCT_2:10;
  hence thesis;
end;
@ 

\begin{theorem}
Let $\mathcal{S}$ be a family of normal subgroups
$\mathcal{S}_{i}\normalSubgroup\mathcal{F}_{i}$ for each $i\in I$.
Then $\prod\mathcal{S}\normalSubgroup\prod\mathcal{F}$.
\end{theorem}

<<Theorem: product of normal subgroup-family is a normal subgroup of the product group>>=
theorem Th90:
  for S being normal Subgroup-Family of F
  holds product S is normal Subgroup of product F
proof
  let S be normal Subgroup-Family of F;
  for g being Element of product F
  holds (product S) |^ g is Subgroup of product S
  proof
    let g be Element of product F;
    for h being Element of product F st h in (product S) |^ g
    holds h in product S
    proof
      let h be Element of product F;
      assume h in (product S) |^ g;
      then consider a being Element of product F such that
      A2: h = a |^ g & a in product S
      by GROUP_3:58;
      A3: dom h = I by GROUP_19:3;
      for i being Element of I holds h.i in S.i
      proof
        let i be Element of I;
        B1: a.i in S.i by A2, GROUP_19:5;
        h.i = (((g") * a) * g).i by A2,GROUP_3:def 2
           .= (((g") * a)/.i) * (g/.i) by GROUP_7:1
           .= (((g")/.i) * (a/.i)) * (g/.i) by GROUP_7:1
           .= (((g/.i)") * (a/.i)) * (g/.i) by GROUP_7:8
           .= (a/.i) |^ (g/.i) by GROUP_3:def 2;
        then h.i in (S.i) |^ (g/.i) by B1, GROUP_3:58;
        then h.i in the multMagma of S.i by GROUP_3:def 13;
        hence thesis;
      end;
      hence h in product S by A3, Th49;
    end;
    hence (product S) |^ g is Subgroup of product S by GROUP_2:58; 
  end;
  hence thesis by GROUP_3:122;
end;
@ %def Th90

\begin{registration}
We should register the fact that the product of a normal subgroup-family
is a normal subgroup.
\end{registration}

<<Register: product of normal subgroup-family is a normal subgroup>>=
registration
  let I be non empty set;
  let F be Group-Family of I;
  let S be normal Subgroup-Family of F;
  cluster product S -> normal for Subgroup of product F;
  correctness by Th90;
end;
@ 
