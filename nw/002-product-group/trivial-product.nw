% -*- mode: poly-noweb; noweb-code-mode: mizar-mode; -*-

\section{Products of Trivial Families}

We need to establish that the null product object in the category of
groups is the terminal object; i.e., if [[I]] is the empty set and [[F]]
is a [[multMagma-Family of I]], then [[product F]] is the trivial group.
Most of our work has been handled in other articles, the hardest bit of
work left is just connecting the dots.

<<On trivial groups and products of empty families>>=
<<Theorem: singleton magma is also a trivial group>>

<<Theorem: the product magma of the empty family is the trivial group>>
@ 

\begin{theorem}
Let $M$ be a strict magma such that its underlying set is a singleton $M=\{x\}$.
Then $M$ is the trivial Group.
\end{theorem}

<<Theorem: singleton magma is also a trivial group>>=
theorem LmTriv:
  for M being strict multMagma
  st (ex x being object st the carrier of M = {x})
  ex G being strict trivial Group st M=G
proof
  let M be strict multMagma;
  given x being object such that 
  A1: the carrier of M = {x};
  the carrier of M is non empty by A1;
  then reconsider M as non empty multMagma;
  x in the carrier of M by A1,TARSKI:def 1;
  then A2: x in M;
  then reconsider x as Element of M;
  A3: for y being Element of M holds y = x by A1,TARSKI:def 1;
  x * x in M by A2,FUNCT_1:def 2;
  then A4: x * x = x by A3;
  for a,b,c being Element of M holds (a * b) * c = a * (b * c)
  <<Proof: singleton magma is associative>>;
  then A5: M is associative by GROUP_1:def 3;
  
  ex e being Element of M st
  for h being Element of M holds
  (h * e = h & e * h = h & ex g being Element of M st (h * g = e & g * h = e))
  <<Proof: singleton magma is group-like>>;
  then A6: M is Group-like by GROUP_1:def 2;
  then reconsider G=M as strict Group by A5;
  
  G is trivial by A1,GROUP_6:def 2;
  then reconsider G as strict trivial Group;
  take G;
  thus thesis;
end;
@ 

<<Proof: singleton magma is associative>>=
proof
  let a,b,c be Element of M;
  a in M;
  B1: a = x & b = x & c = x by A3;
  B2: (a * b) * c = (x * b) * c by A3
                 .= (x * x) * c by A3
                 .= x * c by A4
                 .= x * x by A3
                 .= x by A4;
  a * (b * c) = a * (x * c) by A3
             .= a * (x * x) by A3
             .= a * x by A4
             .= x * x by A3
             .= x by A4;
  hence (a * b) * c = a * (b * c) by B2;
end
@ 

<<Proof: singleton magma is group-like>>=
proof
  take e = x;
  let h be Element of M;
  B1: h = x by A3;
  thus h * e = h by B1,A4;
  thus e * h = h by B1,A4;
  take g = x;
  thus h * g = e by B1,A4;
  thus g * h = e by B1,A4;
end
@

\begin{theorem}
Let $I=\emptyset$ and $\mathcal{F}$ be an $I$-indexed family of magmas. Then
$\prod\mathcal{F}$ is the trivial group.
\end{theorem}

<<Theorem: the product magma of the empty family is the trivial group>>=
theorem
  for I being empty set
  for F being multMagma-Family of I
  holds product F is trivial Group
proof
  let I be empty set;
  let F be multMagma-Family of I;
  I = {};
  then F = {};
  then product (Carrier F) = {{}} by CARD_3:10;
  then the carrier of (product F) = {{}} by GROUP_7:def 2;
  then ex x being object st the carrier of (product F) = {x};
  then ex G being strict trivial Group st (product F)=G by LmTriv;
  hence thesis;
end;
@ 
