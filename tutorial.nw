\documentclass[oneside]{book}
\usepackage{mizar}
\usepackage{tutmac}

\title{Tutorial on Mizar}
\author{Alex Nelson}
\date{December 11, 2021}

\begin{document}

\begin{mizar}
theorem
  (for a, b st b is Element of H holds b |^ a in H) iff H is normal by Lm1,Lm2;

definition
  let G;
  func Aut G -> FUNCTION_DOMAIN of the carrier of G, the carrier of G means
  :
  Def1: ( for f being Element of it holds f is Homomorphism of G, G ) & for h
  holds h in it iff h is one-to-one & h is onto;
  existence
  proof
    set X = { x where x is Element of Funcs (the carrier of G,the carrier of G
    ) : ex h st x = h & h is one-to-one & h is onto };
A1: id the carrier of G in X
    proof
      set I = id the carrier of G;
A2:   I in Funcs (the carrier of G, the carrier of G) by FUNCT_2:8;
      reconsider I as Homomorphism of G, G by GROUP_6:38;
      rng I = the carrier of G by RELAT_1:45;
      then I is onto;
      hence thesis by A2;
    end;
    reconsider X as set;
    X is functional
    proof
      let q be object;
      assume q in X;
      then
      ex x be Element of Funcs (the carrier of G, the carrier of G) st q =
      x & ex h st h = x & h is one-to-one & h is onto;
      hence thesis;
    end;
    then reconsider X as functional non empty set by A1;
    X is FUNCTION_DOMAIN of the carrier of G, the carrier of G
    proof
      let a be Element of X;
      a in X;
      then ex x be Element of Funcs (the carrier of G, the carrier of G ) st a
      = x & ex h st h = x & h is one-to-one & h is onto;
      hence thesis;
    end;
    then reconsider
    X as FUNCTION_DOMAIN of the carrier of G, the carrier of G;
    take X;
    hereby
      let f be Element of X;
      f in X;
      then
      ex x being Element of Funcs (the carrier of G, the carrier of G) st
      f = x & ex h st h = x & h is one-to-one & h is onto;
      hence f is Homomorphism of G, G;
    end;
    let x be Homomorphism of G, G;
    hereby
      assume x in X;
      then
      ex f being Element of Funcs (the carrier of G, the carrier of G) st
      f = x & ex h st h = f & h is one-to-one & h is onto;
      hence x is one-to-one & x is onto;
    end;
A3: x is Element of Funcs (the carrier of G, the carrier of G) by FUNCT_2:8;
    assume x is one-to-one & x is onto;
    hence thesis by A3;
  end;
  uniqueness
  proof
    let F1, F2 be FUNCTION_DOMAIN of the carrier of G, the carrier of G such
    that
A4: for f being Element of F1 holds f is Homomorphism of G, G and
A5: for h holds h in F1 iff h is one-to-one & h is onto and
A6: for f being Element of F2 holds f is Homomorphism of G, G and
A7: for h holds h in F2 iff h is one-to-one & h is onto;
A8: F2 c= F1
    proof
      let q be object;
      assume
A9:   q in F2;
      then reconsider h1 = q as Homomorphism of G, G by A6;
      h1 is one-to-one & h1 is onto by A7,A9;
      hence thesis by A5;
    end;
    F1 c= F2
    proof
      let q be object;
      assume
A10:  q in F1;
      then reconsider h1 = q as Homomorphism of G, G by A4;
      h1 is one-to-one & h1 is onto by A5,A10;
      hence thesis by A7;
    end;
    hence thesis by A8,XBOOLE_0:def 10;
  end;
end;

theorem
  Aut G c= Funcs (the carrier of G, the carrier of G)
proof
  let q be object;
  assume q in Aut G;
  then ex f be Element of Aut G st f = q;
  hence thesis by FUNCT_2:9;
end;

\end{mizar}
\end{document}
