\section{Tarski Grothendieck Set Theory}

\begin{paracol}{2}
%\hbadness5000
For simplicity we adopt the following convention: $x$, $y$,
$z$, $u$ will denote objects of any type; $N$, $M$, $X$, $Y$, $Z$
will denote objects of the type set.
  
Next we will state two axioms:
\begin{equation}
x~\textbf{is}~\textrm{set},
\end{equation}
\begin{equation}
(\textbf{for}~x~\textbf{holds}~x\in X~\textbf{iff}~x\in Y)~\textbf{implies}~X=Y.
\end{equation}

\switchcolumn

\begin{mizar}
 reserve x,y,z,u for object;
 reserve N,M,X,Y,Z for set;

:: Everything is a set
theorem :: TARSKI:1
  for x being object holds x is set;

:: Extensionality
theorem :: TARSKI:2 
  (for x being object
   holds x in X iff x in Y)
  implies X = Y;
\end{mizar}

\switchcolumn*
\ensurevspace{5cm}

We now introduce two functors. Let us consider $y$. The functor
\[ \{\,y\,\} \]
with values of the type set, is defined by
\[ x\in\textbf{it}~\textbf{iff}~x=y.\]
Let us consider $z$. The functor
\[ \{\,y,z\,\} \]
with values of the type set, is defined by
\[ x\in\textbf{it}~\textbf{iff}~x=y~\textbf{or}~x=z.\]
The following axioms hold:
\begin{equation}
X=\{y\}~\textbf{iff\ for}~x~\textbf{holds}~x\in X~\textbf{iff}~x=y,
\end{equation}
\begin{multline}
X=\{y,z\}~\textbf{iff\ for}~x~\textbf{holds}~x\in X\;\textbf{iff}\\
%\textbf{iff}~
x=y~\textbf{or}~x=z.
\end{multline}

\switchcolumn

\begin{mizar}
definition
  let y be object;
  func { y } -> set means
:: TARSKI:def 1
    for x being object
    holds x in it iff x = y;

  let z be object;
  func { y, z } -> set means
:: TARSKI:def 2
    x in it iff x = y or x = z;
  commutativity;
end;
\end{mizar}

\switchcolumn*
\ensurevspace{5cm}

Let us consider $X$, $Y$. The predicate
\[ X\subset Y\quad\mbox{is defined by}\quad x\in
X\;\textbf{implies}\;x\in Y.\]
Let us consider $X$. The functor
\[\bigcup X,\]
with values of the type set, is defined by
\[x\in\textbf{it\ iff\ ex}\;Y\;\textbf{st}\;x\in Y\land Y\in X.\] 
Then we get
\begin{multline}
X=\bigcup Y\;\textbf{iff}\;\textbf{for}\; x\;\textbf{holds}\; x\in
X\;\textbf{iff}\\
\textbf{ex}\; Z\;\textbf{st}\; x\in Z\land Z\in Y,
\end{multline}
\begin{equation}
X=\bool\,Y\;\textbf{iff}\;\textbf{for}\; Z\;\textbf{holds}\;
Z\in X\;\textbf{iff}\;Z\subset Y.
\end{equation}

\switchcolumn

\begin{mizar}
definition 
  let X,Y;
  pred X c= Y means
:: TARSKI:def 3
   for x being object 
   holds x in X implies x in Y;
  reflexivity;
end;

definition 
  let X;
  func union X -> set means
:: TARSKI:def 4
    x in it iff ex Y st x in Y & Y in X;
end;
\end{mizar}

\switchcolumn*
\ensurevspace{5cm}

The regularity axiom claims that
\begin{multline}
x\in X\;\textbf{implies\ ex}\;Y\textbf{st}\; Y\in X\land\\
\textbf{not\ ex}\; x\;\textbf{st}\; x\in X\land x\in Y.
\end{multline}

\switchcolumn

\begin{mizar}
:: Regularity
theorem :: TARSKI:3
  x in X implies
   ex Y st Y in X &
     not ex x st x in X & x in Y;
\end{mizar}

%% \switchcolumn

%% \switchcolumn
\begin{mizar}
definition let x, X be set;
  redefine pred x in X;
  asymmetry;
end;
\end{mizar}

\switchcolumn*
\ensurevspace{5cm}

The scheme \textit{Fraenkel} deals with a constant $\mathcal{A}$ that
has the type set and a binary predicate $\mathcal{P}$ and states that
the following holds:
\begin{multline*}
  \textbf{ex}\; X\;\textbf{st\ for}\;x\;\textbf{holds}\\
  x\in X\;\textbf{iff}\;
\textbf{ex}\;y\;\textbf{st}\;y\in\mathcal{A}\land\mathcal{P}[y,x]
\end{multline*}
provided the parameters satisfy the following extra condition:
\begin{itemize}
\item \textbf{for} $x$, $y$, $z$ \textbf{st}
  $\mathcal{P}[x,y]\land\mathcal{P}[x,z]$ \textbf{holds} $y=z$.
\end{itemize}

\switchcolumn

\begin{mizar}
scheme :: TARSKI:sch 1
 Replacement{ A() -> set,
              P[object,object] }:
 ex X
 st for x being object 
    holds x in X iff
          ex y being object 
          st y in A() & P[y,x]
provided
 for x,y,z being object 
 st P[x,y] & P[x,z]
 holds y = z;
\end{mizar}

\switchcolumn*
\ensurevspace{5cm}

Let us consider $x$, $y$. The functor
\[\langle x,y\rangle,\]
is defined by
\[\textbf{it} = \{\,\{x,y\,\},\{\,x\,\}\,\}.\]
According to the definition
\begin{equation}
\langle x,y\rangle = \{\,\{x,y\,\},\{\,x\,\}\,\}.
\end{equation}

\switchcolumn
\begin{mizar}
definition
  let x,y be object;
  func [x,y] -> object equals
:: TARSKI:def 5
    { { x,y }, { x } };
end;
\end{mizar}

\switchcolumn*
\ensurevspace{5cm}

Let us consider $X$, $Y$. The predicate
\[X\equipotent Y\]
is defined by
\begin{multline*}
\textbf{ex}\;Z\;\textbf{st}(\textbf{for}\;x\;\textbf{st}\;x\in X\;\textbf{ex}\;
y\;\textbf{st}y\in Y\land\langle x,y\rangle\in Z)\land\\
(\textbf{for}\;x\;\textbf{st}\;x\in X\;\textbf{ex}\;
y\;\textbf{st}y\in Y\land\langle x,y\rangle\in Z)\land\\
\textbf{for}\;x,y,z,u\;\textbf{st}\langle x,y\rangle\in Z\land\langle z,u\rangle\in Z\\
\textbf{holds}\;x=z\;\textbf{iff}\;y=u.
\end{multline*}
\

\switchcolumn

\begin{mizar}
definition let X,Y;
  pred X,Y are_equipotent means
:: TARSKI:def 6
  ex Z st
  (for x st x in X
   ex y st y in Y & [x,y] in Z) &
  (for y st y in Y
   ex x st x in X & [x,y] in Z) &
  for x,y,z,u st [x,y] in Z & [z,u] in Z
  holds x = z iff y = u;
end;
\end{mizar}

\switchcolumn

The Tarski's axiom A claims that
\begin{multline}
\textbf{ex}\;M\;\textbf{st}\;N\in M\land\\
(\textbf{for}\;X,Y\;\textbf{holds}\;X\in M\land
Y\subset X\;\textbf{implies}\;Y\in M)\land\\
(\textbf{for}\;X\;\textbf{holds}\;X\in M
\;\textbf{implies}\;\bool X\in M)\land\\
(\textbf{for}\;X\;\textbf{holds}\;X\subset M\;\textbf{implies}\;
X\equipotent M\;\textbf{or}\;X\in M).
\end{multline}

\switchcolumn

\begin{mizar}
theorem :: TARSKI_A:1
 ex M st N in M &
   (for X,Y holds X in M & Y c= X
    implies Y in M) &
   (for X st X in M
    ex Z st Z in M &
            for Y st Y c= X
            holds Y in Z) &
   (for X holds X c= M 
    implies X,M are_equipotent
            or X in M);
\end{mizar}
\end{paracol}
