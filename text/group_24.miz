:: Semidirect products
::  by Alexander M. Nelson
::
:: This code can be distributed under the GNU General Public Licence
:: version 3.0 or later, or the Creative Commons Attribution-ShareAlike
:: License version 3.0 or later, subject to the binding interpretation
:: detailed in file COPYING.interpretation.
:: See COPYING.GPL and COPYING.CC-BY-SA for the full text of these
:: licenses, or see http://www.gnu.org/licenses/gpl.html and
:: http://creativecommons.org/licenses/by-sa/3.0/.

environ
 vocabularies TARSKI, BINOP_1, XBOOLE_0, FUNCT_1, RELAT_1, ALGSTR_0,
   FUNCOP_1, SUBSET_1, FUNCT_2, FINSET_1, STRUCT_0, ZFMISC_1, CARD_1, CARD_3, MONOID_0,
   GROUP_1, GROUP_2, FINSEQ_1, GROUP_6, PARTFUN1, MSSUBFAM, RLVECT_2,
   PRE_TOPC, WELLORD1, AUTGROUP, MOD_4,
   GROUP_4, :: for "gr"
   EQREL_1, :: for "\/"
   NATTRA_1, :: for `*`
   NEWTON, :: for "|^"
   REALSET1, :: for "||"
   NUMBERS, :: for NAT
   GROUP_24;
 notations TARSKI, XBOOLE_0, ENUMSET1, SUBSET_1, RELAT_1, FUNCT_1, ZFMISC_1,
   REALSET1, :: for "||"
   FINSET_1, CARD_1, CARD_3, FINSEQ_1, FINSEQ_2,
   RELSET_1, PARTFUN1, FUNCT_2, FUNCT_4,
   PBOOLE, FUNCOP_1, STRUCT_0, ALGSTR_0, MONOID_0, GROUP_1, GROUP_2,
   LATTICES, GROUP_3,
   GROUP_4,
   GROUP_5, GROUP_6, PRALG_1, BINOP_1,
   AUTGROUP, GROUP_7,
   GROUP_22;
 constructors STRUCT_0, ALGSTR_0, ZFMISC_1, BINOP_1, REALSET1,
   GROUP_1, LATTICES, WELLORD2, CARD_1, 
   FINSUB_1, :: for GROUP_2:81
   FINSET_1, :: for finite
   FINSEQ_1, FINSEQ_2,
   GROUP_2, GROUP_3, GROUP_4, GROUP_5, GROUP_6, MONOID_0, PRALG_1, PRALG_2,
   RELSET_1, FUNCT_2, FUNCT_4, AUTGROUP, GROUP_7, GROUP_10, GROUP_22;
 registrations XBOOLE_0, SUBSET_1, RELAT_1, FUNCT_1, FINSET_1,
   FINSEQ_1, REALSET1, STRUCT_0, GROUP_1, GROUP_2, GROUP_3, MONOID_0,
   LATTICES, ORDINAL1, CARD_1, CARD_3, NEWTON, FUNCOP_1, RELSET_1, FUNCT_2,
   FUNCT_4, GROUP_6, GROUP_7, GR_CY_1, GR_CY_2, GROUPP_1, GROUP_8;
 requirements NUMERALS, REAL, BOOLE, SUBSET, ARITHM;
 definitions STRUCT_0, GROUP_1, TARSKI, ALGSTR_0;
 equalities STRUCT_0, ALGSTR_0, PARTFUN1, AUTGROUP;
 expansions STRUCT_0, GROUP_1, TARSKI;
 theorems SUBSET_1, PRALG_1, FUNCT_1, CARD_3, FUNCT_2, BINOP_1, TARSKI,
   ZFMISC_1, RELAT_1, CARD_1,
   GROUP_1, GROUP_2, GROUP_3, GROUP_4, GROUP_5, GROUP_6, FINSEQ_1,
   XBOOLE_0, MONOID_0, PARTFUN1, AUTGROUP, GROUP_7;
 schemes BINOP_1, FUNCT_2;

begin
reserve G for strict Group,
  A for Group,
  phi for Homomorphism of A, AutGroup G;

begin :: Preliminaries
reserve G for strict Group;
reserve A for Group;
reserve phi for Homomorphism of A,AutGroup(G);

::$N Dedekinds ``Modular'' Identity
theorem DedekindModularProperty:
  for G being Group
  for A,B,C being Subgroup of G
  st A is Subgroup of C
  holds A * (B /\ C) = (A * B) /\ carr C
proof
  let G be Group;
  let A,B,C be Subgroup of G;
  assume A is Subgroup of C;
  then the carrier of A c= the carrier of C by GROUP_2:def 5;
  then the carrier of A c= carr C by GROUP_2:def 9;
  then A2: carr A c= carr C by GROUP_2:def 9;
  for x being Element of G
  st x in A * (B /\ C)
  holds x in (A * B) /\ carr C
  proof
    let x be Element of G;
    assume x in A * (B /\ C);
    then x in (carr A) * carr (B /\ C) by GROUP_4:def 8;
    then consider a,b being Element of G such that
    B2: x = a*b & a in carr A & b in carr (B /\ C)
    by GROUP_2:8;
    b in carr B /\ carr C by B2, GROUP_2:81;
    then B3: b in carr B & b in carr C by XBOOLE_0:def 4;
    then x in (carr A)*(carr B) by B2, GROUP_2:8;
    then B4: x in A*B by GROUP_4:def 8;
    x in carr C by A2,B2,B3,GROUP_2:74;
    hence x in (A * B) /\ carr C by B4, XBOOLE_0:def 4;
  end;
  then A3: A * (B /\ C) c= (A * B) /\ carr C;
  for x being Element of G
  st x in (A * B) /\ carr C
  holds x in A * (B /\ C)
  proof
    let x be Element of G;
    assume B1: x in (A * B) /\ carr C;
    then x in A * B & x in carr C by XBOOLE_0:def 4;
    then x in (carr A) * (carr B) by GROUP_4:def 8;
    then consider a,b being Element of G such that
    B2: x = a * b & a in carr A & b in carr B by GROUP_2:8;
    a" in carr C & x in carr C by A2, B1, B2, GROUP_2:75, XBOOLE_0:def 4;
    then B3: (a") * x in carr C by GROUP_2:74;
    b = 1_G * b by GROUP_1:def 4
     .= (a" * a) * b by GROUP_1:def 5
     .= (a") * x by B2, GROUP_1:def 3;
    then b in B & b in C by B2, B3, GROUP_2:def 9;
    then b in B /\ C by GROUP_2:82;
    then b in carr (B /\ C) by GROUP_2:def 9;
    then a * b in (carr A) * carr (B /\ C) by B2, GROUP_2:8;
    hence thesis by B2, GROUP_4:def 8;
  end;
  then (A * B) /\ carr C c= A * (B /\ C);
  hence A * (B /\ C) = (A * B) /\ carr C by A3, XBOOLE_0:def 10;
end;

theorem Th2:
  for G being Group
  for H being Subgroup of G
  holds the multMagma of H is strict Subgroup of G
proof
  let G be Group;
  let H be Subgroup of G;
  set H0 = the multMagma of H;
  A1: the multMagma of H is non empty Group-like;
  (the carrier of H0 c= the carrier of G)
  & (the multF of H0 = (the multF of G)||(the carrier of H0))
  by GROUP_2:def 5;
  hence the multMagma of H is strict Subgroup of G by A1,GROUP_2:def 5;
end;

theorem Th3:
  for G being Group
  for N being normal Subgroup of G
  holds the multMagma of N is strict normal Subgroup of G
proof
  let G be Group;
  let N be normal Subgroup of G;
  reconsider N0 = the multMagma of N as strict Subgroup of G by Th2;

  for g being Element of G
  holds N0 |^ g = N0
  proof
    let g be Element of G;
    B1: carr N = the carrier of N by GROUP_2:def 9
              .= carr N0 by GROUP_2:def 9;
    B2: N |^ g = the multMagma of N by GROUP_3:def 13;
    the carrier of (N0 |^ g) = (carr N) |^ g by B1,GROUP_3:def 6
                            .= the carrier of N0 by B2, GROUP_3:def 6;
    hence N0 |^ g = N0 by GROUP_2:59;
  end;
  hence the multMagma of N is strict normal Subgroup of G
  by GROUP_3:def 13;
end;

theorem
  for G being Group
  for H being Subgroup of G
  for N being normal Subgroup of G
  st N is Subgroup of H
  holds the multMagma of N = the multMagma of (H,N)`*`
proof
  let G be Group;
  let H be Subgroup of G;
  let N be normal Subgroup of G;
  assume N is Subgroup of H;
  then N is normal Subgroup of H by GROUP_6:8;
  then reconsider N1=the multMagma of N as strict normal Subgroup of H
  by Th3;
  thus the multMagma of N = the multMagma of N1
                         .= the multMagma of (H,N)`*` by GROUP_6:def 1;
end;

theorem ThCapLemma:
  for G being Group
  for H1,H2,K being Subgroup of G
  for K1,K2 being Subgroup of K
  st the multMagma of H1 = the multMagma of K1
  & the multMagma of H2 = the multMagma of K2
  holds H1 /\ H2 = K1 /\ K2
proof
  let G be Group;
  let H1,H2,K be Subgroup of G;
  let K1,K2 be Subgroup of K;
  assume Z1: the multMagma of H1 = the multMagma of K1;
  assume Z2: the multMagma of H2 = the multMagma of K2;
  Z3: K1 /\ K2 is Subgroup of G by GROUP_2:56;
  Z4: for g being Element of G
  st g in H1 /\ H2 holds g in K1 /\ K2
  proof
    let g be Element of G;
    assume g in H1 /\ H2;
    then g in H1 & g in H2 by GROUP_2:82;
    then g in K1 & g in K2 by Z1,Z2;
    hence g in K1 /\ K2 by GROUP_2:82;
  end;

  for g being Element of G
  st g in K1 /\ K2 holds g in H1 /\ H2
  proof
    let g be Element of G;
    assume g in K1 /\ K2;
    then g in K1 & g in K2 by GROUP_2:82;
    then g in H1 & g in H2 by Z1,Z2;
    hence g in H1 /\ H2 by GROUP_2:82;
  end;
  hence H1 /\ H2 = K1 /\ K2 by Z3, Z4, GROUP_2:def 6;
end;

theorem ThProdLemma:
  for G being Group
  for H1,H2,K being Subgroup of G
  for K1,K2 being Subgroup of K
  st the multMagma of H1 = the multMagma of K1
  & the multMagma of H2 = the multMagma of K2
  holds H1 * H2 = K1 * K2
proof
  let G be Group;
  let H1,H2,K be Subgroup of G;
  let K1,K2 be Subgroup of K;
  assume A1: the multMagma of H1 = the multMagma of K1;
  assume A2: the multMagma of H2 = the multMagma of K2;
  A3: carr H1 = the carrier of H1 by GROUP_2:def 9
             .= carr K1 by A1, GROUP_2:def 9;
  A4: carr H2 = the carrier of H2 by GROUP_2:def 9
             .= carr K2 by A2,GROUP_2:def 9;
  for x being object
  holds x in (carr H1) * (carr H2) iff x in (carr K1) * (carr K2)
  proof
    let x be object;
    thus x in (carr H1) * (carr H2) implies x in (carr K1) * (carr K2)
    proof
      assume x in (carr H1) * (carr H2);
      then consider h1,h2 being Element of G such that
      B1: x = h1*h2 & h1 in carr H1 & h2 in carr H2
      by GROUP_2:8;
      reconsider k1=h1,k2=h2 as Element of K by A3,A4,B1;
      k1*k2 = x by B1,GROUP_2:43;
      hence x in (carr K1) * (carr K2) by A3, A4, B1, GROUP_2:8;
    end;

    thus x in (carr K1) * (carr K2) implies x in (carr H1) * (carr H2)
    proof
      assume x in (carr K1) * (carr K2);
      then consider k1,k2 being Element of K such that
      B1: x = k1*k2 & k1 in carr K1 & k2 in carr K2
      by GROUP_2:8;
      reconsider h1=k1,h2=k2 as Element of G by GROUP_2:42;
      h1 * h2 = x by B1,GROUP_2:43;
      hence x in (carr H1) * (carr H2) by A3, A4, B1, GROUP_2:8;
    end;
  end;
  then A5: (carr H1) * (carr H2) = (carr K1) * (carr K2) by TARSKI:2;

  thus H1 * H2 = (carr H1) * (carr H2) by GROUP_4:def 8
              .= K1 * K2 by A5,GROUP_4:def 8;
end;


begin :: Automorphism Group results

registration
  let G be Group;
  let H be Subgroup of G;
  cluster the carrier of H -> non empty;
  correctness;
end;

registration
  let G be Group;
  let H be Subgroup of G;
  cluster carr H -> non empty;
  correctness
  proof
    1_H in H;
    hence thesis by GROUP_2:def 9;
  end;
end;

registration
  let G be Group;
  let H be Subgroup of G;
  cluster Right_Cosets H -> non empty;
  correctness by GROUP_2:135;
end;

registration
  let G be strict Group;
  cluster -> multiplicative for Element of Aut G;
  correctness by AUTGROUP:def 1;
end;

registration
  let A be Group;
  cluster id A -> multiplicative;
  correctness by GROUP_6:38;

  cluster id A -> bijective;
  coherence
  proof
    set f = id A;
    rng (id A) = the carrier of A;
    then (id A is one-to-one & id A is onto) by FUNCT_2:def 3;
    hence id A is bijective;
  end;
end;

registration
  let G be strict Group;
  cluster AutGroup G -> non empty strict constituted-Functions;
  coherence by MONOID_0:80;
end;

definition
  let A, G, phi;
  let a be Element of A;
  func phi /. a -> Homomorphism of G,G equals phi.a;
  correctness
  proof
    phi.a is Element of Aut G;
    hence phi.a is Homomorphism of G, G;
  end;
end;

definition
  let A,G;
  let H be Subgroup of A;
  let phi be Homomorphism of H,AutGroup G;
  let a be Element of A such that
A1: a in H;
  func phi /. a -> Function of G,G equals
  :Def2a: phi.a;
  correctness
  proof
    reconsider h=a as Element of H by A1;
    phi.h = phi.a & phi.h is Element of Aut G;
    hence thesis;
  end;
end;

registration
  let G be strict Group, A be Group;
  let phi be Homomorphism of A, AutGroup G;
  let a be Element of A;
  cluster phi/.a -> multiplicative;
  coherence;
end;


registration
  let G, A be Group;
  cluster product (Carrier <*A,G*>) -> non empty;
  correctness;
  cluster product <*A,G*> -> non empty;
  correctness;
end;

registration
  let G, A;
  cluster product (Carrier <*A,G*>) -> non empty;
  correctness;
  cluster product (Carrier <*G,A*>) -> non empty;
  correctness;
end;

theorem Th7:
  for G1,G2 being Group
  for x being Element of product (<*G1,G2*>)
  holds x.1 in G1 & x.2 in G2 & dom x = {1,2}
proof
  let G1,G2 be Group;
  let x be Element of product (<*G1,G2*>);
  A1: dom (Carrier <*G1,G2*>) = {1,2} by PARTFUN1:def 2;
  x in the carrier of product (<*G1,G2*>);
  then x in product (Carrier <*G1,G2*>) by GROUP_7:def 2;
  then consider f being Function such that
  A2: x = f
      & dom f = dom (Carrier <*G1,G2*>)
      & (for y being object st y in dom (Carrier <*G1,G2*>)
         holds f.y in (Carrier <*G1,G2*>).y)
  by CARD_3:def 5;

  thus x.1 in G1
  proof
    B1: <* G1, G2 *>.1 = G1 by FINSEQ_1:44;
    B2: 1 in dom (Carrier <*G1,G2*>) by A1, TARSKI:def 2;
    then ex R being 1-sorted st
    (R = <*G1,G2*>.1 & (Carrier <*G1,G2*>).1 = the carrier of R)
    by PRALG_1:def 15;
    hence thesis by A2, B1, B2;
  end;

  thus x.2 in G2
  proof
    B1: <* G1, G2 *>.2 = G2 by FINSEQ_1:44;
    B2: 2 in dom (Carrier <*G1,G2*>) by A1, TARSKI:def 2;
    then ex R being 1-sorted st
    (R = <*G1,G2*>.2 & (Carrier <*G1,G2*>).2 = the carrier of R)
    by PRALG_1:def 15;
    hence thesis by A2, B1, B2;
  end;
  thus dom x = {1,2} by A2,PARTFUN1:def 2;
end;

Lm0:
  for G1,G2 being Group
  for x being Element of product (Carrier <*G1,G2*>)
  holds x.1 in G1 & x.2 in G2 & dom x = {1,2}
proof
  let G1,G2 be Group;
  let x be Element of product (Carrier <*G1,G2*>);
  x is Element of the carrier of product <*G1,G2*> by GROUP_7:def 2;
  hence thesis by Th7;
end;

Lm1:
  for A,G being Group
  for a being Element of A, g being Element of G
  holds <*a,g*> in product (Carrier <*A,G*>)
proof
  let A,G be Group;
  let a be Element of A;
  let g be Element of G;
  <*a,g*> in the carrier of (product <*A,G*>);
  hence <*a,g*> in product (Carrier <*A,G*>) by GROUP_7:def 2;
end;

theorem Th8:
  for G1,G2 being Group 
  for H1 being Subgroup of G1
  for H2 being Subgroup of G2
  for h1 being Element of G1 st h1 in H1
  for h2 being Element of G2 st h2 in H2
  holds <*h1,h2*> in product <*H1,H2*>
proof
  let G1,G2 be Group;
  let H1 be Subgroup of G1;
  let H2 be Subgroup of G2;
  let h1 be Element of G1;
  assume A1: h1 in H1;
  let h2 be Element of G2;
  assume A2: h2 in H2;
  set H = <*H1,H2*>;
  A3: dom Carrier H = {1,2} by PARTFUN1:def 2;
  A4: for a being object st a in {1,2}
      holds <*h1,h2*>.a in (Carrier <*H1,H2*>).a
  proof
    let a be object;
    assume B1: a in {1,2};
    then per cases by TARSKI:def 2;
    suppose B2: a = 1;
      then B3: ex R being 1-sorted st R = H.1 & (Carrier H).1 = the carrier of R
      by B1, PRALG_1:def 15;
      H.1 = H1 by FINSEQ_1:44;
      hence thesis by A1,B2,B3,FINSEQ_1:44;
    end;
    suppose B4: a = 2;
      then B5: ex R being 1-sorted st R = H.2 & (Carrier H).2 = the carrier of R
      by B1, PRALG_1:def 15;
      H.2 = H2 by FINSEQ_1:44;
      hence thesis by A2,B4,B5,FINSEQ_1:44;
    end;
  end;
  dom <*h1,h2*> = {1,2} by FINSEQ_1:2,89;
  then <*h1,h2*> in product Carrier (<*H1,H2*>) by A3,A4,CARD_3:9;
  hence <*h1,h2*> in product <*H1,H2*> by GROUP_7:def 2;
end;

theorem Th9:
  for G being Group
  for H,K being strict Subgroup of G
  st H <> K & K is Subgroup of H
  ex g being Element of G
  st g in H & not g in K
proof
  let G be Group;
  let H,K be strict Subgroup of G;
  assume A1: H <> K;
  assume A2: K is Subgroup of H;
  then not H is Subgroup of K by A1, GROUP_2:55;
  then the carrier of K c= the carrier of H
  & (the carrier of H <> the carrier of K) 
  by A2, GROUP_2:def 5, GROUP_2:57;
  then consider x being object such that
  A3: ( x in the carrier of H & not x in the carrier of K)
  by XBOOLE_0:6, XBOOLE_0:def 8;
  x in H & not x in K by A3;
  then x in G by GROUP_2:40;
  then reconsider g=x as Element of G;
  take g;
  thus g in H & not g in K by A3;
end;


begin :: Semidirect Products of Groups
definition
  let G, A, phi;
  func semidirect_product (G, A, phi) -> non empty strict multMagma means
  :Def3:
  the carrier of it = product (Carrier <* G, A *>) &
  (for f, g being Element of product (Carrier <* G, A *>)
   ex h being Function, a1 being Element of A, g2 being Element of G
   st h = (the multF of it).(f, g) &
      a1 = f.2 & g2 = g.1 &
      h.1 = (the multF of G).(f.1, (phi/.a1)/. g2) &
      h.2 = (the multF of A).((f.2), (g.2)));
  existence
  proof
    set U = product (Carrier <* G, A *>);
    defpred P[Element of U, Element of U, set] means
    ex h being Function, a1 being Element of A, g2 being Element of G
    st h = $3 & a1 = $1.2 & g2 = $2.1 &
       h.1 = (the multF of G).($1.1, (phi/.a1)/. g2) &
       h.2 = (the multF of A).(($1.2), ($2.2));

    A1: for x, y being Element of U ex z being Element of U st P[x,y,z]
    proof
      let x, y be Element of U;
      y.1 in G by Lm0; then
      reconsider g2 = y.1 as Element of G;
      x.2 in A by Lm0; then
      reconsider a1 = x.2 as Element of A;
      
      set h = <* (the multF of G).(x.1,(phi/.a1)/.g2),
                 (the multF of A).(x.2, y.2)*>;
      x.2 in A & y.2 in A & x.1 in G by Lm0; then
      (the multF of G).(x.1,(phi/.a1)/.g2) in G & (the multF of A).(x.2, y.2) in A
      by BINOP_1:17;
      then reconsider z = h as Element of product (Carrier <*G,A*>) by Lm1;
      take z;
      h = z & a1 = x.2 & g2 = y.1 &
      h.1 = (the multF of G).(x.1, (phi/.a1)/.g2) &
      h.2 = (the multF of A).(x.2, y.2) by FINSEQ_1:44;
      hence P[x, y, z];
    end;
    
    consider B being Function of [:U, U:],U such that
    A2: for a, b being Element of U holds P[a,b,B.(a,b)] from BINOP_1:sch 3(A1);

    set M = multMagma(# (product (Carrier <*G,A*>)), B #);
    reconsider M as non empty strict multMagma;
    take M;
    thus thesis by A2;
  end;
  uniqueness
  proof
    let M1,M2 being non empty strict multMagma;
    assume that
    A1: (the carrier of M1 = product (Carrier <* G, A *>) &
        (for f, g being Element of product (Carrier <* G, A *>)
         ex h being Function, a1 being Element of A, g2 being Element of G
         st h = (the multF of M1).(f, g) &
            a1 = f.2 & g2 = g.1 &
            h.1 = (the multF of G).(f.1, (phi/.a1)/. g2) &
            h.2 = (the multF of A).((f.2), (g.2)))) and
    A2: (the carrier of M2 = product (Carrier <* G, A *>) &
        (for f, g being Element of product (Carrier <* G, A *>)
         ex h being Function, a1 being Element of A, g2 being Element of G
         st h = (the multF of M2).(f, g) &
            a1 = f.2 & g2 = g.1 &
            h.1 = (the multF of G).(f.1, (phi/.a1)/. g2) &
            h.2 = (the multF of A).((f.2), (g.2))));
    
    for x,y being set st x in the carrier of M1 & y in the carrier of M1
    holds (the multF of M1).(x, y) = (the multF of M2).(x, y)
    proof
      let x,y be set;
      assume B1: x in the carrier of M1;
      assume B2: y in the carrier of M1;
      reconsider f = x, g = y as Element of product (Carrier <*G,A*>) by A1,B1,B2;
      consider h1 being Function, a11 being Element of A, g12 being Element of G
      such that
      B3: h1 = (the multF of M1).(f, g) & a11 = f.2 & g12 = g.1
          & h1.1 = (the multF of G).(f.1, (phi/.a11)/. g12)
          & h1.2 = (the multF of A).((f.2), (g.2)) by A1;
      consider h2 being Function, a21 being Element of A, g22 being Element of G
      such that
      B4: h2 = (the multF of M2).(f, g) & a21 = f.2 & g22 = g.1
          & h2.1 = (the multF of G).(f.1, (phi/.a21)/. g22)
          & h2.2 = (the multF of A).((f.2), (g.2)) by A2;
      B5: dom h1 = {1,2} & dom h2 = {1,2}
      proof
        h1 in product (Carrier <*G,A*>) by A1, B3, BINOP_1:17;
        then ex f1 being Function st h1 = f1 & dom f1 = dom (Carrier <*G,A*>)
        & (for y1 being object st y1 in dom (Carrier <*G,A*>)
        holds f1.y1 in (Carrier <*G,A*>).y1)
        by CARD_3:def 5;
        hence dom h1 = {1,2} by PARTFUN1:def 2; 
        h2 in product (Carrier <*G,A*>) by A2, B4, BINOP_1:17;
        then ex f1 being Function st h2 = f1 & dom f1 = dom (Carrier <*G,A*>)
        & (for y1 being object st y1 in dom (Carrier <*G,A*>)
        holds f1.y1 in (Carrier <*G,A*>).y1)
        by CARD_3:def 5;
        hence dom h2 = {1,2} by PARTFUN1:def 2;
      end;
      for i being object st i in dom h1 holds h1.i = h2.i
      proof
        let i be object;
        assume i in dom h1;
        then per cases by B5, TARSKI:def 2;
        suppose i = 1;
          hence thesis by B3,B4;
        end;
        suppose i = 2;
          hence thesis by B3,B4;
        end;
      end;
      hence (the multF of M1).(x, y) = (the multF of M2).(x, y)
      by B3, B4, B5, FUNCT_1:def 11;
    end;  
    hence thesis by A1,A2,BINOP_1:1;
  end;
end;

registration
  let G, A, phi;
  cluster semidirect_product (G, A, phi) -> non empty strict constituted-Functions;
  coherence
  proof
    the carrier of semidirect_product (G, A, phi) = product (Carrier <*G,A*>)
    by Def3;
    hence thesis by MONOID_0:80;
  end;
end;

registration
  let G, A, phi;
  cluster -> FinSequence-like for Element of (semidirect_product (G, A, phi));
  coherence
  proof
    for b1 being Element of semidirect_product(G, A, phi)
    holds b1 is FinSequence-like
    proof
      let b1 be Element of semidirect_product(G, A, phi);
      b1 is Element of product (Carrier <*G,A*>) by Def3;
      hence b1 is FinSequence-like;
    end;
    hence thesis;
  end;
end;

theorem Th10:
  the carrier of semidirect_product (G, A, phi) = the carrier of product <*G,A*>
proof
  thus the carrier of semidirect_product (G,A,phi)
   = product (Carrier <*G,A*>) by Def3
  .= the carrier of product <*G,A*> by GROUP_7:def 2;
end;

theorem Th11:
  for a being Element of A
  for g being Element of G
  holds <* g, a *> is Element of semidirect_product (G, A, phi) by Th10;

theorem Th12:
  for x being Element of semidirect_product (G, A, phi)
  holds x.1 in G & x.2 in A & dom x = {1,2}
proof
  let x be Element of semidirect_product (G, A, phi);
  x in the carrier of semidirect_product (G, A, phi);
  then x in the carrier of product <*G,A*> by Th10;
  hence thesis by Th7;
end;

theorem Th13:
  for x being Element of semidirect_product (G, A, phi)
  ex g being Element of G, a being Element of A
  st x = <* g, a *>
proof
  let x be Element of semidirect_product (G, A, phi);

  x.1 in G & x.2 in A by Th12;
  then consider g being Element of G, a being Element of A such that
  A1: g = x.1 & a = x.2;

  take g, a;
  
  dom x = {1,2} by Th12;
  then len x = 2 by FINSEQ_1:2,def 3;
  hence x = <* g, a *> by A1, FINSEQ_1:44;
end;

theorem Th14:
  for x1, x2, y1, y2 being object
  holds <*x1,y1*> = <*x2,y2*> iff (x1 = x2 & y1 = y2)
proof
  let x1, x2, y1, y2 be object;
  thus <*x1,y1*> = <*x2,y2*> implies (x1 = x2 & y1 = y2)
  proof
    assume A1: <*x1,y1*> = <*x2,y2*>;
    thus x1 = <*x1,y1*>.1 by FINSEQ_1:44
           .= x2 by A1, FINSEQ_1:44;
    thus y1 = <*x1,y1*>.2 by FINSEQ_1:44
           .= y2 by A1, FINSEQ_1:44;
  end;
  thus (x1 = x2 & y1 = y2) implies <*x1,y1*> = <*x2,y2*>;
end;

theorem Th15:
  for x,y being Element of semidirect_product (G, A, phi)
  for a1,a2 being Element of A
  for g1,g2 being Element of G
  st x = <*g1,a1*> & y = <*g2,a2*>
  holds x * y = <* g1 * ((phi /. a1)/.g2),a1 * a2 *>
proof
  let x,y be Element of semidirect_product (G, A, phi);
  let a1,a2 be Element of A;
  let g1,g2 be Element of G;
  assume A1: x = <*g1,a1*>;
  assume A2: y = <*g2,a2*>;
  
  x in product (Carrier <* G, A*>) & y in product (Carrier <*G,A*>)
  proof
    x in semidirect_product(G,A,phi);
    hence x in product (Carrier <*G,A*>) by Def3;
    y in semidirect_product(G,A,phi);
    hence y in product (Carrier <*G,A*>) by Def3;
  end;
  then consider h being Function, aa1 being Element of A, gg2 being Element
  of G such that
  A3: h = x * y & aa1 = x.2 & gg2 = y.1 
      & h.1 = (the multF of G).(x.1, (phi/.aa1)/.gg2)
      & h.2 = (the multF of A).(x.2, y.2) by Def3;
  A4: a1 = aa1 & g2 = gg2 by A1,A2,A3,FINSEQ_1:44;

  A5: h.2 = (the multF of A).(x.2, a2) by A2,A3,FINSEQ_1:44
         .= (the multF of A).(a1, a2) by A1,FINSEQ_1:44;
  A6: (x * y).1 = g1 * ((phi /. a1)/.g2) by A1,A3,A4,FINSEQ_1:44;
  dom (x * y) = {1,2} by Th12;
  then len (x * y) = 2 by FINSEQ_1:2,def 3;
  hence x * y = <* g1 * ((phi /. a1)/.g2), a1 * a2 *> by A3,A5,A6,FINSEQ_1:44;
end;

theorem Th16:
  for g being Element of G
  holds (phi /. 1_A) /. g = g
proof
  let g be Element of G;
  (phi /. 1_A) = 1_(AutGroup G) by GROUP_6:31
              .= id the carrier of G by AUTGROUP:9;
  hence (phi /. 1_A) /. g = g;
end;

Lm8:
  for x,e being Element of semidirect_product (G, A, phi)
  st e = <* 1_G, 1_A*>
  holds x * e = x & e * x = x
proof
  let x,e be Element of semidirect_product (G, A, phi);
  assume A1: e = <* 1_G, 1_A*>;
  consider g being Element of G, a being Element of A such that
  A2: x = <* g, a *>
  by Th13;

  A3: ((phi /. (1_A)) /. g) * 1_G = g * 1_G by Th16
                                 .= g by GROUP_1:def 4;
  thus x * e = <* g * ((phi /. a) /. 1_G), a * 1_A *> by A1,A2, Th15
            .= <* g * ((phi /. a) /. 1_G), a *> by GROUP_1:def 4
            .= <* g * 1_G, a *> by GROUP_6:31
            .= x by A2, GROUP_1:def 4;
  thus e * x = <* 1_G * ((phi /. (1_A)) /. g), 1_A * a *> by A1,A2,Th15
            .= <* 1_G * ((phi /. (1_A)) /. g), a *> by GROUP_1:def 4
            .= <* (1_G) * g, a *> by A3, GROUP_1:def 4
            .= x by A2, GROUP_1:def 4;
end;

registration
  let G, A, phi;
  cluster semidirect_product (G, A, phi) -> unital;
  correctness
  proof
    set S = semidirect_product (G, A, phi);
    reconsider e = <* 1_G, 1_A*> as Element of S by Th11;
    take e;
    let x be Element of S;
    consider g being Element of G, a being Element of A such that
    A1: x = <* g, a *>
    by Th13;

    A2: ((phi /. (1_A)) /. g) * 1_G = g * 1_G by Th16
                                   .= g by GROUP_1:def 4;
    A3: ((phi /. a) /. (1_G)) = 1_G by GROUP_6:31;
    thus x * e = <* g * ((phi /. a) /. 1_G), a * 1_A *> by A1, Th15
              .= <* g * ((phi /. a) /. 1_G), a *> by GROUP_1:def 4
              .= x by A1, A3, GROUP_1:def 4;
    thus e * x = <* 1_G * ((phi /. (1_A)) /. g), 1_A * a *> by A1,Th15
              .= <* 1_G * ((phi /. (1_A)) /. g), a *> by GROUP_1:def 4
              .= <* (1_G) * (g), a *> by A2, GROUP_1:def 4
              .= x by A1,GROUP_1:def 4;
  end;
end;

theorem Th17:
  1_(semidirect_product (G, A, phi)) = <* 1_G, 1_A *>
proof
  reconsider e = <* 1_G, 1_A *> as Element of semidirect_product (G, A, phi)
  by Th10;
  for x being Element of semidirect_product (G, A, phi)
  holds x * e = x & e * x = x by Lm8;
  hence thesis by GROUP_1:def 4;
end;

theorem Th18:
  for a1,a2 being Element of A
  for g being Element of G
  holds (phi.a1) . ((phi /. a2) . g) = (phi /. (a1 * a2)) . g
proof
  let a1,a2 be Element of A;
  let g be Element of G;
  phi . (a1 * a2) = (phi.a1) * (phi.a2) by GROUP_6:def 6;
  then (phi . (a1 * a2)) . g = ((phi/.a1) * (phi/.a2)).g by AUTGROUP:8
                            .= (phi.a1) . ((phi . a2) . g) by FUNCT_2:15;
  hence (phi.a1) . ((phi /. a2) . g) = (phi /. (a1 * a2)) . g; 
end;

theorem Th19:
  for a being Element of A
  for g being Element of G
  holds (phi/.(a")) /. ((phi /. a) /. g) = g
  & (phi/. a) /. ((phi /. (a")) /. g) = g
proof
  let a be Element of A;
  let g be Element of G;
  thus (phi /. (a")) /. ((phi /. a) /. g) = (phi /. (a" * a)) /. g by Th18
                                         .= (phi /. 1_A) /. g by GROUP_1:def 5
                                         .= g by Th16;
  thus (phi/. a) /. ((phi /. (a")) /. g) = (phi /. (a * a")) /. g by Th18
                                        .= (phi /. 1_A) /. g by GROUP_1:def 5
                                        .= g by Th16;
end;

theorem Th20:
  for x,y being Element of semidirect_product (G, A, phi)
  for a being Element of A
  for g being Element of G
  st x = <* g, a *> & y = <* (phi/.(a"))/.(g"), a" *>
  holds x * y = 1_(semidirect_product (G, A, phi)) & y * x = 1_(semidirect_product (G, A, phi))
proof
  let x,y be Element of semidirect_product (G, A, phi);
  let a be Element of A;
  let g be Element of G;
  assume A1: x = <* g, a *>;
  assume A2: y = <* (phi/.(a"))/.(g"), a" *>;
  A3: ((phi /. (a")) /. (g")) * ((phi/.(a"))/.(g))
   = (phi /. (a")) /. (g" * g) by GROUP_6:def 6
  .= (phi /. (a")) /. 1_G by GROUP_1:def 5
  .= 1_G by GROUP_6:31;
  A4: ((phi /. a) /. ((phi/.(a"))/.(g"))) = g" by Th19;

  thus x * y = <* g * ((phi/.a)/.((phi/.(a"))/.(g"))), a * (a") *> by A1,A2,Th15
            .= <* 1_G, a * a" *> by A4,GROUP_1:def 5
            .= <* 1_G, 1_A *> by GROUP_1:def 5
            .= 1_(semidirect_product (G, A, phi)) by Th17;
  thus y * x = <* (phi/.(a")).(g") * ((phi/. a")/.g), (a") * a *> by A1,A2,Th15
            .= <* 1_G, 1_A *> by A3,GROUP_1:def 5
            .= 1_(semidirect_product (G, A, phi)) by Th17;
end;

theorem thUnitalMagmaIsGroupCond:
  for M being non empty unital multMagma
  st (for h being Element of M ex g being Element of M st h * g = 1_M
  & g * h = 1_M)
  holds M is Group-like
proof
  let M be non empty unital multMagma;
  assume A1: for h being Element of M
  ex g being Element of M
  st h * g = 1_M & g * h = 1_M;
  
  ex e being Element of M st
  for h being Element of M
  holds (h * e = h & e * h = h & ex g being Element of M st
  (h * g = e & g * h = e))
  proof
    take e = 1_M;
    let h be Element of M;
    thus h * e = h by GROUP_1:def 4;
    thus e * h = h by GROUP_1:def 4;
    thus thesis by A1;
  end;
  hence M is Group-like;
end;

registration
  let G be strict Group;
  let A be Group;
  let phi be Homomorphism of A, AutGroup G;
  cluster semidirect_product (G, A, phi) -> associative;
  correctness
  proof
    thus for x, y, z being Element of semidirect_product (G, A, phi)
    holds (x * y) * z = x * (y * z)
    proof
      let x, y, z be Element of semidirect_product (G, A, phi);
      consider g1 being Element of G, a1 being Element of A such that
      A1: x = <* g1, a1 *>
      by Th13;
      consider g2 being Element of G, a2 being Element of A such that
      A2: y = <* g2, a2 *>
      by Th13;
      consider g3 being Element of G, a3 being Element of A such that
      A3: z = <* g3, a3 *>
      by Th13;
      
      x * y = <* g1 * ((phi /. a1)/.g2), a1 * a2 *> by A1,A2,Th15;
      then A5: (x * y) * z = <* (g1 * ((phi /.a1)/.g2)) * ((phi/.(a1*a2))/.g3),
      (a1 * a2) * a3*> by A3,Th15;
      
      y * z = <* g2 * ((phi /. a2)/.g3), a2 * a3 *> by A2,A3,Th15;
      then A6: x * (y * z) = <* g1 * ((phi/.a1)/.(g2 * ((phi/.a2)/.g3))),
      a1 * (a2 * a3) *>
      by A1,Th15;
      
      A7: (a1 * a2) * a3 = a1 * (a2 * a3) by GROUP_1:def 3;
      
      ((phi /. a1) . ((phi /. a2).g3)) = (phi /. (a1 * a2)) . g3 by Th18;
      then (((phi /.a1)/.g2) * ((phi /. (a1 * a2)) /. g3))
      = ((phi /. a1)/.(g2 * ((phi /. a2)/.g3))) by GROUP_6:def 6;
      hence thesis by A5,A6,A7, GROUP_1:def 3;
    end;
  end;
  
  cluster semidirect_product (G, A, phi) -> Group-like;
  correctness
  proof
    set S=semidirect_product (G, A, phi);
    for h being Element of S ex g being Element of S st h * g = 1_S
    & g * h = 1_S
    proof
      let h be Element of S;
      consider g being Element of G, a being Element of A such that
      B1: h = <* g, a *> by Th13;
      reconsider y = <* (phi/.(a"))/.(g"), a" *> as Element of S by Th11;
      take y;
      thus h * y = 1_S by B1,Th20;
      thus y * h = 1_S by B1,Th20;
    end;
    hence thesis by thUnitalMagmaIsGroupCond;
  end;
end;

LmDoubleCheckSemidirectProductIsGroup:
  semidirect_product (G, A, phi) is Group;

theorem Th22:
  for a being Element of A
  for g being Element of G
  for x being Element of semidirect_product (G, A, phi)
  st x = <* g, a *>
  holds x" = <* (phi/.(a"))/.(g"), a" *>
proof
  set S = semidirect_product (G, A, phi);
  let a be Element of A;
  let g be Element of G;
  let x be Element of semidirect_product (G, A, phi);
  assume A1: x = <* g, a *>;
  reconsider y = <* (phi/.(a"))/.(g"), a" *> as Element of S by Th11;
  x * y = 1_S & y * x = 1_S by A1, Th20;
  hence thesis by GROUP_1:5;
end;

theorem Th23:
  for g1,g2 being Element of G
  for x,y,z being Element of semidirect_product (G, A, phi)
  st x = <*g1,1_A*> & y = <*g2,1_A*> & z = <*(g1*g2),1_A*>
  holds x * y = z
proof
  let g1,g2 be Element of G;
  let x,y,z be Element of semidirect_product (G, A, phi);
  assume A1: x = <*g1,1_A*>;
  assume A2: y = <*g2,1_A*>;
  assume A3: z = <*(g1*g2),1_A*>;
  thus x * y = <* g1 * ((phi /. 1_A) /. g2), (1_A) * (1_A) *> by A1,A2,Th15
            .= <* g1 * ((phi /. 1_A) /. g2), 1_A *> by GROUP_1:def 4
            .= z by A3, Th16;
end;

theorem Th24:
  for a1,a2 being Element of A
  for x,y,z being Element of semidirect_product (G, A, phi)
  st x = <*1_G,a1*> & y = <*1_G,a2*> & z = <*1_G,(a1*a2)*>
  holds x * y = z
proof
  let a1,a2 be Element of A;
  let x,y,z be Element of semidirect_product (G, A, phi);
  assume A1: x = <*1_G, a1*>;
  assume A2: y = <*1_G, a2*>;
  assume A3: z = <*1_G, (a1*a2)*>;
  thus x * y = <* 1_G * ((phi /. a1)/.(1_G)), a1 * a2 *> by A1,A2,Th15
            .= <* 1_G * (1_G), a1 * a2 *> by GROUP_6:31
            .= z by A3,GROUP_1:def 4;
end;

:: Compare to AUTGROUP:1, we do not need strictness
theorem Th25:
  for H being Subgroup of A
  holds H is normal
  iff (for a,h being Element of A st h in H holds h |^ a in H)
proof
  let H be Subgroup of A;
  thus H is normal implies (for a,h being Element of A st h in H
  holds h |^ a in H)
  proof
    assume B1: H is normal;
    let a,h be Element of A;
    assume B2: h in H;
    B3: H |^ a = the multMagma of H by B1, GROUP_3:def 13;
    h |^ a in H |^ a by B2, GROUP_3:58;
    hence h |^ a in H by B3;
  end;


  thus (for a,h being Element of A st h in H holds h |^ a in H) implies
  H is normal
  proof
    assume B1: for a,h being Element of A st h in H holds h |^ a in H;
    now
      let a be Element of A;
      thus H * a c= a * H
      proof
        set U = carr H;
        let q be object;
        assume q in H * a;
        then consider h being Element of A such that
        B2: q = h * a and
        B3: h in H by GROUP_2:104;
        h |^ a in H by B1, B3;
        then h |^ a in carr H by GROUP_2:def 9;
        then a * (h |^ a) in a * (carr H) by GROUP_2:27;
        then a * (((a") * h) * a) in a * (carr H) by GROUP_3:def 2;
        then a * ((a") * (h * a)) in a * (carr H) by GROUP_1:def 3;
        then (a * (a")) * (h * a) in a * (carr H) by GROUP_1:def 3;
        then ((a * (a")) * h) * a in a * (carr H) by GROUP_1:def 3;
        then ((1_A) * h) * a in a*(carr H) by GROUP_1:def 5;
        then h * a in a*(carr H) by GROUP_1:def 4;
        hence q in a * H by B2, GROUP_2:def 13;
      end;
    end;
    hence H is normal by GROUP_3:119;
  end;
end;

definition
  let G, A, phi;
  func incl1 (G, A, phi) -> Function of G, semidirect_product (G,A,phi) means
  :Def4:
  for g being Element of G holds it.g = <* g, 1_A *>;

  existence
  proof
    defpred P[Element of G, Element of semidirect_product(G,A,phi)] means
    $2 = <* $1, 1_A *>;

    A1: for x being Element of G
    ex y being Element of semidirect_product(G,A,phi)
    st P[x, y]
    proof
      let x be Element of G;
      reconsider y=<*x,1_A*> as Element of semidirect_product(G,A,phi)
      by Th11;
      take y;
      thus P[x, y];
    end;
    consider f being Function of G, semidirect_product(G, A, phi) such that
    A2: for x being Element of G holds P[x, f.x]
    from FUNCT_2:sch 3(A1);
    take f;
    thus thesis by A2;
  end;
  uniqueness
  proof
    for f1,f2 being Function of G, semidirect_product(G, A, phi)
    st (for g being Element of G holds f1.g = <*g,1_A*>)
     & (for g being Element of G holds f2.g = <*g,1_A*>)
    holds f1 = f2
    proof
      let f1,f2 be Function of G, semidirect_product(G, A, phi);
      assume A1: for g being Element of G holds f1.g = <*g,1_A*>;
      assume A2: for g being Element of G holds f2.g = <*g,1_A*>;
      for g being Element of G holds f1.g = f2.g
      proof
        let g be Element of G;
        thus f1.g = <*g,1_A*> by A1
                 .= f2.g by A2;
      end;

      hence f1 = f2 by FUNCT_2:def 8;
    end;
    hence thesis;
  end;
end;

:: Aschbacher, Finite Group Theory (10.1.2)
registration
  let G, A, phi;
  cluster incl1 (G, A, phi) -> multiplicative;
  coherence
  proof
    for g1,g2 being Element of G
    holds (incl1 (G, A, phi)).(g1 * g2)
    = ((incl1 (G, A, phi)).g1)*((incl1 (G, A, phi)).g2)
    proof
      let g1,g2 be Element of G;
      ((incl1 (G, A, phi)).g1) = <*g1,1_A*>
      & ((incl1 (G, A, phi)).g2) = <*g2,1_A*>
      & (incl1 (G, A, phi)).(g1 * g2) = <*g1*g2,1_A*> by Def4;
      hence thesis by Th23;
    end;
    hence thesis by GROUP_6:def 6;
  end;
  cluster incl1 (G, A, phi) -> one-to-one;
  coherence
  proof
    for g1,g2 being Element of G
    st (incl1 (G, A, phi)).g1 = (incl1 (G, A, phi)).g2
    holds g1 = g2
    proof
      let g1,g2 be Element of G;
      assume A1: (incl1 (G, A, phi)).g1 = (incl1 (G, A, phi)).g2;
      <*g1, 1_A*> = (incl1 (G, A, phi)).g1 by Def4
                 .= <*g2,1_A*> by A1,Def4;
      hence g1 = g2 by Th14;
    end;
    hence incl1 (G, A, phi) is one-to-one by GROUP_6:1;
  end;
end;

definition
  let G, A, phi;
  func incl2 (G, A, phi) -> Function of A, semidirect_product (G,A,phi) means
  :Def5:
  for a being Element of A holds it.a = <* 1_G, a *>;
  existence
  proof
    defpred P[Element of A, Element of semidirect_product(G,A,phi)] means
    $2 = <* 1_G, $1 *>;

    A1: for x being Element of A
    ex y being Element of semidirect_product(G,A,phi)
    st P[x, y]
    proof
      let x be Element of A;
      reconsider y=<*1_G,x*> as Element of semidirect_product(G,A,phi)
      by Th11;
      take y;
      thus P[x, y];
    end;
    consider f being Function of A, semidirect_product(G, A, phi) such that
    A2: for x being Element of A holds P[x, f.x]
    from FUNCT_2:sch 3(A1);
    take f;
    thus thesis by A2;
  end;
  uniqueness
  proof
    for f1,f2 being Function of A, semidirect_product(G, A, phi)
    st (for a being Element of A holds f1.a = <*1_G,a*>)
     & (for a being Element of A holds f2.a = <*1_G,a*>)
    holds f1 = f2
    proof
      let f1,f2 be Function of A, semidirect_product(G, A, phi);
      assume A1: for a being Element of A holds f1.a = <*1_G,a*>;
      assume A2: for a being Element of A holds f2.a = <*1_G,a*>;
      for a being Element of A holds f1.a = f2.a
      proof
        let a be Element of A;
        thus f1.a = <*1_G,a*> by A1
                 .= f2.a by A2;
      end;

      hence f1 = f2 by FUNCT_2:def 8;
    end;
    hence thesis;
  end;
end;

:: Aschbacher, Finite Group Theory (10.1.2)
registration
  let G, A, phi;
  cluster incl2 (G, A, phi) -> multiplicative;
  coherence
  proof
    for a1,a2 being Element of A
    holds (incl2 (G, A, phi)).(a1 * a2)
    = ((incl2 (G, A, phi)).a1)*((incl2 (G, A, phi)).a2)
    proof
      let a1,a2 be Element of A;
      ((incl2 (G, A, phi)).a1) = <*1_G,a1*>
      & ((incl2 (G, A, phi)).a2) = <*1_G,a2*>
      & (incl2 (G, A, phi)).(a1 * a2) = <*1_G,a1*a2*> by Def5;
      hence thesis by Th24;
    end;
    hence thesis by GROUP_6:def 6;
  end;

  cluster incl2 (G, A, phi) -> one-to-one;
  coherence
  proof
    for a1,a2 being Element of A
    st (incl2 (G, A, phi)).a1 = (incl2 (G, A, phi)).a2
    holds a1 = a2
    proof
      let a1,a2 be Element of A;
      assume A1: (incl2 (G, A, phi)).a1 = (incl2 (G, A, phi)).a2;
      <*1_G, a1*> = (incl2 (G, A, phi)).a1 by Def5
                 .= <*1_G, a2*> by A1,Def5;
      hence a1 = a2 by Th14;
    end;
    hence incl2 (G, A, phi) is one-to-one by GROUP_6:1;
  end;
end;

:: Aschbacher, Finite Group Theory (10.1.3)
theorem Th26:
  Image (incl1 (G, A, phi)) is normal Subgroup of
  semidirect_product (G, A, phi)
proof
  for x,g being Element of semidirect_product (G, A, phi)
  st g in Image (incl1 (G, A, phi))
  holds g |^ x in Image (incl1 (G, A, phi))
  proof
    let x,g be Element of semidirect_product (G, A, phi);
    assume g in Image (incl1 (G, A, phi));
    then consider g0 being Element of G such that
    A1: g = (incl1 (G, A, phi)).g0 by GROUP_6:45;
    A2: g = <*g0,1_A*> by A1,Def4;
    consider x1 being Element of G, x2 being Element of A such that
    A3: x = <* x1, x2 *>
    by Th13;
    (x") = <* (phi/.(x2"))/.(x1"),x2" *> by A3, Th22;
    then (x") * g = <* ((phi/.(x2"))/.(x1"))*((phi/.(x2"))/.g0),
                       (x2") * (1_A) *> by A2,Th15
    .= <* ((phi/.(x2"))/.(x1"))*((phi/.(x2"))/.g0), (x2") *> by GROUP_1:def 4
    .= <* (phi/.(x2"))/.((x1") * g0), (x2") *> by GROUP_6:def 6;
    then A4: ((x") * g) * x
    = <* ((phi/.(x2"))/.((x1")*g0)*((phi/.(x2"))/.x1)), (x2") * x2 *> by A3,Th15
    .= <* ((phi/.(x2"))/.((x1")*g0)*((phi/.(x2"))/.x1)), 1_A *>
       by GROUP_1:def 5
    .= <* ((phi/.(x2"))/.(((x1") * g0) * x1)), 1_A *> by GROUP_6:def 6
    .= <* (phi /. (x2"))/.(g0 |^ x1), 1_A *> by GROUP_3:def 2;
    g |^ x = ((x") * g) * x by GROUP_3:def 2
    .= ((incl1 (G, A, phi)).((phi /. (x2"))/.(g0 |^ x1))) by A4,Def4;
    hence g |^ x in Image (incl1 (G, A, phi)) by GROUP_6:45;
  end;
  hence thesis by Th25;
end;

registration
  let A, G, phi;
  cluster Image (incl1 (G, A, phi)) -> normal;
  correctness by Th26;
end;

theorem
  Image (incl2 (G, A, phi)) /\ Image (incl1 (G, A, phi))
  = (1).(semidirect_product(G,A,phi))
proof
  set ImA = Image (incl2 (G, A, phi)), ImG = Image (incl1 (G, A, phi));
  set S = semidirect_product(G,A,phi);
  for x being object st x in the carrier of (ImA /\ ImG)
  holds x in {1_S}
  proof
    let x be object;
    assume x in the carrier of (ImA /\ ImG);
    then x in ImA /\ ImG;
    then B1: x in ImA & x in ImG by GROUP_2:82;
    then consider g being Element of G such that
    B2: x = (incl1 (G, A, phi)).g
    by GROUP_6:45;
    consider a being Element of A such that
    B3: x = (incl2 (G, A, phi)).a
    by B1, GROUP_6:45;
    <*1_G, a*> = x by B3,Def5
              .= <*g,1_A*> by B2,Def4;
    then a = 1_A & 1_G = g by Th14;
    then x = <*1_G,1_A*> by B2,Def4
          .= 1_S by Th17;
    hence x in {1_S} by TARSKI:def 1;
  end;
  then the carrier of (ImA /\ ImG) c= {1_S};
  then the carrier of (ImA /\ ImG) c= the carrier of (1).S by GROUP_2:def 7;
  then A1: ImA /\ ImG is Subgroup of (1).S by GROUP_2:57;
  (1).S is Subgroup of ImA /\ ImG by GROUP_2:65;
  hence thesis by A1, GROUP_2:55;
end;

theorem Th28:
  for x being Element of semidirect_product(G,A,phi)
  ex g being Element of G,a being Element of A
  st ((incl1(G,A,phi)).g) * ((incl2(G,A,phi)).a) = x
proof
  let x be Element of semidirect_product(G, A, phi);
  consider g being Element of G, a being Element of A such that
  A1: x = <*g,a*>
  by Th13;
  take g,a;
  ((incl1(G,A,phi)).g) = <*g,1_A*> & ((incl2(G,A,phi)).a) = <*1_G,a*>
  by Def4,Def5;
  hence ((incl1(G,A,phi)).g) * ((incl2(G,A,phi)).a)
  = <* g * ((phi /. (1_A))/.(1_G)), (1_A) * (a) *> by Th15
  .= <* g * (1_G), (1_A) * (a) *> by Th16
  .= <* g * (1_G), a *> by GROUP_1:def 4
  .= x by A1, GROUP_1:def 4;
end;

theorem Th29:
  (Image incl1(G,A,phi)) * (Image incl2(G,A,phi))
  = the carrier of (semidirect_product(G,A,phi))
proof
  for x being Element of semidirect_product (G, A, phi)
  holds x in (Image incl1(G,A,phi)) * (Image incl2(G,A,phi))
  proof
    let x be Element of semidirect_product (G, A, phi);
    
    consider g being Element of G, a being Element of A such that
    B1: ((incl1(G,A,phi)).g) * ((incl2(G,A,phi)).a) = x
    by Th28;
    ((incl1(G,A,phi)).g) in Image(incl1(G,A,phi)) &
    ((incl2(G,A,phi)).a) in Image(incl2(G,A,phi)) by GROUP_6:45;
    hence x in (Image incl1(G,A,phi)) * (Image incl2(G,A,phi))
    by B1, GROUP_5:4;
  end;
  then A1: the carrier of (semidirect_product(G,A,phi))
  c= (Image incl1(G,A,phi)) * (Image incl2(G,A,phi));
  set S = semidirect_product(G,A,phi);
  thus thesis by A1, XBOOLE_0:def 10;
end;

theorem
  Image (incl1 (G, A, phi)) "\/" Image (incl2 (G, A, phi))
  = (semidirect_product(G,A,phi))
proof
  set S = semidirect_product(G,A,phi);
  set U = (Image incl1(G,A,phi)) * (Image incl2(G,A,phi));
  U c= the carrier of gr U by GROUP_4:def 4; then
  the carrier of S c= the carrier of (gr U) by Th29;
  then S = gr U by GROUP_2:61;
  hence thesis by GROUP_4:50;
end;

:: Aschbacher, Finite Group Theory (10.1.3)
theorem
  G, Image (incl1 (G, A, phi)) are_isomorphic by GROUP_6:68;

:: Aschbacher, Finite Group Theory (10.1.4) --- note there is a sign
:: error in Aschbacher's book.
theorem
  for a being Element of A
  for g being Element of G
  holds ((incl1(G,A,phi)).g) |^ ((incl2(G,A,phi)).a)
  = <*(phi /. (a"))/.g,1_A *>
proof
  let a be Element of A;
  let g be Element of G;
  set xg = (incl1(G,A,phi)).g;
  set xa = (incl2(G,A,phi)).a;
  A1: xa = <* 1_G,a *> by Def5;
  then A2: xa" = <* ((phi/.(a"))/.((1_G)")), a" *> by Th22
              .= <* ((phi/.(a"))/.(1_G)), a" *> by GROUP_1:8
              .= <* 1_G, a" *> by GROUP_6:31;
  xg = <*g, 1_A*> by Def4; then
  (xa") * xg = <* (1_G) * ((phi /. (a"))/.g), (a") * (1_A) *> by A2,Th15
            .= <* (1_G) * ((phi /. (a"))/.g), (a") *> by GROUP_1:def 4
            .= <* ((phi /. (a"))/.g), (a") *> by GROUP_1:def 4;
  then ((xa")*xg)*xa = <* ((phi/.(a"))/.g)*((phi/.(a"))/.(1_G)), (a")*a *>
  by A1, Th15
                    .= <* ((phi /. (a"))/.g) * (1_G), (a")*a *> by GROUP_6:31
                    .= <* ((phi /. (a"))/.g), (a")*a *> by GROUP_1:def 4
                    .= <* ((phi /. (a"))/.g), 1_A *> by GROUP_1:def 5;
  hence thesis by GROUP_3:def 2;
end;

theorem
  semidirect_product (G, A, 1:(A, AutGroup G)) = product <*G, A*>
proof
  set S = semidirect_product (G, A, 1:(A, AutGroup G));
  A1: the carrier of S = the carrier of product <*G,A*> by Th10;
  set B1 = the multF of S;
  set B2 = the multF of product <*G, A*>;
  set U = product (Carrier <*G,A*>);

  A2: B1 is BinOp of U & B2 is BinOp of U
  proof
    the carrier of S = product (Carrier<*G,A*>) &
    B1 is BinOp of the carrier of S by Def3;
    hence B1 is BinOp of U;
    
    the carrier of product <*G,A*> = product (Carrier <*G,A*>) &
    the multF of product <*G,A*> is BinOp of the carrier of product <*G,A*>
    by GROUP_7:def 2;
    hence B2 is BinOp of U;
  end;

  for x,y being Element of product (Carrier <*G,A*>)
  holds B1.(x,y) = B2.(x,y)
  proof
    let x,y be Element of product (Carrier <*G,A*>);
    x is Element of S by Def3;
    then consider g1 being Element of G, a1 being Element of A such that
    A4: x = <*g1,a1*> by Th13;
    y is Element of S by Def3;
    then consider g2 being Element of G, a2 being Element of A such that
    A5: y = <*g2,a2*> by Th13;
    1:(A, AutGroup G) = A --> (1_(AutGroup G)) by GROUP_6:def 7; then
    (1:(A, AutGroup G)).a1 = id the carrier of G by AUTGROUP:9; then
    A6: (((1:(A, AutGroup G)) /. a1) /. g2) = g2;
    reconsider x0=x,y0=y as Element of S by Def3;
    A8: x0 * y0 = <* g1 * g2, a1 * a2 *> by A4, A5, A6, Th15;

    B2.(x,y) = <*g1,a1*> * <*g2, a2*> by A4,A5
            .= <* g1 * g2,a1 * a2 *> by GROUP_7:29;
    hence B1.(x,y) = B2.(x,y) by A8;
  end;
  hence thesis by A1, A2, BINOP_1:2;
end;

begin :: Complementary Subgroups

definition
  let G,H,N be Group;
  pred H,N are_complements_in G means :Def6:
  ex H1 being strict Subgroup of G st
  ex N1 being strict normal Subgroup of G
  st H1 = the multMagma of H
   & N1 = the multMagma of N
   & H1 * N1 = the carrier of G
   & H1 /\ N1 = (1).G;
end;

theorem
  for G being Group
  for H,K being Subgroup of G
  for HK being Subgroup of K
  st the multMagma of H = the multMagma of HK
  holds carr H = carr HK
proof
  let G be Group;
  let H,K be Subgroup of G;
  let HK be Subgroup of K;
  assume A1: the multMagma of H = the multMagma of HK;
  thus carr H = the carrier of H by GROUP_2:def 9
             .= carr HK by A1, GROUP_2:def 9;
end;

theorem Th35:
  for G being Group
  for H,K being Subgroup of G st H is Subgroup of K
  for N being Subgroup of G
  st N is normal Subgroup of K
  holds N * H = H * N
proof
  let G be Group;
  let H,K be Subgroup of G;
  assume H is Subgroup of K;
  then reconsider H0=H as Subgroup of K;
  let N be Subgroup of G;
  assume N is normal Subgroup of K;
  then reconsider N0=N as normal Subgroup of K;
  A1: the multMagma of N0 = the multMagma of N;
  A2: the multMagma of H0 = the multMagma of H;
  thus N * H = N0 * H0 by ThProdLemma,A1,A2
            .= H0 * N0 by GROUP_5:8
            .= H * N by ThProdLemma,A1,A2;
end;

theorem Th36:
  for G being Group
  for H being Subgroup of G
  for N being normal Subgroup of G
  st N is Subgroup of H
  holds the multMagma of N = the multMagma of (H,N)`*`
proof
  let G be Group;
  let H be Subgroup of G;
  let N be normal Subgroup of G;
  assume N is Subgroup of H;
  then N is normal Subgroup of H by GROUP_6:8;
  then reconsider N1=the multMagma of N as strict normal Subgroup of H
  by Th3;
  thus the multMagma of N = the multMagma of N1
                         .= the multMagma of (H,N)`*` by GROUP_6:def 1;
end;

theorem Th37:
  for G being Group
  for H1,N1,H2,N2 being Subgroup of G
  st the multMagma of H1 = the multMagma of H2
  & the multMagma of N1 = the multMagma of N2
  holds H1*N1 = H2*N2 & H1 /\ N1 = H2 /\ N2
proof
  let G be Group;
  let H1,N1,H2,N2 be Subgroup of G;
  assume A1: the multMagma of H1 = the multMagma of H2;
  assume A2: the multMagma of N1 = the multMagma of N2;
  reconsider K=G as Subgroup of G by GROUP_2:54;
  reconsider HK=the multMagma of H1,NK=the multMagma of N1 as Subgroup of K
  by Th2;
  thus H1*N1 = HK*NK by ThProdLemma
            .= H2*N2 by A1,A2,ThProdLemma;
  thus H1 /\ N1 = HK /\ NK by ThCapLemma
               .= H2 /\ N2 by A1,A2,ThCapLemma;
end;

definition
  let G be Group;
  let H,N be Subgroup of G;
  redefine pred H,N are_complements_in G means :Def7:
  N is normal & H * N = the carrier of G & H /\ N = (1).G;
  compatibility
  proof
    thus H,N are_complements_in G
    implies (N is normal & H*N = the carrier of G & H/\N = (1).G)
    proof
      assume H,N are_complements_in G;
      then consider H1 being strict Subgroup of G,
      N1 being strict normal Subgroup of G such that
      A1: H1 = the multMagma of H
      & N1 = the multMagma of N
      & H1 * N1 = the carrier of G
      & H1 /\ N1 = (1).G;
      for g being Element of G
      holds N |^ g = the multMagma of N
      proof
        let g be Element of G;
        B1: carr N1 = the carrier of N1 by GROUP_2:def 9
                   .= carr N by A1, GROUP_2:def 9;
        the carrier of N |^ g = (carr N) |^ g by GROUP_3:def 6
        .= the carrier of (N1 |^ g) by B1,GROUP_3:def 6
        .= the carrier of N1 by GROUP_3:def 13
        .= carr N by B1,GROUP_2:def 9
        .= the carrier of N by GROUP_2:def 9;
        hence N |^ g = the multMagma of N by GROUP_2:59;
      end;
      hence N is normal by GROUP_3:def 13;
      thus H * N = the carrier of G by A1,Th37;
      thus H /\ N = (1).G by A1,Th37;
    end;    
    assume A1: N is normal;
    assume A2: H * N = the carrier of G;
    assume A3: H /\ N = (1).G;

    ex H1 being strict Subgroup of G st
    ex N1 being strict normal Subgroup of G
    st H1 = the multMagma of H
    & N1 = the multMagma of N
    & H1 * N1 = the carrier of G
    & H1 /\ N1 = (1).G
    proof
      reconsider H1=the multMagma of H as strict Subgroup of G by Th2;
      take H1;
      thus thesis
      proof
        reconsider N1=the multMagma of N as strict normal Subgroup of G
        by A1,Th3;
        take N1;
        thus H1 = the multMagma of H;
        thus N1 = the multMagma of N;
        thus H1 * N1 = the carrier of G by A2,Th37;
        thus H1 /\ N1 = (1).G by A3,Th37;
      end;
    end;
    hence H,N are_complements_in G;
  end;
end;

theorem Th38:
  for G being Group
  for H,K being Subgroup of G st H is Subgroup of K
  for N being Subgroup of G st N is normal Subgroup of K
  holds H,N are_complements_in K
  iff ((N * H = the carrier of K) & (H /\ N = (1).K))
proof
  let G be Group;
  let H,K be Subgroup of G;
  assume H is Subgroup of K;
  then reconsider H1=the multMagma of H as strict Subgroup of K
  by Th2;
  let N be Subgroup of G;
  assume N is normal Subgroup of K;
  then reconsider N1=the multMagma of N as strict normal Subgroup of K
  by Th3;
  A1: N * H = N1 * H1 by ThProdLemma;
  hence H,N are_complements_in K implies ((N * H = the carrier of K) & (H /\ N = (1).K))
  by ThCapLemma, GROUP_5:8;

  thus ((N * H = the carrier of K) & (H /\ N = (1).K)) implies H,N are_complements_in K
  proof
    assume A2: N * H = the carrier of K;
    assume H /\ N = (1).K;
    then H1, N1 are_complements_in K by A1, A2, ThCapLemma, GROUP_5:8;
    hence H,N are_complements_in K;
  end;
end;

theorem
  for G being Group
  for H,K being Subgroup of G st H is Subgroup of K
  for N being Subgroup of G st N is normal Subgroup of K
  holds H,N are_complements_in K
  iff ((H * N = the carrier of K) & (H /\ N = (1).K))
proof
  let G be Group;
  let H,K be Subgroup of G;
  assume A1: H is Subgroup of K;
  let N be Subgroup of G;
  assume A2: N is normal Subgroup of K;
  then N*H=H*N by A1,Th35;
  hence thesis by A1,A2,Th38;
end;

theorem Th40:
  for G being Group
  for H,K being Subgroup of G st H is Subgroup of K
  for N being normal Subgroup of G st N is Subgroup of K
  holds H,((K,N)`*`) are_complements_in K
  iff ((N * H = the carrier of K) & (H /\ N = (1).K))
proof
  let G be Group;
  let H,K be Subgroup of G;
  assume A1: H is Subgroup of K;
  then reconsider H1=the multMagma of H as strict Subgroup of K by Th2;
  let N be normal Subgroup of G;
  assume N is Subgroup of K;
  then A2: N is normal Subgroup of K by GROUP_6:8;
  then the multMagma of (K,N)`*` = the multMagma of N by Th36;
  then H1,((K,N)`*`) are_complements_in K iff H,N are_complements_in K;
  hence thesis by A1,A2,Th38;
end;

theorem Th41:
  for G being Group
  for H,K being Subgroup of G st H is Subgroup of K
  for N being normal Subgroup of G st N is Subgroup of K
  holds H,N are_complements_in K
  iff H,((K,N)`*`) are_complements_in K
proof
  let G be Group;
  let H,K be Subgroup of G;
  assume A1: H is Subgroup of K;
  let N be normal Subgroup of G;
  assume A2: N is Subgroup of K;
  then A3: N is normal Subgroup of K by GROUP_6:8;
  hereby 
    assume H,N are_complements_in K;
    then ((N * H = the carrier of K) & (H /\ N = (1).K)) by A1,A3,Th38;
    hence H,((K,N)`*`) are_complements_in K by A1,A2,Th40;
  end;
  assume H,((K,N)`*`) are_complements_in K;
  then ((N * H = the carrier of K) & (H /\ N = (1).K)) by A1,A2,Th40;
  hence H,N are_complements_in K by A1,A3,Th38;
end;

theorem
  for G being Group
  for K being Subgroup of G
  for H being Subgroup of K
  for N being normal Subgroup of G st N is Subgroup of K
  holds H,N are_complements_in K
  iff H,((K,N)`*`) are_complements_in K by Th41;

theorem Th43:
  for G being Group
  for A being Subset of G st A = the carrier of G
  holds gr A = the multMagma of G
proof
  let G be Group;
  let A be Subset of G;
  assume A = the carrier of G;
  then the carrier of G c= the carrier of (gr A) by GROUP_4:def 4;
  hence the multMagma of G = gr A by GROUP_2:61;
end;

theorem Th44:
  for G being Group
  for H being Subgroup of G
  for N being normal Subgroup of G
  holds H,N are_complements_in G
  iff (H "\/" N = the multMagma of G & H /\ N = (1).G)
proof
  let G be Group;
  let H be Subgroup of G;
  let N be normal Subgroup of G;
  the carrier of G c= the carrier of G; then
  reconsider A = the carrier of G as Subset of G;
  thus H,N are_complements_in G implies (H "\/" N = the multMagma of G & H /\ N = (1).G)
  proof
    assume A1: H,N are_complements_in G;
    then H "\/" N = gr (A) by GROUP_4:50;
    hence H "\/" N = the multMagma of G by Th43;
    thus H /\ N = (1).G by A1;
  end;
  thus (H "\/" N = the multMagma of G & H /\ N = (1).G) implies H,N are_complements_in G
  proof
    assume A1: H "\/" N = the multMagma of G;
    A2: H * N = carr(H) * N by GROUP_4:43
             .= N * carr(H) by GROUP_3:120
             .= N * H by GROUP_4:43;
    assume H /\ N = (1).G;
    hence H,N are_complements_in G by A1, A2, GROUP_4:51;
  end;
end;

theorem LmOnto:
  for A,B being non empty set
  for f being Function of A,B
  st (for y being Element of B ex x being Element of A st f.x = y)
  holds f is onto
proof
  let A,B be non empty set;
  let f be Function of A,B;
  assume A1: for y being Element of B ex x being Element of A st f.x = y;
  for y being object st y in B
  ex x being object st (x in A & y = f.x)
  proof
    let y be object;
    assume y in B;
    then reconsider y0=y as Element of B;
    ex x being Element of A st f.x = y0 by A1;
    hence thesis;
  end;
  then rng f = B by FUNCT_2:10;
  hence f is onto by FUNCT_2:def 3;
end;

theorem
  G = the multMagma of A implies A,G are_isomorphic
proof
  assume A1: G = the multMagma of A;
  ex f being Homomorphism of A,G st f is bijective
  proof
    reconsider f=id A as Function of A,G by A1;
    for a1,a2 being Element of A
    holds f.(a1 * a2) = (f.a1) * (f.a2) by A1;

    then reconsider f=id A as Homomorphism of A,G by GROUP_6:def 6;
    take f;
    for y being Element of G
    ex x being Element of A
    st f.x = y
    proof
      let y be Element of G;
      reconsider x=y as Element of A by A1;
      take x;
      thus f.x = y;
    end;
    then f is onto by LmOnto;
    hence thesis;
  end;
  hence A,G are_isomorphic by GROUP_6:def 11;
end;

theorem Th47:
  for G being Group
  for N being normal Subgroup of G
  for g1,g2 being Element of G
  st g1 * N = g2 * N
  ex n being Element of G
  st n in N & g1 = g2 * n
proof
  let G be Group;
  let N be normal Subgroup of G;
  let g1,g2 be Element of G;
  assume A1: g1 * N = g2 * N;
  consider n being Element of G such that
  A2: n = (g2") * g1;
  take n;
  thus n in N by A1, A2, GROUP_2:114;
  thus g2 * n = (g2 * (g2")) * g1 by A2, GROUP_1:def 3
             .= (1_G) * g1 by GROUP_1:def 5
             .= g1 by GROUP_1:def 4;
end;

::$N Universal Property of Quotient Groups
theorem UniversalPropertyQuotientGroups:
  for G1,G2 being Group
  for N being normal Subgroup of G1
  for f being Homomorphism of G1,G2
  st N is Subgroup of Ker f
  ex fBar being Homomorphism of G1./.N,G2
  st f = fBar * (nat_hom N)
proof
  let G1,G2 be Group;
  let N be normal Subgroup of G1;
  let f be Homomorphism of G1,G2;
  assume A1: N is Subgroup of Ker f;

  defpred P[Element of G1./.N, Element of G2] means
  ex g being Element of G1 st $1 = g*N & $2 = f.g;

  A2: for x being Element of G1./.N
  ex y being Element of G2 st P[x,y]
  proof
    let x be Element of G1./.N;
    x in G1./.N; then
    consider g1 being Element of G1 such that
    B1: x = g1 * N & x = N *g1
    by GROUP_6:23;
    consider y being Element of G2 such that
    B2: y = f.g1;
    take y;
    thus P[x,y] by B1,B2;
  end;

  consider fBar being Function of G1./.N,G2 such that
  A3: for x being Element of G1./.N holds P[x, fBar.x]
  from FUNCT_2:sch 3(A2);

  for x1,x2 being Element of G1./.N
  holds fBar.(x1 * x2) = (fBar.x1) * (fBar.x2)
  proof
    let x1,x2 be Element of G1./.N;
    consider g12 being Element of G1 such that
    B1: x1 * x2 = g12*N & fBar.(x1 * x2) = f.g12
    by A3;
    consider g1 being Element of G1 such that
    B2: x1 = g1*N & fBar.x1 = f.g1
    by A3;
    consider g2 being Element of G1 such that
    B3: x2 = g2*N & fBar.x2 = f.g2
    by A3;

    B4: (nat_hom N).g1 = x1 & (nat_hom N).g2 = x2 by B2,B3,GROUP_6:def 8;

    (g1 * g2) * N = (nat_hom N).(g1 * g2) by GROUP_6:def 8
                 .= g12 * N by B1, B4, GROUP_6:def 6;
    then consider n being Element of G1 such that
    B5: n in N & g12 = (g1 * g2) * n
    by Th47;
    f.g12 = (f.(g1 * g2)) * (f.n) by B5, GROUP_6:def 6
         .= (f.(g1 * g2)) * (1_G2) by A1, B5, GROUP_2:40, GROUP_6:41
         .= f.(g1 * g2) by GROUP_1:def 4
         .= (f.g1) * (f.g2) by GROUP_6:def 6;
    hence fBar.(x1 * x2) = (fBar.x1) * (fBar.x2) by B1,B2,B3;
  end;

  then reconsider fBar as Homomorphism of G1./.N,G2 by GROUP_6:def 6;
  take fBar;

  for g being Element of G1
  holds f.g = (fBar * (nat_hom N)).g
  proof
    let g be Element of G1;
    consider g1 being Element of G1 such that
    B1: (nat_hom N).g = g1 * N & fBar.((nat_hom N).g) = f.g1
    by A3;
    g * N = g1 * N by B1, GROUP_6:def 8;
    then consider n being Element of G1 such that
    B2: n in N & g = g1 * n
    by Th47;
    f.g = (f.g1) * (f.n) by B2, GROUP_6:def 6
       .= (f.g1) * (1_G2) by A1, B2, GROUP_2:40, GROUP_6:41
       .= f.g1 by GROUP_1:def 4
       .= (fBar * (nat_hom N)).g by B1, FUNCT_2:15;
    hence f.g = (fBar * (nat_hom N)).g;
  end;

  hence f = fBar * (nat_hom N) by FUNCT_2:def 8;
end;

theorem
  for G1,G2 being Group
  for N1 being normal Subgroup of G1
  for N2 being normal Subgroup of G2
  for phi being Homomorphism of G1,G2
  st phi is bijective & phi .: the carrier of N1 = the carrier of N2
  holds G1./.N1, G2./.N2 are_isomorphic
proof
  let G1,G2 be Group;
  let N1 be normal Subgroup of G1;
  let N2 be normal Subgroup of G2;
  let phi be Homomorphism of G1,G2;
  assume A1: phi is bijective;
  assume A2: phi .: the carrier of N1 = the carrier of N2;
  for g being Element of G1
  st g in N1
  holds g in Ker ((nat_hom N2) * phi)
  proof
    let g be Element of G1;
    assume g in N1;
    then B1: phi.g in N2 by A2, FUNCT_2:35;
    then (nat_hom N2).(phi.g) = (phi.g) * N2 & 1_(G2./.N2) = carr N2
    & phi.g in carr N2 by GROUP_2:def 9, GROUP_6:24, GROUP_6:def 8;
    then (nat_hom N2).(phi.g) = 1_(G2./.N2) by B1, GROUP_2:113;
    then ((nat_hom N2) * phi).g = 1_(G2./.N2) by FUNCT_2:15;
    hence g in Ker ((nat_hom N2) * phi) by GROUP_6:41;
  end;
  then N1 is Subgroup of Ker ((nat_hom N2) * phi) by GROUP_2:58;
  then consider phiBar being Homomorphism of G1./.N1,G2./.N2 such that
  A3: ((nat_hom N2) * phi) = phiBar * (nat_hom N1) by UniversalPropertyQuotientGroups;

  for y being Element of G2./.N2
  ex x being Element of G1./.N1
  st phiBar.x = y
  proof
    let y be Element of G2./.N2;
    consider g2 being Element of G2 such that
    B1: y = g2 * N2 & y = N2 * g2
    by GROUP_6:21;
    B2: phi" is Homomorphism of G2,G1 by A1, GROUP_6:62;
    then (phi").g2 in G1 by FUNCT_2:5;
    then (nat_hom N1).((phi").g2) in G1./.N1 by FUNCT_2:5;
    then consider x being Element of G1./.N1 such that
    B3: x = (nat_hom N1).((phi").g2);
    take x;
    B4: phi is one-to-one & rng phi = the carrier of G2 by A1, FUNCT_2:def 3;

    g2 in G2;
    then g2 in dom (phi") by B2, FUNCT_2:def 1; then
    B5: (phi.((phi").g2)) = (phi * (phi")).g2 by FUNCT_1:13
                         .= (id the carrier of G2).g2 by B4,FUNCT_2:29
                         .= g2;
    thus phiBar.x = (phiBar * (nat_hom N1)).((phi").g2) by B2, B3, FUNCT_2:5,15
                 .= (nat_hom N2).(phi.((phi").g2)) by A3, B2, FUNCT_2:5,15
                 .= y by B1, B5, GROUP_6:def 8;
  end;
  then A4: phiBar is onto by LmOnto;

  for a,b being Element of G1./.N1 st phiBar.a = phiBar.b
  holds a = b
  proof
    let a,b be Element of G1./.N1;
    assume B1: phiBar.a = phiBar.b;
    consider x1 being Element of G1 such that
    B2: a = x1 * N1 & a = N1 * x1
    by GROUP_6:21;
    consider x2 being Element of G1 such that
    B3: b = x2 * N1 & b = N1 * x2
    by GROUP_6:21;
    B4: phiBar.a = phiBar.((nat_hom N1).x1) by B2,GROUP_6:def 8
                .= ((nat_hom N2) * phi).x1 by A3,FUNCT_2:15
                .= (nat_hom N2).(phi.x1) by FUNCT_2:15;
    phiBar.b = phiBar.((nat_hom N1).x2) by B3, GROUP_6:def 8
            .= (phiBar * (nat_hom N1)).x2 by FUNCT_2:15
            .= (nat_hom N2).(phi.x2) by A3, FUNCT_2:15;
    then (phi.x1) * N2 = (nat_hom N2).(phi.x2) by B1,B4,GROUP_6:def 8
                      .= (phi.x2) * N2 by GROUP_6:def 8;
    then consider n being Element of G2 such that
    B5: n in N2 & phi.x1 = (phi.x2) * n
    by Th47;
    consider n1 being object such that
    B6: n1 in the carrier of G1 & n1 in the carrier of N1 & n = phi.n1
    by A2, B5, FUNCT_2:64;
    reconsider n1 as Element of G1 by B6;
    phi.x1 = phi.(x2 * n1) by B5, B6, GROUP_6:def 6;
    then (x2") * x1 = (x2") * (x2 * n1) by A1, GROUP_6:1
                   .= ((x2") * x2) * n1 by GROUP_1:def 3
                   .= (1_G1) * n1 by GROUP_1:def 5
                   .= n1 by GROUP_1:def 4;
    then (x2") * x1 in N1 by B6;
    hence a = b by B2, B3, GROUP_2:114;
  end;
  then phiBar is one-to-one by GROUP_6:1;
  hence G1./.N1, G2./.N2 are_isomorphic by A4, GROUP_6:def 11;
end;

theorem Th50:
  for G being Group
  for H being Subgroup of G
  for N being normal Subgroup of G
  st H,N are_complements_in G
  ex phi being Homomorphism of H, G./.N
  st (for h being Element of H
      for g being Element of G st g = h
      holds phi.h = g*N) & phi is bijective
proof
  let G be Group;
  let H be Subgroup of G;
  let N be normal Subgroup of G;
  assume H,N are_complements_in G;
  then A2: H * N = the carrier of G & H /\ N = (1).G;

  defpred P[Element of H, Element of G./.N] means
  ex g being Element of G st g = $1 & $2 = g*N;

  A3: for x being Element of H
  ex y being Element of G./.N
  st P[x,y]
  proof
    let x be Element of H;
    reconsider g=x as Element of G by GROUP_2:42;
    take y = (nat_hom N).g;
    y = g*N by GROUP_6:def 8;
    hence P[x,y];
  end;

  consider phi being Function of H,G./.N such that
  A4: for x being Element of H holds P[x, phi.x]
  from FUNCT_2:sch 3(A3);

  A5: for h being Element of H
      for g being Element of G st g = h
      holds phi.h = g*N
  proof
    let h be Element of H;
    let g be Element of G;
    assume B1: g = h;
    P[h, phi.h] by A4;
    hence phi.h = g*N by B1;
  end;


  for a,b being Element of H
  holds phi.(a * b) = (phi.a) * (phi.b)
  proof
    let a,b be Element of H;
    reconsider g1=a,g2=b as Element of G by GROUP_2:42;
    B1: phi.a = g1 * N by A5
             .= (nat_hom N).g1 by GROUP_6:def 8;
    B2: phi.b = g2 * N by A5
             .= (nat_hom N).g2 by GROUP_6:def 8;
    a * b = g1 * g2 by GROUP_2:43; then
    phi.(a * b) = (g1 * g2) * N by A5
               .= (nat_hom N).(g1 * g2) by GROUP_6:def 8;
    hence phi.(a * b) = (phi.a) * (phi.b) by B1,B2,GROUP_6:def 6;
  end;
  then reconsider phi as Homomorphism of H,G./.N by GROUP_6:def 6;
  take phi;
  thus for h being Element of H
       for g being Element of G st g = h
       holds phi.h = g*N by A5;

  for y being Element of G./.N
  ex x being Element of H
  st phi.x = y
  proof
    let y be Element of G./.N;
    y in G./.N; then
    consider g being Element of G such that
    Z1: y = g*N & y = N*g by GROUP_6:23;
    consider h,n being Element of G such that
    Z2: g = h * n & h in H & n in N by A2, GROUP_5:4;
    reconsider x=h as Element of H by Z2;
    take x;
    g * N = h * (n * N) by Z2,GROUP_2:105
         .= h * (carr N) by Z2,GROUP_2:113
         .= h * N by GROUP_2:def 13;
    hence phi.x = y by A5,Z1;
  end;

  then A3: phi is onto by LmOnto;

  for a,b being Element of H st phi.a = phi.b
  holds a = b
  proof
    let a,b be Element of H;
    assume Z1: phi.a = phi.b;
    reconsider ga=a, gb=b as Element of G by GROUP_2:42;
    ga*N = phi.a by A5
        .= gb*N by Z1,A5;
    then Z2: (gb")*ga in N by GROUP_2:114;
    gb in H; then
    gb" in H & ga in H by GROUP_2:51;
    then (gb")*ga in H by GROUP_2:50;
    then (gb")*ga = 1_G by A2,Z2,GROUP_2:82,GROUP_5:1;
    then ga = gb * 1_G by GROUP_1:13
           .= gb by GROUP_1:def 4;
    hence a = b;
  end;

  then phi is one-to-one by GROUP_6:1;
  hence phi is bijective by A3;
end;

theorem Th51:
  for G being Group
  for H being Subgroup of G
  for N being normal Subgroup of G
  st H,N are_complements_in G
  holds G ./. N, H are_isomorphic
proof
  let G be Group;
  let H be Subgroup of G;
  let N be normal Subgroup of G;
  assume H,N are_complements_in G;
  then ex phi being Homomorphism of H,G./.N
  st (for h being Element of H
      for g being Element of G st g = h
      holds phi.h = g*N) & phi is bijective by Th50;
  hence G ./. N, H are_isomorphic by GROUP_6:def 11;
end;

theorem
  for G being Group
  for H1, H2 being Subgroup of G
  for N being normal Subgroup of G
  st H1,N are_complements_in G & H2,N are_complements_in G
  holds H1,H2 are_isomorphic
proof
  let G be Group;
  let H1, H2 be Subgroup of G;
  let N be normal Subgroup of G;
  assume A1: H1,N are_complements_in G;
  assume A2: H2,N are_complements_in G;

  A3: G ./. N, H1 are_isomorphic by A1,Th51;
  A4: G ./. N, H2 are_isomorphic by A2,Th51;
  thus H1,H2 are_isomorphic by A3,A4,GROUP_6:67;
end;

:: Bourbaki mentions this, literally in passing in a sentence in
:: General Topology Chapter II, not even making a proposition or lemma
:: out of it.
theorem Th53:
  for G being Group
  for H,K being Subgroup of G
  for phi being Function of product <*H,K*>,G
  st (for h,k being Element of G st h in H & k in K
      holds phi.(<*h,k*>) = h*k)
  holds phi is one-to-one iff H /\ K = (1).G
proof
  let G be Group;
  let H,K be Subgroup of G;
  let phi be Function of product <*H,K*>,G;
  assume A1: for h,k being Element of G st h in H & k in K
  holds phi.(<*h,k*>) = h*k;
  A2: 1_G in H & 1_G in K by GROUP_2:46;

  thus phi is one-to-one implies H /\ K = (1).G
  proof
    assume B1: phi is one-to-one;
    B2: for h1,h2,k1,k2 being Element of G
    st h1 in H & h2 in H & k1 in K & k2 in K
       & phi.(<*h1,k1*>) = phi.(<*h2,k2*>)
    holds <*h1,k1*> = <*h2,k2*>
    proof
      let h1,h2,k1,k2 be Element of G;
      assume Z1: h1 in H;
      assume Z2: h2 in H;
      assume Z3: k1 in K;
      assume Z4: k2 in K;
      assume Z5: phi.(<*h1,k1*>) = phi.(<*h2,k2*>);
      <*h1,k1*> in product <*H,K*> by Z1,Z3,Th8;
      then reconsider x1 = <*h1,k1*> as Element of product <*H,K*>;
      <*h2,k2*> in product <*H,K*> by Z2,Z4,Th8;
      then reconsider x2 = <*h2,k2*> as Element of product <*H,K*>;
      phi.x1 = phi.x2 by Z5;
      hence <*h1,k1*> = <*h2,k2*> by B1, GROUP_6:1;
    end;

    H /\ K <> (1).G implies contradiction
    proof
      assume Z1: H /\ K <> (1).G;
      (1).G is Subgroup of H /\ K by GROUP_2:65;
      then consider h1 being Element of G such that
      Z2: h1 in H /\ K & not h1 in (1).G by Z1,Th9;
      Z3: h1 <> 1_G by Z2, GROUP_5:1;
      h1 in H & h1 in K by Z2, GROUP_2:82; then
      Z4: h1 in H & h1" in K by GROUP_2:51;
      then phi.(<*h1,h1"*>) = h1 * (h1") by A1
                           .= 1_G by GROUP_1:def 5
                           .= (1_G) * (1_G) by GROUP_1:def 4
                           .= phi.(<*1_G, 1_G*>) by A1,A2;
      then <*h1, h1"*> = <*1_G,1_G*> by A2, B2, Z4;
      hence contradiction by Z3, Th14;
    end;
    hence H /\ K = (1).G;
  end;
  thus H /\ K = (1).G implies phi is one-to-one
  proof
    assume B1: H /\ K = (1).G;
    for x,y being Element of product <*H,K*>
    st phi.x = phi.y
    holds x = y
    proof
      let x,y be Element of product <*H,K*>;
      assume B2: phi.x = phi.y;
      x.1 in H & x.2 in K & dom x = {1,2} by Th7;
      then x.1 in H & x.1 in G & x.2 in K & x.2 in G & dom x = {1,2} by GROUP_2:41;
      then consider h1,k1 being Element of G such that
      B3: x.1 = h1 & x.2 = k1 & h1 in H & k1 in K;
      dom x = {1,2} by Th7;
      then len x = 2 by FINSEQ_1:2,def 3;
      then B4: x.1 = h1 & x.2 = k1 & h1 in H & k1 in K & x = <*h1,k1*>
      by B3,FINSEQ_1:44;
      y.1 in H & y.2 in K & dom y = {1,2} by Th7;
      then y.1 in G & y.1 in H & y.2 in G & y.2 in K & dom y = {1,2} by GROUP_2:41;
      then consider h2,k2 being Element of G such that
      B5: y.1 = h2 & y.2 = k2 & h2 in H & k2 in K;
      dom y = {1,2} by Th7;
      then len y = 2 by FINSEQ_1:2,def 3;
      then B6: y.1 = h2 & y.2 = k2 & h2 in H & k2 in K & y = <*h2,k2*>
      by B5,FINSEQ_1:44;
      h1*k1 = phi.x by A1,B4
           .= h2*k2 by A1,B2,B6;
      then (h2") * (h1 * k1) = ((h2") * h2) * k2 by GROUP_1:def 3
                            .= (1_G) * k2 by GROUP_1:def 5
                            .= k2 by GROUP_1:def 4;
      then B7: k2 * (k1") = (((h2") * h1) * k1) * (k1") by GROUP_1:def 3
                         .= ((h2") * h1) * (k1 * (k1")) by GROUP_1:def 3
                         .= ((h2") * h1) * 1_G by GROUP_1:def 5
                         .= (h2") * h1 by GROUP_1:def 4;
      k2 in K & k1" in K by B4,B6, GROUP_2:51;
      then B8: k2 * (k1") in K by GROUP_2:50;
      h1 in H & h2" in H by B4,B6, GROUP_2:51;
      then B9: (h2") * h1 in H by GROUP_2:50;
      (h2") * h1 = 1_G by B1,B7,B8,B9,GROUP_2:82,GROUP_5:1;
      then B10: h1 = (h2")" by GROUP_1:12
                  .= h2;
      k2 * (k1") = 1_G by B1,B7,B8,B9,GROUP_2:82,GROUP_5:1;
      then k2 = (k1")" by GROUP_1:12
             .= k1;
      hence x = y by B4,B6,B10;
    end;

    hence phi is one-to-one by GROUP_6:1;
  end;
end;

theorem Th54:
  for G being Group
  for H,K being Subgroup of G
  ex phi being Function of product (Carrier <*H,K*>),G
  st (for h,k being Element of G st h in H & k in K
      holds phi.(<*h,k*>) = h*k)
  & (phi is one-to-one iff H /\ K = (1).G)
proof
  let G be Group;
  let H,K be Subgroup of G;
  defpred P[Element of product (Carrier <*H,K*>), Element of G] means
  ex h,k being Element of G st h in H & k in K & $1 = <*h,k*> & $2 = h*k;

  A1: for x being Element of product (Carrier <*H,K*>)
  ex y being Element of G
  st P[x, y]
  proof
    let x be Element of product (Carrier <*H,K*>);
    x in product (Carrier <*H,K*>);
    then x in the carrier of product <*H,K*> by GROUP_7:def 2;
    then B1: x.1 in H & x.2 in K & dom x = {1,2} by Th7;
    then x.1 in H & x.1 in G & x.2 in K & x.2 in G by GROUP_2:40;
    then consider h,k being Element of G such that
    B2: h = x.1 & k = x.2 & h in H & k in K;
    len x = 2 by B1,FINSEQ_1:2,def 3;
    then B3: x = <*h, k*> by B2, FINSEQ_1:44;
    take y = h*k;
    thus P[x, y] by B2,B3;
  end;

  consider phi being Function of product (Carrier <*H,K*>),G such that
  A2: for x being Element of product (Carrier <*H,K*>) holds P[x, phi.x]
  from FUNCT_2:sch 3(A1);
  take phi;

  A3: for h,k being Element of G st h in H & k in K
  holds phi.(<*h,k*>) = h*k
  proof
    let h,k be Element of G;
    assume B1: h in H;
    assume B2: k in K;

    <*h,k*> in product <*H,K*> by B1,B2,Th8; then
    reconsider x=<*h,k*> as Element of product (Carrier <*H,K*>)
    by GROUP_7:def 2;
    consider h1,k1 being Element of G such that
    B3: h1 in H & k1 in K & x = <*h1,k1*> & phi.x = h1*k1
    by A2;
    h1 = h & k1 = k by B3,Th14;
    hence phi.(<*h,k*>) = h*k by B3;
  end;

  hence for h,k being Element of G st h in H & k in K
  holds phi.(<*h,k*>) = h*k;

  the carrier of product <*H,K*> = product (Carrier <*H,K*>) by GROUP_7:def 2;
  hence phi is one-to-one iff H /\ K = (1).G by A3,Th53;
end;

theorem Th55:
  for G being Group
  for H being Subgroup of G
  for N being strict normal Subgroup of G
  for phi being Homomorphism of H,AutGroup N
  ex psi being Function of semidirect_product (N,H,phi),G
  st (for n,h being Element of G st n in N & h in H
      holds psi.(<*n,h*>) = n*h)
  & (psi is one-to-one iff N /\ H = (1).G)
proof
  let G be Group;
  let H be Subgroup of G;
  let N be strict normal Subgroup of G;
  let phi be Homomorphism of H, AutGroup N;
  A1: for f being Function of product (Carrier <*N,H*>),G
  holds f is Function of semidirect_product (N,H,phi), G
  proof
    let f be Function of product (Carrier <*N,H*>),G;
    the carrier of semidirect_product (N,H,phi) = product (Carrier <*N,H*>)
    by Def3;
    hence f is Function of semidirect_product (N,H,phi), G;
  end;
  consider psi being Function of product (Carrier <*N,H*>),G such
  that
  A2: (for n,h being Element of G st n in N & h in H holds psi.(<*n,h*>)=n*h)
  & (psi is one-to-one iff N /\ H = (1).G)
  by Th54;
  reconsider psi as Function of semidirect_product (N,H,phi), G by A1;
  take psi;
  thus thesis by A2;
end;

theorem Th56:
  for G being Group
  for H1,H2 being Subgroup of G
  holds H1*H2 c= the carrier of H1 "\/" H2
proof
  let G be Group;
  let H1,H2 be Subgroup of G;
  H1 "\/" H2 = gr (H1 * H2) by GROUP_4:50;
  hence H1 * H2 c= the carrier of H1 "\/" H2 by GROUP_4:def 4;
end;

theorem Th57:
  for G being Group
  for H1,H2 being Subgroup of G
  holds H1*H2 c= the carrier of H1 "\/" H2
  & H2 * H1  c= the carrier of H1 "\/" H2
proof
  let G be Group;
  let H1,H2 be Subgroup of G;
  thus H1*H2 c= the carrier of H1 "\/" H2 by Th56;
  H2 * H1 c= the carrier of H2 "\/" H1 by Th56;
  hence H2 * H1 c= the carrier of H1 "\/" H2 by GROUP_4:56;
end;

theorem Th58:
  for G being Group
  for H1,H2 being Subgroup of G
  st H1 * H2 = the carrier of (H1 "\/" H2)
  holds H1 * H2 = H2 * H1
proof
  let G be Group;
  let H1,H2 be Subgroup of G;
  assume A1: H1 * H2 = the carrier of (H1 "\/" H2);

  then A2: H2 * H1 c= H1 * H2 by Th57;
  for x being Element of G st x in H1 * H2 holds x in H2 * H1
  proof
    let x be Element of G;
    assume x in H1 * H2;
    then x in H1 "\/" H2 by A1;
    then x" in H1 "\/" H2 by GROUP_2:51;
    then consider h1,h2 being Element of G such that
    B2: x" = h1*h2 & h1 in H1 & h2 in H2
    by A1, GROUP_5:4;
    B3: x = (x")"
         .= (h2") * (h1") by B2, GROUP_1:17;
    h2" in H2 & h1" in H1 by B2,GROUP_2:51;
    hence x in H2 * H1 by B3,GROUP_5:4;
  end;

  then A3: H1 * H2 c= H2 * H1;
  thus thesis by A2,A3,XBOOLE_0:def 10;
end;

theorem Th59:
  for G being Group
  for H being Subgroup of G
  for N being normal Subgroup of G
  st H,N are_complements_in G
  holds H * N = the carrier of G & N * H = the carrier of G
proof
  let G be Group;
  let H be Subgroup of G;
  let N be normal Subgroup of G;
  assume A1: H,N are_complements_in G;
  then H "\/" N = the multMagma of G by Th44;
  hence thesis by A1, Th58;
end;

:: Aschbacher, Theorem 10.2
theorem
  for G being Group
  for N being strict normal Subgroup of G
  for H being Subgroup of G
  st H,N are_complements_in G
  for alpha being Homomorphism of H,AutGroup N
  st (for h,n being Element of G st h in H & n in N
      holds (alpha/.h).n = n |^ (h"))
  ex beta being Homomorphism of semidirect_product(N,H,alpha), G
  st (for gh,gn being Element of G
      for h being Element of H
      for n being Element of N st gh = h & gn = n
      holds beta.(<*n,h*>) = gn*gh)
   & beta is bijective
proof
  let G be Group;
  let N be strict normal Subgroup of G;
  let H be Subgroup of G;
  assume A1: H,N are_complements_in G;
  let alpha be Homomorphism of H,AutGroup N;
  assume A2: for h,n being Element of G st h in H & n in N
  holds (alpha/.h).n = n |^ (h");
  set S = semidirect_product(N,H,alpha);
  consider beta being Function of S, G such that
  A3: (for n,h being Element of G st n in N & h in H
       holds beta.(<*n,h*>) = n*h)
  & (beta is one-to-one iff N /\ H = (1).G)
  by Th55;

  for x,y being Element of S
  holds beta.(x * y) = (beta.x) * (beta.y)
  proof
    let x,y be Element of S;
    consider n1 being Element of N, a1 being Element of H such that
    B1: x = <*n1,a1*> by Th13;
    reconsider ga1=a1,gn1=n1 as Element of G by GROUP_2:42;
    consider n2 being Element of N, a2 being Element of H such that
    B2: y = <*n2,a2*> by Th13;
    reconsider ga2=a2,gn2=n2 as Element of G by GROUP_2:42;
    B6: ga1 in H & gn1 in N & ga2 in H & gn2 in N;
    B7: gn2 |^ (ga1") = (((ga1")")*gn2) * (ga1") by GROUP_3:def 2
                     .= (ga1 * gn2) * (ga1");
    beta.x = gn1*ga1 & beta.y = gn2*ga2 by A3,B2,B1,B6;
    then B5: (beta.x) * (beta.y) = gn1 * ga1 * gn2 * ga2 by GROUP_1:def 3
    .= gn1 * ga1 * gn2 * (1_G) * ga2 by GROUP_1:def 4
    .= gn1 * ga1 * gn2 * ((ga1") * ga1) * ga2 by GROUP_1:def 5
    .= gn1 * ga1 * gn2 * (ga1") * ga1 * ga2 by GROUP_1:def 3
    .= gn1 * (ga1 * gn2) * (ga1") * ga1 * ga2 by GROUP_1:def 3
    .= gn1 * (gn2 |^ (ga1")) * ga1 * ga2 by B7, GROUP_1:def 3;
    B4: ((alpha /. ga1) . gn2) = gn2 |^ (ga1") & gn2 |^ (ga1") in N
    by A2,B6,Th25;
    then B10: ga1 * ga2 in H & gn1 * (gn2 |^ (ga1")) in N by B6, GROUP_2:50;
    B11: n1 * ((alpha/.a1).n2) = (the multF of G).(gn1, (alpha/.ga1).gn2)
    proof
      C1: (alpha/.a1).n2 = (alpha/.ga1).gn2 by B6, Def2a;
      then (alpha/.ga1).gn2 in N;
      then (alpha/.ga1).gn2 in G by GROUP_2:40;
      then reconsider y = (alpha/.ga1).gn2 as Element of G;
      reconsider x = (alpha/.a1).n2 as Element of N;
      gn1 * y = (the multF of G).(gn1, (alpha/.ga1).gn2);
      hence thesis by C1, GROUP_2:43;
    end;
    x * y = <*n1 * ((alpha/.a1)/.n2), a1*a2 *> by B1,B2,Th15
         .= <* gn1 * (gn2 |^ (ga1")), ga1*ga2 *> by B4, B11, GROUP_2:43;
    then beta.(x * y) = (gn1 * (gn2 |^ (ga1"))) * (ga1 * ga2) by A3,B10
                     .= (beta.x) * (beta.y) by B5, GROUP_1:def 3;
    hence beta.(x * y) = (beta.x) * (beta.y);
  end;

  then reconsider beta as Homomorphism of S, G by GROUP_6:def 6;
  take beta;

  thus for gh,gn being Element of G
  for h being Element of H
  for n being Element of N st gh = h & gn = n
  holds beta.(<*n,h*>) = gn*gh
  proof
    let gh,gn be Element of G;
    let h be Element of H;
    let n be Element of N;
    assume B1: gh = h & gn = n;
    then gh in H & gn in N;
    hence beta.(<*n,h*>) = gn*gh by B1,A3;
  end;

  for y being Element of G
  ex x being Element of S
  st beta.x = y
  proof
    let y be Element of G;
    y in the carrier of G;
    then y in N * H by A1,Th59;
    then consider n,h being Element of G such that
    B1: y = n * h & n in N & h in H
    by GROUP_5:4;
    reconsider h1=h as Element of H by B1;
    reconsider n1=n as Element of N by B1;
    reconsider x = <*n1,h1*> as Element of S by Th11;
    take x;
    thus beta.x = y by B1,A3;
  end;

  then beta is onto by LmOnto;
  hence thesis by A1, A3;
end;

begin :: Split extensions of groups.

definition
  let G,K be Group;
  pred G splits_over K means :Def8:
  ex N being strict normal Subgroup of G
  st N = the multMagma of K & (ex H being Subgroup of G
  st H,N are_complements_in G);
end;

Lm4:
  for G being Group
  for H being Subgroup of G
  for N being normal Subgroup of G
  for N1 being strict normal Subgroup of G
  st N1 = the multMagma of N
  holds H * N = H * N1 & H /\ N = H /\ N1
proof
  let G be Group;
  let H be Subgroup of G;
  let N be normal Subgroup of G;
  let N1 be strict normal Subgroup of G;
  assume A1: N1 = the multMagma of N;
  reconsider H1=the multMagma of H as strict Subgroup of G by Th2;
  H * N = H1 * N1 & H /\ N = H1 /\ N1 by A1,Th37;
  hence thesis by Th37;
end;

theorem Th61:
  for G being Group
  for N being normal Subgroup of G
  holds G splits_over N
  iff (ex H being Subgroup of G st H,N are_complements_in G)
proof
  let G be Group;
  let N be normal Subgroup of G;
  thus G splits_over N implies (ex H being Subgroup of G st H,N are_complements_in G)
  proof
    given N1 being strict normal Subgroup of G such that 
    A1: N1 = the multMagma of N & (ex H being Subgroup of G st H,N1 are_complements_in G);
    consider H being Subgroup of G such that
    A2: H,N1 are_complements_in G by A1;
    take H;
    thus thesis by A1,A2;
  end;
  thus (ex H being Subgroup of G st H,N are_complements_in G) implies G splits_over N
  proof
    given H being Subgroup of G such that 
    A1: H,N are_complements_in G;
    ex N1 being strict normal Subgroup of G
    st N1 = the multMagma of N
    & (ex H being Subgroup of G st H,N1 are_complements_in G)
    proof
      reconsider N1=the multMagma of N as strict normal Subgroup of G by Th3;
      take N1;
      thus N1 = the multMagma of N;
      take H;
      thus thesis by A1;
    end;
    hence G splits_over N;
  end;
end;

definition
  let G be Group;
  let N be normal Subgroup of G;
  redefine pred G splits_over N means :Def9:
  ex H being Subgroup of G st H,N are_complements_in G;
  compatibility by Th61;
end;

theorem :: 62
  for G being Group
  for N being normal Subgroup of G
  for K being Subgroup of G st N is Subgroup of K
  holds K splits_over N iff ex H being Subgroup of K st
  H,N are_complements_in K
proof
  let G be Group;
  let N be normal Subgroup of G;
  let K be Subgroup of G;
  assume N is Subgroup of K;
  then reconsider N1=N as normal Subgroup of K by GROUP_6:8;
  thus K splits_over N implies ex H being Subgroup of K
  st H,N are_complements_in K
  proof
    assume K splits_over N;
    then K splits_over N1;
    hence ex H being Subgroup of K st H,N are_complements_in K;
  end;
  given H being Subgroup of K such that 
  A1: H,N are_complements_in K;
  K splits_over N1 by A1;
  hence K splits_over N;
end;

theorem
  for G being Group
  for N being normal Subgroup of G
  for H being Subgroup of G
  st H,N are_complements_in G
  for g being Element of G
  ex h,n being Element of G
  st h in H & n in N & h*n = g
proof
  let G be Group;
  let N be normal Subgroup of G;
  let H be Subgroup of G;
  assume A1: H,N are_complements_in G;
  let g be Element of G;
  consider h,n being Element of G such that
  A2: g = h*n & h in H & n in N by A1, GROUP_5:4;
  take h,n;
  thus h in H by A2;
  thus n in N by A2;
  thus h*n = g by A2;
end;


LmAssoc:
  for G being Group
  for g1,g2,g3,g4,g5 being Element of G
  holds (g1*(g2 * g3 * g4))*g5 = g1*g2*g3*g4*g5
proof
  let G be Group;
  let g1,g2,g3,g4,g5 be Element of G;
  thus (g1*(g2 * g3 * g4))*g5 = (g1*(g2*(g3*g4)))*g5 by GROUP_1:def 3
                             .= ((g1*g2)*(g3*g4))*g5 by GROUP_1:def 3
                             .= g1*g2*g3*g4*g5 by GROUP_1:def 3;
end;


:: Bourbaki, General Topology, Chapter 3, section 2, p.240, Proposition 27
::$N Universal Property of Semidirect Products (Bourbaki~\cite[III \S2.10]{bourbaki2013general})
theorem
  for H,G being Group
  for N being strict Group
  for f being Homomorphism of N,G
  for g being Homomorphism of H,G
  for phi being Homomorphism of H,AutGroup N
  st (for n being Element of N
      for h being Element of H
      holds f.((phi/.h).n) = (g.h) * (f.n) * (g.(h")))
  ex k being Homomorphism of semidirect_product(N,H,phi), G
  st f = k * (incl1 (N, H, phi)) & g = k * (incl2 (N, H, phi))
proof
  let H,G be Group;
  let N be strict Group;
  let f be Homomorphism of N,G;
  let g be Homomorphism of H,G;
  let phi be Homomorphism of H,AutGroup N;
  assume A1: for n being Element of N
  for h being Element of H
  holds f.((phi/.h).n) = (g.h) * (f.n) * (g.(h"));
  set S = semidirect_product(N,H,phi);
  defpred P[Element of S, Element of G] means
  for n being Element of N,h being Element of H st $1 = <*n,h*> holds $2 = (f.n)*(g.h);

  A2: for x being Element of S
  ex y being Element of G
  st P[x,y]
  proof
    let x be Element of S;
    consider n being Element of N, h being Element of H such that
    B1: x = <*n,h*> by Th13;
    take y = (f.n) * (g.h);
    for n1 being Element of N,h1 being Element of H st x = <*n1,h1*>
    holds y = (f.n1)*(g.h1)
    proof
      let n1 be Element of N;
      let h1 be Element of H;
      assume x = <*n1,h1*>;
      then n=n1 & h=h1 by B1,Th14;
      hence y = (f.n1)*(g.h1);
    end;
    hence P[x,y];
  end;

  consider k being Function of S, G such that
  A3: for x being Element of S holds P[x,k.x]
  from FUNCT_2:sch 3(A2);

  for x1,x2 being Element of S
  holds k.(x1 * x2) = (k.x1) * (k.x2)
  proof
    let x1,x2 be Element of S;
    consider n1 being Element of N, h1 being Element of H such that
    B1: x1 = <*n1,h1*> by Th13;
    consider n2 being Element of N, h2 being Element of H such that
    B2: x2 = <*n2,h2*> by Th13;
    x1 * x2 = <* n1 * ((phi /. h1)/.n2), h1 * h2*> by B1,B2,Th15;
    hence k.(x1 * x2) = (f.(n1 * ((phi/.h1)/.n2))) * (g.(h1 * h2)) by A3
    .= ((f.n1)*(f.((phi/.h1)/.n2))) * (g.(h1 * h2)) by GROUP_6:def 6
    .= (f.n1)*((g.h1) * (f.n2) * (g.(h1")))*(g.(h1 * h2)) by A1
    .= (f.n1)*(g.h1)*(f.n2)*(g.(h1"))*(g.(h1 * h2)) by LmAssoc
    .= ((f.n1)*(g.h1)*(f.n2)*(g.(h1")))*((g.h1)*(g.h2)) by GROUP_6:def 6
    .= ((f.n1)*(g.h1)*(f.n2)*(g.(h1")))*(g.h1)*(g.h2) by GROUP_1:def 3
    .= (f.n1)*(g.h1)*(f.n2)*((g.(h1"))*(g.h1))*(g.h2) by GROUP_1:def 3
    .= (f.n1)*(g.h1)*(f.n2)*(g.((h1")*h1))*(g.h2) by GROUP_6:def 6
    .= (f.n1)*(g.h1)*(f.n2)*(g.(1_H))*(g.h2) by GROUP_1:def 5
    .= (f.n1)*(g.h1)*(f.n2)*(1_G)*(g.h2) by GROUP_6:31
    .= (f.n1)*(g.h1)*(f.n2)*(g.h2) by GROUP_1:def 4
    .= (k.x1)*(f.n2)*(g.h2) by A3,B1
    .= (k.x1)*((f.n2)*(g.h2)) by GROUP_1:def 3
    .= (k.x1)*(k.x2) by A3, B2;
  end;
  then reconsider k as Homomorphism of S,G by GROUP_6:def 6;

  take k;

  A4: for n being Element of N
  for h being Element of H
  holds k.(<*n,h*>)=(f.n)*(g.h)
  proof
    let n be Element of N;
    let h be Element of H;
    reconsider x=<*n,h*> as Element of S by Th11;
    P[x,k.x] by A3;
    hence k.(<*n,h*>)=(f.n)*(g.h);
  end;

  for n being Element of N
  holds f.n = (k * (incl1 (N, H, phi))).n
  proof
    let n be Element of N;
    thus (k * (incl1 (N, H, phi))).n = k.((incl1 (N, H, phi)).n) by FUNCT_2:15
    .= k.(<*n, 1_H*>) by Def4
    .= (f.n)*(g.(1_H)) by A4
    .= (f.n)*(1_G) by GROUP_6:31
    .= f.n by GROUP_1:def 4;
  end;
  hence f = k * (incl1 (N, H, phi)) by FUNCT_2:63;

  for h being Element of H
  holds g.h = (k * (incl2 (N, H, phi))).h
  proof
    let h be Element of H;
    thus (k * (incl2 (N, H, phi))).h = k.((incl2 (N, H, phi)).h) by FUNCT_2:15
    .= k.(<*1_N,h*>) by Def5
    .= (f.(1_N))*(g.h) by A4
    .= (1_G)*(g.h) by GROUP_6:31
    .= g.h by GROUP_1:def 4;
  end;
  hence g = k * (incl2 (N, H, phi)) by FUNCT_2:63;
end;

begin :: Intermezzo on cosets

reserve G for Group, H for Subgroup of G;

:: should be moved to GROUP_2
registration
  let G, H;
  cluster -> non empty for Element of Left_Cosets H;
  correctness
  proof
    let U be Element of Left_Cosets H;
    consider g being Element of G such that
    A1: U = g * H by GROUP_2:def 15;
    thus thesis by A1, GROUP_2:108;
  end;
  cluster -> non empty for Element of Right_Cosets H;
  correctness
  proof
    let U be Element of Right_Cosets H;
    consider g being Element of G such that
    A1: U = H * g by GROUP_2:def 16;
    thus thesis by A1, GROUP_2:108;
  end;
end;

theorem Th65:
  for U,V being Element of Left_Cosets H
  holds U = V or U misses V
proof
  let U,V be Element of Left_Cosets H;
  assume A1: not U = V;
  consider g1 being Element of G such that
  A2: U = g1 * H by GROUP_2:def 15;
  consider g2 being Element of G such that
  A3: V = g2 * H by GROUP_2:def 15;
  thus U misses V by A1,A2,A3,GROUP_2:115;
end;

theorem Th66: :: 66
  for U,V being Element of Right_Cosets H
  holds U = V or U misses V
proof
  let U,V be Element of Right_Cosets H;
  assume A1: not U = V;
  consider g1 being Element of G such that
  A2: U = H * g1 by GROUP_2:def 16;
  consider g2 being Element of G such that
  A3: V = H * g2 by GROUP_2:def 16;
  thus U misses V by A1,A2,A3,GROUP_2:121;
end;

theorem Th67: :: 67
  for K being Subgroup of H
  holds K * (carr H) = carr H & (carr H) * K = carr H
proof
  let K be Subgroup of H;
  for x being object
  holds x in K * (carr H) iff x in carr H
  proof
    let x be object;
    thus x in K * (carr H) implies x in carr H
    proof
      assume x in K * (carr H);
      then consider g1,g2 being Element of G such that
      A1: x = g1 * g2 & g1 in K & g2 in carr H by GROUP_2:95;
      g1 in H & g2 in H by A1, TARSKI:def 3, GROUP_2:def 5,def 9;
      then g1 * g2 in H by GROUP_2:50;
      hence x in carr H by A1, GROUP_2:def 9;
    end;
    assume A1: x in carr H;
    ex g1,g2 being Element of G
    st x = g1*g2 & g1 in K & g2 in carr H
    proof
      reconsider g2=x as Element of G by A1;
      take g1 = 1_G, g2;
      thus x = g1 * g2 by GROUP_1:def 4;
      thus g1 in K by GROUP_2:46;
      thus g2 in carr H by A1;
    end;
    hence x in K * (carr H) by GROUP_2:95;
  end;
  hence K * (carr H) = carr H by TARSKI:2;

  for x being object
  holds x in (carr H) * K iff x in carr H
  proof
    let x be object;
    thus x in (carr H) * K implies x in carr H
    proof
      assume x in (carr H) * K;
      then consider g1,g2 being Element of G such that
      A1: x = g1 * g2 & g1 in carr H & g2 in K by GROUP_2:94;
      g1 in H & g2 in H by A1, TARSKI:def 3, GROUP_2:def 5,def 9;
      then g1 * g2 in H by GROUP_2:50;
      hence x in carr H by A1, GROUP_2:def 9;
    end;
    assume A1: x in carr H;
    ex g1,g2 being Element of G
    st x = g1*g2 & g1 in carr H & g2 in K
    proof
      reconsider g1=x as Element of G  by A1;
      take g1, g2 = 1_G;
      thus x = g1 * g2 by GROUP_1:def 4;
      thus g1 in carr H by A1;
      thus g2 in K by GROUP_2:46;
    end;
    hence x in (carr H) * K by GROUP_2:94;
  end;
  hence (carr H) * K = carr H by TARSKI:2;
end;

theorem :: 68
  for K being Subgroup of H
  for U being Element of Left_Cosets (K qua Subgroup of G)
  holds U*H in Left_Cosets H
proof
  let K be Subgroup of H;
  let U be Element of Left_Cosets (K qua Subgroup of G);
  consider k being Element of G such that
  A1: U = k*K by GROUP_2:def 15;
  U * H = (k * K) * (carr H) by A1,GROUP_2:def 11
       .= k * (K * (carr H)) by GROUP_3:10
       .= k * (carr H) by Th67
       .= k * H by GROUP_2:def 13;
  hence U*H in Left_Cosets H by GROUP_2:def 15;
end;

theorem :: 69
  for K being Subgroup of H
  for U being Element of Right_Cosets (K qua Subgroup of G)
  holds H*U in Right_Cosets H
proof
  let K be Subgroup of H;
  let U be Element of Right_Cosets (K qua Subgroup of G);
  consider k being Element of G such that
  A1: U = K * k by GROUP_2:def 16;
  H * U = (carr H) * (K * k) by A1, GROUP_2:def 12
       .= ((carr H) * K) * k by GROUP_3:11
       .= (carr H) * k by Th67
       .= H * k by GROUP_2:def 14;
  hence H*U in Right_Cosets H by GROUP_2:def 16;
end;

theorem :: 70
  for U being Element of Left_Cosets H
  holds U*H = U
proof
  let U be Element of Left_Cosets H;
  consider g being Element of G such that
  A1: U = g*H by GROUP_2:def 15;
  thus U * H = U * (carr H) by GROUP_2:def 11
            .= (g * (carr H)) * (carr H) by A1, GROUP_2:def 13
            .= g * ((carr H) * (carr H)) by GROUP_2:29
            .= g * (carr H) by GROUP_2:76
            .= U by A1, GROUP_2:def 13;
end;

theorem :: 71
  for U being Element of Right_Cosets H
  holds H*U = U
proof
  let U be Element of Right_Cosets H;
  consider g being Element of G such that
  A1: U = H*g by GROUP_2:def 16;
  thus H * U = (carr H) * U by GROUP_2:def 12
            .= (carr H) * ((carr H) * g) by A1, GROUP_2:def 14
            .= ((carr H) * (carr H)) * g by GROUP_2:31
            .= (carr H) * g by GROUP_2:76
            .= U by A1, GROUP_2:def 14;
end;

theorem Th72: :: 72
  for U being Element of Left_Cosets H
  for h being Element of G st h in H
  holds U * h = U & U * h in Left_Cosets H
proof
  let U be Element of Left_Cosets H;
  let h be Element of G;
  assume A1: h in H;
  consider g being Element of G such that
  A2: U = g*H by GROUP_2:def 15;
  thus U * h = g * (H * h) by A2, GROUP_2:106
            .= g * (carr H) by A1, GROUP_2:119
            .= U by A2, GROUP_2:def 13;
  hence U * h in Left_Cosets H;
end;

theorem Th73: :: 73
  for U being Element of Right_Cosets H
  for h being Element of G st h in H
  holds h * U = U & h * U in Right_Cosets H
proof
  let U be Element of Right_Cosets H;
  let h be Element of G;
  assume A1: h in H;
  consider g being Element of G such that
  A2: U = H*g by GROUP_2:def 16;
  thus h * U = (h * H) * g by A2, GROUP_2:106
            .= (carr H) * g by A1, GROUP_2:113
            .= U by A2, GROUP_2:def 14;
  hence h * U in Right_Cosets H;
end;

theorem Th74: :: 74
  for A,B being Subset of G
  for g being Element of G
  holds (A /\ B) * g = (A * g) /\ (B * g)
proof
  let A,B be Subset of G;
  let g be Element of G;

  for x being object
  holds x in (A /\ B) * g iff x in (A * g) /\ (B * g)
  proof
    let x be object;
    thus x in (A /\ B) * g implies x in (A * g) /\ (B * g)
    proof
      assume x in (A /\ B) * g;
      then x in (A /\ B) * {g} by GROUP_2:def 4;
      then x in (A * {g}) /\ (B * {g}) by GROUP_2:15, TARSKI:def 3;
      then x in (A * g) /\ (B * {g}) by GROUP_2:def 4;
      hence x in (A * g) /\ (B * g) by GROUP_2:def 4;
    end;
    assume x in (A * g) /\ (B * g);
    then A1: x in A * g & x in B * g by XBOOLE_0:def 4;
    then consider a being Element of G such that
    A2: x = a*g & a in A
    by GROUP_2:28;
    consider b being Element of G such that
    A3: x = b*g & b in B
    by A1, GROUP_2:28;
    a = b by A2,A3,GROUP_1:6;
    then a in A /\ B by A2, A3, XBOOLE_0:def 4;
    hence x in (A /\ B) * g by A2, GROUP_2:28;
  end;
  hence thesis by TARSKI:2;
end;

theorem Th75: :: Th75
  for A,B being Subset of G
  for g being Element of G
  holds g * (A /\ B) = (g * A) /\ (g * B)
proof
  let A,B be Subset of G;
  let g be Element of G;
  for x being object
  holds x in g * (A /\ B) iff x in (g * A) /\ (g * B)
  proof
    let x be object;
    thus x in g * (A /\ B) implies x in (g * A) /\ (g * B)
    proof
      assume x in g * (A /\ B);
      then x in {g} * (A /\ B) by GROUP_2:def 3;
      then x in ({g} * A) /\ ({g} * B) by GROUP_2:14, TARSKI:def 3;
      then x in (g * A) /\ ({g} * B) by GROUP_2:def 3;
      hence x in (g * A) /\ (g * B) by GROUP_2:def 3;
    end;
    assume x in (g * A) /\ (g * B);
    then A1: x in g * A & x in g * B by XBOOLE_0:def 4;
    then consider a being Element of G such that
    A2: x = g * a & a in A by GROUP_2:27;
    consider b being Element of G such that
    A3: x = g * b & b in B by A1, GROUP_2:27;
    b = a by A2,A3,GROUP_1:6;
    then a in A /\ B by A2, A3, XBOOLE_0:def 4;
    hence x in g * (A /\ B) by A2, GROUP_2:27;
  end;
  hence thesis by TARSKI:2;
end;

:: Mentioned in passing in Philip Hall's 1957 book on group theory
theorem :: Th76:
  for K being Subgroup of H
  for g being Element of G
  holds g*K misses carr H or g*K c= carr H
proof
  let K be Subgroup of H;
  let g be Element of G;
  assume not g*K misses carr H;
  then consider x being object such that
  A1: x in g*K & x in carr H
  by XBOOLE_0:3;
  A2: g in H
  proof
    consider k being Element of G such that
    B1: x = g*k & k in K by A1, GROUP_2:103;
    k in H by B1, GROUP_2:40; then
    B2: k" in H by GROUP_2:51;
    g*k in H by A1, B1, GROUP_2:def 9;
    then (g * k) * (k") in H by B2, GROUP_2:50;
    then g * (k * k") in H by GROUP_1:def 3;
    then g * (1_G) in H by GROUP_1:def 5;
    hence g in H by GROUP_1:def 4;
  end;
  g*K c= g*H by GROUP_3:6;
  hence g*K c= carr H by A2, GROUP_2:113;
end;

theorem :: Th77:
  for K being Subgroup of H
  for g being Element of G
  holds K*g misses carr H or K*g c= carr H
proof
  let K be Subgroup of H;
  let g be Element of G;
  assume not K*g misses carr H;
  then consider x being object such that
  A1: x in K*g & x in carr H
  by XBOOLE_0:3;
  A2: g in H
  proof
    consider k being Element of G such that
    B1: x = k*g & k in K by A1, GROUP_2:104;
    k in H by B1, GROUP_2:40; then
    B2: k" in H by GROUP_2:51;
    k * g in H by A1, B1, GROUP_2:def 9;
    then (k") * (k * g)  in H by B2, GROUP_2:50;
    then (k" * k) * g in H by GROUP_1:def 3;
    then (1_G) * g in H by GROUP_1:def 5;
    hence g in H by GROUP_1:def 4;
  end;
  K*g c= H*g by GROUP_3:6;
  hence K*g c= carr H by A2, GROUP_2:119;
end;


begin :: Transversals of Subgroups

definition
  let G,H;
  let IT be Subset of G;
  attr IT is H-left-transversal-like means
  :DefLTL:
  for U being Element of Left_Cosets H
  ex g being Element of G st U /\ IT = {g};
  attr IT is H-right-transversal-like means
  :DefRTL:
  for U being Element of Right_Cosets H
  ex g being Element of G st U /\ IT = {g};
end;

registration
  let G,H;
  cluster H-left-transversal-like for Subset of G;
  existence
  proof
    set IT = {the Element of U where U is Element of Left_Cosets H : not contradiction};
    for x being object st x in IT holds x in the carrier of G
    proof
      let x be object;
      assume x in IT;
      then ex U being Element of Left_Cosets H st x = the Element of U;
      hence x in the carrier of G;
    end;
  
    then IT c= the carrier of G;
    then reconsider IT as Subset of G;
    take IT;
    for U being Element of Left_Cosets H
    ex g being Element of G st U /\ IT = {g}
    proof
      let U be Element of Left_Cosets H;
      consider g being Element of G such that
      Z1: g = the Element of U;
      take g;
      for x being object
      holds x in {g} iff (x in U & x in IT)
      proof
        let x be object;
        thus x in {g} implies (x in U & x in IT)
        proof
          assume x in {g};
          then Z2: x = g by TARSKI:def 1;
          thus x in U by Z1,Z2;
          thus x in IT by Z1,Z2;
        end;
        thus x in U & x in IT implies x in {g}
        proof
          assume Z2: x in U;
          assume x in IT;
          then consider V being Element of Left_Cosets H such that
          Z3: x = the Element of V;
          per cases by Th65;
          suppose U = V;
            hence thesis by Z1, Z3, TARSKI:def 1;
          end;
          suppose U misses V;
            then U /\ V = {} by XBOOLE_0:def 7;
            hence thesis by Z2, Z3, XBOOLE_0:def 4;
          end;
        end;
      end;
      hence U /\ IT = {g} by XBOOLE_0:def 4;
    end;
    hence thesis;
  end;

  cluster H-right-transversal-like for Subset of G;
  existence
  proof
    set IT = {the Element of U where U is Element of Right_Cosets H : not contradiction};
    for x being object st x in IT holds x in the carrier of G
    proof
      let x be object;
      assume x in IT;
      then ex U being Element of Right_Cosets H st x = the Element of U;
      hence thesis;
    end;
  
    then IT c= the carrier of G;
    then reconsider IT as Subset of G;
    take IT;
    for U being Element of Right_Cosets H
    ex g being Element of G st U /\ IT = {g}
    proof
      let U be Element of Right_Cosets H;
      consider g being Element of G such that
      Z1: g = the Element of U;
      take g;
      for x being object
      holds x in {g} iff (x in U & x in IT)
      proof
        let x be object;
        thus x in {g} implies (x in U & x in IT)
        proof
          assume x in {g};
          then Z2: x = g by TARSKI:def 1;
          thus x in U by Z1,Z2;
          thus x in IT by Z1,Z2;
        end;
        thus x in U & x in IT implies x in {g}
        proof
          assume Z2: x in U;
          assume x in IT;
          then consider V being Element of Right_Cosets H such that
          Z3: x = the Element of V;
          per cases by Th66;
          suppose U = V;
            :: then x = g by Z1, Z3;
            hence thesis by Z1, Z3, TARSKI:def 1;
          end;
          suppose U misses V;
            then U /\ V = {} by XBOOLE_0:def 7;
            hence thesis by Z2, Z3, XBOOLE_0:def 4;
          end;
        end;
      end;
      hence U /\ IT = {g} by XBOOLE_0:def 4;
    end;
    hence thesis;
  end;
end;

registration
  let G, H;
  cluster H-left-transversal-like -> non empty for Subset of G;
  correctness
  proof
    let L be Subset of G;
    assume A1: L is H-left-transversal-like;
    carr H in Left_Cosets H by GROUP_2:135;
    then ex g being Element of G st (carr H) /\ L = {g} by A1;
    hence thesis;
  end;
  cluster H-right-transversal-like -> non empty for Subset of G;
  correctness
  proof
    let R be Subset of G;
    assume A1: R is H-right-transversal-like;
    carr H in Right_Cosets H by GROUP_2:135;
    then ex g being Element of G st (carr H) /\ R = {g} by A1;
    hence thesis;
  end;
end;

definition
  let G, H;
  let A be Subset of H;
  let B be Subset of G;
  func A * B -> Subset of G equals :Def12:
  { (g * h) where g, h is Element of G : g in A & h in B };
  coherence
  proof
    set S = {(g * h) where g, h is Element of G : (g in A & h in B)};
    {(g * h) where g, h is Element of G : ( g in A & h in B )} c= the carrier of G
    proof
      let x be object;
      assume x in { (g * h) where g, h is Element of G : ( g in A & h in B ) };
      then ex g, h being Element of G st
      ( x = g * h & g in A & h in B ) ;
      hence x in the carrier of G;
    end;
    hence { (g * h) where g, h is Element of G : ( g in A & h in B ) } is Subset of G;
  end;
end;

definition
  let G, H;
  let A be Subset of G;
  let B be Subset of H;
  func A * B -> Subset of G equals :Def13:
  { (g * h) where g, h is Element of G : g in A & h in B };
  coherence
  proof
    set S = {(g * h) where g, h is Element of G : (g in A & h in B)};
    {(g * h) where g, h is Element of G : ( g in A & h in B )} c= the carrier of G
    proof
      let x be object;
      assume x in { (g * h) where g, h is Element of G : ( g in A & h in B ) };
      then ex g, h being Element of G st
      ( x = g * h & g in A & h in B ) ;
      hence x in the carrier of G;
    end;
    hence { (g * h) where g, h is Element of G : ( g in A & h in B ) } is Subset of G;
  end;
end;

definition
  let G,H;
  mode Left_Transversal of H is H-left-transversal-like Subset of G;
  mode Right_Transversal of H is H-right-transversal-like Subset of G;
end;

:: Probably should be in GROUP_1
registration
  let G;
  sethood of Subset of G
  proof
    take X = {a where a is Element of bool (the carrier of G) : not contradiction};
    thus thesis;
  end;
end;

registration
  let G,H;
  sethood of H-left-transversal-like Subset of G
  proof
    take X = {a where a is Subset of G : a is H-left-transversal-like};
    thus thesis;
  end;
  sethood of H-right-transversal-like Subset of G
  proof
    take X = {a where a is Subset of G : a is H-right-transversal-like};
    thus thesis;
  end;
end;

reserve LH for Left_Transversal of H,
        RH for Right_Transversal of H;

theorem Th78:
  for U being Element of Left_Cosets H
  for g being Element of G
  holds U /\ LH = {g} iff (U = g*H & g in LH)
proof
  let U be Element of Left_Cosets H;
  let g be Element of G;
  thus U /\ LH = {g} implies U = g*H & g in LH
  proof
    assume A1: U /\ LH = {g};
    A2: g*H in Left_Cosets H by GROUP_2:def 15;
    A3: g in {g} by TARSKI:def 1;
    g in g*H & g in U by A1, A3, XBOOLE_0:def 4, GROUP_2:108;
    then g in (g*H) /\ U by XBOOLE_0:def 4;
    hence U = g*H by A2, Th65, XBOOLE_0:def 7;
    thus g in LH by A1, A3, XBOOLE_0:def 4;
  end;
  assume A1: U = g*H;
  assume A2: g in LH;
  g in U by A1, GROUP_2:108;
  then A3: g in U /\ LH by A2, XBOOLE_0:def 4;
  consider h being Element of G such that
  A4: U /\ LH = {h} by DefLTL;
  thus U /\ LH = {g} by A3, A4, TARSKI:def 1;
end;

theorem Th79:
  for U being Element of Right_Cosets H
  for g being Element of G
  holds U /\ RH = {g} iff (U = H*g & g in RH)
proof
  let U be Element of Right_Cosets H;
  let g be Element of G;
  thus U /\ RH = {g} implies U = H*g & g in RH
  proof
    assume A1: U /\ RH = {g};
    A2: H*g in Right_Cosets H by GROUP_2:def 16;
    A3: g in {g} by TARSKI:def 1;
    then g in H*g & g in U by A1, GROUP_2:108, XBOOLE_0:def 4;
    then g in (H*g) /\ U by XBOOLE_0:def 4;
    hence U = H*g by A2, Th66, XBOOLE_0:def 7;
    thus g in RH by A1, A3, XBOOLE_0:def 4;
  end;
  assume A1: U = H*g;
  assume A2: g in RH;
  g in U by A1, GROUP_2:108;
  then A3: g in U /\ RH by A2, XBOOLE_0:def 4;
  ex h being Element of G st U /\ RH = {h} by DefRTL;
  hence U /\ RH = {g} by A3, TARSKI:def 1;
end;

theorem Th80:
  for h being Element of G st h in H
  holds LH*h is Left_Transversal of H
proof
  let h be Element of G;
  assume A1: h in H;
  for U being Element of Left_Cosets H
  ex g being Element of G st U /\ (LH * h) = {g}
  proof
    let U be Element of Left_Cosets H;
    consider g0 being Element of G such that
    A2: U /\ LH = {g0} by DefLTL;
    
    take g = g0*h;

    A3: (U /\ LH) * h = {g0} * {h} by A2, GROUP_2:def 4
                     .= {g} by GROUP_2:18;

    (U /\ LH) * h = (U * h) /\ (LH * h) by Th74
                 .= U /\ (LH * h) by A1, Th72;
    hence U /\ (LH * h) = {g} by A3;
  end;

  hence LH*h is Left_Transversal of H by DefLTL;
end;

theorem Th81:
  for h being Element of G st h in H
  holds h*RH is Right_Transversal of H
proof
  let h be Element of G;
  assume A1: h in H;
  for U being Element of Right_Cosets H
  ex g being Element of G st U /\ (h * RH) = {g}
  proof
    let U be Element of Right_Cosets H;
    consider g0 being Element of G such that
    A2: U /\ RH = {g0} by DefRTL;
    take g = h * g0;

    A3: h * (U /\ RH) = {h} * {g0} by A2,GROUP_2:def 3
                     .= {g} by GROUP_2:18;

    h * (U /\ RH) = (h * U) /\ (h * RH) by Th75
                 .= U /\ (h * RH) by A1, Th73;
    hence U /\ (h * RH) = {g} by A3;
  end;
  hence h*RH is Right_Transversal of H by DefRTL;
end;

theorem :: Th82:
  ex LH being Left_Transversal of H
  st 1_G in LH
proof
  set LH0 = the Left_Transversal of H;
  carr H in Left_Cosets H by GROUP_2:135;
  then consider h being Element of G such that
  A1: (carr H) /\ LH0 = {h}
  by DefLTL;
  h in H
  proof
    h in {h} by TARSKI:def 1;
    then h in carr H by A1, XBOOLE_0:def 4;
    hence thesis by GROUP_2:def 9;
  end;
  then reconsider LH = LH0 * (h") as Left_Transversal of H by Th80, GROUP_2:51;
  take LH;
  h in {h} by TARSKI:def 1;
  then h in LH0 by A1, XBOOLE_0:def 4;
  then h * (h") in LH0 * (h") by GROUP_2:28;
  hence thesis by GROUP_1:def 5;
end;

theorem :: Th83:
  ex RH being Right_Transversal of H
  st 1_G in RH
proof
  set RH0 = the Right_Transversal of H;
  carr H in Right_Cosets H by GROUP_2:135;
  then consider h being Element of G such that
  A1: (carr H) /\ RH0 = {h} by DefRTL;
  h in H
  proof
    h in {h} by TARSKI:def 1;
    then h in carr H by A1, XBOOLE_0:def 4;
    hence thesis by GROUP_2:def 9;
  end;
  then reconsider RH = (h") * RH0 as Right_Transversal of H by Th81, GROUP_2:51;
  take RH;
  h in {h} by TARSKI:def 1;
  then h in RH0 by A1, XBOOLE_0:def 4;
  then (h") * h in (h") * RH0  by GROUP_2:27;
  hence thesis by GROUP_1:def 5;
end;

theorem :: Th84:
  for g being Element of G
  ex h being Element of G st g*H /\ LH = {h} & h in LH
proof
  let g be Element of G;
  g*H in Left_Cosets H by GROUP_2:def 15;
  then consider h being Element of G such that
  A1: g*H /\ LH = {h} by DefLTL;
  take h;
  thus g*H /\ LH = {h} by A1;
  h in {h} by TARSKI:def 1;
  hence thesis by A1, XBOOLE_0:def 4;
end;

theorem :: Th85:
  for g being Element of G
  ex h being Element of G st H*g /\ RH = {h} & h in RH
proof
  let g be Element of G;
  H*g in Right_Cosets H by GROUP_2:def 16;
  then consider h being Element of G such that
  A1: H*g /\ RH = {h} by DefRTL;
  take h;
  thus H*g /\ RH = {h} by A1;
  h in {h} by TARSKI:def 1;
  hence thesis by A1, XBOOLE_0:def 4;
end;

theorem Th86:
  for g being Element of G
  holds g*H /\ LH = {g} iff g in LH
proof
  let g be Element of G;
  g*H in Left_Cosets H by GROUP_2:def 15;
  hence thesis by Th78;
end;

theorem Th87:
  for g being Element of G
  holds H*g /\ RH = {g} iff g in RH
proof
  let g be Element of G;
  H*g in Right_Cosets H by GROUP_2:def 16;
  hence thesis by Th79;
end;

theorem Th88:
  for g1,g2 being Element of G
  holds g1 in g2*H iff g2 in g1*H
proof
  let g1,g2 be Element of G;
  hereby 
    assume A1: g1 in g2*H;
    g1 in g1*H by GROUP_2:108;
    then g1*H = g2*H by A1, XBOOLE_0:3, GROUP_2:115;
    hence g2 in g1*H by GROUP_2:108;
  end;
  assume A1: g2 in g1*H;
  g2 in g2*H by GROUP_2:108;
  then g1*H = g2*H by A1, XBOOLE_0:3, GROUP_2:115;
  hence g1 in g2*H by GROUP_2:108;
end;

theorem Th89:
  for g1,g2 being Element of G
  holds g1 in H*g2 iff g2 in H*g1
proof
  let g1,g2 be Element of G;
  hereby 
    assume A1: g1 in H*g2;
    g1 in H*g1 by GROUP_2:108;
    then H*g1 = H*g2 by A1, XBOOLE_0:3, GROUP_2:121;
    hence g2 in H*g1 by GROUP_2:108;
  end;
  assume A1: g2 in H*g1;
  g2 in H*g2 by GROUP_2:108;
  then H*g1 = H*g2 by A1, XBOOLE_0:3, GROUP_2:121;
  hence g1 in H*g2 by GROUP_2:108;
end;

theorem Th90:
  for A being Subset of G
  holds A is H-left-transversal-like
  iff (for g being Element of G
       ex a being Element of G
       st a in A & g in a*H
        & for b being Element of G st b in A
          holds b*H misses a*H or a = b)
proof
  let A be Subset of G;
  hereby 
    assume A1: A is H-left-transversal-like;
    let g be Element of G;
    g*H is Element of Left_Cosets H by GROUP_2:def 15;
    then consider a being Element of G such that
    A2: (g*H) /\ A = {a} by A1;
    take a;
    A3: a in {a} by TARSKI:def 1;
    thus A4: a in A by A2, A3, XBOOLE_0:def 4;
    a in g*H by A2, A3, XBOOLE_0:def 4;
    hence g in a*H by Th88;
    let b be Element of G;
    assume A5: b in A;
    assume A6: not b*H misses a*H;
    {b} = b*H /\ A by A1,A5,Th86
       .= a*H /\ A by A6, GROUP_2:115
       .= {a} by A1,A4,Th86;
    hence a = b by ZFMISC_1:3;
  end;
  assume A1: for g being Element of G
             ex a being Element of G
             st a in A & g in a*H
              & for b being Element of G st b in A
                holds b*H misses a*H or a = b;

  for U being Element of Left_Cosets H
  ex a being Element of G st U /\ A = {a}
  proof
    let U be Element of Left_Cosets H;
    consider u being Element of G such that
    A2: U = u*H by GROUP_2:def 15;
    consider a being Element of G such that
    A3: a in A & u in a*H and
    A4: for b being Element of G st b in A holds b*H misses a*H or a=b
    by A1;
    take a;
    for x being object holds x in {a} iff x in U & x in A
    proof
      let x be object;
      a in U by A2, A3, Th88;
      hence x in {a} implies x in U & x in A by A3, TARSKI:def 1;
      assume A5: x in U;
      assume A6: x in A;
      then reconsider x0=x as Element of G;
      per cases;
      suppose x = a;
        hence thesis by TARSKI:def 1;
      end;
      suppose A7: x <> a;
        a in a*H & a in U by A2, A3, Th88, GROUP_2:108;
        then A8: a*H = U by A2, GROUP_2:115, XBOOLE_0:3;
        x0 in x0*H & x0 in U by A5, GROUP_2:108;
        then x0 in x0*H /\ U by XBOOLE_0:def 4;
        hence thesis by A4,A6,A7,A8,XBOOLE_0:def 7;
      end;
    end;

    hence U /\ A = {a} by XBOOLE_0:def 4;
  end;

  hence A is H-left-transversal-like;
end;

theorem Th91:
  for A being Subset of G
  holds A is H-right-transversal-like
  iff (for g being Element of G
       ex a being Element of G
       st a in A & g in H*a
        & for b being Element of G st b in A
        holds H*b misses H*a or a = b)
proof
  let A be Subset of G;
  thus A is H-right-transversal-like 
  implies (for g being Element of G
           ex a being Element of G
           st a in A & g in H*a
            & for b being Element of G st b in A
              holds H*b misses H*a or a = b)
  proof
    assume A1: A is H-right-transversal-like;
    let g be Element of G;
    H*g is Element of Right_Cosets H by GROUP_2:def 16;
    then consider a being Element of G such that
    A2: (H*g) /\ A = {a} by A1;
    take a;
    A3: a in {a} by TARSKI:def 1;
    hence A4: a in A by A2, XBOOLE_0:def 4;
    a in H*g by A2, A3, XBOOLE_0:def 4;
    hence g in H*a by Th89;
    let b be Element of G;
    assume A5: b in A;
    assume A6: not H*b misses H*a;
    {b} = (H*b) /\ A by A1,A5,Th87
       .= (H*a) /\ A by A6, GROUP_2:121
       .= {a} by A1,A4,Th87;
    hence a = b by ZFMISC_1:3;
  end;
  assume A1: for g being Element of G
             ex a being Element of G
             st a in A & g in H*a
                & for b being Element of G st b in A
                  holds H*b misses H*a or a = b;
  for U being Element of Right_Cosets H
  ex a being Element of G st U /\ A = {a}
  proof
    let U be Element of Right_Cosets H;
    consider u being Element of G such that
    A2: U = H*u by GROUP_2:def 16;
    consider a being Element of G such that
    A3: a in A & u in H*a and
    A4: for b being Element of G st b in A holds H*b misses H*a or a=b
    by A1;
    take a;
    for x being object holds x in {a} iff x in U & x in A
    proof
      let x be object;
      a in U by A2, A3, Th89;
      hence x in {a} implies x in U & x in A by A3, TARSKI:def 1;
      assume A5: x in U;
      assume A6: x in A;
      then reconsider x0=x as Element of G;
      per cases;
      suppose x = a;
        hence thesis by TARSKI:def 1;
      end;
      suppose A7: x <> a;
        a in H*a & a in U by A2, A3, Th89, GROUP_2:108;
        then A8: H*a = U by A2, GROUP_2:121, XBOOLE_0:3;
        x0 in H*x0 & x0 in U by A5, GROUP_2:108;
        then x0 in H*x0 /\ U by XBOOLE_0:def 4;
        hence thesis by A4, A6, A7, A8, XBOOLE_0:def 7;
      end;
    end;

    hence U /\ A = {a} by XBOOLE_0:def 4;
  end;

  hence A is H-right-transversal-like;
end;

theorem Th92:
  for A being Subset of G
  holds A is H-left-transversal-like
  iff (for g being Element of G
       ex a,h being Element of G
       st a in A & h in H & g = a*h
        & for b,h1 being Element of G st b in A & h1 in H & g = b*h1
          holds b = a & h1 = h)
proof
  let A be Subset of G;
  (for g being Element of G
  ex a being Element of G
  st a in A & g in a*H 
  & for b being Element of G st b in A
    holds b*H misses a*H or a = b) iff
  (for g being Element of G
   ex a,h being Element of G
   st a in A & h in H & g = a*h
   & for b,h1 being Element of G st b in A & h1 in H & g = b*h1
     holds b = a & h1 = h)
  proof
    thus (for g being Element of G
    ex a being Element of G
    st a in A & g in a*H 
    & for b being Element of G st b in A
    holds b*H misses a*H or a = b) implies
    (for g being Element of G
    ex a,h being Element of G
    st a in A & h in H & g = a*h
    & for b,h1 being Element of G st b in A & h1 in H & g = b*h1
    holds b = a & h1 = h)
    proof
      assume A1: for g being Element of G
      ex a being Element of G
      st a in A & g in a*H
       & for b being Element of G st b in A holds b*H misses a*H or a = b;
      let g be Element of G;
      consider a being Element of G such that
      A2: a in A & g in a*H and
      A3: for b being Element of G st b in A holds b*H misses a*H or a = b
      by A1;
      consider h being Element of G such that
      A4: g = a*h & h in H by A2, GROUP_2:103;
      
      take a,h;
      thus a in A by A2;
      thus h in H by A4;
      thus g = a*h by A4;
      let b,h1 be Element of G;
      assume A5: b in A;
      assume A6: h1 in H;
      assume A7: g = b*h1;

      per cases by A3,A5;
      suppose A8: b*H misses a*H;
        g in b*H by A6, A7, GROUP_2:103;
        hence thesis by A2, A8, XBOOLE_0:3;
      end;
      suppose A9: a = b;
        hence a = b;
        thus thesis by A4, A7, A9, GROUP_1:6;
      end;
    end;
    assume A1: for g being Element of G
    ex a,h being Element of G
    st a in A & h in H & g = a*h
    & for b,h1 being Element of G st b in A & h1 in H & g = b*h1
      holds b = a & h1 = h;
    let g be Element of G;
    consider a,h being Element of G such that
    A2: a in A & h in H & g = a*h and
    A3: for b,h1 being Element of G st b in A & h1 in H & g = b*h1
        holds b = a & h1 = h
    by A1;
    take a;
    thus a in A by A2;
    thus g in a*H by A2,GROUP_2:103;
    let b be Element of G;
    assume A4: b in A;
    assume not b*H misses a*H;
    then a*H = b*H by GROUP_2:115;
    then ex h1 being Element of G st g = b * h1 & h1 in H by A2, GROUP_2:103;
    hence a = b by A3, A4;
  end;
  hence thesis by Th90;
end;

theorem Th93:
  for A being Subset of G
  holds A is H-right-transversal-like
  iff (for g being Element of G
       ex a,h being Element of G
       st a in A & h in H & g = h*a
        & for b,h1 being Element of G st b in A & h1 in H & g = h1*b
          holds b = a & h1 = h)
proof
  let A be Subset of G;
  (for g being Element of G
       ex a being Element of G
       st a in A & g in H*a
        & for b being Element of G st b in A
        holds H*b misses H*a or a = b)
  iff (for g being Element of G
       ex a,h being Element of G
       st a in A & h in H & g = h*a
        & for b,h1 being Element of G st b in A & h1 in H & g = h1*b
        holds b = a & h1 = h)
  proof
    thus (for g being Element of G
       ex a being Element of G
       st a in A & g in H*a
        & for b being Element of G st b in A
        holds H*b misses H*a or a = b)
      implies (for g being Element of G
           ex a,h being Element of G
           st a in A & h in H & g = h*a
            & for b,h1 being Element of G st b in A & h1 in H & g = h1*b
              holds b = a & h1 = h)
    proof
      assume A1: for g being Element of G
      ex a being Element of G
      st a in A & g in H*a
       & for b being Element of G st b in A holds H*b misses H*a or a = b;
      let g be Element of G;
      consider a being Element of G such that
      A2: a in A & g in H*a and
      A3: for b being Element of G st b in A holds H*b misses H*a or a = b
      by A1;
      consider h being Element of G such that
      A4: g = h*a & h in H by A2,GROUP_2:104;
      take a,h;
      thus a in A by A2;
      thus h in H by A4;
      thus g = h*a by A4;
      let b,h1 be Element of G;
      assume A5: b in A;
      assume A6: h1 in H;
      assume A7: g = h1*b;
      per cases by A3,A5;
      suppose A8: H*b misses H*a;
        g in H*b by A6, A7, GROUP_2:104;
        hence thesis by A2, A8, XBOOLE_0:3;
      end;
      suppose a = b;
        hence thesis by A4, A7, GROUP_1:6;
      end;
    end;
    assume A1: for g being Element of G ex a,h being Element of G st a in A & h in H & g = h*a & for b,h1 being Element of G st b in A & h1 in H & g = h1*b holds b = a & h1 = h;
    let g be Element of G;
    consider a,h being Element of G such that
    A2: a in A & h in H & g = h*a and
    A3: for b,h1 being Element of G st b in A & h1 in H & g = h1*b holds b = a & h1 = h
    by A1;
    take a;
    thus a in A by A2;
    thus g in H*a by A2, GROUP_2:104;
    let b be Element of G;
    assume A4: b in A;
    assume not H*b misses H*a;
    then H*b = H*a by GROUP_2:121;
    then consider h1 being Element of G such that
    A6: g = h1 * b & h1 in H by A2, GROUP_2:104;
    thus a = b by A3,A4,A6;
  end;
  hence thesis by Th91;
end;

theorem Th94:
  for K being Subgroup of H
  for A being Subset of H
  holds A is (K qua Subgroup of H)-left-transversal-like
  iff (for h being Element of G st h in H
       ex a,k being Element of G
       st a in A & k in K & h = a*k
        & for b,k1 being Element of G st b in A & k1 in K & h = b*k1
          holds b = a & k1 = k)
proof
  let K be Subgroup of H;
  let A be Subset of H;
  (for h being Element of G st h in H
   ex a,k being Element of G
   st a in A & k in K & h = a*k
    & for b,k1 being Element of G st b in A & k1 in K & h = b*k1
      holds b = a & k1 = k)
  iff (for h being Element of H
       ex a,k being Element of H
       st a in A & k in K & h = a*k
        & for b,k1 being Element of H st b in A & k1 in K & h = b*k1
        holds b = a & k1 = k)
  proof
    thus (for h being Element of G st h in H
    ex a,k being Element of G
    st a in A & k in K & h = a*k
     & for b,k1 being Element of G st b in A & k1 in K & h = b*k1
       holds b = a & k1 = k)
    implies (for h being Element of H
    ex a,k being Element of H
    st a in A & k in K & h = a*k
     & for b,k1 being Element of H st b in A & k1 in K & h = b*k1
       holds b = a & k1 = k)
    proof
      assume A1: for h being Element of G st h in H
      ex a,k being Element of G st a in A & k in K & h = a*k
      & for b,k1 being Element of G st b in A & k1 in K & h = b*k1
        holds b = a & k1 = k;
      let h be Element of H;
      reconsider gh=h as Element of G by GROUP_2:42;
      h in H; then
      consider ga,gk being Element of G such that
      A2: ga in A & gk in K & gh = ga*gk and
      A3: for b,k1 being Element of G st b in A & k1 in K & gh = b*k1
          holds b = ga & k1 = gk
      by A1;
      reconsider a = ga, k = gk as Element of H by A2, GROUP_2:42;
      take a,k;
      thus a in A by A2;
      thus k in K by A2;
      thus h = a*k by A2, GROUP_2:43;
      let b,k1 be Element of H;
      assume A4: b in A;
      assume A5: k1 in K;
      assume A6: h = b*k1;
      reconsider gb = b, gk1=k1 as Element of G by GROUP_2:42;
      h = gb * gk1 by A6,GROUP_2:43;
      hence thesis by A4,A5,A3;
    end;
    assume A1: for h being Element of H ex a,k being Element of H st a in A & k in K & h = a*k & for b,k1 being Element of H st b in A & k1 in K & h = b*k1 holds b = a & k1 = k;
    let h be Element of G;
    assume h in H;
    then reconsider hh=h as Element of H;
    consider ha,hk being Element of H such that
    A2: ha in A & hk in K & hh = ha*hk and
    A3: for b,k1 being Element of H st b in A & k1 in K & h = b*k1 holds b = ha & k1 = hk
    by A1;
    ha in G & hk in G by GROUP_2:41;
    then reconsider a=ha, k=hk as Element of G;
    take a,k;
    thus a in A by A2;
    thus k in K by A2;
    thus h = a*k by A2, GROUP_2:43;
    let b,k1 be Element of G;
    assume A4: b in A;
    assume A5: k1 in K;
    assume A6: h = b*k1;
    b in H & k1 in H by A4,A5,GROUP_2:41;
    then reconsider hb=b, hk1=k1 as Element of H;
    b*k1 = hb*hk1 by GROUP_2:43;
    hence b = a & k1 = k by A3, A4, A5, A6;
  end;
  
  hence thesis by Th92;
end;

theorem Th95:
  for K being Subgroup of H
  for A being Subset of H
  holds A is (K qua Subgroup of H)-right-transversal-like
  iff (for h being Element of G st h in H
       ex a,k being Element of G
       st a in A & k in K & h = k*a
        & for b,k1 being Element of G st b in A & k1 in K & h = k1*b
          holds b = a & k1 = k)
proof
  let K be Subgroup of H;
  let A be Subset of H;
  (for h being Element of G st h in H
  ex a,k being Element of G
  st a in A & k in K & h = k*a
   & for b,k1 being Element of G st b in A & k1 in K & h = k1*b
     holds b = a & k1 = k)
  iff (for h being Element of H
       ex a,k being Element of H
       st a in A & k in K & h = k*a
        & for b,k1 being Element of H st b in A & k1 in K & h = k1*b
        holds b = a & k1 = k)
  proof
    hereby 
      assume A1: for h being Element of G st h in H ex a,k being Element of G st a in A & k in K & h = k*a & for b,k1 being Element of G st b in A & k1 in K & h = k1*b holds b = a & k1 = k;
      let h be Element of H;
      reconsider gh=h as Element of G by GROUP_2:42;
      h in H; then
      consider ga,gk being Element of G such that
      A2: ga in A & gk in K & gh = gk*ga and
      A3: for b,k1 being Element of G st b in A & k1 in K & gh = k1*b
          holds b = ga & k1 = gk
      by A1;
      reconsider a = ga, k = gk as Element of H by A2, GROUP_2:42;
      take a,k;
      thus a in A by A2;
      thus k in K by A2;
      thus h = k*a by A2, GROUP_2:43;
      let b,k1 be Element of H;
      assume A4: b in A;
      assume A5: k1 in K;
      assume A6: h = k1*b;
      reconsider gb = b, gk1=k1 as Element of G by GROUP_2:42;
      k1 * b = gk1 * gb by GROUP_2:43;
      hence b = a & k1 = k by A3,A4,A5,A6;
    end;
    assume A1: for h being Element of H ex a,k being Element of H st a in A & k in K & h = k*a & for b,k1 being Element of H st b in A & k1 in K & h = k1*b holds b = a & k1 = k;
    let h be Element of G;
    assume h in H;
    then reconsider hh=h as Element of H;
    consider ha,hk being Element of H such that
    A2: ha in A & hk in K & hh = hk*ha and
    A3: for b,k1 being Element of H st b in A & k1 in K & h = k1*b holds b = ha & k1 = hk
    by A1;
    ha in G & hk in G by GROUP_2:41;
    then reconsider a=ha, k=hk as Element of G;
    take a,k;
    thus a in A by A2;
    thus k in K by A2;
    thus h = k*a by A2, GROUP_2:43;
    let b,k1 be Element of G;
    assume A4: b in A;
    assume A5: k1 in K;
    assume A6: h = k1*b;
    b in H & k1 in H by A4,A5,GROUP_2:41;
    then reconsider hb=b, hk1=k1 as Element of H;
    k1*b = hk1*hb by GROUP_2:43;
    hence b = a & k1 = k by A3, A4, A5, A6;
  end;
  
  hence thesis by Th93;
end;

theorem LTLProductProperty:
  for K being Subgroup of H
  for LK being Left_Transversal of (K qua Subgroup of H)
  for LH being Left_Transversal of H
  holds LH * LK is (K qua Subgroup of G)-left-transversal-like
proof
  let K be Subgroup of H;
  let LK be Left_Transversal of (K qua Subgroup of H);
  let LH be Left_Transversal of H;

  for g being Element of G
  ex a,k being Element of G
  st a in LH * LK & k in K & g = a*k
  & for b,k1 being Element of G st b in LH*LK & k1 in K & g = b*k1
  holds b = a & k1 = k
  proof
    let g be Element of G;
    consider ah,h being Element of G such that
    A1: ah in LH & h in H & g = ah*h and
    A2: for b1,h1 being Element of G st b1 in LH & h1 in H & g = b1*h1 holds b1 = ah & h1 = h
    by Th92;
    consider ak,k being Element of G such that
    A3: ak in LK & k in K & h = ak*k and
    A4: for b1,k1 being Element of G st b1 in LK & k1 in K & h = b1*k1 holds b1 = ak & k1 = k
    by A1,Th94;
    take a = ah * ak;
    take k;
    thus a in LH * LK by A1,A3;
    thus k in K by A3;
    thus g = a * k by A1,A3,GROUP_1:def 3;
    let b,k1 be Element of G;
    assume A5: b in LH*LK;
    assume A6: k1 in K;
    assume A7: g = b*k1;
    consider bh,bk being Element of G such that
    A8: b = bh * bk & bh in LH & bk in LK by A5;

    A9: bk*k1 in H
    proof
      B1: bk in H by A8;
      k1 in H by A6, GROUP_2:40;
      hence thesis by B1, GROUP_2:50;
    end;

    A10: g = bh * (bk * k1) by A7, A8, GROUP_1:def 3;
    then A11: bk in LK & k1 in K & bk * k1 = h by A2,A6,A8,A9;
    then bk = ak & k1 = k by A4;
    then g = ah * (ak * k) by A2,A8,A9,A10
          .= (ah * ak) * k by GROUP_1:def 3
          .= a * k1 by A4,A11;
    hence b = a by A7, GROUP_1:6;
    thus k1 = k by A4,A11;
  end;

  hence thesis by Th92;
end;

theorem RTLProductProperty:
  for K being Subgroup of H
  for RK being Right_Transversal of K
  for RH being Right_Transversal of H
  holds RK * RH is (K qua Subgroup of G)-right-transversal-like
proof
  let K be Subgroup of H;
  let RK be Right_Transversal of K;
  let RH be Right_Transversal of H;

  for g being Element of G
  ex a,k being Element of G
  st a in RK * RH & k in K & g = k*a
  & for b,k1 being Element of G st b in RK*RH & k1 in K & g = k1*b
  holds b = a & k1 = k
  proof
    let g be Element of G;
    consider ah,h being Element of G such that
    A1: ah in RH & h in H & g = h*ah and
    A2: for b,h1 being Element of G st b in RH & h1 in H & g = h1*b
        holds b = ah & h1 = h
    by Th93;
    consider ak,k being Element of G such that
    A3: ak in RK & k in K & h = k*ak and
    A4: for b1,k1 being Element of G st b1 in RK & k1 in K & h = k1*b1 holds b1 = ak & k1 = k
    by A1,Th95;
    take a = ak*ah,k;
    thus a in RK * RH by A1,A3;
    thus k in K by A3;
    thus g = k*a by A1, A3, GROUP_1:def 3;
    let b,k1 be Element of G;
    assume A5: b in RK*RH;
    assume A6: k1 in K;
    assume A7: g = k1*b;
    consider bk,bh being Element of G such that
    A8: b = bk*bh & bk in RK & bh in RH by A5;

    A9: k1*bk in H
    proof
      B1: bk in H by A8;
      k1 in H by A6, GROUP_2:40;
      hence thesis by B1, GROUP_2:50;
    end;

    A10: g = (k1 * bk) * bh by A7, A8, GROUP_1:def 3;
    then A11: bh = ah & (k1 * bk) = h by A2,A8,A9;
    then bk = ak & k1 = k by A4,A6,A8;
    then g = (k * ak) * ah by A2,A8,A9,A10
          .= k * (ak * ah) by GROUP_1:def 3
          .= k1 * a by A4,A6,A8,A11;
    hence b = a by A7, GROUP_1:6;
    thus k1 = k by A4,A6,A8,A11;
  end;

  hence RK * RH is (K qua Subgroup of G)-right-transversal-like by Th93;
end;

registration
  let G, H;
  let K be Subgroup of H;
  let LH be Left_Transversal of H;
  let LK be Left_Transversal of K;
  cluster LH * LK -> K-left-transversal-like;
  correctness by LTLProductProperty;
end;

registration
  let G, H;
  let K be Subgroup of H;
  let RH be Right_Transversal of H;
  let RK be Right_Transversal of K;
  cluster RK * RH -> K-right-transversal-like;
  correctness by RTLProductProperty;
end;

theorem Th98:
  for g being Element of G
  for A being Subset of G
  holds (g * A)" = (A ")*(g ")
proof
  let g be Element of G;
  let A be Subset of G;
  for x being object
  holds x in (g * A)" iff x in (A ")*(g ")
  proof
    let x be object;
    hereby 
      assume x in (g * A)";
      then consider gx being Element of G such that
      A1: x = gx" & gx in g * A by GROUP_2:2;
      consider a being Element of G such that
      A2: gx = g * a & a in A by A1, GROUP_2:27;
      A3: gx " = (a ")*(g ") by A2,GROUP_1:17;
      a" in A" by A2, GROUP_2:2;
      hence x in (A ")*(g ") by A1,A3, GROUP_2:28;
    end;
    assume x in (A ")*(g ");
    then consider ai being Element of G such that
    A1: x = ai*(g ") & ai in A" by GROUP_2:28;
    consider a being Element of G such that
    A2: a" = ai & a in A by A1, GROUP_2:2;
    A3: g * a in g*A by A2,GROUP_2:27;
    x = (g * a)" by A1,A2,GROUP_1:17;
    hence x in (g * A)" by A3, GROUP_2:2;
  end;
  hence (g * A)" = (A ")*(g ") by TARSKI:2;
end;

theorem Th99:
  for g being Element of G
  for A being Subset of G
  holds (A * g)" = (g ")*(A ")
proof
  let g be Element of G;
  let A be Subset of G;
  for x being object
  holds x in (A * g)" iff x in (g ")*(A ")
  proof
    let x be object;
    hereby 
      assume x in (A * g)";
      then consider ag being Element of G such that
      A1: x = ag" & ag in A * g by GROUP_2:2;
      consider a being Element of G such that
      A2: ag = a*g & a in A by A1, GROUP_2:28;
      A3: a" in A" by A2,GROUP_2:2;
      ag" = (g ")*(a ") by A2,GROUP_1:17;
      hence x in (g ")*(A ") by A1,A3,GROUP_2:27;
    end;
    assume x in (g ")*(A ");
    then consider ai being Element of G such that
    A1: x = (g ")*ai & ai in A " by GROUP_2:27;
    consider a being Element of G such that
    A2: ai = a" & a in A by A1, GROUP_2:2;
    A3: a * g in A*g by A2,GROUP_2:28;
    x = (a * g)" by A1,A2,GROUP_1:17;
    hence x in (A * g)" by A3, GROUP_2:2;
  end;

  hence (A * g)" = (g ")*(A ") by TARSKI:2;
end;

theorem Th100:
  for A,B being Subset of G
  holds (A /\ B)" = (A ") /\ (B ")
proof
  let A,B be Subset of G;
  for x being object
  holds x in (A /\ B)" iff x in (A ") /\ (B ")
  proof
    let x be object;
    hereby 
      assume x in (A /\ B)";
      then consider ab being Element of G such that
      A1: x = ab" & ab in (A /\ B) by GROUP_2:2;
      ab in A & ab in B by A1, XBOOLE_0:def 4;
      then ab" in A" & ab" in B" by GROUP_2:2;
      hence x in (A ") /\ (B ") by A1, XBOOLE_0:def 4;
    end;
    assume x in (A ") /\ (B ");
    then A1: x in A" & x in B" by XBOOLE_0:def 4;
    then consider a being Element of G such that
    A2: x = a" & a in A by GROUP_2:2;
    a in B
    proof
      ex b being Element of G st a" = b" & b in B by A1,A2,GROUP_2:2;
      hence thesis by GROUP_1:9;
    end;
    then a in A /\ B by A2, XBOOLE_0:def 4;
    hence x in (A /\ B)" by A2, GROUP_2:2;
  end;
  hence (A /\ B)" = (A ") /\ (B ") by TARSKI:2;
end;

theorem Th101:
  RH " is Left_Transversal of H
proof
  for U being Element of Left_Cosets H
  ex g being Element of G st U /\ (RH " ) = {g}
  proof
    let U be Element of Left_Cosets H;
    consider g0 being Element of G such that
    A1: U = g0*H by GROUP_2:def 15;
    U = g0 * (carr H) by A1, GROUP_2:def 13;
    then
    U" = ((carr H)") * (g0 ") by Th98
      .= (carr H) * (g0 ") by GROUP_2:77
      .= H * (g0 ") by GROUP_2:def 14;
    then U" in Right_Cosets H by GROUP_2:def 16;
    then consider g1 being Element of G such that
    A2: (U") /\ RH = {g1} by DefRTL;
    A3: ((U ") /\ RH)" = ((U ") ") /\ (RH ") by Th100
                      .= U /\ (RH ");
    take g = g1 ";
    thus U /\ (RH " ) = {g} by A2, A3, GROUP_2:3;
  end;
  hence RH " is Left_Transversal of H by DefLTL;
end;

theorem Th102:
  LH " is Right_Transversal of H
proof
  for U being Element of Right_Cosets H
  ex g being Element of G st U /\ (LH ") = {g}
  proof
    let U be Element of Right_Cosets H;
    consider g0 being Element of G such that
    A1: U = H*g0 by GROUP_2:def 16;
    U = (carr H) * g0 by A1, GROUP_2:def 14; then
    U" = (g0 ") * ((carr H) ") by Th99
      .= (g0 ") * (carr H) by GROUP_2:77
      .= (g0 ") * H by GROUP_2:def 13;
    then U" in Left_Cosets H by GROUP_2:def 15;
    then consider g1 being Element of G such that
    A2: (U") /\ LH = {g1} by DefLTL;
    A3: ((U ") /\ LH)" = ((U ") ") /\ (LH ") by Th100
                      .= U /\ (LH ");
    take g = g1 ";
    thus U /\ (LH ") = {g} by A2, A3, GROUP_2:3;
  end;
  hence LH " is Right_Transversal of H by DefRTL;
end;

registration
  let G,H;
  let RH be Right_Transversal of H;
  cluster RH " -> H-left-transversal-like;
  correctness by Th101;
end;

registration
  let G,H;
  let LH be Left_Transversal of H;
  cluster LH " -> H-right-transversal-like;
  correctness by Th102;
end;

:: Kurzweil and Stellmacher, proposition 1.9, but for both left and right
:: transversals of H
theorem Th102:
  for A being Subset of G
  holds A is H-left-transversal-like iff A*H = the carrier of G &
  (for s,t being Element of G st s <> t & s in A & t in A
  holds not (s") * t in H)
proof
  let A be Subset of G;
  hereby 
    assume A1: A is H-left-transversal-like;
    for g being Element of G
    holds g in A*H
    proof
      let g be Element of G;
      ex a,h being Element of G
      st a in A & h in H & g = a*h
         & for b,h1 being Element of G st b in A & h1 in H & g = b*h1
           holds b = a & h1 = h
      by A1, Th92;
      hence thesis by GROUP_2:94;
    end;
    then the carrier of G c= A*H;
    hence A*H = the carrier of G by XBOOLE_0:def 10;
    let s,t be Element of G;
    assume A2: s <> t;
    assume A3: s in A;
    assume A4: t in A;
    (s") * t in H implies contradiction
    proof
      assume A5: (s") * t in H;
      {s} = s*H /\ A by A1, A3, Th86
         .= t*H /\ A by A5, GROUP_2:114
         .= {t} by A1, A4, Th86;
      hence contradiction by A2, ZFMISC_1:3;
    end;
    hence not (s") * t in H;
  end;
  assume A1: A*H = the carrier of G;
  assume A2: for s,t being Element of G st s <> t & s in A & t in A holds not (s") * t in H;
  for g being Element of G
  ex a being Element of G
  st a in A & g in a*H
  & for b being Element of G st b in A
  holds b*H misses a*H or a = b
  proof
    let g be Element of G;
    consider a,h being Element of G such that
    A3: g = a*h & a in A & h in H by A1, GROUP_2:94;
    take a;
    thus a in A by A3;
    thus g in a*H by A3,GROUP_2:103;
    let b be Element of G;
    assume A4: b in A;
    assume A5: not b*H misses a*H;
    a*H in Left_Cosets H & b*H in Left_Cosets H by GROUP_2:def 15;
    then (a") * b in H by A5, Th65, GROUP_2:114;
    hence a = b by A2,A3,A4;
  end;
  hence A is H-left-transversal-like by Th90;
end;

theorem Th103:
  for A being Subset of G
  holds A is H-right-transversal-like iff H*A = the carrier of G &
  (for s,t being Element of G st s <> t & s in A & t in A
  holds not s * (t") in H)
proof
  let A be Subset of G;
  hereby 
    assume A1: A is H-right-transversal-like;
    for g being Element of G
    holds g in H*A
    proof
      let g be Element of G;
      ex a,h being Element of G
      st a in A & h in H & g = h*a
       & for b,h1 being Element of G st b in A & h1 in H & g = h1*b
         holds b = a & h1 = h
      by A1, Th93;
      hence g in H*A by GROUP_2:95;
    end;

    then the carrier of G c= H*A;
    hence H*A = the carrier of G by XBOOLE_0:def 10;
    let s,t be Element of G;
    assume A2: s <> t;
    assume A3: s in A;
    assume A4: t in A;
    s * (t") in H implies contradiction
    proof
      assume A5: s * (t") in H;
      {s} = H*s /\ A by A1, A3, Th87
         .= H*t /\ A by A5, GROUP_2:120
         .= {t} by A1, A4, Th87;
      hence contradiction by A2, ZFMISC_1:3;
    end;
    hence not s * (t") in H;
  end;
  assume A1: H*A = the carrier of G;
  assume A2: for s,t being Element of G st s <> t & s in A & t in A
  holds not s * (t") in H;
  for g being Element of G
  ex a being Element of G
  st a in A & g in H*a
  & for b being Element of G st b in A
  holds H*b misses H*a or a = b
  proof
    let g be Element of G;
    consider h,a being Element of G such that
    A3: g = h*a & h in H & a in A by A1, GROUP_2:95;
    take a;
    thus a in A by A3;
    thus g in H*a by A3,GROUP_2:104;
    let b be Element of G;
    assume A4: b in A;
    assume A5: not H*b misses H*a;
    H*a in Right_Cosets H & H*b in Right_Cosets H by GROUP_2:def 16;
    then b * (a") in H by A5, Th66, GROUP_2:120;
    hence a = b by A2,A3,A4;
  end;

  hence A is H-right-transversal-like by Th91;
end;

theorem Th104:
  LH*H = the carrier of G
proof
  for g being Element of G
  holds g in LH*H
  proof
    let g be Element of G;
    ex a,h being Element of G st a in LH & h in H & g = a*h
    & for b,h1 being Element of G st b in LH & h1 in H & g = b*h1
      holds b = a & h1 = h by Th92;
    hence g in LH*H by GROUP_2:94;
  end;

  then the carrier of G c= LH*H;
  hence thesis by XBOOLE_0:def 10;
end;

theorem Th105:
  H * RH = the carrier of G
proof
  for g being Element of G holds g in H*RH
  proof
    let g be Element of G;
    ex a,h being Element of G st a in RH & h in H & g = h*a
    & for b,h1 being Element of G st b in RH & h1 in H & g = h1*b
      holds b = a & h1 = h by Th93;
    hence g in H*RH by GROUP_2:95;
  end;
  then the carrier of G c= H * RH;
  hence thesis by XBOOLE_0:def 10;
end;

theorem Th106:
  for K being Subgroup of G
  st carr K is H-left-transversal-like
  holds H /\ K = (1).G
proof
  let K be Subgroup of G;
  assume A1: carr K is H-left-transversal-like;
  carr H in Left_Cosets H by GROUP_2:135;
  then consider h being Element of G such that
  A2: (carr H) /\ (carr K) = {h} by A1;
  A3: carr (H /\ K) = {h} by A2, GROUP_2:81;
  then A4: H /\ K is finite by GROUP_2:def 9;
  card (H /\ K) = card {h} by A3, GROUP_2:def 9
               .= 1 by CARD_1:30;
  hence H /\ K = (1).G by A4, GROUP_2:70;
end;

theorem Th107:
  for K being Subgroup of G
  st carr K is H-right-transversal-like
  holds H /\ K = (1).G
proof
  let K be Subgroup of G;
  assume A1: carr K is H-right-transversal-like;
  carr H in Right_Cosets H by GROUP_2:135;
  then consider h being Element of G such that
  A2: (carr H) /\ (carr K) = {h} by A1;
  A3: carr (H /\ K) = {h} by A2, GROUP_2:81;
  then A4: H /\ K is finite by GROUP_2:def 9;
  card (H /\ K) = card {h} by A3, GROUP_2:def 9
               .= 1 by CARD_1:30;
  hence H /\ K = (1).G by A4, GROUP_2:70;
end;

:: Now, we can establish the fact that complements to normal subgroups
:: describe transversals to that normal subgroup.
theorem Th108:
  for H1,H2 being Subgroup of G
  for a,b,c,d being Element of G
  st a in H1 & c in H1 & b in H2 & d in H2
  holds a*b = c*d implies (c")*a = d*(b") & (c")*a in H1 /\ H2 &
  d*(b") in H1 /\ H2
proof
  let H1,H2 be Subgroup of G;
  let a,b,c,d be Element of G;
  assume A1: a in H1;
  assume A2: c in H1;
  assume A3: b in H2;
  assume A4: d in H2;
  assume A5: a*b = c*d;
  then a = (c*d)*(b ") by GROUP_1:14
        .= c*(d * (b")) by GROUP_1:def 3;
  hence A6: (c ")*a = d * (b ") by GROUP_1:13;

  c " in H1 by A2, GROUP_2:51;
  then A7: (c ")*a in H1 by A1, GROUP_2:50;
  b" in H2 by A3, GROUP_2:51;
  then d*(b ") in H2 by A4, GROUP_2:50;
  hence thesis by A6, A7, GROUP_2:82;
end;

theorem Th109:
  for g,h being Element of G
  st g*(h ") in (1).G or (g ")*h in (1).G
  holds g = h
proof
  let g,h be Element of G;
  assume g*(h ") in (1).G or (g ")*h in (1).G;
  then g*(h ") in {1_G} or (g ")*h in {1_G} by GROUP_2:def 7;
  then g*(h ") = 1_G or (g ")*h = 1_G by TARSKI:def 1;
  then (g = (h ") " & (h ") = g ") or (g " = h " & h = (g ") ") by GROUP_1:12;
  hence thesis;
end;

theorem Th110:
  for N being Subgroup of G
  st H,N are_complements_in G
  for g being Element of G
  ex h,n being Element of G
  st h in H & n in N & g = h*n
   & (for h1, n1 being Element of G st h1 in H & n1 in N & g = h1*n1
      holds h1 = h & n1 = n)
proof
  let N be Subgroup of G;
  assume A1: H,N are_complements_in G;
  let g be Element of G;
  H*N = the carrier of G by A1; then
  consider h,n being Element of G such that
  A2: g = h*n & h in H & n in N by GROUP_5:4;
  take h,n;
  thus h in H by A2;
  thus n in N by A2;
  thus g = h*n by A2;
  let h1, n1 be Element of G;
  assume A3: h1 in H;
  assume A4: n1 in N;
  assume A5: g = h1*n1;
  then h*n = h1*n1 by A2;
  then n1*(n") in H /\ N & (h1")*h in H /\ N & (h1")*h = n1 * (n ")
  by A2,A3,A4,Th108;
  then A6: n1*(n") in (1).G & (h1")*h in (1).G by A1;
  hence thesis by Th109;
end;

theorem Th111:
  for N being Subgroup of G
  st H,N are_complements_in G
  for g being Element of G
  ex h,n being Element of G
  st h in H & n in N & g = n*h
   & for h1, n1 being Element of G st h1 in H & n1 in N & g = n1*h1
     holds h1 = h & n1 = n
proof
  let N be Subgroup of G;
  assume A1: H,N are_complements_in G;
  let g be Element of G;
  N*H = the carrier of G by A1,Th59; then
  consider n,h being Element of G such that
  A2: g = n*h & n in N & h in H by GROUP_5:4;
  take h,n;
  thus h in H & n in N & g = n*h by A2;
  let h1, n1 be Element of G;
  assume A3: h1 in H;
  assume A4: n1 in N;
  assume A5: g = n1*h1;
  then n*h = n1*h1 by A2;
  then (n1")*n = h1 * (h ") & (n1")*n in N /\ H & h*(h1") in N /\ H
  by A2,A3,A4,Th108;
  then (n1")*n in (1).G & h*(h1") in (1).G by A1;
  hence thesis by Th109;
end;

:: Kurzweil and Stellmacher, proposition 1.10, as two propositions
theorem
  for N being Subgroup of G
  st H,N are_complements_in G
  holds carr H is N-left-transversal-like
proof
  let N be Subgroup of G;
  assume A1: H,N are_complements_in G;
  for g being Element of G
  ex h,n being Element of G
  st h in carr H & n in N & g = h*n
   & (for h1, n1 being Element of G st h1 in carr H & n1 in N & g = h1*n1
      holds h1 = h & n1 = n)
  proof
    let g be Element of G;
    consider h,n being Element of G such that
    A2: h in H & n in N & g = h*n and
    A3: for h1, n1 being Element of G st h1 in H & n1 in N & g = h1*n1
        holds h1 = h & n1 = n
    by A1, Th110;
    take h,n;
    thus h in carr H by A2, GROUP_2:def 9;
    thus n in N by A2;
    thus g = h*n by A2;
    let h1, n1 be Element of G;
    assume A4: h1 in carr H;
    assume A5: n1 in N;
    assume A6: g = h1*n1;
    h1 in H by A4, GROUP_2:def 9;
    hence thesis by A3,A5,A6;
  end;
  hence carr H is N-left-transversal-like by Th92;
end;

theorem
  for N being Subgroup of G
  st H,N are_complements_in G
  holds carr H is N-right-transversal-like
proof
  let N be Subgroup of G;
  assume A1: H,N are_complements_in G;
  for g being Element of G
  ex h,n being Element of G
  st h in carr H & n in N & g = n*h
   & for h1, n1 being Element of G st h1 in carr H & n1 in N & g = n1*h1
     holds h1 = h & n1 = n
  proof
    let g be Element of G;
    consider h,n being Element of G such that
    A2: h in H & n in N & g = n*h and
    A3: for h1, n1 being Element of G st h1 in H & n1 in N & g = n1*h1
        holds h1 = h & n1 = n
    by A1, Th111;
    take h,n;
    thus h in carr H by A2, GROUP_2:def 9;
    thus n in N by A2;
    thus g = n*h by A2;
    let h1, n1 be Element of G;
    assume A4: h1 in carr H;
    assume A5: n1 in N;
    assume A6: g = n1*h1;
    h1 in H by A4, GROUP_2:def 9;
    hence thesis by A3,A5,A6;
  end;
  hence carr H is N-right-transversal-like by Th93;
end;
