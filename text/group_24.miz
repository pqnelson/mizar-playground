:: Semidirect products of groups

environ

 vocabularies TARSKI, BINOP_1, XBOOLE_0, FUNCT_1, RELAT_1, ALGSTR_0,
   FUNCOP_1, SUBSET_1, FUNCT_2, STRUCT_0, ZFMISC_1, CARD_3, MONOID_0,
   GROUP_1, GROUP_2, FINSEQ_1, GROUP_6, PARTFUN1, MSSUBFAM, RLVECT_2,
   PRE_TOPC, WELLORD1, AUTGROUP, MOD_4,
   :: ORDINAL1, :: for "natural"
   RLSUB_1, :: for "(Omega)."
   CARD_1, :: for "card", "0"
   GROUP_3, :: for "are_conjugated"
   GROUP_4, :: for "gr"
   EQREL_1, :: for "\/"
   NATTRA_1, :: for `*`
   NEWTON, :: for "|^"
   REALSET1, :: for "||"
   NAT_1, NUMBERS, :: for NAT
   GROUP_10, :: for Sylow subgroup stuff
   FINSET_1, :: for "finite"
   UPROOTS, :: for "canFS"
   ARYTM_3, :: for "<"
   INT_1, :: for "mod"
   GROUP_24;
 notations TARSKI, XBOOLE_0, ENUMSET1, SUBSET_1, RELAT_1, FUNCT_1, ZFMISC_1,
   ORDINAL1, :: for Nat
   NUMBERS, NAT_1, NAT_D, CARD_1,
   XCMPLX_0, :: for "*"
   XXREAL_0,
   FINSET_1, :: for finite
   NEWTON, :: for GROUP_10:def 17
   REALSET1, :: for "||"
   CARD_3, FINSEQ_1,
   RELSET_1, PARTFUN1, FUNCT_2, FUNCT_4,
   INT_2, :: for Prime
   PBOOLE, FUNCOP_1, STRUCT_0, ALGSTR_0, MONOID_0, GROUP_1, GROUP_2,
   LATTICES, GROUP_3, :: CARD_3,
   GROUP_4,
   GROUP_5, GROUP_6, PRALG_1, BINOP_1, :: FINSEQ_1,
   AUTGROUP, GROUP_7,
   GROUP_10, GROUP_22;
 constructors STRUCT_0, ALGSTR_0, ZFMISC_1, BINOP_1,
   ARYTM_2, XXREAL_0, :: for "<"
   ARYTM_0, XCMPLX_0, :: for "*"
   RELAT_1, :: for "dom"
   REALSET1,
   GROUP_1, LATTICES, WELLORD2, CARD_1, 
   FINSUB_1, :: for GROUP_2:81
   ORDINAL1, NAT_1, NAT_D, :: for GROUP_10:def 18
   FINSET_1, :: for finite
   FINSEQ_1,
   INT_2, :: for Prime
   NEWTON, :: for GROUP_10:def 17
   GROUP_2, GROUP_3, GROUP_4, GROUP_5, GROUP_6, MONOID_0, PRALG_1, PRALG_2,
   RELSET_1, FUNCT_2, FUNCT_4, AUTGROUP, GROUP_7, GROUP_10, GROUP_22;
 registrations XBOOLE_0, SUBSET_1, RELAT_1, FUNCT_1, FINSET_1,
   XCMPLX_0, :: for "m * n" to be a Nat
   XREAL_0, :: needed for |^ to work properly
   NAT_1, :: for "m * n" to be a Nat
   GROUP_8, :: for "-> non empty for Left_Cosets"
   FINSEQ_1, REALSET1, STRUCT_0, GROUP_1, GROUP_2, GROUP_3, MONOID_0,
   LATTICES, ORDINAL1, CARD_1, CARD_3, NEWTON, FUNCOP_1, RELSET_1, FUNCT_2,
   FUNCT_4, GROUP_6, GROUP_7, GR_CY_1, GR_CY_2, GROUPP_1;
 requirements NUMERALS, REAL, BOOLE, SUBSET, ARITHM;
 definitions STRUCT_0, GROUP_1, TARSKI, ALGSTR_0;
 equalities STRUCT_0, ALGSTR_0, PARTFUN1, AUTGROUP;
 expansions STRUCT_0, GROUP_1, TARSKI;
 theorems :: STRUCT_0, ALGSTR_0, SUBSET_1,
   ZFMISC_1, SUBSET_1, RELSET_1, XTUPLE_0,
   PRALG_1, FUNCT_1, CARD_3, FUNCT_2, BINOP_1, TARSKI, GROUP_1, GROUP_2,
   GROUP_3, GROUP_4, GROUP_5, GROUP_6, FINSEQ_1, NEWTON, XBOOLE_0,
   XBOOLE_1, MONOID_0, PARTFUN1, AUTGROUP, GROUP_7, GROUP_10;
 schemes BINOP_1, FUNCT_2;

begin  :: The product of the families of the groups


registration
  let G be strict Group;
  cluster -> multiplicative for Element of Aut G;
  correctness by AUTGROUP:def 1;
end;

LmDoubleCheckAutElementsAreMorphisms:
  for G being strict Group
  for f being Element of Aut G
  holds f is Homomorphism of G,G;

reserve G for strict Group,
  A for Group,
  phi for Homomorphism of A, AutGroup G;

registration
  let A;
  cluster id A -> multiplicative;
  correctness by GROUP_6:38;

  cluster id A -> bijective;
  coherence
  proof
    set f = id A;
    rng (id A) = the carrier of A;
    then (id A is one-to-one & id A is onto) by FUNCT_2:def 3;
    hence id A is bijective;
  end;
end;

LmCheckIdIsAutomorphism:
  id A is Automorphism of A;

registration
  let G;
  cluster AutGroup G -> non empty strict constituted-Functions;
  coherence by MONOID_0:80;
end;

definition
  let A,G,phi;
  let a be Element of A;
  func phi /. a -> Function of G,G equals :: GROUP_24:def 1
  phi.a;
  correctness
  proof
    phi.a is Element of Aut G;
    hence thesis;
  end;
end;

definition
  let A,G;
  let H be Subgroup of A;
  let phi be Homomorphism of H,AutGroup G;
  let a be Element of A such that
A1: a in H;
  func phi /. a -> Function of G,G equals :: GROUP_24:def 2
  :Def2a: phi.a;
  correctness
  proof
    reconsider h=a as Element of H by A1;
    phi.h = phi.a & phi.h is Element of Aut G;
    hence thesis;
  end;
end;

registration
  let G be strict Group, A be Group;
  let phi be Homomorphism of A, AutGroup G;
  let a be Element of A;
  cluster phi/.a -> multiplicative;
  coherence;
end;


:: :: Since there is no way to tell Mizar that the Element of AutGroup G
:: :: is automatically an Element of Aut G which is also automatically
:: :: a Function of G,G; we just short-circuit the whole machinery.
:: definition
::   let G be strict Group, A be Group;
::   let phi be Homomorphism of A, AutGroup G;
::   let a be Element of A;
::   func phi/.a -> Homomorphism of G,G equals phi.a;
::   coherence;
:: end;



registration
  let G, A be Group;
  cluster product (Carrier <*A,G*>) -> non empty;
  correctness;
end;

registration
  let G, A;
  cluster product (Carrier <*A,G*>) -> non empty;
  correctness;
end;


theorem th4a: :: GROUP_24:1
  for G1,G2 being Group
  for x being Element of product (<*G1,G2*>)
  holds x.1 in G1 & x.2 in G2 & dom x = {1,2}
proof
  let G1,G2 be Group;
  let x be Element of product (<*G1,G2*>);
  A1: dom (Carrier <*G1,G2*>) = {1,2} by PARTFUN1:def 2;
  x in the carrier of product (<*G1,G2*>);
  then x in product (Carrier <*G1,G2*>) by GROUP_7:def 2;
  then consider f being Function such that
  A2: x = f
      & dom f = dom (Carrier <*G1,G2*>)
      & (for y being object st y in dom (Carrier <*G1,G2*>)
         holds f.y in (Carrier <*G1,G2*>).y)
  by CARD_3:def 5;

  thus x.1 in G1
  proof
    B1: <* G1, G2 *>.1 = G1 by FINSEQ_1:44;
    B2: 1 in dom (Carrier <*G1,G2*>) by A1, TARSKI:def 2;
    then ex R being 1-sorted st
    (R = <*G1,G2*>.1 & (Carrier <*G1,G2*>).1 = the carrier of R)
    by PRALG_1:def 15;
    hence thesis by A2, B1, B2;
  end;

  thus x.2 in G2
  proof
    B1: <* G1, G2 *>.2 = G2 by FINSEQ_1:44;
    B2: 2 in dom (Carrier <*G1,G2*>) by A1, TARSKI:def 2;
    then ex R being 1-sorted st
    (R = <*G1,G2*>.2 & (Carrier <*G1,G2*>).2 = the carrier of R)
    by PRALG_1:def 15;
    hence thesis by A2, B1, B2;
  end;
  thus dom x = {1,2} by A2,PARTFUN1:def 2; :: A1,
end;

:: Lm0:
::   for x being Element of product (Carrier <*A,G*>)
::   holds x.1 in A & x.2 in G
:: proof
::   let x be Element of product (Carrier <*A,G*>);
::   A1: dom (Carrier <*A,G*>) = {1,2} by PARTFUN1:def 2;

::   A2: (Carrier <*A,G*>).1 = the carrier of A
::   proof
::     1 in {1,2} by TARSKI:def 2; then
::     1 in dom (Carrier <*A,G*>) by A1; then
::     ex R being 1-sorted st (R = <*A,G*>.1 & (Carrier <*A,G*>).1 = the
::     carrier of R) by PRALG_1:def 15;
::     hence thesis by FINSEQ_1:44;
::   end;
::   A3: (Carrier <*A,G*>).2 = the carrier of G
::   proof
::     2 in {1,2} by TARSKI:def 2; then
::     2 in dom (Carrier <*A,G*>) by A1; then
::     ex R being 1-sorted st (R = <*A,G*>.2 & (Carrier <*A,G*>).2 = the
::     carrier of R) by PRALG_1:def 15;
::     hence thesis by FINSEQ_1:44;
::   end;
::   x in product (Carrier <*A,G*>);
::   then dom x = dom (Carrier <*A,G*>) & (for i being object st i in dom
::   (Carrier <*A,G*>) holds x.i in (Carrier <*A,G*>).i) by CARD_3:9;
::   then A4: dom x = {1,2} & (for i being object st i in {1,2}
::   holds x.i in (Carrier <*A,G*>).i) by A1;

::   1 in {1,2} by TARSKI:def 2;
::   then x.1 in (Carrier <*A,G*>).1 by A4;
::   then x.1 in the carrier of A by A2;
::   hence x.1 in A;

::   2 in {1,2} by TARSKI:def 2;
::   then x.2 in (Carrier <*A,G*>).2 by A4;
::   then x.2 in the carrier of G by A3;
::   hence x.2 in G;
:: end;


Lm0:
  for G1,G2 being Group
  for x being Element of product (Carrier <*G1,G2*>)
  holds x.1 in G1 & x.2 in G2 & dom x = {1,2}
proof
  let G1,G2 be Group;
  let x be Element of product (Carrier <*G1,G2*>);
  x is Element of the carrier of product <*G1,G2*> by GROUP_7:def 2;
  hence thesis by th4a;
end;


Lm1:
  for a being Element of A, g being Element of G
  holds <*a,g*> in product (Carrier <*A,G*>)
proof
  let a be Element of A;
  let g be Element of G;
  <*a,g*> in the carrier of (product <*A,G*>);
  hence <*a,g*> in product (Carrier <*A,G*>) by GROUP_7:def 2;
end;

:: Using notation from Aschbacher's "Finite Group Theory" Section 10,
:: but Aschbacher uses "backwards" notation for function
:: composition...so we fix it.
definition
  let A, G, phi;
  func semidirect_product (A, G, phi) -> non empty strict multMagma means
  :Def1:  :: GROUP_24:def 3
  the carrier of it = product (Carrier <* A, G *>) &
  (for f, g being Element of product (Carrier <* A, G *>)
   ex h being Function, a1 being Element of A, g2 being Element of G
   st h = (the multF of it).(f, g) &
      a1 = f.1 & g2 = g.2 &
      h.1 = (the multF of A).((f.1), (g.1)) &
      h.2 = (the multF of G).(f.2, (phi/.a1)/. g2));
  :: the carrier of it = the carrier of product <* A, G *> &
  :: for a1,a2 being Element of A
  :: for g1,g2 being Element of G
  :: holds (the multF of it).(<*a1, g1*>, <*a2, g2*>)
  ::       = <*a1 * a2, ((phi/.a2)/.g1)*g2 *>;
  existence
  proof
    set U = product (Carrier <* A, G *>);
    defpred P[Element of U, Element of U, set] means
    ex h being Function, a1 being Element of A, g2 being Element of G
    st h = $3 & a1 = $1.1 & g2 = $2.2 &
       h.1 = (the multF of A).(($1.1), ($2.1)) &
       h.2 = (the multF of G).($1.2, (phi/.a1)/. g2);

    A1: for x, y being Element of U ex z being Element of U st P[x,y,z]
    proof
      let x, y be Element of U;
      
      ex z being Element of U st P[x, y, z]
      proof
        x.1 in A by Lm0;
        then reconsider a1 = x.1 as Element of A;
        y.2 in G by Lm0; then
        reconsider g2 = y.2 as Element of G;

        set h = <* (the multF of A).(x.1, y.1),
                   (the multF of G).(x.2,(phi/.a1)/.g2)*>;
        C1: h = <* h.1, h.2 *>
        proof
          len h = 2 by FINSEQ_1:44;
          hence thesis by FINSEQ_1:44;
        end;
        h in product (Carrier <*A,G*>)
        proof
          x.1 in A & y.1 in A by Lm0; then
          (the multF of A).(x.1, y.1) in A by BINOP_1:17;
          then D1: h.1 in A by FINSEQ_1:44;
          (phi/.a1)/.g2 in G & x.2 in G by Lm0; then
          (the multF of G).(x.2, (phi/.a1)/.g2) in G by BINOP_1:17;
          then h.2 in G by FINSEQ_1:44;
          hence thesis by C1,D1,Lm1;
        end;
        then reconsider z = h as Element of product (Carrier <*A,G*>);
        take z;
        h = z & a1 = x.1 & g2 = y.2 &
        h.1 = (the multF of A).(x.1, y.1) &
        h.2 = (the multF of G).(x.2, (phi/.a1)/.g2) by FINSEQ_1:44;
        hence P[x, y, z];
      end;
      hence thesis;
    end;

    consider B being Function of [:U, U:],U such that
A2: for a, b being Element of U holds P[a,b,B.(a,b)] from BINOP_1:sch 3(A1);

    set M = multMagma(# (product (Carrier <*A,G*>)), B #);
    reconsider M as non empty strict multMagma;
    take M;
    thus thesis by A2;
  end;
  uniqueness
  proof
    let M1,M2 being non empty strict multMagma;
    assume that
    A1: (the carrier of M1 = product (Carrier <* A, G *>) &
         (for f, g being Element of product (Carrier <* A, G *>)
          ex h being Function, a1 being Element of A, g2 being Element of G
          st h = (the multF of M1).(f, g) &
             a1 = f.1 & g2 = g.2 &
             h.1 = (the multF of A).((f.1), (g.1)) &
             h.2 =  (the multF of G).(f.2, (phi/.a1)/. g2))) and
    A2: (the carrier of M2 = product (Carrier <* A, G *>) &
         (for f, g being Element of product (Carrier <* A, G *>)
          ex h being Function, a1 being Element of A, g2 being Element of G
          st h = (the multF of M2).(f, g) &
             a1 = f.1 & g2 = g.2 &
             h.1 = (the multF of A).((f.1), (g.1)) &
             h.2 =  (the multF of G).(f.2, (phi/.a1)/. g2)));

    for x,y being set st x in the carrier of M1 & y in the carrier of M1
    holds (the multF of M1).(x, y) = (the multF of M2).(x, y) 
    proof
      let x,y be set;
      assume B1: x in the carrier of M1;
      assume B2: y in the carrier of M1;
      reconsider f = x, g = y as Element of product (Carrier <*A,G*>)
      by A1,B1,B2;
      consider h1 being Function, a11 being Element of A,
               g12 being Element of G
      such that
      B3: h1 = (the multF of M1).(f, g) and
      B4: a11 = f.1 & g12 = g.2 and
      B5: h1.1 = (the multF of A).((f.1), (g.1)) and
      B6: h1.2 = (the multF of G).(f.2, (phi/.a11)/. g12) by A1;
      consider h2 being Function,
               a21 being Element of A,
               g22 being Element of G
      such that
      B7: h2 = (the multF of M2).(f, g) and
      B8: a21 = f.1 & g22 = g.2 and
      B9: h2.1 = (the multF of A).((f.1), (g.1)) and
      B10: h2.2 = (the multF of G).(f.2, (phi/.a21)/. g22) by A2;
      B11: dom h1 = {1,2} & dom h2 = {1,2}
      proof
        h1 in product (Carrier <*A,G*>) by A1,B3,BINOP_1:17;
        then ex f1 being Function st h1 = f1 & dom f1 = dom (Carrier <*A,G*>)
        & (for y1 being object st y1 in dom (Carrier <*A,G*>)
        holds f1.y1 in (Carrier <*A,G*>).y1)
        by CARD_3:def 5;
        hence dom h1 = {1,2} by PARTFUN1:def 2;
        h2 in product (Carrier <*A,G*>) by A2,B7,BINOP_1:17;
        then ex f1 being Function st h2 = f1 & dom f1 = dom (Carrier <*A,G*>)
        & (for y1 being object st y1 in dom (Carrier <*A,G*>)
        holds f1.y1 in (Carrier <*A,G*>).y1)
        by CARD_3:def 5;
        hence dom h2 = {1,2} by PARTFUN1:def 2;
      end;
      for i being object st i in dom h1 holds h1.i = h2.i
      proof
        let i be object;
        assume i in dom h1;
        then per cases by B11, TARSKI:def 2;
        suppose i = 1;
          hence thesis by B5,B9;
        end;
        suppose i = 2;
          hence thesis by B4,B6,B8,B10;
        end;
      end;
      then h1 = h2 by B11, FUNCT_1:def 11;
      hence (the multF of M1).(x, y) = (the multF of M2).(x, y) by B3,B7;
    end;
    hence thesis by A1,A2,BINOP_1:1;
    end;
end;


registration
  let A, G, phi;
  cluster semidirect_product (A, G, phi) -> non empty strict
  constituted-Functions;
  coherence
  proof
    the carrier of semidirect_product (A, G, phi) = product (Carrier <*A,G*>)
    by Def1;
    hence thesis by MONOID_0:80;
  end;
end;

registration
  let A, G, phi;
  cluster -> FinSequence-like for Element of (semidirect_product (A, G, phi));
  coherence
  proof
    for b1 being Element of semidirect_product(A, G, phi)
    holds b1 is FinSequence-like
    proof
      let b1 be Element of semidirect_product(A, G, phi);
      b1 is Element of product (Carrier <*A,G*>) by Def1;
      hence b1 is FinSequence-like;
    end;
    hence thesis;
  end;
end;

registration
  let A, G, phi;
  cluster -> FinSequence-like for Element of the carrier of
  (semidirect_product (A, G, phi));
  coherence;
end;

theorem th2: :: GROUP_24:2
  the carrier of semidirect_product (A, G, phi)
  = the carrier of product <*A,G*>
proof
  thus the carrier of semidirect_product (A,G,phi)
   = product (Carrier <*A,G*>) by Def1
  .= the carrier of product <*A,G*> by GROUP_7:def 2;
end;

theorem th3: :: GROUP_24:3
  for a being Element of A
  for g being Element of G
  holds <* a, g *> is Element of semidirect_product (A, G, phi)
  by th2;

theorem th4: :: GROUP_24:4
  for x being Element of semidirect_product (A, G, phi)
  holds x.1 in A & x.2 in G & dom x = {1,2}
proof
  let x be Element of semidirect_product (A, G, phi);
  x in the carrier of semidirect_product (A, G, phi);
  then x in the carrier of product <*A,G*> by th2;
  hence thesis by th4a;
end;

theorem th5: :: GROUP_24:5
  for x being Element of semidirect_product (A, G, phi)
  ex a being Element of A, g being Element of G
  st x = <* a, g *>
proof
  let x be Element of semidirect_product (A, G, phi);

  x.1 in A & x.2 in G by th4;
  then consider a being Element of A, g being Element of G such that
  A1: a = x.1 & g = x.2;

  take a, g;
  
  dom x = {1,2} by th4;
  then len x = 2 by FINSEQ_1:2,def 3;
  hence x = <* a, g *> by A1, FINSEQ_1:44;
end;

theorem Lm1: :: GROUP_24:6
  for x1, x2, y1, y2 being object
  holds <*x1,y1*> = <*x2,y2*> iff (x1 = x2 & y1 = y2)
proof
  let x1, x2, y1, y2 be object;
  thus <*x1,y1*> = <*x2,y2*> implies (x1 = x2 & y1 = y2)
  proof
    assume A1: <*x1,y1*> = <*x2,y2*>;
    thus x1 = <*x1,y1*>.1 by FINSEQ_1:44
           .= x2 by A1, FINSEQ_1:44;
    thus y1 = <*x1,y1*>.2 by FINSEQ_1:44
           .= y2 by A1, FINSEQ_1:44;
  end;
  thus (x1 = x2 & y1 = y2) implies <*x1,y1*> = <*x2,y2*>;
end;

theorem th6: :: GROUP_24:7
  for x,y being Element of semidirect_product (A, G, phi)
  for a1,a2 being Element of A
  for g1,g2 being Element of G
  st x = <*a1,g1*> & y = <*a2,g2*>
  holds x * y = <* a1 * a2, g1 * ((phi /. a1)/.g2) *>
proof
  let x,y be Element of semidirect_product (A, G, phi);
  let a1,a2 be Element of A;
  let g1,g2 be Element of G;
  assume A1: x = <*a1,g1*>;
  assume A2: y = <*a2,g2*>;
  
  x in product (Carrier <* A, G*>) & y in product (Carrier <*A,G*>)
  proof
    x in semidirect_product(A,G,phi);
    hence x in product (Carrier <*A,G*>) by Def1;
    y in semidirect_product(A,G,phi);
    hence y in product (Carrier <*A,G*>) by Def1;
  end;

  then consider h being Function, aa1 being Element of A, gg2 being Element
  of G such that
  A3: h = x * y & aa1 = x.1 & gg2 = y.2 and
  A4: h.1 = (the multF of A).(x.1, y.1) and
  A5: h.2 = (the multF of G).(x.2, (phi/.aa1)/.gg2) by Def1;

  A6: a1 = aa1 & g2 = gg2 by A1,A2,A3,FINSEQ_1:44;

  h.1 = (the multF of A).(x.1, a2) by A2,A4,FINSEQ_1:44
     .= (the multF of A).(a1, a2) by A1,FINSEQ_1:44;
  then
  A7: (x * y).1 = a1 * a2 by A3;
  h.2 = (the multF of G).(g1, (phi/.a1)/.g2) by A1,A5,A6,FINSEQ_1:44;
  then A8: (x * y).2 = g1 * ((phi /. a1)/.g2) by A3;

  dom (x * y) = {1,2} by th4;
  then len (x * y) = 2 by FINSEQ_1:2,def 3;
  hence x * y = <* a1 * a2, g1 * ((phi /. a1)/.g2) *> by A7,A8,FINSEQ_1:44;
end;

theorem th7: :: GROUP_24:8
  for g being Element of G
  holds (phi /. 1_A) /. g = g
proof
  let g be Element of G;
  (phi /. 1_A) = 1_(AutGroup G) by GROUP_6:31
              .= id the carrier of G by AUTGROUP:9;
  hence (phi /. 1_A) /. g = g;
end;

theorem th8: :: GROUP_24:9
  for x,e being Element of semidirect_product (A, G, phi)
  st e = <* 1_A, 1_G*>
  holds x * e = x & e * x = x
proof
  let x,e be Element of semidirect_product (A, G, phi);
  assume A1: e = <* 1_A, 1_G*>;
  consider a being Element of A, g being Element of G such that
  A2: x = <* a, g *>
  by th5;

  A3: ((phi /. (1_A)) /. g) * 1_G = g * 1_G by th7
                                 .= g by GROUP_1:def 4;
  A4: ((phi /. a) /. (1_G)) = 1_G by GROUP_6:31;
  thus x * e = <* a * 1_A, g * ((phi /. a) /. 1_G) *> by A1,A2, th6
            .= <* a, g * ((phi /. a) /. 1_G) *> by GROUP_1:def 4
::            .= <* a, g *> by A4, GROUP_1:def 4
            .= x by A2, A4, GROUP_1:def 4;
  thus e * x = <* 1_A * a, 1_G * ((phi /. (1_A)) /. g) *> by A1,A2,th6
            .= <* a, 1_G * ((phi /. (1_A)) /. g) *> by GROUP_1:def 4
            .= <* a, (1_G) * (g) *> by A3, GROUP_1:def 4
            .= x by A2,GROUP_1:def 4;
end;

theorem th9: :: GROUP_24:10
  for a1,a2 being Element of A
  for g being Element of G
  holds (phi.a1) . ((phi /. a2) . g) = (phi /. (a1 * a2)) . g
proof
  let a1,a2 be Element of A;
  let g be Element of G;
  phi . (a1 * a2) = (phi.a1) * (phi.a2) by GROUP_6:def 6;
  then (phi . (a1 * a2)) . g = ((phi/.a1) * (phi/.a2)).g by AUTGROUP:8
                            .= (phi.a1) . ((phi . a2) . g) by FUNCT_2:15;
  hence (phi.a1) . ((phi /. a2) . g) = (phi /. (a1 * a2)) . g;
end;

theorem th10: :: GROUP_24:11
  for a being Element of A
  for g being Element of G
  holds (phi/.(a")) /. ((phi /. a) /. g) = g
  & (phi/. a) /. ((phi /. (a")) /. g) = g
proof
  let a be Element of A;
  let g be Element of G;
  thus (phi /. (a")) /. ((phi /. a) /. g) = (phi /. (a" * a)) /. g by th9
                                         .= (phi /. 1_A) /. g by GROUP_1:def 5
                                         .= g by th7;
  thus (phi/. a) /. ((phi /. (a")) /. g) = (phi /. (a * a")) /. g by th9
                                        .= (phi /. 1_A) /. g by GROUP_1:def 5
                                        .= g by th7;
end;

theorem th11: :: GROUP_24:12
  for x,y,e being Element of semidirect_product (A, G, phi)
  for a being Element of A
  for g being Element of G
  st x = <* a, g *> & e = <* 1_A, 1_G *> & y = <* a", (phi/.(a"))/.(g") *>
  holds x * y = e & y * x = e
proof
  let x,y,e be Element of semidirect_product (A, G, phi);
  let a be Element of A;
  let g be Element of G;
  assume A1: x = <* a, g *>;
  assume A2: e = <* 1_A, 1_G *>;
  assume A3: y = <* a", (phi/.(a"))/.(g") *>;
  A4: ((phi /. (a")) /. (g")) * ((phi/.(a"))/.(g))
   = (phi /. (a")) /. (g" * g) by GROUP_6:def 6
  .= (phi /. (a")) /. 1_G by GROUP_1:def 5
  .= 1_G by GROUP_6:31;
  A5: ((phi /. a) /. ((phi/.(a"))/.(g"))) = g" by th10;

  thus x * y = <* a * (a"), g * ((phi/.a)/.((phi/.(a"))/.(g"))) *> by A1,A3,th6
            .= <* a * a", 1_G *> by A5,GROUP_1:def 5
            .= e by A2,GROUP_1:def 5;
  thus y * x = <* (a") * a, (phi/.(a")).(g") * ((phi/. a")/.g)*> by A1,A3,th6
            .= e by A2,A4,GROUP_1:def 5;
end;

registration
  let G be strict Group;
  let A be Group;
  let phi be Homomorphism of A, AutGroup G;
  cluster semidirect_product (A, G, phi) -> associative;
  correctness
  proof
    thus for x, y, z being Element of semidirect_product (A, G, phi)
    holds (x * y) * z = x * (y * z)
    proof
      let x, y, z be Element of semidirect_product (A, G, phi);
      consider a1 being Element of A, g1 being Element of G such that
      A1: x = <* a1, g1 *>
      by th5;
      consider a2 being Element of A, g2 being Element of G such that
      A2: y = <* a2, g2 *>
      by th5;
      consider a3 being Element of A, g3 being Element of G such that
      A3: z = <* a3, g3 *>
      by th5;
      
      x * y = <* a1 * a2, g1 * ((phi /. a1)/.g2) *> by A1,A2,th6;
      then A5: (x * y) * z = <* (a1 * a2) * a3, (g1 * ((phi /.a1)/.g2)) *
      ((phi /. (a1 * a2)) /. g3)*> by A3,th6;
      
      y * z = <* a2 * a3, g2 * ((phi /. a2)/.g3) *> by A2,A3,th6;
      then A6: x * (y * z) = <* a1 * (a2 * a3),
      g1 * ((phi /. a1)/.(g2 * ((phi /. a2)/.g3))) *>
      by A1,th6;
      
      A7: (a1 * a2) * a3 = a1 * (a2 * a3) by GROUP_1:def 3;
      
      ((phi /. a1) . ((phi /. a2).g3)) = (phi /. (a1 * a2)) . g3 by th9;
      then 
      ((phi /. a1).(g2 * ((phi /. a2)/.g3)))
      = ((phi /. a1).g2) * ((phi /. (a1 * a2)) . g3) by GROUP_6:def 6;
      then (((phi /.a1)/.g2) * ((phi /. (a1 * a2)) /. g3))
      = ((phi /. a1)/.(g2 * ((phi /. a2)/.g3)));
      then 
      (g1 * ((phi /.a1)/.g2)) * ((phi /. (a1 * a2)) /. g3)
      = g1 * ((phi /. a1)/.(g2 * ((phi /. a2)/.g3)))
      by GROUP_1:def 3;
      hence thesis by A5,A6,A7;
    end;
  end;
  
  cluster semidirect_product (A, G, phi) -> Group-like;
  correctness
  proof
    thus ex e being Element of semidirect_product (A, G, phi)
    st (for h being Element of semidirect_product (A, G, phi)
        holds h * e = h & e * h = h
        & ex g being Element of semidirect_product (A, G, phi)
        st h * g = e & g * h = e)
    proof
      <* 1_A, 1_G *> is Element of semidirect_product (A, G, phi) by th3;
      then consider e being Element of semidirect_product (A, G, phi) such that
      A1: e = <* 1_A, 1_G *>;

      take e;
      A2: for h being Element of semidirect_product (A, G, phi)
      holds h * e = h & e * h = h by A1, th8;
      
      for h being Element of semidirect_product (A, G, phi)
      ex g being Element of semidirect_product (A, G, phi)
      st h * g = e & g * h = e
      proof
        let h be Element of semidirect_product (A, G, phi);
        consider a being Element of A, g being Element of G such that
        B1: h = <* a, g *> by th5;
        set g2 = (phi/.(a"))/.(g");
        reconsider g2 as Element of G;
        reconsider g = <* a", g2 *> as Element of semidirect_product (A,G,phi)
        by th3;
        take g;
        thus h * g = e & g * h = e by A1,B1,th11;
      end;
      hence thesis by A2;
    end;
  end;
end;

LmDoubleCheckSemidirectProductIsGroup:
  semidirect_product (A, G, phi) is Group;

theorem th14:
  1_(semidirect_product (A, G, phi)) = <* 1_A, 1_G *>
proof
  reconsider e = <* 1_A, 1_G *> as Element of semidirect_product (A, G, phi)
  by th3;
  for x being Element of semidirect_product (A, G, phi)
  holds x * e = x & e * x = x by th8;
  hence thesis by GROUP_1:def 4;
end;

theorem th15:
  for a being Element of A
  for g being Element of G
  for x being Element of semidirect_product (A, G, phi)
  st x = <* a, g *>
  holds x" = <* a", (phi/.(a"))/.(g") *>
proof
  let a be Element of A;
  let g be Element of G;
  let x be Element of semidirect_product (A, G, phi);
  assume A1: x = <* a, g *>;
  reconsider e = <* 1_A, 1_G *> as Element of semidirect_product (A, G, phi)
  by th3;
  reconsider y = <* a", (phi/.(a"))/.(g") *> as Element of
  semidirect_product (A, G, phi)
  by th3;
  x * y = e & y * x = e by A1,th11;
  then x * y = 1_(semidirect_product(A, G, phi))
  & y * x = 1_(semidirect_product(A, G, phi)) by th14; then
  y = x" by GROUP_1:def 5;
  hence x" = <* a", (phi/.(a"))/.(g") *>;
end;

theorem th16:
  for g1,g2 being Element of G
  for x,y,z being Element of semidirect_product (A, G, phi)
  st x = <*1_A, g1*> & y = <*1_A, g2*> & z = <*1_A, (g1*g2)*>
  holds x * y = z
proof
  let g1,g2 be Element of G;
  let x,y,z be Element of semidirect_product (A, G, phi);
  assume A1: x = <*1_A, g1*>;
  assume A2: y = <*1_A, g2*>;
  assume A3: z = <*1_A, (g1*g2)*>;
  thus x * y = <* (1_A) * (1_A), g1 * ((phi /. 1_A) /. g2) *> by A1,A2,th6
            .= <* 1_A, g1 * ((phi /. 1_A) /. g2) *> by GROUP_1:def 4
::            .= <* 1_A, g1 * g2 *> by th7
            .= z by A3, th7;
end;


theorem th17:
  for a1,a2 being Element of A
  for x,y,z being Element of semidirect_product (A, G, phi)
  st x = <*a1, 1_G*> & y = <*a2, 1_G*> & z = <*(a1*a2),1_G*>
  holds x * y = z
proof
  let a1,a2 be Element of A;
  let x,y,z be Element of semidirect_product (A, G, phi);
  assume A1: x = <*a1, 1_G*>;
  assume A2: y = <*a2, 1_G*>;
  assume A3: z = <*(a1*a2),1_G*>;
  thus x * y = <* a1 * a2, 1_G * ((phi /. a1)/.(1_G)) *> by A1,A2,th6
            .= <* a1 * a2, 1_G * (1_G) *> by GROUP_6:31
::            .= <* a1 * a2, 1_G *> by GROUP_1:def 4
            .= z by A3,GROUP_1:def 4;
end;

:: Compare to AUTGROUP:1, we do not need strictness
theorem th23:
  for H being Subgroup of A
  holds H is normal
  iff (for a,h being Element of A st h in H holds h |^ a in H)
proof
  let H be Subgroup of A;
  thus H is normal implies (for a,h being Element of A st h in H
  holds h |^ a in H)
  proof
    assume B1: H is normal;
    let a,h be Element of A;
    assume B2: h in H;
    H |^ a = the multMagma of H by B1, GROUP_3:def 13; then
    B3: the carrier of (H |^ a) = the carrier of H;
    h |^ a in H |^ a by B2, GROUP_3:58;
    then h |^ a in the carrier of H |^ a;
    then h |^ a in the carrier of H by B3;
    then h |^ a in H;
    hence h |^ a in H;
  end;

  thus (for a,h being Element of A st h in H holds h |^ a in H) implies
  H is normal
  proof
    assume B1: for a,h being Element of A st h in H holds h |^ a in H;
    now
      let a be Element of A;
      thus H * a c= a * H
      proof
        set U = carr H;
        let q be object;
        assume q in H * a;
        then consider h being Element of A such that
        B2: q = h * a and
        B3: h in H by GROUP_2:104;
        h |^ a in H by B1, B3;
        then h |^ a in carr H by GROUP_2:def 9;
        then a * (h |^ a) in a * (carr H) by GROUP_2:27;
        then a * (((a") * h) * a) in a * (carr H) by GROUP_3:def 2;
        then a * ((a") * (h * a)) in a * (carr H) by GROUP_1:def 3;
        then (a * (a")) * (h * a) in a * (carr H) by GROUP_1:def 3;
        then ((a * (a")) * h) * a in a * (carr H) by GROUP_1:def 3;
        then ((1_A) * h) * a in a*(carr H) by GROUP_1:def 5;
        then h * a in a*(carr H) by GROUP_1:def 4;
        hence q in a * H by B2, GROUP_2:def 13;
      end;
    end;
    hence H is normal by GROUP_3:119;
  end;
end;

definition
  let A, G, phi;
  func incl1 (A, G, phi) -> Function of A, semidirect_product (A,G,phi) means
  :Def10:
  for a being Element of A holds it.a = <* a, 1_G *>;

  existence
  proof
    defpred P[Element of A, Element of semidirect_product(A,G,phi)] means
    $2 = <* $1, 1_G *>;

    A1: for x being Element of A
    ex y being Element of semidirect_product(A,G,phi)
    st P[x, y]
    proof
      let x be Element of A;
      reconsider y=<*x,1_G*> as Element of semidirect_product(A,G,phi)
      by th3;
      take y;
      thus P[x, y];
    end;
    consider f being Function of A, semidirect_product(A, G, phi) such that
    A2: for x being Element of A holds P[x, f.x]
    from FUNCT_2:sch 3(A1);
    take f;
    thus thesis by A2;
  end;
  uniqueness
  proof
    for f1,f2 being Function of A, semidirect_product(A, G, phi)
    st (for a being Element of A holds f1.a = <*a,1_G*>)
     & (for a being Element of A holds f2.a = <*a,1_G*>)
    holds f1 = f2
    proof
      let f1,f2 be Function of A, semidirect_product(A, G, phi);
      assume A1: for a being Element of A holds f1.a = <*a,1_G*>;
      assume A2: for a being Element of A holds f2.a = <*a,1_G*>;
      for a being Element of A holds f1.a = f2.a
      proof
        let a be Element of A;
        thus f1.a = <*a,1_G*> by A1
                 .= f2.a by A2;
      end;

      hence f1 = f2 by FUNCT_2:def 8;
    end;
    hence thesis;
  end;
end;

:: Aschbacher, Finite Group Theory (10.1.2)
registration
  let A, G, phi;
  cluster incl1 (A, G, phi) -> multiplicative;
  coherence
  proof
    for a1,a2 being Element of A
    holds (incl1 (A, G, phi)).(a1 * a2)
    = ((incl1 (A, G, phi)).a1)*((incl1 (A, G, phi)).a2)
    proof
      let a1,a2 be Element of A;
      ((incl1 (A, G, phi)).a1) = <*a1,1_G*>
      & ((incl1 (A, G, phi)).a2) = <*a2,1_G*>
      & (incl1 (A, G, phi)).(a1 * a2) = <*a1*a2, 1_G*> by Def10;
      hence thesis by th17;
    end;
    hence thesis by GROUP_6:def 6;
  end;

  cluster incl1 (A, G, phi) -> one-to-one;
  coherence
  proof
    for a1,a2 being Element of A
    st (incl1 (A, G, phi)).a1 = (incl1 (A, G, phi)).a2
    holds a1 = a2
    proof
      let a1,a2 be Element of A;
      assume A1: (incl1 (A, G, phi)).a1 = (incl1 (A, G, phi)).a2;
      <*a1, 1_G*> = (incl1 (A, G, phi)).a1 by Def10
                 .= <*a2, 1_G*> by A1,Def10;
      hence a1 = a2 by Lm1;
    end;
    hence incl1 (A, G, phi) is one-to-one by GROUP_6:1;
  end;
end;

definition
  let A, G, phi;
  func incl2 (A, G, phi) -> Function of G, semidirect_product (A,G,phi) means
  :Def11:
  for g being Element of G holds it.g = <* 1_A, g *>;
  existence
  proof
    defpred P[Element of G, Element of semidirect_product(A,G,phi)] means
    $2 = <* 1_A, $1 *>;

    A1: for x being Element of G
    ex y being Element of semidirect_product(A,G,phi)
    st P[x, y]
    proof
      let x be Element of G;
      reconsider y=<*1_A,x*> as Element of semidirect_product(A,G,phi)
      by th3;
      take y;
      thus P[x, y];
    end;
    consider f being Function of G, semidirect_product(A, G, phi) such that
    A2: for x being Element of G holds P[x, f.x]
    from FUNCT_2:sch 3(A1);
    take f;
    thus thesis by A2;
  end;
  uniqueness
  proof
    for f1,f2 being Function of G, semidirect_product(A, G, phi)
    st (for g being Element of G holds f1.g = <*1_A,g*>)
     & (for g being Element of G holds f2.g = <*1_A,g*>)
    holds f1 = f2
    proof
      let f1,f2 be Function of G, semidirect_product(A, G, phi);
      assume A1: for g being Element of G holds f1.g = <*1_A,g*>;
      assume A2: for g being Element of G holds f2.g = <*1_A,g*>;
      for g being Element of G holds f1.g = f2.g
      proof
        let g be Element of G;
        thus f1.g = <*1_A,g*> by A1
                 .= f2.g by A2;
      end;

      hence f1 = f2 by FUNCT_2:def 8;
    end;
    hence thesis;
  end;
end;

:: Aschbacher, Finite Group Theory (10.1.2)
registration
  let A, G, phi;
  cluster incl2 (A, G, phi) -> multiplicative;
  coherence
  proof
    for g1,g2 being Element of G
    holds (incl2 (A, G, phi)).(g1 * g2)
    = ((incl2 (A, G, phi)).g1)*((incl2 (A, G, phi)).g2)
    proof
      let g1,g2 be Element of G;
      ((incl2 (A, G, phi)).g1) = <*1_A,g1*>
      & ((incl2 (A, G, phi)).g2) = <*1_A,g2*>
      & (incl2 (A, G, phi)).(g1 * g2) = <*1_A,g1*g2*> by Def11;
      hence thesis by th16;
    end;
    hence thesis by GROUP_6:def 6;
  end;
  cluster incl2 (A, G, phi) -> one-to-one;
  coherence
  proof
    for g1,g2 being Element of G
    st (incl2 (A, G, phi)).g1 = (incl2 (A, G, phi)).g2
    holds g1 = g2
    proof
      let g1,g2 be Element of G;
      assume A1: (incl2 (A, G, phi)).g1 = (incl2 (A, G, phi)).g2;
      <*1_A, g1*> = (incl2 (A, G, phi)).g1 by Def11
                 .= <*1_A, g2*> by A1,Def11;
      hence g1 = g2 by Lm1;
    end;
    hence incl2 (A, G, phi) is one-to-one by GROUP_6:1;
  end;
end;

:: Aschbacher, Finite Group Theory (10.1.3)
theorem Th28:
  Image (incl2 (A, G, phi)) is normal Subgroup of
  semidirect_product (A, G, phi)
proof
  for x,g being Element of semidirect_product (A, G, phi)
  st g in Image (incl2 (A, G, phi))
  holds g |^ x in Image (incl2 (A, G, phi))
  proof
    let x,g be Element of semidirect_product (A, G, phi);
    assume g in Image (incl2 (A, G, phi));
    then consider g0 being Element of G such that
    A1: g = (incl2 (A, G, phi)).g0 by GROUP_6:45;
    A2: g = <*1_A,g0*> by A1,Def11;
    consider x1 being Element of A, x2 being Element of G such that
    A3: x = <* x1, x2 *>
    by th5;
    (x") = <* x1", (phi/.(x1"))/.(x2") *> by A3, th15;
    then (x") * g = <* (x1") * (1_A),
                       ((phi/.(x1"))/.(x2"))*((phi/.(x1"))/.g0) *> by A2,th6
    .= <* (x1"), ((phi/.(x1"))/.(x2"))*((phi/.(x1"))/.g0) *> by GROUP_1:def 4
    .= <* (x1"), (phi/.(x1"))/.((x2") * g0) *> by GROUP_6:def 6;
    then A4: ((x") * g) * x
    = <* (x1") * x1, ((phi/.(x1"))/.((x2")*g0)*((phi/.(x1"))/.x2)) *> by A3,th6
    .= <* 1_A, ((phi/.(x1"))/.((x2")*g0)*((phi/.(x1"))/.x2)) *>
       by GROUP_1:def 5
    .= <* 1_A, ((phi/.(x1"))/.(((x2") * g0) * x2)) *> by GROUP_6:def 6
    .= <* 1_A, (phi /. (x1"))/.(g0 |^ x2) *> by GROUP_3:def 2;
    g |^ x = ((x") * g) * x by GROUP_3:def 2
    .= ((incl2 (A, G, phi)).((phi /. (x1"))/.(g0 |^ x2))) by A4,Def11;
    hence g |^ x in Image (incl2 (A, G, phi)) by GROUP_6:45;
  end;
  hence thesis by th23;
end;

registration
  let A, G, phi;
  cluster Image (incl2 (A, G, phi)) -> normal;
  correctness by Th28;
end;

theorem Th29:
  Image (incl1 (A, G, phi)) /\ Image (incl2 (A, G, phi))
  = (1).(semidirect_product(A,G,phi))
proof
  set ImA = Image (incl1 (A, G, phi)), ImG = Image (incl2 (A, G, phi));
  set S = semidirect_product(A,G,phi);
  for x being object st x in the carrier of (ImA /\ ImG)
  holds x in {1_S}
  proof
    let x be object;
    assume x in the carrier of (ImA /\ ImG);
    then x in ImA /\ ImG;
    then B1: x in ImA & x in ImG by GROUP_2:82;
    then consider a being Element of A such that
    B2: x = (incl1 (A, G, phi)).a
    by GROUP_6:45;
    consider g being Element of G such that
    B3: x = (incl2 (A, G, phi)).g
    by B1, GROUP_6:45;
    <*a, 1_G*> = x by B2,Def10
              .= <*1_A,g*> by B3,Def11;
    then a = 1_A & 1_G = g by Lm1;
    then x = <*1_A,1_G*> by B2,Def10
          .= 1_S by th14;
    hence x in {1_S} by TARSKI:def 1;
  end;
  then the carrier of (ImA /\ ImG) c= {1_S};
  then the carrier of (ImA /\ ImG) c= the carrier of (1).S by GROUP_2:def 7;
  then A1: ImA /\ ImG is Subgroup of (1).S by GROUP_2:57;
  (1).S is Subgroup of ImA /\ ImG by GROUP_2:65;
  hence thesis by A1, GROUP_2:55;
end;

theorem Th30:
  for x being Element of semidirect_product(A,G,phi)
  ex a being Element of A, g being Element of G
  st ((incl2(A,G,phi)).g) * ((incl1(A,G,phi)).a) = x
proof
  let x be Element of semidirect_product(A, G, phi);
  consider a being Element of A, g being Element of G such that
  A1: x = <*a,g*>
  by th5;
  take a,g;
  ((incl2(A,G,phi)).g) = <*1_A,g*> & ((incl1(A,G,phi)).a) = <*a,1_G*>
  by Def10,Def11;
  hence ((incl2(A,G,phi)).g) * ((incl1(A,G,phi)).a)
  = <* (1_A) * (a), g * ((phi /. (1_A))/.(1_G)) *> by th6
  .= <* (1_A) * (a), g * (1_G) *> by th7
  .= <* a, g * (1_G) *> by GROUP_1:def 4
  .= x by A1, GROUP_1:def 4;
end;

theorem Th31:
  (Image incl2(A,G,phi)) * (Image incl1(A,G,phi))
  = the carrier of (semidirect_product(A,G,phi))
proof
  for x being Element of semidirect_product (A, G, phi)
  holds x in (Image incl2(A,G,phi)) * (Image incl1(A,G,phi))
  proof
    let x be Element of semidirect_product (A, G, phi);
    consider a being Element of A, g being Element of G such that
    B1: ((incl2(A,G,phi)).g) * ((incl1(A,G,phi)).a) = x
    by Th30;
    ((incl2(A,G,phi)).g) in Image(incl2(A,G,phi)) &
    ((incl1(A,G,phi)).a) in Image(incl1(A,G,phi)) by GROUP_6:45;
    hence x in (Image incl2(A,G,phi)) * (Image incl1(A,G,phi))
    by B1, GROUP_5:4;
  end;
  then A1: the carrier of (semidirect_product(A,G,phi))
  c= (Image incl2(A,G,phi)) * (Image incl1(A,G,phi));
  set S = semidirect_product(A,G,phi);
  thus thesis by A1, XBOOLE_0:def 10;
end;

theorem Th32:
  Image (incl2 (A, G, phi)) "\/" Image (incl1 (A, G, phi))
  = (semidirect_product(A,G,phi))
proof
  set S = semidirect_product(A,G,phi);
  set U = (Image incl2(A,G,phi)) * (Image incl1(A,G,phi));
  U c= the carrier of gr U by GROUP_4:def 4; then
  the carrier of S c= the carrier of (gr U) by Th31;
  then S = gr U by GROUP_2:61;
  hence thesis by GROUP_4:50;
end;

:: Aschbacher, Finite Group Theory (10.1.3)
theorem
  G, Image (incl2 (A, G, phi)) are_isomorphic by GROUP_6:68;
  
:: Aschbacher, Finite Group Theory (10.1.4) --- note there is a sign
:: error in Aschbacher's book.
theorem Th27:
  for a being Element of A
  for g being Element of G
  holds ((incl2(A,G,phi)).g) |^ ((incl1(A,G,phi)).a)
  = <*1_A, (phi /. (a"))/.g *>
proof
  let a be Element of A;
  let g be Element of G;
  set xg = (incl2(A,G,phi)).g;
  set xa = (incl1(A,G,phi)).a;
  A1: xa = <* a, 1_G *> by Def10;
  then A2: xa" = <* a", ((phi/.(a"))/.((1_G)")) *> by th15
              .= <* a", ((phi/.(a"))/.(1_G)) *> by GROUP_1:8
              .= <* a", 1_G *> by GROUP_6:31;
  xg = <*1_A, g*> by Def11; then
  (xa") * xg = <* (a") * (1_A), (1_G) * ((phi /. (a"))/.g) *> by A2,th6
            .= <* (a"), (1_G) * ((phi /. (a"))/.g) *> by GROUP_1:def 4
            .= <* (a"), ((phi /. (a"))/.g) *> by GROUP_1:def 4;
  then ((xa")*xg)*xa = <* (a")*a, ((phi/.(a"))/.g)*((phi/.(a"))/.(1_G)) *>
  by A1, th6
                    .= <* (a") * a, ((phi /. (a"))/.g) * (1_G) *> by GROUP_6:31
                    .= <* (a") * a, ((phi /. (a"))/.g) *> by GROUP_1:def 4
                    .= <* 1_A, ((phi /. (a"))/.g) *> by GROUP_1:def 5;
  hence thesis by GROUP_3:def 2;
end;

:: Aschbacher, Finite Group Theory, 10.1.4 (slicker proof)
theorem
  for a being Element of A
  for g being Element of G
  for x,y,z being Element of semidirect_product(A,G,phi)
  st x = <*1_A,g*> & y = <*a",1_G*> & z = <*1_A,(phi/.a).g*>
  holds x |^ y = z
proof
  let a be Element of A;
  let g be Element of G;
  let x,y,z be Element of semidirect_product(A,G,phi);
  assume A1: x = <*1_A,g*>;
  assume A2: y = <*a",1_G*>;
  assume A3: z = <*1_A,(phi/.a).g*>;
  A4: x = (incl2(A,G,phi)).g by A1,Def11;
  y = (incl1(A,G,phi)).(a") by A2,Def10;
  then x |^ y = <* 1_A, (phi /. ((a")")) /. g *> by A4,Th27
             .= z by A3;
  hence thesis;
end;

:: Aschbacher, Finite Group Theory, 10.1.4
theorem
  for a being Element of A
  for g being Element of G
  for x,y,z being Element of semidirect_product(A,G,phi)
  st x = <*1_A,g*> & y = <*a",1_G*> & z = <*1_A,(phi/.a).g*>
  holds x |^ y = z
proof
  let a be Element of A;
  let g be Element of G;
  let x,y,z be Element of semidirect_product(A,G,phi);
  assume A1: x = <*1_A,g*>;
  assume A2: y = <*a",1_G*>;
  assume A3: z = <*1_A,(phi/.a).g*>;
  A4: y" = <* (a")", (phi /. ((a")"))/.((1_G)")*> by A2,th15
        .= <* a, (phi /. a)/.(1_G) *> by GROUP_1:8
        .= <* a, 1_G *> by GROUP_6:31;
  (y") * x = <* (a) * (1_A), (1_G) * ((phi /. (a)) /. g) *> by A1,A4,th6
          .= <* (a), (1_G) * ((phi /. (a)) /. g) *> by GROUP_1:def 4
          .= <* (a), ((phi /. (a)) /. g) *> by GROUP_1:def 4; then
  ((y") * x) * y = <* a*(a"), ((phi/.(a))/.g)*((phi/.(a))/.1_G) *> by A2,th6
                .= <* 1_A, ((phi/.(a))/.g)*((phi/.(a))/.1_G) *>
                   by GROUP_1:def 5
                .= <* 1_A, ((phi/.(a))/.g)*(1_G) *> by GROUP_6:31
                .= z by A3, GROUP_1:def 4;
  hence x |^ y = z by GROUP_3:def 2;
end;


theorem
  semidirect_product (A, G, 1:(A, AutGroup G)) = product <*A, G*>
proof
  set S = semidirect_product (A, G, 1:(A, AutGroup G));
  A1: the carrier of S = the carrier of product <*A,G*> by th2;
  set B1 = the multF of S;
  set B2 = the multF of product <*A, G*>;
  set U = product (Carrier <*A,G*>);
  the carrier of S = product (Carrier<*A,G*>) &
  B1 is BinOp of the carrier of S by Def1;
  then A2: B1 is BinOp of U;
  the carrier of product <*A,G*> = product (Carrier <*A,G*>) &
  the multF of product <*A,G*> is BinOp of the carrier of product <*A,G*>
  by GROUP_7:def 2;
  then A3: B2 is BinOp of U;

  for x,y being Element of product (Carrier <*A,G*>)
  holds B1.(x,y) = B2.(x,y)
  proof
    let x,y be Element of product (Carrier <*A,G*>);
    x is Element of S by Def1;
    then consider a1 being Element of A, g1 being Element of G such that
    A4: x = <*a1,g1*> by th5;
    y is Element of S by Def1;
    then consider a2 being Element of A, g2 being Element of G such that
    A5: y = <*a2,g2*> by th5;
    1:(A, AutGroup G) = A --> (1_(AutGroup G)) by GROUP_6:def 7; then
    (1:(A, AutGroup G)).a1 = id the carrier of G by AUTGROUP:9; then
    ((1:(A, AutGroup G)) . a1) . g2 = g2;
    then A6: (((1:(A, AutGroup G)) /. a1) /. g2) = g2;
    reconsider x0=x,y0=y as Element of S by Def1;
    x0 = <*a1,g1*> & y0 = <*a2,g2*> by A4,A5;
    then A7: x0 * y0 = <* a1 * a2, g1 * g2 *> by A6,th6;
    A8: B1.(x,y) = <* a1 * a2, g1 * g2 *> by A7;

    B2.(x,y) = <*a1,g1*> * <*a2,g2*> by A4,A5
            .= <* a1 * a2, g1 * g2 *> by GROUP_7:29;
    hence B1.(x,y) = B2.(x,y) by A8;
  end;
  then B1 = B2 by A2,A3,BINOP_1:2;
  hence thesis by A1;
end;

:: for alpha being Homomorphism of A,B st alpha is bijective
:: for beta being Automorphism of A
:: holds alpha * beta * alpha" is Automorphism of B;


:: for alpha being Homomorphism of A,B st alpha is bijective
:: ex alpha_star being Homomorphism of AutGroup A, AutGroup B
:: st for beta being Automorphism of A holds alpha_star.beta = alpha * beta * alpha";

:: Aschbacher, Finite Group Theory, section 4 (page 9)
:: This is a highly idiosyncratic notion, which I haven't seen outside
:: Aschbacher's book.
definition
  let A1,A2 be Group;
  let G1,G2 be strict Group;
  let pi1 be Homomorphism of A1,AutGroup G1;
  let pi2 be Homomorphism of A2,AutGroup G2;
  pred pi1,pi2 are_quasi-equivalent means
  ex beta being Homomorphism of G2,G1,
     alpha being Homomorphism of A1,A2
  st alpha is bijective & beta is bijective &
     for a being Element of A2
     for f being Homomorphism of G1,G2
     st f = (pi1 * beta).a
     holds pi2.a = alpha * f * alpha";
end;

begin :: Complementary Subgroups

definition
  let G,H,N be Group;
  pred H,N are_complements_in G means :Def7a:
  ex H1 being strict Subgroup of G st
  ex N1 being strict normal Subgroup of G
  st H1 = the multMagma of H
   & N1 = the multMagma of N
   & H1 * N1 = the carrier of G
   & H1 /\ N1 = (1).G;
end;

theorem ThProdLemma:
  for G being Group
  for H1,H2,K being Subgroup of G
  for K1,K2 being Subgroup of K
  st the multMagma of H1 = the multMagma of K1
  & the multMagma of H2 = the multMagma of K2
  holds H1 * H2 = K1 * K2
proof
  let G be Group;
  let H1,H2,K be Subgroup of G;
  let K1,K2 be Subgroup of K;
  assume A1: the multMagma of H1 = the multMagma of K1;
  assume A2: the multMagma of H2 = the multMagma of K2;
  A3: carr H1 = the carrier of H1 by GROUP_2:def 9
  .= the carrier of K1 by A1
  .= carr K1 by GROUP_2:def 9;
  carr H2 = the carrier of H2 by GROUP_2:def 9
  .= the carrier of K2 by A2
  .= carr K2 by GROUP_2:def 9;
  then A4: carr H1 = carr K1 & carr H2 = carr K2 by A3;

  for x being object
  holds x in (carr H1) * (carr H2) iff x in (carr K1) * (carr K2)
  proof
    let x be object;
    thus x in (carr H1) * (carr H2) implies x in (carr K1) * (carr K2)
    proof
      assume x in (carr H1) * (carr H2);
      then consider h1,h2 being Element of G such that
      B1: x = h1*h2 & h1 in carr H1 & h2 in carr H2
      by GROUP_2:8;
      B2: h1 in carr K1 & h2 in carr K2 by A4,B1;
      then h1 in the carrier of K1 & h2 in the carrier of K2 by GROUP_2:def 9;
      then reconsider k1=h1,k2=h2 as Element of K by GROUP_2:42;
      k1 = h1 & k2 = h2; then
      B3: k1*k2 = h1*h2 by GROUP_2:43
               .= x by B1;
      k1 in carr K1 & k2 in carr K2 by B2;
      then k1 * k2 in (carr K1) * (carr K2) by GROUP_2:8;
      hence x in (carr K1) * (carr K2) by B3;
    end;

    thus x in (carr K1) * (carr K2) implies x in (carr H1) * (carr H2)
    proof
      assume x in (carr K1) * (carr K2);
      then consider k1,k2 being Element of K such that
      B1: x = k1*k2 & k1 in carr K1 & k2 in carr K2
      by GROUP_2:8;
      B2: k1 in carr H1 & k2 in carr H2 by A4,B1;
      then k1 in the carrier of H1 & k2 in the carrier of H2 by GROUP_2:def 9;
      then reconsider h1=k1,h2=k2 as Element of G by GROUP_2:42;
      h1 = k1 & h2 = k2; then
      B3: h1*h2 = k1*k2 by GROUP_2:43
               .= x by B1;
      h1 in carr H1 & h2 in carr H2 by B2;
      then h1*h2 in (carr H1) * (carr H2) by GROUP_2:8;
      hence x in (carr H1) * (carr H2) by B3;
    end;

  end;
  then A5: (carr H1) * (carr H2) = (carr K1) * (carr K2) by TARSKI:2;

  thus H1 * H2 = (carr H1) * (carr H2) by GROUP_4:def 8
  .= (carr K1) * (carr K2) by A5
  .= K1 * K2 by GROUP_4:def 8;
end;



theorem LmDef5a:
  for G being Group
  for H being Subgroup of G
  holds the multMagma of H is strict Subgroup of G
proof
  let G be Group;
  let H be Subgroup of G;
  set H0 = the multMagma of H;
  A1: the multMagma of H is non empty Group-like;
  the carrier of H c= the carrier of G
  & the multF of H = (the multF of G)||(the carrier of H) by GROUP_2:def 5;
  then (the carrier of H0 c= the carrier of G)
  & (the multF of H0 = (the multF of G)||(the carrier of H0))
  by GROUP_2:def 5;
  hence the multMagma of H is strict Subgroup of G by A1,GROUP_2:def 5;
end;

theorem LmDef5:
  for G being Group
  for N being normal Subgroup of G
  holds the multMagma of N is strict normal Subgroup of G
proof
  let G be Group;
  let N be normal Subgroup of G;
  reconsider N0 = the multMagma of N as strict Subgroup of G by LmDef5a;

  for g being Element of G
  holds N0 |^ g = N0
  proof
    let g be Element of G;
    B1: carr N = the carrier of N by GROUP_2:def 9
              .= the carrier of N0
              .= carr N0 by GROUP_2:def 9;
    B2: N |^ g = the multMagma of N by GROUP_3:def 13;
    the carrier of (N0 |^ g) = (carr N0) |^ g by GROUP_3:def 6
                            .= (carr N) |^ g by B1
                            .= the carrier of (N |^ g) by GROUP_3:def 6
                            .= the carrier of N by B2
                            .= the carrier of N0;
    hence N0 |^ g = N0 by GROUP_2:59, GROUP_3:def 6;
  end;
  hence the multMagma of N is strict normal Subgroup of G
  by GROUP_3:def 13;
end;

theorem ThDef7Sub2:
  for G being Group
  for H,K being Subgroup of G st H is Subgroup of K
  for N being Subgroup of G
  st N is normal Subgroup of K
  holds H,N are_complements_in K
  iff ((H * N = the carrier of K) & (H /\ N = (1).K))
proof
  let G be Group;
  let H,K be Subgroup of G;
  assume A1: H is Subgroup of K;
  let N be Subgroup of G;
  assume A2: N is normal Subgroup of K;
  thus H,N are_complements_in K implies ((H * N = the carrier of K) & (H /\ N = (1).K))
  proof
    assume H,N are_complements_in K;
    then consider H1 being strict Subgroup of K,
    N1 being strict normal Subgroup of K such that
    B1: H1 = the multMagma of H
    & N1 = the multMagma of N
    & H1 * N1 = the carrier of K
    & H1 /\ N1 = (1).K by Def7a;
    
    thus H * N = H1 * N1 by B1,ThProdLemma
              .= the carrier of K by B1;

    B2: carr (H1 /\ N1) = the carrier of (H1 /\ N1) by GROUP_2:def 9
                       .= (carr H1) /\ (carr N1) by GROUP_2:def 10;

    B3: carr H = the carrier of H by GROUP_2:def 9
              .= the carrier of H1 by B1
              .= carr H1 by GROUP_2:def 9;
    B4: (1).K is Subgroup of G by GROUP_2:56;
    carr N = the carrier of N by GROUP_2:def 9
          .= the carrier of N1 by B1
          .= carr N1 by GROUP_2:def 9;
    then carr H = carr H1 & carr N = carr N1 by B3;
    then (carr H) /\ (carr N) = (carr H1) /\ (carr N1)
    .= carr (H1 /\ N1) by B2, GROUP_2:81
    .= the carrier of (H1 /\ N1) by GROUP_2:def 9
    .= the carrier of (1).K by B1;
    then the carrier of (H /\ N) = the carrier of (1).K by GROUP_2:def 10;
    hence H /\ N = (1).K by B4, GROUP_2:59;
  end;
  assume A3: H * N = the carrier of K;
  assume A4: H /\ N = (1).K;
  ex H1 being strict Subgroup of K st
  ex N1 being strict normal Subgroup of K
  st H1 = the multMagma of H
   & N1 = the multMagma of N
   & H1 * N1 = the carrier of K
   & H1 /\ N1 = (1).K
  proof
    the multMagma of H is strict Subgroup of K by A1,LmDef5a;
    then reconsider H1=the multMagma of H as strict Subgroup of K;
    take H1;
    the multMagma of N is strict normal Subgroup of K by A2,LmDef5;
    then reconsider N1=the multMagma of N as strict normal Subgroup of K;
    take N1;
    thus H1 = the multMagma of H;
    thus N1 = the multMagma of N;
    thus H1 * N1 = the carrier of K by A3, ThProdLemma;
    B1: (the carrier of H1) /\ (the carrier of N1)
    = (carr H1) /\ (the carrier of N1) by GROUP_2:def 9
    .= (carr H1) /\ (carr N1) by GROUP_2:def 9
    .= the carrier of (H1 /\ N1) by GROUP_2:def 10;
    the carrier of H1 = the carrier of H &
    the carrier of N1 = the carrier of N; then
    (the carrier of H1) /\ (the carrier of N1)
     = (the carrier of H) /\ (the carrier of N)
    .= the carrier of (1).K by A4,GROUP_2:80;
    hence H1 /\ N1 = (1).K by B1, GROUP_2:59;
  end;

  hence H,N are_complements_in K;
end;

theorem Th58Helper:
  for G being Group
  for H,K being Subgroup of G st H is Subgroup of K
  for N being Subgroup of G
  st N is normal Subgroup of K
  holds N * H = H * N
proof
  let G be Group;
  let H,K be Subgroup of G;
  assume A1: H is Subgroup of K;
  let N be Subgroup of G;
  assume A2: N is normal Subgroup of K;
  reconsider H1=H as Subgroup of K by A1;
  reconsider N1=N as normal Subgroup of K by A2;
  for x being object
  holds x in H * N iff x in H1 * N1
  proof
    let x be object;
    hereby 
      assume x in H * N;
      then x in (carr H) * (carr N) by GROUP_4:def 8;
      then consider h,n being Element of G such that
      B1: x = h * n & h in carr H & n in carr N
      by GROUP_2:8;
      h in the carrier of H by B1, GROUP_2:def 9;
      then h in the carrier of H1;
      then B2: h in H1;
      then reconsider h1=h as Element of K by GROUP_2:42;
      n in the carrier of N by B1, GROUP_2:def 9;
      then n in the carrier of N1;
      then B3: n in N1;
      then reconsider n1=n as Element of K by GROUP_2:42;
      h1 = h & n1 = n; then
      B4: h1 * n1 = h * n by GROUP_2:43
                 .= x by B1;
      h1 in carr H1 & n1 in carr N1 by B2, B3, GROUP_2:def 9;
      then h1*n1 in (carr H1) * (carr N1) by B1,B2,B3,GROUP_2:8;
      then x in (carr H1) * (carr N1) by B4;
      hence x in H1 * N1 by GROUP_4:def 8;
    end;
    assume Z2: x in H1 * N1;
    then x in (carr H1) * (carr N1) by GROUP_4:def 8;
    then consider h1,n1 being Element of K such that
    B1: x = h1 * n1 & h1 in carr H1 & n1 in carr N1
    by GROUP_2:8;
    h1 in the carrier of H1 by B1, GROUP_2:def 9;
    then h1 in the carrier of H1;
    then B2: h1 in H;
    then reconsider h=h1 as Element of G by GROUP_2:42;
    n1 in the carrier of N1 by B1, GROUP_2:def 9;
    then n1 in the carrier of N1;
    then B3: n1 in N;
    then reconsider n=n1 as Element of G by GROUP_2:42;
    h1 = h & n1 = n; then
    B4: h * n = h1 * n1 by GROUP_2:43
             .= x by B1;
    h in carr H & n in carr N by B2,B3,GROUP_2:def 9;
    then h*n in (carr H) * (carr N) by B1,B2,B3,GROUP_2:8;
    then x in (carr H) * (carr N) by B4;
    hence x in H * N by GROUP_4:def 8;
  end;
  then A3: H * N = H1 * N1 by TARSKI:2;

  for x being object
  holds x in N * H iff x in N1 * H1
  proof
    let x be object;
    hereby 
      assume x in N * H;
      then x in (carr N) * (carr H) by GROUP_4:def 8;
      then consider n,h being Element of G such that
      B1: x = n * h & n in carr N & h in carr H by GROUP_2:8;
      n in the carrier of N by B1, GROUP_2:def 9;
      then n in the carrier of N1;
      then B2: n in N1;
      then reconsider n1=n as Element of K by GROUP_2:42;
      h in the carrier of H1 by B1, GROUP_2:def 9;
      then h in the carrier of H1;
      then B3: h in H1;
      then reconsider h1=h as Element of K by GROUP_2:42;
      n1 = n & h1 = h; then
      B4: n1 * h1 = n * h by GROUP_2:43
      .= x by B1;
      n1 in carr N1 & h1 in carr H1 by B2,B3,GROUP_2:def 9;
      then n1*h1 in (carr N1)*(carr H1) by B1,B2,B3,GROUP_2:8;
      then x in (carr N1)*(carr H1) by B4;
      hence x in N1 * H1 by GROUP_4:def 8;
    end;
    assume x in N1 * H1;
    then x in (carr N1) * (carr H1) by GROUP_4:def 8;
    then consider n1,h1 being Element of K such that
    B1: x = n1 * h1 & n1 in carr N1 & h1 in carr H1 by GROUP_2:8;
    n1 in the carrier of N1 by B1, GROUP_2:def 9;
    then n1 in the carrier of N;
    then B2: n1 in N;
    then reconsider n=n1 as Element of G by GROUP_2:42;
    h1 in the carrier of H1 by B1, GROUP_2:def 9;
    then h1 in the carrier of H;
    then B3: h1 in H;
    then reconsider h=h1 as Element of G by GROUP_2:42;
    n1 = n & h1 = h; then
    B4: n * h = n1 * h1 by GROUP_2:43
    .= x by B1;
    n in carr N & h in carr H by B2, B3, GROUP_2:def 9;
    then n * h in (carr N) * (carr H) by B2,B3,GROUP_2:8;
    then x in (carr N) * (carr H) by B4;
    hence x in N * H by GROUP_4:def 8;
  end;

  then A4: N * H = N1 * H1 by TARSKI:2; 
  thus N * H = N1 * H1 by A4
            .= H1 * N1 by GROUP_5:8
            .= H * N by A3;
end;


theorem ThDef7Sub3:
  for G being Group
  for H,K being Subgroup of G st H is Subgroup of K
  for N being Subgroup of G
  st N is normal Subgroup of K
  holds H,N are_complements_in K
  iff ((N * H = the carrier of K) & (H /\ N = (1).K))
proof
  let G be Group;
  let H,K be Subgroup of G;
  assume A1: H is Subgroup of K;
  let N be Subgroup of G;
  assume A2: N is normal Subgroup of K;
  thus H,N are_complements_in K
  implies ((N * H = the carrier of K) & (H /\ N = (1).K))
  proof
    assume H,N are_complements_in K;
    then B1: ((H * N = the carrier of K) & (H /\ N = (1).K))
    by A1,A2,ThDef7Sub2;
    thus N * H = H * N by A1,A2,Th58Helper
              .= the carrier of K by B1;
    thus H /\ N = (1).K by B1;
  end;
  thus ((N * H = the carrier of K) & (H /\ N = (1).K))
  implies H,N are_complements_in K
  proof
    assume B1: N * H = the carrier of K;
    assume B2: H /\ N = (1).K;
    H * N = N * H by A1,A2,Th58Helper
         .= the carrier of K by B1;
    hence H,N are_complements_in K by A1,A2,B2,ThDef7Sub2;
  end;
end;


theorem ThDef7Sub4:
  for G being Group
  for H,K being Subgroup of G st H is Subgroup of K
  for N being normal Subgroup of G st N is Subgroup of K
  holds H,((K,N)`*`) are_complements_in K
  iff ((N * H = the carrier of K) & (H /\ N = (1).K))
proof
  let G be Group;
  let H,K be Subgroup of G;
  assume A1: H is Subgroup of K;
  let N be normal Subgroup of G;
  assume A2: N is Subgroup of K;
  then A3: N is normal Subgroup of K by GROUP_6:8;
  then B1: the multMagma of N is strict Subgroup of K by LmDef5a;
  B2: the multMagma of H is strict Subgroup of K by A1,LmDef5a;
  A4: N * H = H * N by A1,A3,Th58Helper;

  thus H,((K,N)`*`) are_complements_in K
  implies ((N * H = the carrier of K) & (H /\ N = (1).K))
  proof
    assume B1: H,((K,N)`*`) are_complements_in K;
    then consider H1 being strict Subgroup of K,
                  N1 being strict normal Subgroup of K such that
    B1: H1 = the multMagma of H
      & N1 = the multMagma of ((K,N)`*`)
      & H1 * N1 = the carrier of K
      & H1 /\ N1 = (1).K by Def7a;
    the carrier of K c= the carrier of G by GROUP_2:def 5;
    then reconsider UK = the carrier of K as Subset of G;
    B2: the carrier of H1 = the carrier of H by B1;
    the multMagma of N is Subgroup of K by A2,LmDef5a; then
    B3: the carrier of N1 = the carrier of N by B1,GROUP_6:def 1;
    for x being Element of G
    holds x in N * H iff x in UK
    proof
      let x be Element of G;
      thus x in N * H implies x in UK
      proof
        assume Z1: x in N * H;
        then x in H * N by A4;
        then consider h,n being Element of G such that
        Z2: x = h * n & h in H & n in N by GROUP_5:4;
        Z3: h in H1 by Z2,B2;
        Z4: n in N1 by Z2,B3;
        then reconsider h1=h,n1=n as Element of K by B1,Z3,Z4,GROUP_2:42;
        x = h1*n1 & h1 in H1 & n1 in N1 by Z2,Z3,Z4,GROUP_2:43;
        then x in H1*N1 by GROUP_5:4;
        then x in the carrier of K by B1;
        hence x in UK;
      end;
      assume Z1: x in UK;
      then x in the carrier of K;
      then x in H1*N1 by B1;
      then consider h1,n1 being Element of K such that
      Z2: x = h1*n1 & h1 in H1 & n1 in N1 by GROUP_5:4;
      Z3: h1 in H by Z2,B2;
      Z4: n1 in N by Z2,B3;
      then reconsider h=h1,n=n1 as Element of G by Z3,Z4,GROUP_2:42;
      x = h * n & h in H & n in N by Z2,Z3,Z4,B2,B3,GROUP_2:43;
      then x in H * N by GROUP_5:4;
      hence x in N * H by A4;
    end;
    then N * H = UK by SUBSET_1:3;
    hence N * H = the carrier of K;
    
    for x being Element of G
    holds x in H /\ N iff x in (1).G
    proof
      let x be Element of G;
      thus x in H /\ N implies x in (1).G
      proof
        assume x in H /\ N;
        then x in H & x in N by GROUP_2:82;
        then x in the carrier of H & x in N;
        then x in the carrier of H & x in the carrier of N;
        then x in the carrier of H1 & x in the carrier of N by B2;
        then x in the carrier of H1 & x in the carrier of N1 by B3;
        then x in the carrier of H1 & x in N1;
        then x in H1 & x in N1;
        then x in (H1 /\ N1) by GROUP_2:82;
        then x in (1).K by B1;
        hence x in (1).G by GROUP_2:63;

      end;
      assume Z1: x in (1).G;
      then x in (1).K by GROUP_2:63;
      then x in (H1 /\ N1) by B1;
      then x in H1 & x in N1 by GROUP_2:82;
      then x in the carrier of H1 & x in N1;
      then x in the carrier of H1 & x in the carrier of N1;
      then x in the carrier of H1 & x in the carrier of N by B3;
      then x in the carrier of H & x in the carrier of N by B2;
      then x in the carrier of H & x in N;
      then x in H & x in N;
      hence x in H /\ N by GROUP_2:82;
    end;
    hence H /\ N = (1).G by GROUP_2:def 6
                .= (1).K by GROUP_2:63;
  end;


  thus ((N * H = the carrier of K) & (H /\ N = (1).K))
  implies H,((K,N)`*`) are_complements_in K
  proof
    assume B1: N * H = the carrier of K;
    assume B2: H /\ N = (1).K;
    reconsider H1=the multMagma of H as strict Subgroup of K by A1,LmDef5a;

    ex H1 being strict Subgroup of K st
    ex N1 being strict normal Subgroup of K
    st H1 = the multMagma of H
    & N1 = the multMagma of ((K,N)`*`)
    & H1 * N1 = the carrier of K
    & H1 /\ N1 = (1).K
    proof
      take H1;
      take N1 = ((K,N)`*`);
      thus H1 = the multMagma of H;
      thus N1 = the multMagma of ((K,N)`*`);
      B3: the carrier of H1 = the carrier of H;
      the multMagma of N is Subgroup of K by A2,LmDef5a; then
      B4: the carrier of N1 = the carrier of N by GROUP_6:def 1;
      for x being object
      holds x in H1 * N1 iff x in the carrier of K
      proof
        let x be object;
        thus x in H1*N1 implies x in the carrier of K
        proof
          assume Z1: x in H1 * N1;
          then ex a, b being Element of K st
          ( x = a * b & a in H1 & b in N1) by GROUP_5:4;
          then consider n1,h1 being Element of K such that
          Z3: x = h1*n1 & h1 in H1 & n1 in N1 by GROUP_5:4;
          Z4: n1 in N by B4,Z3;
          Z5: h1 in H by B3,Z3;
          reconsider n=n1,h=h1 as Element of G by Z4,Z5,GROUP_2:42;
          x = h * n & h in H & n in N by Z3,Z4,Z5, GROUP_2:43;
          then x in H * N by GROUP_5:4;
          then x in N * H by A4;
          hence x in the carrier of K by B1,GROUP_2:42;
        end;
        assume Z2: x in the carrier of K;
        then x in N * H by B1;
        then x in H * N by A4;
        then consider h,n being Element of G such that
        Z3: x = h*n & h in H & n in N by GROUP_5:4;
        Z4: n in N1 by B4,Z3;
        Z5: h in H1 by B3,Z3;
        reconsider n1=n,h1=h as Element of K by Z4,Z5,GROUP_2:42;
        x = h1 * n1 & h1 in H1 & n1 in N1 by Z3,Z4,Z5,GROUP_2:43;
        hence x in H1 * N1 by GROUP_5:4;
      end;
      hence H1 * N1 = the carrier of K by TARSKI:2;


      for x being Element of K
      holds x in H1 /\ N1 iff x in (1).K
      proof
        let x be Element of K;
        thus x in H1 /\ N1 implies x in (1).K
        proof
          assume Z1: x in H1 /\ N1;
          then x in H1 & x in N1 by GROUP_2:82;
          then x in the carrier of H1 & x in N1;
          then x in the carrier of H1 & x in the carrier of N1;
          then x in the carrier of H1 & x in the carrier of N by B4;
          then x in the carrier of H & x in the carrier of N by B3;
          then x in the carrier of H & x in N;
          then x in H & x in N;
          then x in (H /\ N) by GROUP_2:82;
          hence x in (1).K by B2;
        end;
        assume Z2: x in (1).K;
        then x in (H /\ N) by B2;
        then x in H & x in N by GROUP_2:82;
        then x in the carrier of H & x in N;
        then x in the carrier of H & x in the carrier of N;
        then x in the carrier of H1 & x in the carrier of N by B3;
        then x in the carrier of H1 & x in the carrier of N1 by B4;
        then x in the carrier of H1 & x in N1;
        then x in H1 & x in N1;
        hence x in H1 /\ N1 by GROUP_2:82;
      end;

      hence H1 /\ N1 = (1).K by GROUP_2:def 6;
    end;
    hence H,((K,N)`*`) are_complements_in K by Def7a;
  end;

end;



theorem ThDef7Sub5:
  for G being Group
  for H,K being Subgroup of G st H is Subgroup of K
  for N being normal Subgroup of G st N is Subgroup of K
  holds H,N are_complements_in K
  iff H,((K,N)`*`) are_complements_in K
proof
  let G be Group;
  let H,K be Subgroup of G;
  assume A1: H is Subgroup of K;
  let N be normal Subgroup of G;
  assume A2: N is Subgroup of K;
  then A3: N is normal Subgroup of K by GROUP_6:8;
  hereby 
    assume H,N are_complements_in K;
    then ((N * H = the carrier of K) & (H /\ N = (1).K)) by A1,A2,A3,ThDef7Sub3;
    hence H,((K,N)`*`) are_complements_in K by A1,A2,ThDef7Sub4;
  end;
  assume H,((K,N)`*`) are_complements_in K;
  then ((N * H = the carrier of K) & (H /\ N = (1).K)) by A1,A2,ThDef7Sub4;
  hence H,N are_complements_in K by A1,A2,A3,ThDef7Sub3;
end;



theorem ThDef7Sub6:
  for G being Group
  for K being Subgroup of G
  for H being Subgroup of K
  for N being normal Subgroup of G st N is Subgroup of K
  holds H,N are_complements_in K
  iff H,((K,N)`*`) are_complements_in K
proof
  let G be Group;
  let K be Subgroup of G;
  let H be Subgroup of K;
  let N be normal Subgroup of G;
  assume A2: N is Subgroup of K;
  then A3: N is normal Subgroup of K by GROUP_6:8;
  hereby 
    assume H,N are_complements_in K;
    then ((N * H = the carrier of K) & (H /\ N = (1).K)) by A2,A3,ThDef7Sub3;
    hence H,((K,N)`*`) are_complements_in K by A2,ThDef7Sub4;
  end;
  assume H,((K,N)`*`) are_complements_in K;
  then ((N * H = the carrier of K) & (H /\ N = (1).K)) by A2,ThDef7Sub4;
  hence H,N are_complements_in K by A2,A3,ThDef7Sub3;
end;



theorem ThDef7HelperCore:
  for G being Group
  for H,N being Subgroup of G
  for H1,N1 being Subgroup of G
  st the carrier of H1 = the carrier of H
   & the carrier of N1 = the carrier of N
  holds H*N = H1*N1 & H /\ N = H1 /\ N1
proof
  let G be Group;
  let H,N be Subgroup of G;
  let H1,N1 be Subgroup of G;
  assume A1: the carrier of H1 = the carrier of H;
  assume A2: the carrier of N1 = the carrier of N;


  for x being object
  holds x in H1 * N1 iff x in H * N
  proof
    let x be object;
    thus x in H1 * N1 implies x in H * N
    proof
      assume x in H1 * N1;
      then consider h1, n1 being Element of G such that
      B1: x = h1 * n1 & h1 in H1 & n1 in N1
      by GROUP_5:4;
      the carrier of H1 = the carrier of H by A1;
      then B2: h1 in H by B1;
      the carrier of N1 = the carrier of N by A2;
      then B3: n1 in N by B1;
      then h1 * n1 in H * N by B2, GROUP_5:4;
      hence x in H * N by B1;
    end;

    thus x in H * N implies x in H1 * N1
    proof
      assume x in H * N;
      then consider h, n being Element of G such that
      B1: x = h * n & h in H & n in N
      by GROUP_5:4;
      the carrier of H = the carrier of H1 by A1;
      then B2: h in H1 by B1;
      the carrier of N = the carrier of N1 by A2;
      then n in N1 by B1;
      then h * n in H1 * N1 by B2, GROUP_5:4;
      hence x in H1 * N1 by B1;
    end;
  end;
  hence H*N = H1*N1 by TARSKI:2;


  for x being Element of G
  holds x in H /\ N iff x in H1 /\ N1
  proof
    let x be Element of G;
    thus x in H /\ N implies x in H1 /\ N1
    proof
      assume x in H /\ N;
      then x in H & x in N by GROUP_2:82;
      then x in the carrier of H & x in the carrier of N;
      then x in the carrier of H1 & x in the carrier of N by A1;
      then x in the carrier of H1 & x in the carrier of N1 by A2;
      then x in H1 & x in N1;
      hence x in (H1 /\ N1) by GROUP_2:82;
    end;
    thus x in H1 /\ N1 implies x in H /\ N
    proof
      assume x in H1 /\ N1;
      then x in H1 & x in N1 by GROUP_2:82;
      then x in the carrier of H1 & x in the carrier of N1;
      then x in the carrier of H & x in the carrier of N1 by A1;
      then x in the carrier of H & x in the carrier of N by A2;
      then x in H & x in N;
      hence x in (H /\ N) by GROUP_2:82;
    end;
  end;
  hence thesis by GROUP_2:60;
end;


theorem ThDef7Helper:
  for G being Group
  for H,N being Subgroup of G
  for H1,N1 being strict Subgroup of G
  st H1 = the multMagma of H & N1 = the multMagma of N
  holds H*N = H1*N1 & H /\ N = H1 /\ N1
proof
  let G be Group;
  let H,N be Subgroup of G;
  let H1,N1 be strict Subgroup of G;
  assume A1: H1 = the multMagma of H;
  assume A2: N1 = the multMagma of N;
  then the carrier of H1 = the carrier of H
  & the carrier of N1 = the carrier of N by A1;
  hence thesis by ThDef7HelperCore;
end;

theorem ThDef7SubAlt:
  for G being Group
  for H,N being Subgroup of G
  holds H,N are_complements_in G iff
  (N is normal & H * N = the carrier of G & H /\ N = (1).G)
proof
  let G be Group;
  let H,N be Subgroup of G;
  thus H,N are_complements_in G
  implies (N is normal & H*N = the carrier of G & H/\N = (1).G)
  proof
    assume H,N are_complements_in G;
    then consider H1 being strict Subgroup of G,
    N1 being strict normal Subgroup of G such that
    A1: H1 = the multMagma of H
    & N1 = the multMagma of N
    & H1 * N1 = the carrier of G
    & H1 /\ N1 = (1).G by Def7a;
    for g being Element of G
    holds N |^ g = the multMagma of N
    proof
      let g be Element of G;
      B1: carr N1 = the carrier of N1 by GROUP_2:def 9
                 .= the carrier of N by A1
                 .= carr N by GROUP_2:def 9;
      the carrier of N |^ g = (carr N) |^ g by GROUP_3:def 6
      .= (carr N1) |^ g by B1
      .= the carrier of (N1 |^ g) by GROUP_3:def 6
      .= the carrier of N1 by GROUP_3:def 13
      .= (carr N1) by GROUP_2:def 9
      .= carr N by B1
      .= the carrier of N by GROUP_2:def 9;
      hence N |^ g = the multMagma of N by GROUP_2:59, GROUP_3:def 6;
    end;
    hence N is normal by GROUP_3:def 13;

    thus H * N = H1 * N1 by A1,ThDef7Helper
    .= the carrier of G by A1;

    thus H /\ N = H1 /\ N1 by A1,ThDef7Helper
    .= (1).G by A1;

  end;
  assume A0: N is normal;
  assume A1: H * N = the carrier of G;
  assume A2: H /\ N = (1).G;

  ex H1 being strict Subgroup of G st
  ex N1 being strict normal Subgroup of G
  st H1 = the multMagma of H
   & N1 = the multMagma of N
   & H1 * N1 = the carrier of G
   & H1 /\ N1 = (1).G
  proof
    reconsider H1=the multMagma of H as strict Subgroup of G by LmDef5a;
    take H1;
    thus thesis
    proof
      reconsider N1=the multMagma of N as strict normal Subgroup of G
      by A0,LmDef5;
      take N1;
      thus H1 = the multMagma of H;
      thus N1 = the multMagma of N;
      thus H1 * N1 = H * N by ThDef7Helper
      .= the carrier of G by A1;
      thus H1 /\ N1 = H /\ N by ThDef7Helper
      .= (1).G by A2;
    end;
  end;
  hence H,N are_complements_in G;
end;

definition
  let G be Group;
  let H,N be Subgroup of G;
  redefine pred H,N are_complements_in G means :Def7:
  N is normal & H * N = the carrier of G & H /\ N = (1).G;
  compatibility by ThDef7SubAlt;
end;

theorem Th25:
  for G being Group
  for A being Subset of G st A = the carrier of G
  holds gr A = the multMagma of G
proof
  let G be Group;
  let A be Subset of G;
  assume A = the carrier of G;
  then the carrier of G c= the carrier of (gr A) by GROUP_4:def 4;
  hence the multMagma of G = gr A by GROUP_2:61;
end;

theorem Th26:
  for G being Group
  for H being Subgroup of G
  for N being normal Subgroup of G
  holds H,N are_complements_in G
  iff (H "\/" N = the multMagma of G & H /\ N = (1).G)
proof
  let G be Group;
  let H be Subgroup of G;
  let N be normal Subgroup of G;
  the carrier of G c= the carrier of G; then
  reconsider A = the carrier of G as Subset of G;
  thus H,N are_complements_in G
       implies (H "\/" N = the multMagma of G & H /\ N = (1).G)
  proof
    assume A1: H,N are_complements_in G;
    then H "\/" N = gr (A) by GROUP_4:50;
    hence H "\/" N = the multMagma of G by Th25;
    thus H /\ N = (1).G by A1;
  end;
  thus (H "\/" N = the multMagma of G & H /\ N = (1).G)
       implies H,N are_complements_in G
  proof
    assume A1: H "\/" N = the multMagma of G;
    A2: H * N = carr(H) * N by GROUP_4:43
             .= N * carr(H) by GROUP_3:120
             .= N * H by GROUP_4:43;
    assume H /\ N = (1).G;
    hence H,N are_complements_in G by A1, A2, GROUP_4:51;
  end;
end;



:: Aschbacher, Finite Group Theory (10.1.3)
theorem
  Image incl1(A,G,phi), Image incl2(A,G,phi) are_complements_in
  semidirect_product(A,G,phi)
proof
  set S = semidirect_product(A,G,phi);
  A1: Image incl1(A,G,phi) "\/" Image incl2(A,G,phi)
  = Image (incl2 (A, G, phi)) "\/" Image (incl1 (A, G, phi)) by GROUP_4:56
  .= S by Th32;
  Image (incl1 (A, G, phi)) /\ Image (incl2 (A, G, phi)) = (1).S by Th29;
  hence thesis by A1,Th26;
end;

theorem LmOnto:
  for G1,G2 being Group
  for f being Function of G1,G2
  st (for y being Element of G2 ex x being Element of G1 st f.x = y)
  holds f is onto
proof
  let G1,G2 be Group;
  let f be Function of G1,G2;
  assume A1: for y being Element of G2 ex x being Element of G1 st f.x = y;
  for y being object st y in the carrier of G2
  ex x being object st (x in the carrier of G1 & y = f.x)
  proof
    let y be object;
    assume y in the carrier of G2;
    then reconsider y0=y as Element of G2;
    ex x being Element of G1 st f.x = y0 by A1;
    hence thesis;
  end;

  then rng f = the carrier of G2 by FUNCT_2:10;
  hence f is onto by FUNCT_2:def 3;
end;

theorem
  G = the multMagma of A implies A,G are_isomorphic
proof
  assume A1: G = the multMagma of A;
  ex f being Homomorphism of A,G st f is bijective
  proof
    reconsider f=id A as Function of A,G by A1;
    for a1,a2 being Element of A
    holds f.(a1 * a2) = (f.a1) * (f.a2) by A1;

    then reconsider f=id A as Homomorphism of A,G by GROUP_6:def 6;
    take f;
    for y being Element of G
    ex x being Element of A
    st f.x = y
    proof
      let y be Element of G;
      reconsider x=y as Element of A by A1;
      take x;
      thus f.x = y;
    end;
    then f is onto by LmOnto;
    hence thesis;
  end;
  hence A,G are_isomorphic by GROUP_6:def 11;
end;

::  GROUP_2:114 may suffice?
theorem Th30b:
  for G being Group
  for N being normal Subgroup of G
  for g1,g2 being Element of G
  st g1 * N = g2 * N
  ex n being Element of G
  st n in N & g1 = g2 * n
proof
  let G be Group;
  let N be normal Subgroup of G;
  let g1,g2 be Element of G;
  assume A1: g1 * N = g2 * N;
  consider n being Element of G such that
  A2: n = (g2") * g1;
  take n;
  thus n in N by A1, A2, GROUP_2:114;
  thus g2 * n = (g2 * (g2")) * g1 by A2, GROUP_1:def 3
             .= (1_G) * g1 by GROUP_1:def 5
             .= g1 by GROUP_1:def 4;
end;

::$N Universal Property of Quotient Groups
theorem UniversalPropertyQuotientGroups:
  for G1,G2 being Group
  for N being normal Subgroup of G1
  for f being Homomorphism of G1,G2
  st N is Subgroup of Ker f
  ex fBar being Homomorphism of G1./.N,G2
  st f = fBar * (nat_hom N)
proof
  let G1,G2 be Group;
  let N be normal Subgroup of G1;
  let f be Homomorphism of G1,G2;
  assume A1: N is Subgroup of Ker f;

  defpred P[Element of G1./.N, Element of G2] means
  ex g being Element of G1 st $1 = g*N & $2 = f.g;

  A2: for x being Element of G1./.N
  ex y being Element of G2 st P[x,y]
  proof
    let x be Element of G1./.N;
    x in G1./.N; then
    consider g1 being Element of G1 such that
    B1: x = g1 * N & x = N *g1
    by GROUP_6:23;
    consider y being Element of G2 such that
    B2: y = f.g1;
    take y;
    thus P[x,y] by B1,B2;
  end;

  consider fBar being Function of G1./.N,G2 such that
  A3: for x being Element of G1./.N holds P[x, fBar.x]
  from FUNCT_2:sch 3(A2);

  for x1,x2 being Element of G1./.N
  holds fBar.(x1 * x2) = (fBar.x1) * (fBar.x2)
  proof
    let x1,x2 be Element of G1./.N;
    consider g12 being Element of G1 such that
    B1: x1 * x2 = g12*N & fBar.(x1 * x2) = f.g12
    by A3;
    consider g1 being Element of G1 such that
    B2: x1 = g1*N & fBar.x1 = f.g1
    by A3;
    consider g2 being Element of G1 such that
    B3: x2 = g2*N & fBar.x2 = f.g2
    by A3;

    B4: (nat_hom N).g1 = x1 & (nat_hom N).g2 = x2 by B2,B3,GROUP_6:def 8;

    (g1 * g2) * N = (nat_hom N).(g1 * g2) by GROUP_6:def 8
                 .= g12 * N by B1, B4, GROUP_6:def 6;
    then consider n being Element of G1 such that
    B5: n in N & g12 = (g1 * g2) * n
    by Th30b;
    f.g12 = (f.(g1 * g2)) * (f.n) by B5, GROUP_6:def 6
         .= (f.(g1 * g2)) * (1_G2) by A1, B5, GROUP_2:40, GROUP_6:41
         .= f.(g1 * g2) by GROUP_1:def 4
         .= (f.g1) * (f.g2) by GROUP_6:def 6;
    hence fBar.(x1 * x2) = (fBar.x1) * (fBar.x2) by B1,B2,B3;
  end;

  then reconsider fBar as Homomorphism of G1./.N,G2 by GROUP_6:def 6;
  take fBar;

  for g being Element of G1
  holds f.g = (fBar * (nat_hom N)).g
  proof
    let g be Element of G1;
    consider g1 being Element of G1 such that
    B1: (nat_hom N).g = g1 * N & fBar.((nat_hom N).g) = f.g1
    by A3;
    g * N = g1 * N by B1, GROUP_6:def 8;
    then consider n being Element of G1 such that
    B2: n in N & g = g1 * n
    by Th30b;
    f.g = (f.g1) * (f.n) by B2, GROUP_6:def 6
       .= (f.g1) * (1_G2) by A1, B2, GROUP_2:40, GROUP_6:41
       .= f.g1 by GROUP_1:def 4
       .= (fBar * (nat_hom N)).g by B1, FUNCT_2:15;
    hence f.g = (fBar * (nat_hom N)).g;
  end;

  hence f = fBar * (nat_hom N) by FUNCT_2:def 8;
end;


theorem Th27:
  for G1,G2 being Group
  for N1 being normal Subgroup of G1
  for N2 being normal Subgroup of G2
  for phi being Homomorphism of G1,G2
  st phi is bijective & phi .: the carrier of N1 = the carrier of N2
  holds G1./.N1, G2./.N2 are_isomorphic
proof
  let G1,G2 be Group;
  let N1 be normal Subgroup of G1;
  let N2 be normal Subgroup of G2;
  let phi be Homomorphism of G1,G2;
  assume A1: phi is bijective;
  assume A2: phi .: the carrier of N1 = the carrier of N2;
  for g being Element of G1
  st g in N1
  holds g in Ker ((nat_hom N2) * phi)
  proof
    let g be Element of G1;
    assume g in N1;
    then B1: phi.g in N2 by A2, FUNCT_2:35;
    then (nat_hom N2).(phi.g) = (phi.g) * N2 & 1_(G2./.N2) = carr N2
    & phi.g in carr N2 by GROUP_2:def 9, GROUP_6:24, GROUP_6:def 8;
    then (nat_hom N2).(phi.g) = 1_(G2./.N2) by B1, GROUP_2:113;
    then ((nat_hom N2) * phi).g = 1_(G2./.N2) by FUNCT_2:15;
    hence g in Ker ((nat_hom N2) * phi) by GROUP_6:41;
  end;
  then N1 is Subgroup of Ker ((nat_hom N2) * phi) by GROUP_2:58;
  then consider phiBar being Homomorphism of G1./.N1,G2./.N2 such that
  A5: ((nat_hom N2) * phi) = phiBar * (nat_hom N1)
  by UniversalPropertyQuotientGroups;

  for y being Element of G2./.N2
  ex x being Element of G1./.N1
  st phiBar.x = y
  proof
    let y be Element of G2./.N2;
    consider g2 being Element of G2 such that
    B1: y = g2 * N2 & y = N2 * g2
    by GROUP_6:21;
    B2: phi" is Homomorphism of G2,G1 by A1, GROUP_6:62;
    then (phi").g2 in G1 by FUNCT_2:5;
    then (nat_hom N1).((phi").g2) in G1./.N1 by FUNCT_2:5;
    then consider x being Element of G1./.N1 such that
    B4: x = (nat_hom N1).((phi").g2);
    take x;
    B5: phi is one-to-one & rng phi = the carrier of G2 by A1, FUNCT_2:def 3;

    g2 in G2;
    then g2 in dom (phi") by B2, FUNCT_2:def 1; then
    B6: (phi.((phi").g2)) = (phi * (phi")).g2 by FUNCT_1:13
                         .= (id the carrier of G2).g2 by B5,FUNCT_2:29
                         .= g2;
    thus phiBar.x = (phiBar * (nat_hom N1)).((phi").g2) by B2, B4, FUNCT_2:5,15
                 .= (nat_hom N2).(phi.((phi").g2)) by A5, B2, FUNCT_2:5,15
                 .= y by B1, B6, GROUP_6:def 8;
  end;
  then A6: phiBar is onto by LmOnto;

  for a,b being Element of G1./.N1 st phiBar.a = phiBar.b
  holds a = b
  proof
    let a,b be Element of G1./.N1;
    assume B1: phiBar.a = phiBar.b;
    consider x1 being Element of G1 such that
    B2: a = x1 * N1 & a = N1 * x1
    by GROUP_6:21;
    consider x2 being Element of G1 such that
    B3: b = x2 * N1 & b = N1 * x2
    by GROUP_6:21;
    B5: phiBar.a = phiBar.((nat_hom N1).x1) by B2,GROUP_6:def 8
                .= ((nat_hom N2) * phi).x1 by A5,FUNCT_2:15
                .= (nat_hom N2).(phi.x1) by FUNCT_2:15;
    phiBar.b = phiBar.((nat_hom N1).x2) by B3, GROUP_6:def 8
            .= (phiBar * (nat_hom N1)).x2 by FUNCT_2:15
            .= (nat_hom N2).(phi.x2) by A5, FUNCT_2:15;
    then (phi.x1) * N2 = (nat_hom N2).(phi.x2) by B1,B5,GROUP_6:def 8
                      .= (phi.x2) * N2 by GROUP_6:def 8;
    then consider n being Element of G2 such that
    B6: n in N2 & phi.x1 = (phi.x2) * n
    by Th30b;
    consider n1 being object such that
    B7: n1 in the carrier of G1 & n1 in the carrier of N1 & n = phi.n1
    by A2, B6, FUNCT_2:64;
    reconsider n1 as Element of G1 by B7;
    phi.x1 = phi.(x2 * n1) by B6, B7, GROUP_6:def 6;
    then (x2") * x1 = (x2") * (x2 * n1) by A1, GROUP_6:1
                   .= ((x2") * x2) * n1 by GROUP_1:def 3
                   .= (1_G1) * n1 by GROUP_1:def 5
                   .= n1 by GROUP_1:def 4;
    then (x2") * x1 in N1 by B7;
    then x1 * N1 = x2 * N1 by GROUP_2:114;
    hence a = b by B2, B3;
  end;
  then phiBar is one-to-one by GROUP_6:1;
  hence G1./.N1, G2./.N2 are_isomorphic by A6, GROUP_6:def 11;
end;


theorem Th28b:
  for G being Group
  for H being Subgroup of G
  for N being normal Subgroup of G
  st H,N are_complements_in G
  ex phi being Homomorphism of H, G./.N
  st (for h being Element of H
      for g being Element of G st g = h
      holds phi.h = g*N) & phi is bijective
proof
  let G be Group;
  let H be Subgroup of G;
  let N be normal Subgroup of G;
  assume A1: H,N are_complements_in G;
  then A2: H * N = the carrier of G & H /\ N = (1).G by Def7;

  defpred P[Element of H, Element of G./.N] means
  ex g being Element of G st g = $1 & $2 = g*N;

  A3: for x being Element of H
  ex y being Element of G./.N
  st P[x,y]
  proof
    let x be Element of H;
    reconsider g=x as Element of G by GROUP_2:42;
    take y = (nat_hom N).g;
    y = g*N by GROUP_6:def 8;
    hence P[x,y];
  end;

  consider phi being Function of H,G./.N such that
  A4: for x being Element of H holds P[x, phi.x]
  from FUNCT_2:sch 3(A3);

  A5: for h being Element of H
      for g being Element of G st g = h
      holds phi.h = g*N
  proof
    let h be Element of H;
    let g be Element of G;
    assume B1: g = h;
    P[h, phi.h] by A4; then
    ex g0 being Element of G st g0 = h & phi.h = g0*N;
    hence phi.h = g*N by B1;
  end;


  for a,b being Element of H
  holds phi.(a * b) = (phi.a) * (phi.b)
  proof
    let a,b be Element of H;
    reconsider g1=a,g2=b as Element of G by GROUP_2:42;
    B1: phi.a = g1 * N by A5
             .= (nat_hom N).g1 by GROUP_6:def 8;
    B2: phi.b = g2 * N by A5
             .= (nat_hom N).g2 by GROUP_6:def 8;
    a * b = g1 * g2 by GROUP_2:43; then
    B3: phi.(a * b) = (g1 * g2) * N by A5
                   .= (nat_hom N).(g1 * g2) by GROUP_6:def 8;
    thus phi.(a * b) = (nat_hom N).(g1 * g2) by B3
                    .= ((nat_hom N).g1) * ((nat_hom N).g2) by GROUP_6:def 6
                    .= (phi.a) * (phi.b) by B1,B2,GROUP_2:43;
  end;
  then reconsider phi as Homomorphism of H,G./.N by GROUP_6:def 6;
  take phi;
  thus for h being Element of H
       for g being Element of G st g = h
       holds phi.h = g*N by A5;

  for y being Element of G./.N
  ex x being Element of H
  st phi.x = y
  proof
    let y be Element of G./.N;
    y in G./.N; then
    consider g being Element of G such that
    Z1: y = g*N & y = N*g by GROUP_6:23;
    g in the carrier of G; then
    g in H * N by A2; then
    consider h,n being Element of G such that
    Z2: g = h * n & h in H & n in N by GROUP_5:4;
    g * N = (h * n) * N by Z2
         .= h * (n * N) by GROUP_2:105
         .= h * (carr N) by Z2,GROUP_2:113
         .= h * N by GROUP_2:def 13;
    then Z3: y = h * N by Z1;
    reconsider x=h as Element of H by Z2, GROUP_2:42;
    take x;
    phi.x = h*N by A5
         .= y by Z3;
    hence phi.x = y by Z1,Z2,A5;
  end;

  then A3: phi is onto by LmOnto;

  for a,b being Element of H st phi.a = phi.b
  holds a = b
  proof
    let a,b be Element of H;
    assume Z1: phi.a = phi.b;
    reconsider ga=a, gb=b as Element of G by GROUP_2:42;
    phi.a = ga*N by A5;
    ga*N = phi.a by A5
    .= phi.b by Z1
    .= gb*N by A5;
    then Z2: (gb")*ga in N by GROUP_2:114;
    gb in H; then
    gb" in H & ga in H by GROUP_2:51;
    then (gb")*ga in H by GROUP_2:50;
    then (gb")*ga in H /\ N by Z2,GROUP_2:82;
    then (gb")*ga in (1).G by A2;
    then (gb")*ga = 1_G by GROUP_5:1;
    then ga = gb * 1_G by GROUP_1:13
    .= gb by GROUP_1:def 4;
    hence a = b;
  end;

  then phi is one-to-one by GROUP_6:1;
  hence phi is bijective by A3;
end;

theorem Th28:
  for G being Group
  for H being Subgroup of G
  for N being normal Subgroup of G
  st H,N are_complements_in G
  holds G ./. N, H are_isomorphic
proof
  let G be Group;
  let H be Subgroup of G;
  let N be normal Subgroup of G;
  assume Z1: H,N are_complements_in G;
  then ex phi being Homomorphism of H,G./.N
  st (for h being Element of H
      for g being Element of G st g = h
      holds phi.h = g*N) & phi is bijective by Th28b;
  then H, (G./.N) are_isomorphic by GROUP_6:def 11;
  hence G ./. N, H are_isomorphic by GROUP_6:66;
end;

theorem
  for G being Group
  for H1, H2 being Subgroup of G
  for N being normal Subgroup of G
  st H1,N are_complements_in G & H2,N are_complements_in G
  holds H1,H2 are_isomorphic
proof
  let G be Group;
  let H1, H2 be Subgroup of G;
  let N be normal Subgroup of G;
  assume A1: H1,N are_complements_in G;
  assume A2: H2,N are_complements_in G;

  A3: G ./. N, H1 are_isomorphic by A1,Th28;
  A4: G ./. N, H2 are_isomorphic by A2,Th28;
  thus H1,H2 are_isomorphic by A3,A4,GROUP_6:67;
end;

definition
  let G,K be Group;
  pred G splits_over K means :Def6:
  ex N being strict normal Subgroup of G
  st N = the multMagma of K & (ex H being Subgroup of G
  st H,N are_complements_in G);
end;


LmDef5c:
  for G being Group
  for H being Subgroup of G
  for N being normal Subgroup of G
  for N1 being strict normal Subgroup of G
  st N1 = the multMagma of N
  holds H * N = H * N1 & H /\ N = H /\ N1
proof
  let G be Group;
  let H be Subgroup of G;
  let N be normal Subgroup of G;
  let N1 be strict normal Subgroup of G;
  assume A1: N1 = the multMagma of N;
  then
  A2: the carrier of H = the carrier of H
  & the carrier of N1 = the carrier of N;
  hence thesis by ThDef7HelperCore;
end;


theorem Th30:
  for G being Group
  for N being normal Subgroup of G
  holds G splits_over N
  iff (ex H being Subgroup of G st H,N are_complements_in G)
proof
  let G be Group;
  let N be normal Subgroup of G;
  thus G splits_over N implies (ex H being Subgroup of G st H,N are_complements_in G)
  proof
    given N1 being strict normal Subgroup of G such that 
    A1: N1 = the multMagma of N & (ex H being Subgroup of G st H,N1 are_complements_in G);
    consider H being Subgroup of G such that
    A2: H,N1 are_complements_in G by A1;
    take H;
    A3: H * N = H * N1 by A1,LmDef5c
             .= the carrier of G by A2,Def7;
    A4: H /\ N = H /\ N1 by A1,LmDef5c
              .= (1).G by A2,Def7;
    thus H,N are_complements_in G by A3,A4,Def7;
  end;
  thus (ex H being Subgroup of G st H,N are_complements_in G) implies G splits_over N
  proof
    given H being Subgroup of G such that 
    A1: H,N are_complements_in G;
    ex N1 being strict normal Subgroup of G
    st N1 = the multMagma of N
    & (ex H being Subgroup of G st H,N1 are_complements_in G)
    proof
      reconsider N1=the multMagma of N as strict normal Subgroup of G by LmDef5;
      take N1;
      thus N1 = the multMagma of N;
      take H;
      A2: H * N1 = H * N by LmDef5c
                .= the carrier of G by A1,Def7;
      A3: H /\ N1 = H /\ N by LmDef5c
                 .= (1).G by A1,Def7;
      thus H,N1 are_complements_in G by A2,A3,Def7;
    end;
    hence G splits_over N by Def6;
  end;
end;


definition
  let G be Group;
  let N be normal Subgroup of G;
  redefine pred G splits_over N means :Def5:
  ex H being Subgroup of G st H,N are_complements_in G;
  compatibility by Th30;
end;

:: theorem ThDef5:
::   for G being Group
::   for H being Subgroup of G
::   for N being Subgroup of G st N is normal Subgroup of H
::   holds H splits_over N
::   iff (ex K being Subgroup of H st K,N are_complements_in H);

theorem Th40:
  for G being Group
  for N being normal Subgroup of G
  for H being Subgroup of G
  st H,N are_complements_in G
  for g being Element of G
  ex h,n being Element of G
  st h in H & n in N & h*n = g
proof
  let G be Group;
  let N be normal Subgroup of G;
  let H be Subgroup of G;
  assume A1: H,N are_complements_in G;
  let g be Element of G;
  g in H*N by A1;
  then consider h,n being Element of G such that
  A2: g = h*n & h in H & n in N by GROUP_5:4;
  take h,n;
  thus h in H by A2;
  thus n in N by A2;
  thus h*n = g by A2;
end;



theorem Th50:
  for G1,G2 being Group 
  for H1 being Subgroup of G1
  for H2 being Subgroup of G2
  for h1 being Element of G1 st h1 in H1
  for h2 being Element of G2 st h2 in H2
  holds <*h1,h2*> in product <*H1,H2*>
proof
  let G1,G2 be Group;
  let H1 be Subgroup of G1;
  let H2 be Subgroup of G2;
  let h1 be Element of G1;
  assume A1: h1 in H1;
  let h2 be Element of G2;
  assume A2: h2 in H2;
  set H = <*H1,H2*>;
  A3: dom Carrier H = {1,2} by PARTFUN1:def 2;
  A4: <*h1,h2*>.2 = h2 by FINSEQ_1:44;
  A5: H.2 = H2 by FINSEQ_1:44;
  A6: H.1 = H1 by FINSEQ_1:44;
  A7: <*h1,h2*>.1 = h1 by FINSEQ_1:44;
  A8: for a being object st a in {1,2}
      holds <*h1,h2*>.a in (Carrier <*H1,H2*>).a
  proof
    let a be object;
    assume B1: a in {1,2};
    then per cases by TARSKI:def 2;
    suppose B2: a = 1;
      then ex R being 1-sorted st R = H.1 & (Carrier H).1 = the carrier of R
      by B1, PRALG_1:def 15;
      hence thesis by A1,A7,A6,B2;
    end;
    suppose B3: a = 2;
      then ex R being 1-sorted st R = H.2 & (Carrier H).2 = the carrier of R
      by B1, PRALG_1:def 15;
      hence thesis by A2,A4,A5,B3;

    end;
  end;

  dom <*h1,h2*> = {1,2} by FINSEQ_1:2,89;
  then <*h1,h2*> in product Carrier (<*H1,H2*>) by A3,A8,CARD_3:9;
  hence <*h1,h2*> in product <*H1,H2*> by GROUP_7:def 2;
end;

theorem Th51:
  for G being Group
  for H,K being strict Subgroup of G
  st H <> K & K is Subgroup of H
  ex g being Element of G
  st g in H & not g in K
proof
  let G be Group;
  let H,K be strict Subgroup of G;
  assume A1: H <> K;
  assume A2: K is Subgroup of H;
  then A3: the carrier of K c= the carrier of H by GROUP_2:def 5;
  not H is Subgroup of K by A1, A2, GROUP_2:55; then
  not (the carrier of H c= the carrier of K)
  by GROUP_2:57;
  then the carrier of K c= the carrier of H
  & (the carrier of H <> the carrier of K) 
  by A3;
  then the carrier of K c< the carrier of H by XBOOLE_0:def 8;
  then consider x being object such that
  A4: ( x in the carrier of H & not x in the carrier of K) by XBOOLE_0:6;
  A5: x in H & not x in K by A4;
  then x in G by GROUP_2:40;
  then reconsider g=x as Element of G;
  take g;
  thus g in H & not g in K by A5;
end;


:: Bourbaki mentions this, literally in passing in a sentence in
:: General Topology Chapter II, not even making a proposition or lemma
:: out of it.
theorem Th55:
  for G being Group
  for H,K being Subgroup of G
  for phi being Function of product <*H,K*>,G
  st (for h,k being Element of G st h in H & k in K
      holds phi.(<*h,k*>) = h*k)
  holds phi is one-to-one iff H /\ K = (1).G
proof
  let G be Group;
  let H,K be Subgroup of G;
  let phi be Function of product <*H,K*>,G;
  assume A1: for h,k being Element of G st h in H & k in K
  holds phi.(<*h,k*>) = h*k;
  A2: 1_G in H & 1_G in K by GROUP_2:46;

  thus phi is one-to-one implies H /\ K = (1).G
  proof
    assume B1: phi is one-to-one;
    B1b: for h1,h2,k1,k2 being Element of G st h1 in H & h2 in H
    & k1 in K & k2 in K & phi.(<*h1,k1*>) = phi.(<*h2,k2*>)
    holds <*h1,k1*> = <*h2,k2*>
    proof
      let h1,h2,k1,k2 be Element of G;
      assume Z1: h1 in H;
      assume Z2: h2 in H;
      assume Z3: k1 in K;
      assume Z4: k2 in K;
      assume Z5: phi.(<*h1,k1*>) = phi.(<*h2,k2*>);
      <*h1,k1*> in product <*H,K*> by Z1,Z3,Th50;
      then reconsider x1 = <*h1,k1*> as Element of product <*H,K*>;
      <*h2,k2*> in product <*H,K*> by Z2,Z4,Th50;
      then reconsider x2 = <*h2,k2*> as Element of product <*H,K*>;
      phi.x1 = phi.x2 by Z5;
      then x1 = x2 by B1,GROUP_6:1;
      hence <*h1,k1*> = <*h2,k2*>;
    end;


    H /\ K <> (1).G implies contradiction
    proof
      assume B2: H /\ K <> (1).G;
      (1).G is Subgroup of H /\ K by GROUP_2:65;
      then consider h1 being Element of G such that
      B3: h1 in H /\ K & not h1 in (1).G by B2,Th51;
      B4: h1 in H & h1 in K by B3, GROUP_2:82;
      B5: h1 <> 1_G by B3, GROUP_5:1;
      h1" in K by B4, GROUP_2:51;
      then B6: h1 in H & h1" in K by B4;
      then phi.(<*h1,h1"*>) = h1 * (h1") by A1
      .= 1_G by GROUP_1:def 5
      .= (1_G) * (1_G) by GROUP_1:def 4
      .= phi.(<*1_G, 1_G*>) by A1,A2;
      then <*h1, h1"*> = <*1_G,1_G*> by A2,B1b,B6;
      then h1 = 1_G & h1" = 1_G by Lm1;
      hence contradiction by B5;
    end;

    hence H /\ K = (1).G;
  end;
  thus H /\ K = (1).G implies phi is one-to-one
  proof
    assume B1: H /\ K = (1).G;
    for x,y being Element of product <*H,K*>
    st phi.x = phi.y
    holds x = y
    proof
      let x,y be Element of product <*H,K*>;
      assume B2: phi.x = phi.y;
      x.1 in H & x.2 in K & dom x = {1,2} by th4a;
      then x.1 in H & x.1 in G & x.2 in K & x.2 in G & dom x = {1,2}
      by GROUP_2:41;
      then consider h1,k1 being Element of G such that
      B3a: x.1 = h1 & x.2 = k1 & h1 in H & k1 in K;
      dom x = {1,2} by th4a;
      then len x = 2 by FINSEQ_1:2,def 3;
      then x = <*h1,k1*> by B3a,FINSEQ_1:44;
      then B3: x.1 = h1 & x.2 = k1 & h1 in H & k1 in K & x = <*h1,k1*>
      by B3a;
      y.1 in H & y.2 in K & dom y = {1,2} by th4a;
      then y.1 in G & y.1 in H & y.2 in G & y.2 in K & dom y = {1,2}
      by GROUP_2:41;
      then consider h2,k2 being Element of G such that
      B4a: y.1 = h2 & y.2 = k2 & h2 in H & k2 in K;
      dom y = {1,2} by th4a;
      then len y = 2 by FINSEQ_1:2,def 3;
      then y = <*h2,k2*> by B4a,FINSEQ_1:44;
      then B4: y.1 = h2 & y.2 = k2 & h2 in H & k2 in K & y = <*h2,k2*>
      by B4a;
      h1*k1 = phi.x by A1,B3
           .= h2*k2 by A1,B2,B4;
      then (h2") * (h1 * k1) = ((h2") * h2) * k2 by GROUP_1:def 3
                            .= (1_G) * k2 by GROUP_1:def 5
                            .= k2 by GROUP_1:def 4;
      then B6: k2 * (k1") = (((h2") * h1) * k1) * (k1") by GROUP_1:def 3
                         .= ((h2") * h1) * (k1 * (k1")) by GROUP_1:def 3
                         .= ((h2") * h1) * 1_G by GROUP_1:def 5
                         .= (h2") * h1 by GROUP_1:def 4;
      k2 in K & k1" in K by B3,B4, GROUP_2:51;
      then B7: k2 * (k1") in K by GROUP_2:50;
      then B8: (h2") * h1 in K by B6;
      h1 in H & h2" in H by B3,B4, GROUP_2:51;
      then B9: (h2") * h1 in H by GROUP_2:50;
      then B10: k2 * (k1") in H by B6;
      (h2") * h1 in H /\ K by B8,B9,GROUP_2:82;
      then (h2") * h1 = 1_G by B1,GROUP_5:1;
      then B11: h1 = (h2")" by GROUP_1:12
      .= h2;
      k2 * (k1") in H /\ K by B7,B10,GROUP_2:82;
      then k2 * (k1") = 1_G by B1,GROUP_5:1;
      then k2 = (k1")" by GROUP_1:12
      .= k1;
      then <*h1,k1*> = <*h2,k2*> by B11;
      hence x = y by B3,B4;
    end;

    hence phi is one-to-one by GROUP_6:1;
  end;
end;

theorem Th55b:
  for G being Group
  for H,K being Subgroup of G
  for phi being Function of product <*H,K*>,G
  st (for h,k being Element of G st h in H & k in K
      holds phi.(<*h,k*>) = k*h)
  holds phi is one-to-one iff H /\ K = (1).G
proof
  let G be Group;
  let H,K be Subgroup of G;
  let phi be Function of product <*H,K*>,G;
  assume A1: for h,k being Element of G st h in H & k in K
  holds phi.(<*h,k*>) = k*h;

  set P = product <*H,K*>;
  A2: 1_G in H & 1_G in K by GROUP_2:46;
  
  thus phi is one-to-one implies H /\ K = (1).G
  proof
    assume phi is one-to-one;
    then B2: for x,y being Element of product <*H,K*>
    st phi.x = phi.y holds x=y by GROUP_6:1;
    H /\ K <> (1).G implies contradiction
    proof
      assume B3: H /\ K <> (1).G;
      (1).G is Subgroup of H /\ K by GROUP_2:65;
      then consider h1 being Element of G such that
      B4: h1 in H /\ K & not h1 in (1).G by B3, Th51;
      B5: h1 in H & h1 in K by B4, GROUP_2:82;
      B6: h1 <> 1_G by B4, GROUP_5:1;
      B7: 1_G in H & 1_G in K by GROUP_2:46;
      h1 in H & h1" in K by B5, GROUP_2:51; then
      B9: <*h1,h1"*> in product <*H,K*> & <*1_G,1_G*> in product <*H,K*>
      by B7,Th50;
      h1" in K by B5, GROUP_2:51;
      then h1 in H & h1" in K by B5;
      then phi.(<*h1,h1"*>) = (h1") * h1 by A1
      .= 1_G by GROUP_1:def 5
      .= (1_G) * (1_G) by GROUP_1:def 4
      .= phi.(<*1_G, 1_G*>) by A1,A2;
      then <*h1, h1"*> = <*1_G,1_G*> by B2,B9;
      then h1 = 1_G & h1" = 1_G by Lm1;
      hence contradiction by B6;
    end;
    hence H /\ K = (1).G;
  end;
  thus H /\ K = (1).G implies phi is one-to-one
  proof
    assume B1: H /\ K = (1).G;
    for x,y being Element of product <*H,K*>
    st phi.x = phi.y
    holds x = y
    proof
      let x,y be Element of product <*H,K*>;
      assume B2: phi.x = phi.y;
      x.1 in H & x.2 in K & dom x = {1,2} by th4a;
      then x.1 in H & x.1 in G & x.2 in K & x.2 in G & dom x = {1,2}
      by GROUP_2:41;
      then consider h1,k1 being Element of G such that
      B3a: x.1 = h1 & x.2 = k1 & h1 in H & k1 in K;
      dom x = {1,2} by th4a;
      then len x = 2 by FINSEQ_1:2,def 3;
      then x = <*h1,k1*> by B3a,FINSEQ_1:44;
      then B3: x.1 = h1 & x.2 = k1 & h1 in H & k1 in K & x = <*h1,k1*>
      by B3a;
      y.1 in H & y.2 in K & dom y = {1,2} by th4a;
      then y.1 in G & y.1 in H & y.2 in G & y.2 in K & dom y = {1,2}
      by GROUP_2:41;
      then consider h2,k2 being Element of G such that
      B4a: y.1 = h2 & y.2 = k2 & h2 in H & k2 in K;
      dom y = {1,2} by th4a;
      then len y = 2 by FINSEQ_1:2,def 3;
      then y = <*h2,k2*> by B4a,FINSEQ_1:44;
      then B4: y.1 = h2 & y.2 = k2 & h2 in H & k2 in K & y = <*h2,k2*>
      by B4a;
      k1*h1 = phi.x by A1,B3
      .= k2*h2 by A1,B2,B4;
      then (k1 * h1) * (h2") = k2 * (h2 * (h2")) by GROUP_1:def 3
      .= k2 * 1_G by GROUP_1:def 5
      .= k2 by GROUP_1:def 4;
      then B6: (k1") * k2 = ((k1") * (k1 * h1)) * (h2") by GROUP_1:def 3
      .= (((k1") * k1) * h1) * (h2") by GROUP_1:def 3
      .= ((k1") * k1) * (h1 * (h2")) by GROUP_1:def 3
      .= 1_G * (h1 * (h2")) by GROUP_1:def 5
      .= h1 * (h2") by GROUP_1:def 4;
      k2 in K & k1" in K by B3,B4, GROUP_2:51;
      then B7: (k1") * k2 in K by GROUP_2:50;
      then B8: h1 * (h2") in K by B6;
      h1 in H & h2" in H by B3,B4, GROUP_2:51;
      then B9: h1 * (h2") in H by GROUP_2:50;
      then B10: (k1") * k2 in H by B6;
      h1 * (h2") in H /\ K by B8,B9,GROUP_2:82;
      then  h1 * (h2") = 1_G by B1,GROUP_5:1;
      then B11: h1 = (h2")" by GROUP_1:12
      .= h2;
      (k1") * k2 in H /\ K by B7,B10,GROUP_2:82;
      then (k1") * k2 = 1_G by B1,GROUP_5:1;
      then k2 = (k1")" by GROUP_1:12
      .= k1;
      then <*h1,k1*> = <*h2,k2*> by B11;
      hence x = y by B3,B4;
    end;

    hence phi is one-to-one by GROUP_6:1;
  end;
end;

theorem Th56:
  for G being Group
  for H,K being Subgroup of G
  ex phi being Function of product (Carrier <*H,K*>),G
  st (for h,k being Element of G st h in H & k in K
      holds phi.(<*h,k*>) = k*h)
  & (phi is one-to-one iff H /\ K = (1).G)
proof
  let G be Group;
  let H,K be Subgroup of G;
  defpred P[Element of product (Carrier <*H,K*>), Element of G] means
  ex h,k being Element of G st h in H & k in K & $1 = <*h,k*> & $2 = k*h;

  A1: for x being Element of product (Carrier <*H,K*>)
  ex y being Element of G
  st P[x, y]
  proof
    let x be Element of product (Carrier <*H,K*>);
    x in product (Carrier <*H,K*>);
    then x in the carrier of product <*H,K*> by GROUP_7:def 2;
    then x in product (<*H,K*>);
    then B1: x.1 in H & x.2 in K & dom x = {1,2} by th4a;
    then x.1 in H & x.1 in G & x.2 in K & x.2 in G by GROUP_2:40;
    then consider h,k being Element of G such that
    B2: h = x.1 & k = x.2 & h in H & k in K;
    len x = 2 by B1,FINSEQ_1:2,def 3;
    then B3: x = <*h, k*> by B2, FINSEQ_1:44;
    take y = k * h;
    thus P[x, y] by B2,B3;
  end;

  consider phi being Function of product (Carrier <*H,K*>),G such that
  A2: for x being Element of product (Carrier <*H,K*>) holds P[x, phi.x]
  from FUNCT_2:sch 3(A1);
  take phi;

  A3: for h,k being Element of G st h in H & k in K
  holds phi.(<*h,k*>) = k*h
  proof
    let h,k be Element of G;
    assume B1: h in H;
    assume B2: k in K;

    <*h,k*> in product <*H,K*> by B1,B2,Th50; then
    <*h,k*> in the carrier of product <*H,K*>; then
    reconsider x=<*h,k*> as Element of product (Carrier <*H,K*>)
    by GROUP_7:def 2;
    P[x, phi.x] by A2;
    then consider h1,k1 being Element of G such that
    B3: h1 in H & k1 in K & x = <*h1,k1*> & phi.x = k1*h1;
    h1 = h & k1 = k by B3,Lm1;
    hence phi.(<*h,k*>) = k*h by B3;
  end;

  thus for h,k being Element of G st h in H & k in K
  holds phi.(<*h,k*>) = k*h by A3;

  the carrier of product <*H,K*> = product (Carrier <*H,K*>) by GROUP_7:def 2;
  then phi is Function of product <*H,K*>,G;
  hence phi is one-to-one iff H /\ K = (1).G by A3,Th55b;
end;

theorem Th57:
  for G being Group
  for H being Subgroup of G
  for N being strict normal Subgroup of G
  for phi being Homomorphism of H,AutGroup N
  ex psi being Function of semidirect_product (H,N,phi),G
  st (for h,n being Element of G st h in H & n in N
      holds psi.(<*h,n*>) = n*h)
  & (psi is one-to-one iff H /\ N = (1).G)
proof
  let G be Group;
  let H be Subgroup of G;
  let N be strict normal Subgroup of G;
  let phi be Homomorphism of H, AutGroup N;
  A1: for f being Function of product (Carrier <*H,N*>),G
  holds f is Function of semidirect_product (H,N,phi), G
  proof
    let f be Function of product (Carrier <*H,N*>),G;
    the carrier of semidirect_product (H,N,phi) = product (Carrier <*H,N*>)
    by Def1;
    hence f is Function of semidirect_product (H,N,phi), G;
  end;
  consider psi being Function of product (Carrier <*H,N*>),G such
  that
  A2: (for h,n being Element of G st h in H & n in N holds psi.(<*h,n*>)=n*h)
  & (psi is one-to-one iff H /\ N = (1).G)
  by Th56;
  reconsider psi as Function of semidirect_product (H,N,phi), G by A1;
  take psi;
  thus thesis by A2;
end;

theorem Lm60:
  for G being Group
  for H1,H2 being Subgroup of G
  holds H1*H2 c= the carrier of H1 "\/" H2
proof
  let G be Group;
  let H1,H2 be Subgroup of G;
  H1 "\/" H2 = gr (H1 * H2) by GROUP_4:50;
  hence H1 * H2 c= the carrier of H1 "\/" H2 by GROUP_4:def 4;
end;


theorem Th60:
  for G being Group
  for H1,H2 being Subgroup of G
  holds H1*H2 c= the carrier of H1 "\/" H2
  & H2 * H1  c= the carrier of H1 "\/" H2
proof
  let G be Group;
  let H1,H2 be Subgroup of G;
  thus H1*H2 c= the carrier of H1 "\/" H2 by Lm60;
  H2 * H1 c= the carrier of H2 "\/" H1 by Lm60;
  hence H2 * H1 c= the carrier of H1 "\/" H2 by GROUP_4:56;
end;

theorem Th88:
  for G being Group
  for H1,H2 being Subgroup of G
  st H1 * H2 = the carrier of (H1 "\/" H2)
  holds H1 * H2 = H2 * H1
proof
  let G be Group;
  let H1,H2 be Subgroup of G;
  assume A1: H1 * H2 = the carrier of (H1 "\/" H2);

  then A2: H2 * H1 c= H1 * H2 by Th60;
  for x being Element of G st x in H1 * H2 holds x in H2 * H1
  proof
    let x be Element of G;
    assume x in H1 * H2;
    then x in H1 "\/" H2 by A1;
    then x" in H1 "\/" H2 by GROUP_2:51;
    then x" in H1 * H2 by A1;
    then consider h1,h2 being Element of G such that
    B2: x" = h1*h2 & h1 in H1 & h2 in H2
    by GROUP_5:4;
    B3: x = (x")"
         .= (h2") * (h1") by B2, GROUP_1:17;
    h2" in H2 & h1" in H1 by B2,GROUP_2:51;
    hence x in H2 * H1 by B3,GROUP_5:4;
  end;

  then A3: H1 * H2 c= H2 * H1;
  thus thesis by A2,A3,XBOOLE_0:def 10;
end;

theorem Th89:
  for G being Group
  for H being Subgroup of G
  for N being normal Subgroup of G
  st H,N are_complements_in G
  holds N * H = the carrier of G
proof
  let G be Group;
  let H be Subgroup of G;
  let N be normal Subgroup of G;
  assume A1: H,N are_complements_in G;
  A2: H * N = the carrier of G by A1;
  H "\/" N = the multMagma of G by A1,Th26; then
  A3: the carrier of (H "\/" N) = the carrier of G;
  H * N = the carrier of (H "\/" N) by A2,A3;
  then H * N = N * H by Th88;
  hence thesis by A2;
end;

theorem Th90:
  for G being Group
  for H being Subgroup of G
  for N being normal Subgroup of G
  st (H /\ N = (1).G & N * H = the carrier of G)
  holds H,N are_complements_in G
proof
  let G be Group;
  let H be Subgroup of G;
  let N be normal Subgroup of G;
  assume A1: H /\ N = (1).G;
  assume A2: N * H = the carrier of G;
  then H * N = the carrier of G by GROUP_5:8;
  hence H,N are_complements_in G by A1, Def7;
end;


:: Aschbacher, Theorem 10.2
theorem Th70:
  for G being Group
  for N being strict normal Subgroup of G
  for H being Subgroup of G
  st H,N are_complements_in G
  for alpha being Homomorphism of H,AutGroup N
  st (for h,n being Element of G st h in H & n in N
      holds (alpha/.h).n = n |^ (h"))
  ex beta being Homomorphism of semidirect_product(H,N,alpha), G
  st (for gh,gn being Element of G
      for h being Element of H
      for n being Element of N st gh = h & gn = n
      holds beta.(<*h,n*>) = gn*gh)
   & beta is bijective
proof
  let G be Group;
  let N be strict normal Subgroup of G;
  let H be Subgroup of G;
  assume A1: H,N are_complements_in G;
  let alpha be Homomorphism of H,AutGroup N;
  assume A2: for h,n being Element of G st h in H & n in N
  holds (alpha/.h).n = n |^ (h");
  set S = semidirect_product(H,N,alpha);
  consider beta being Function of S, G such that
  A3: (for h,n being Element of G st h in H & n in N
       holds beta.(<*h,n*>) = n*h)
  & (beta is one-to-one iff H /\ N = (1).G)
  by Th57;

  for x,y being Element of S
  holds beta.(x * y) = (beta.x) * (beta.y)
  proof
    let x,y be Element of S;
    consider a1 being Element of H, n1 being Element of N such that
    B1: x = <*a1,n1*> by th5;
    a1 is Element of G & n1 is Element of G by GROUP_2:42;
    then reconsider ga1=a1,gn1=n1 as Element of G;
    consider a2 being Element of H, n2 being Element of N such that
    B2: y = <*a2,n2*> by th5;
    a2 is Element of G & n2 is Element of G by GROUP_2:42;
    then reconsider ga2=a2,gn2=n2 as Element of G;
    B6: ga1 in H & gn1 in N & ga2 in H & gn2 in N;
    B7: gn2 |^ (ga1") = (((ga1")")*gn2) * (ga1") by GROUP_3:def 2
                     .= (ga1 * gn2) * (ga1");
    beta.x = gn1 * ga1 by B1,B6,A3;
    then beta.x = gn1*ga1 & beta.y = gn2*ga2 by A3,B2,B6;
    then B5: (beta.x) * (beta.y) = gn1 * ga1 * gn2 * ga2 by GROUP_1:def 3
    .= gn1 * ga1 * gn2 * (1_G) * ga2 by GROUP_1:def 4
    .= gn1 * ga1 * gn2 * ((ga1") * ga1) * ga2 by GROUP_1:def 5
    .= gn1 * ga1 * gn2 * (ga1") * ga1 * ga2 by GROUP_1:def 3
    .= gn1 * (ga1 * gn2) * (ga1") * ga1 * ga2 by GROUP_1:def 3
    .= gn1 * (gn2 |^ (ga1")) * ga1 * ga2 by B7, GROUP_1:def 3;
    B4: ((alpha /. ga1) . gn2) = gn2 |^ (ga1") & gn2 |^ (ga1") in N
    by A2,B6,th23;
    then B10: ga1 * ga2 in H & gn1 * (gn2 |^ (ga1")) in N by B6, GROUP_2:50;
    B11: n1 * ((alpha/.a1).n2) = (the multF of G).(gn1, (alpha/.ga1).gn2)
    proof
      alpha/.a1 = alpha/.ga1 by B6, Def2a;
      then C1: (alpha/.a1).n2 = (alpha/.ga1).gn2;
      (alpha/.ga1).gn2 in N by C1;
      then (alpha/.ga1).gn2 in G by GROUP_2:40;
      then reconsider y = (alpha/.ga1).gn2 as Element of G;
      reconsider x = (alpha/.a1).n2 as Element of N;
      gn1 * y = (the multF of G).(gn1, (alpha/.ga1).gn2);
      hence thesis by C1, GROUP_2:43;
    end;
    x * y = <*a1*a2, n1 * ((alpha/.a1)/.n2) *> by B1,B2,th6
         .= <* ga1*ga2, gn1 * (gn2 |^ (ga1")) *> by B4, B11, GROUP_2:43;
    then beta.(x * y) = (gn1 * (gn2 |^ (ga1"))) * (ga1 * ga2) by A3,B10
                     .= (beta.x) * (beta.y) by B5, GROUP_1:def 3;
    hence beta.(x * y) = (beta.x) * (beta.y);
  end;

  then reconsider beta as Homomorphism of S, G by GROUP_6:def 6;
  take beta;

  (for h,n being Element of G st h in H & n in N holds beta.(<*h,n*>) = n*h)
  implies (for gh,gn being Element of G
  for h being Element of H
  for n being Element of N st gh = h & gn = n
  holds beta.(<*h,n*>) = gn*gh)
  proof
    assume for h,n being Element of G st h in H & n in N
    holds beta.(<*h,n*>) = n*h;
    let gh,gn be Element of G;
    let h be Element of H;
    let n be Element of N;
    assume B2: gh = h;
    assume B3: gn = n;
    gh in H & gn in N by B2,B3;
    then B5: beta.(<*gh,gn*>) = gn*gh by A3;
    <*gh,gn*> = <*h,n*> by B2,B3;
    hence beta.(<*h,n*>) = gn*gh by B5;
  end;
  hence (for gh,gn being Element of G
  for h being Element of H
  for n being Element of N st gh = h & gn = n
  holds beta.(<*h,n*>) = gn*gh) by A3;

  H /\ N = (1).G by A1;
  then A4: beta is one-to-one by A3;

  for y being Element of G
  ex x being Element of S
  st beta.x = y
  proof
    let y be Element of G;
    y in the carrier of G;
    then y in N * H by A1,Th89;
    then consider n,h being Element of G such that
    B1: y = n * h & n in N & h in H
    by GROUP_5:4;
    reconsider h1=h as Element of H by B1;
    reconsider n1=n as Element of N by B1;
    reconsider x = <*h1,n1*> as Element of S by th3;
    take x;
    thus beta.x = y by B1,A3;
  end;

  then beta is onto by LmOnto;
  hence thesis by A4;
end;

begin :: Dedekind Modular property

::$N Dedekinds ``Modular'' Identity
theorem DedekindModularProperty:
  for G being Group
  for A,B,C being Subgroup of G
  st A is Subgroup of C
  holds A * (B /\ C) = (A * B) /\ carr C
proof
  let G be Group;
  let A,B,C be Subgroup of G;
  assume A is Subgroup of C;
  then the carrier of A c= the carrier of C by GROUP_2:def 5;
  then the carrier of A c= carr C by GROUP_2:def 9;
  then A2: carr A c= carr C by GROUP_2:def 9;
  for x being Element of G
  st x in A * (B /\ C)
  holds x in (A * B) /\ carr C
  proof
    let x be Element of G;
    assume x in A * (B /\ C);
    then x in (carr A) * carr (B /\ C) by GROUP_4:def 8;
    then consider a,b being Element of G such that
    B2: x = a*b & a in carr A & b in carr (B /\ C)
    by GROUP_2:8;
    b in carr (B /\ C) by B2;
    then b in carr B /\ carr C by GROUP_2:81;
    then B3: b in carr B & b in carr C by XBOOLE_0:def 4;
    then x in (carr A)*(carr B) by B2, GROUP_2:8;
    then B4: x in A*B by GROUP_4:def 8;
    a in carr C by B2, A2;
    then x in carr C by B2,B3,GROUP_2:74;
    hence x in (A * B) /\ carr C by B4, XBOOLE_0:def 4;
  end;
  then A3: A * (B /\ C) c= (A * B) /\ carr C;
  for x being Element of G
  st x in (A * B) /\ carr C
  holds x in A * (B /\ C)
  proof
    let x be Element of G;
    assume B1: x in (A * B) /\ carr C;
    then x in A * B & x in carr C by XBOOLE_0:def 4;
    then x in (carr A) * (carr B) by GROUP_4:def 8;
    then consider a,b being Element of G such that
    B2: x = a * b & a in carr A & b in carr B by GROUP_2:8;
    a" in carr A by B2, GROUP_2:75;
    then a" in carr C by A2;
    then a" in carr C & x in carr C by B1, XBOOLE_0:def 4;
    then (a") * x in carr C by GROUP_2:74;
    then B3: (a") * x in C by GROUP_2:def 9;
    b = 1_G * b by GROUP_1:def 4
     .= (a" * a) * b by GROUP_1:def 5
     .= (a") * x by B2, GROUP_1:def 3;
    then b in C by B3;
    then b in B & b in C by B2, GROUP_2:def 9;
    then b in B /\ C by GROUP_2:82;
    then b in carr (B /\ C) by GROUP_2:def 9;
    then a in carr A & b in carr (B /\ C) by B2;
    then a * b in (carr A) * carr (B /\ C) by GROUP_2:8;
    then a * b in A * (B /\ C) by GROUP_4:def 8;
    hence thesis by B2;
  end;
  then (A * B) /\ carr C c= A * (B /\ C);
  hence A * (B /\ C) = (A * B) /\ carr C by A3, XBOOLE_0:def 10;
end;



begin :: Universal Property of Semidirect Products

:: :: pr1, pr2 from FUNCT_3 may be useful?
:: :: delta from FUNCT_3 may be useful?
:: Bourbaki, General Topology, Chapter 3, section 2, p.240, Proposition 27
::$N Universal Property of Semidirect Products (Bourbaki~\cite[III \S2.10]{bourbaki2013general})
:: theorem
::   for H,G being Group
::   for N being strict Group
::   for f being Homomorphism of N,G
::   for g being Homomorphism of H,G
::   for phi being Homomorphism of H,AutGroup N
::   st (for n being Element of N
::       for h being Element of H
::       holds f.((phi/.h).n) = (g.h) * (f.n) * (g.(h")))
::   ex k being Homomorphism of semidirect_product(H,N,phi), G
::   st f = k * (incl2 (H,N, phi)) & g = k * (incl1 (H, N, phi))
:: proof
::   let H,G be Group;
::   let N be strict Group;
::   let f be Homomorphism of N,G;
::   let g be Homomorphism of H,G;
::   let phi be Homomorphism of H,AutGroup N;
::   assume A1: for n being Element of N
::   for h being Element of H
::   holds f.((phi/.h).n) = (g.h) * (f.n) * (g.(h"));
::   consider k being Homomorphism of semidirect_product(H,N,phi), G such that
::   not contradiction;
::   take k;
::   thus f = k * (incl2 (H,N, phi));
::   thus g = k * (incl1 (H, N, phi));
:: end;


begin :: Results concerning Transversals

registration
  let G be Group;
  let H be Subgroup of G;
  cluster the carrier of H -> non empty;
  correctness
  proof
    1_H in H by GROUP_1:def 4;
    hence thesis;
  end;
end;

registration
  let G be Group;
  let H be Subgroup of G;
  cluster carr H -> non empty;
  correctness
  proof
    the carrier of H is non empty;
    hence thesis by GROUP_2:def 9;
  end;
end;

registration
  let G be finite Group;
  cluster the carrier of G -> finite;
  correctness;
  let H be Subgroup of G;
  cluster Index H -> finite;
  correctness
  proof
    Left_Cosets H is finite by GROUP_2:134;
    then card (Left_Cosets H) is finite;
    hence Index H is finite by GROUP_2:def 17;
  end;
end;

theorem Th104:
  for G being Group
  for N being normal Subgroup of G
  ex x being Function of G./.N,G
  st ((nat_hom N) * x = id (G./.N))
  & (for a being Element of G./.N holds a = (x.a)*N)
proof
  let G be Group;
  let N be normal Subgroup of G;

  defpred Px[Element of G./.N, Element of G] means
  $1 = $2*N;

  A1: for a being Element of G./.N
  ex g being Element of G
  st Px[a, g]
  proof
    let a be Element of G./.N;
    a in G./.N; then
    consider g being Element of G such that
    B1: a = g*N & a = N*g by GROUP_6:23;
    take g;
    thus Px[a, g] by B1;
  end;

  consider x being Function of G./.N,G such that
  A2: for a being Element of G./.N holds Px[a, x.a]
  from FUNCT_2:sch 3(A1);
  take x;

  for a being Element of G./.N
  holds ((nat_hom N) * x).a = (id (G./.N)).a
  proof
    let a be Element of G./.N;
    a = (x.a) * N by A2
     .= (nat_hom N).(x.a) by GROUP_6:def 8
     .= ((nat_hom N) * x).a by FUNCT_2:15;
    hence thesis;
  end;
  hence (nat_hom N) * x = id (G./.N) by FUNCT_2:def 7;
  thus thesis by A2;
end;

:: Existence of a Left Transversal
theorem Th74:
  for G being Group
  for A being Subgroup of G
  for g0 being Element of G st g0 in A
  ex x being Function of Left_Cosets A,G
  st (for a being Element of Left_Cosets A holds a = (x/.a)*A)
  & x.(carr A) = g0
proof
  let G be Group;
  let A be Subgroup of G;
  let g0 be Element of G;
  assume A0: g0 in A;

  defpred P[Element of Left_Cosets A, Element of G] means
  ($1 = carr A & $2 = g0) or ($1 <> carr A & $1 = $2*A);

  A1: for a being Element of Left_Cosets A
  ex g being Element of G
  st P[a, g]
  proof
    let a be Element of Left_Cosets A;
    per cases;
    suppose B1: a = carr A;
      take g = g0;
      thus P[a, g] by B1;
    end;
    suppose B2: a <> carr A;
      a in Left_Cosets A; then
      consider g being Element of G such that
      B3: a = g*A by GROUP_2:def 15;
      take g;
      thus P[a, g] by B2,B3;
    end;
  end;

  consider x being Function of Left_Cosets A,G such that
  A2: for a being Element of Left_Cosets A holds P[a, x.a]
  from FUNCT_2:sch 3(A1);
  take x;
  thus for a being Element of Left_Cosets A holds a = (x/.a)*A
  proof
    let a be Element of Left_Cosets A;
    per cases;
    suppose B1: a = carr A;
      then x.a = g0 by A2;
      hence (x/.a)*A = (g0)*A
                    .= carr A by A0, GROUP_2:113
                    .= a by B1;
    end;
    suppose B2: a <> carr A;
      hence thesis by A2;
    end;
  end;
  carr A in Left_Cosets A by GROUP_2:135; then
  reconsider a=carr A as Element of Left_Cosets A;
  thus x.(carr A) = x.a
                 .= g0 by A2;
end;

theorem Th75:
  for G being Group
  for A being Subgroup of G
  holds A * A = carr A
proof
  let G be Group;
  let A be Subgroup of G;

  for g being Element of G
  holds g in A * A iff g in carr A
  proof
    let g be Element of G;
    hereby 
      assume g in A * A;
      then ex a1,a2 being Element of G st g = a1*a2 & a1 in A & a2 in A by GROUP_5:4;
      then g in A by GROUP_2:50;
      hence g in carr A by GROUP_2:def 9;
    end;
    assume g in carr A;
    then g in the carrier of A by GROUP_2:def 9;
    then g in A & 1_G in A by GROUP_2:46;
    then g * (1_G) in A*A by GROUP_5:4;
    hence g in A * A by GROUP_1:def 4;
  end;
  hence A * A = carr A by SUBSET_1:3;
end;

theorem Th76:
  for G being Group
  for A,B being Subgroup of G st B is Subgroup of A
  holds A * B = carr A & B * A = carr A
proof
  let G be Group;
  let A,B be Subgroup of G;
  assume A1: B is Subgroup of A;

  A2: A * B c= carr A & B * A c= carr A
  proof
    the carrier of B c= the carrier of A by A1, GROUP_2:def 5;
    then carr B c= the carrier of A by GROUP_2:def 9;
    then B1: carr B c= carr A by GROUP_2:def 9;
    B2: carr A c= carr A;
    (carr A) * (carr B) c= (carr A) * (carr A) by B1,B2,GROUP_3:4;
    then A * B c= (carr A) * (carr A) by GROUP_4:def 8;
    then A * B c= A * A by GROUP_4:def 8;
    hence A * B c= carr A by Th75;
    (carr B) * (carr A) c= (carr A) * (carr A) by B1,B2,GROUP_3:4;
    then B * A c= (carr A) * (carr A) by GROUP_4:def 8;
    then B * A c= A * A by GROUP_4:def 8;
    hence B * A c= carr A by Th75;
  end;

  for g being Element of G
  st g in carr A
  holds g in A * B
  proof
    let g be Element of G;
    assume B1: g in carr A;
    then g in the carrier of A by GROUP_2:def 9;
    then g in A & 1_G in B by GROUP_2:46;
    then g * (1_G) in A * B by GROUP_5:4;
    hence g in A * B by GROUP_1:def 4;
  end;
  then carr A c= A * B by SUBSET_1:2;
  hence A * B = carr A by A2, XBOOLE_0:def 10;
  
  for g being Element of G
  st g in carr A
  holds g in B * A
  proof
    let g be Element of G;
    assume B1: g in carr A;
    then g in the carrier of A by GROUP_2:def 9;
    then 1_G in B & g in A by GROUP_2:46;
    then (1_G) * g in B * A by GROUP_5:4;
    hence g in B * A by GROUP_1:def 4;
  end;
  then carr A c= B * A by SUBSET_1:2;
  hence B * A = carr A by A2, XBOOLE_0:def 10;
end;

theorem Th77:
  for G being Group
  for A,B being Subgroup of G st B is Subgroup of A
  for b being Element of Left_Cosets B
  holds b * A in Left_Cosets A
proof
  let G be Group;
  let A,B be Subgroup of G;
  assume A1: B is Subgroup of A;
  let b be Element of Left_Cosets B;
  consider g being Element of G such that
  A2: b = g * B by GROUP_2:def 15;
  b * A = (g * B) * A by A2
       .= g * (B * A) by GROUP_4:45
       .= g * (carr A) by A1,Th76
       .= g * A by GROUP_2:def 13;
  hence b * A in Left_Cosets A by GROUP_2:def 15;
end;


theorem Th77b:
  for G being Group
  for A,B being Subgroup of G st B is Subgroup of A
  for g being Element of G
  for b being Element of Left_Cosets B st b = g * B
  holds b * A = g * A
proof
  let G be Group;
  let A,B be Subgroup of G;
  assume A1: B is Subgroup of A;
  let g be Element of G;
  let b be Element of Left_Cosets B;
  assume A2: b = g * B;
  thus b * A = (g * B) * A by A2
            .= g * (B * A) by GROUP_4:45
            .= g * (carr A) by A1,Th76
            .= g * A by GROUP_2:def 13;
end;

:: theorem Th78:
::   for G being Group
::   for A,B being Subgroup of G st B is Subgroup of A
::   for B1 being Subgroup of A st the carrier of B = the carrier of B1
::   for b being Element of Left_Cosets B
::   ex a being Element of Left_Cosets A
::   st b c= a;

theorem Th81:
  for G being Group
  for A,B being Subgroup of G st B is Subgroup of A
  for B1 being Subgroup of A st the carrier of B = the carrier of B1
  for a being Element of A
  for g being Element of G
  st a = g
  holds a*B1 = g*B
proof
  let G be Group;
  let A,B be Subgroup of G;
  assume A1: B is Subgroup of A;
  let B1 be Subgroup of A;
  assume A2: the carrier of B = the carrier of B1;
  let a be Element of A;
  let g be Element of G;
  assume A3: a = g;
  for x being object
  holds x in a*B1 iff x in g*B
  proof
    let x be object;
    thus x in a*B1 implies x in g*B
    proof
      assume x in a*B1;
      then consider h1 being Element of A such that
      Z1: x = a*h1 & h1 in B1
      by GROUP_2:103;
      reconsider h=h1 as Element of G by GROUP_2:42;
      Z2: h in B by A2,Z1;
      a = g & h = h1 by A3; then
      g*h = a*h1 by GROUP_2:43
         .= x by Z1;
      hence x in g*B by Z2,GROUP_2:103;
    end;
    assume x in g*B;
    then consider h being Element of G such that
    Z1: x = g*h & h in B by GROUP_2:103;
    h in B1 by A2,Z1;
    then reconsider h1=h as Element of A by GROUP_2:42;
    Z2: h1 in B1 by A2,Z1;
    a = g & h = h1 by A3; then
    a*h1 = g*h by GROUP_2:43
        .= x by Z1;
    hence x in a*B1 by Z2, GROUP_2:103;
  end;

  hence a*B1 = g*B by TARSKI:2;
end;


theorem Th82:
  for G being Group
  for A being Subgroup of G
  for x,y,z being Element of G
  st x*y*A = x*z*A
  holds y*A = z*A
proof
  let G be Group;
  let A be Subgroup of G;
  let x,y,z be Element of G;
  assume A1: x*y*A = x*z*A;
  for w being Element of G holds w in y*A
  iff w in z*A
  proof
    let w be Element of G;
    thus w in y*A implies w in z*A
    proof
      assume Z1: w in y*A;
      then x*w in x*(y*A) by GROUP_2:27;
      then x*w in x*y*A by GROUP_2:105;
      then x*w in x*z*A by A1;
      then x*w in x*(z*A) by GROUP_2:105;
      then consider h being Element of G such that
      Z2: x*w = x*h & h in z*A by GROUP_2:27;
      w = h by Z2, GROUP_1:6;
      hence w in z*A by Z2,GROUP_2:27;
    end;
    assume Z1: w in z*A;
    then x*w in x*(z*A) by GROUP_2:27;
    then x*w in x*z*A by GROUP_2:105;
    then x*w in x*y*A by A1;
    then x*w in x*(y*A) by GROUP_2:105;
    then consider h being Element of G such that
    Z2: x*w = x*h & h in y*A by GROUP_2:27;
    w = h by Z2, GROUP_1:6;
    hence w in y*A by Z2,GROUP_2:27;
  end;
  hence thesis by SUBSET_1:3;
end;

theorem Th83a:
  for G1,G2 being Group
  for phi being Homomorphism of G1,G2
  for h being Element of G2 st h in rng phi
  ex g being Element of G1 st phi.g = h;
::>                                   *4

theorem Th83:
  for G being Group
  for A being Subgroup of G
  for f being Function of Left_Cosets A,G
  st (for a being Element of Left_Cosets A holds (f/.a)*A = a)
  for g being Element of G
  st g in rng f
  holds f.(g*A) = g
proof
  let G be Group;
  let A be Subgroup of G;
  let f be Function of Left_Cosets A,G;
  assume A1: for a being Element of Left_Cosets A holds (f/.a)*A = a;
  let g be Element of G;
  assume A2: g in rng f;
  ex a being Element of Left_Cosets A st g = f.a
  proof
    consider a0 being object such that
    B1: a0 in dom f & g = f.a0 by A2,FUNCT_1:def 3;
    dom f = Left_Cosets A by FUNCT_2:def 1;
    then reconsider a=a0 as Element of Left_Cosets A by B1;
    take a;
    thus thesis by B1;
  end;
  then consider a being Element of Left_Cosets A such that
  A3: g = f.a;
  a = (f/.a)*A by A1
   .= g*A by A3;
  hence f.(g*A) = g by A3;
end;

theorem Th84:
  for G1,G2 being Group
  for phi being Homomorphism of G1,G2
  holds rng phi c= the carrier of G2
proof
  let G1,G2 be Group;
  let phi be Homomorphism of G1,G2;
  rng phi = the carrier of Image phi by GROUP_6:44;
  hence rng phi c= the carrier of G2 by GROUP_2:def 5;
end;

theorem Th85:
  for G being Group
  for H,N being Subgroup of G st H,N are_complements_in G
  ex f being Function of Left_Cosets N,G
  st (f.(carr N) = 1_G)
  & (for n being Element of Left_Cosets N holds n = (f/.n)*N)
  & (for n being Element of Left_Cosets N holds f.n in H);
::>                                                     *4


theorem Th78c:
  for G being Group
  for A,B being Subgroup of G st B is Subgroup of A
  for B0 being Subgroup of A st the carrier of B = the carrier of B0
  for fa being Function of Left_Cosets A,G
  st (for a being Element of Left_Cosets A holds (fa/.a) * A = a)
  for fb being Function of Left_Cosets B0,A
  st (for b being Element of Left_Cosets B0 holds (fb/.b) * B0 = b)
  ex x being Function of Left_Cosets B,G
  st (for b being Element of Left_Cosets B holds b = (x/.b)*B)
  & (for rngB being Subset of G st rngB = rng fb holds rng(x) = (rng(fa))*rngB)
  & (for g,y being Element of G st g in A
     for a being Element of A st a = g & y = fb.(a * B0)
     holds x.(g*B) = (fa/.(carr A))*y)
proof
  let G be Group;
  let A,B be Subgroup of G;
  assume A1: B is Subgroup of A;
  let B1 be Subgroup of A;
  assume A2: the carrier of B = the carrier of B1;
  let fa be Function of Left_Cosets A,G;
  assume A3: for a being Element of Left_Cosets A holds (fa/.a) * A = a;
  let fb be Function of Left_Cosets B1,A;
  assume A4: for b being Element of Left_Cosets B1 holds (fb/.b) * B1 = b;

  set x = {[ya*yb*B,ya*yb] where ya,yb is Element of G : ya in rng fa &
  yb in rng fb};
  for z being object
  st z in x
  holds z in [: (Left_Cosets B), (the carrier of G) :]
  proof
    let z be object;
    assume Z1: z in x;
    then consider ya,yb being Element of G such that
    Z2: z = [ya*yb*B,ya*yb] & ya in rng fa & yb in rng fb;
    Z3: ya*yb*B in Left_Cosets B
    proof
      Y1: (ya*yb)*B in Left_Cosets B by GROUP_2:def 15;
      ya*yb*B = (ya*yb)*B by GROUP_2:105;
      hence thesis by Y1;
    end;
    ya*yb in the carrier of G;
    hence z in [: (Left_Cosets B), (the carrier of G) :]
    by Z2,Z3,ZFMISC_1:def 2;
  end;

  then x c= [:(Left_Cosets B),(the carrier of G) :] by TARSKI:def 3;
  then x is Subset of [:Left_Cosets B,the carrier of G:];
  then reconsider x as Relation of Left_Cosets B,the carrier of G;

  for b being Element of Left_Cosets B
  holds b in dom x
  proof
    let b be Element of Left_Cosets B;
    consider g being Element of G such that
    Z0: b = g*B by GROUP_2:def 15;

    ex xalpha, ybeta being Element of G
    st xalpha in rng(fa) & ybeta in rng(fb) & xalpha*ybeta*B = b
    & [b, xalpha*ybeta] in x
    proof
      ZZ1: b * A in Left_Cosets A by A1,Th77;
      then (fa/.(b * A)) * A = b * A by A3
      .= g * A by A1, Z0, Th77b;
      
      then ((fa/.(b * A)) ") * g in A by GROUP_2:114;
      then consider a0 being Element of G such that
      Z1: a0 = ((fa/.(b * A)) ") * g & a0 in A
      by GROUP_2:114;
      set xalpha = (fa/.(b * A));

      Z2: xalpha in rng fa
      proof
        Y1: dom fa = Left_Cosets A by FUNCT_2:def 1;
        Y2: (b*A) in Left_Cosets A & the carrier of G <> {} by A1,Th77; then
        fa.(b * A) in rng fa by FUNCT_2:4; then
        fa/.(b * A) in rng fa by Y1,Y2,PARTFUN1:def 6;
        hence xalpha in rng fa by PARTFUN1:def 6;
      end;

      a0 = (xalpha")*g by Z1;
      :: now construct ybeta
      a0 in A by Z1;
      then reconsider a1=a0 as Element of A;
      a1*B1 in Left_Cosets B1 by GROUP_2:def 15;
      then (fb/.(a1*B1)) * B1 = a1*B1 by A4;
      then consider yb being Element of A such that
      Z3: yb = fb/.(a1 * B1);

      a1*B1 in Left_Cosets B1 by GROUP_2:def 15; then
      Z3b: a1 * B1 = (fb/.(a1 * B1)) * B1 by A4
                  .= yb * B1 by Z3;
      then consider b0 being Element of A such that
      Z4: b0 = (yb") * a1 & b0 in B1
      by GROUP_2:114;
      Z5: yb in rng(fb)
      proof
        Y1: dom fb = Left_Cosets B1 by FUNCT_2:def 1;
        Y2: yb*B1 in Left_Cosets B1 & the carrier of A <> {} by GROUP_2:def 15; then
        fb.(yb * B1) in rng fb by FUNCT_2:4; then
        fb/.(yb * B1) in rng fb by Y1,Y2,PARTFUN1:def 6;
        hence yb in rng fb by Z3, Z3b;
      end;
      reconsider ybeta=yb as Element of G by GROUP_2:42;
      take xalpha,ybeta;
      thus xalpha in rng fa by Z2;
      thus ybeta in rng fb by Z5;
      Z6: xalpha * ybeta * B = b
      proof
        the carrier of B = the carrier of B1 & a1 = a0 by A2; then
        Y1: a1 * B1 = a0 * B by A1,A2,Th81;
        the carrier of B = the carrier of B1 & yb = ybeta by A1,A2; then
        Y2: ybeta * B = yb * B1 by A1,A2,Th81;
        
        ybeta * B = yb * B1 by Y2
        .= a1 * B1 by Z3b
        .= a0 * B by Y1
        .= ((xalpha")*g)*B by Z1
        .= (xalpha")*(g*B) by GROUP_2:105;
        then
        xalpha * (ybeta * B) = xalpha * ((xalpha")*(g*B))
        .= (xalpha * (xalpha")) * (g * B) by GROUP_2:32
        .= (1_G) * (g * B) by GROUP_1:def 5
        .= ((1_G) * g) * B by GROUP_2:105
        .= g * B by GROUP_1:def 4
        .= b by Z0;
        hence thesis by GROUP_2:105;
      end;
      then [xalpha*ybeta*B,xalpha*ybeta] in x by Z2,Z5;
      hence thesis by Z6;
    end;
    hence b in dom x by XTUPLE_0:def 12;
  end;
  then Left_Cosets B c= dom x;
  then A5a: dom x = Left_Cosets B by XBOOLE_0:def 10;
  then A5: x is quasi_total by FUNCT_2:def 1;

  for b,y1,y2 being object
  st [b,y1] in x & [b,y2] in x
  holds y1 = y2
  proof
    let b,y1,y2 be object;
    assume Z1: [b,y1] in x;
    then consider ya1,yb1 being Element of G such that
    Z2: [b,y1] = [ya1*yb1*B,ya1*yb1] & ya1 in rng fa & yb1 in rng fb;
    assume Z3: [b,y2] in x;
    then consider ya2,yb2 being Element of G such that
    Z4: [b,y2] = [ya2*yb2*B,ya2*yb2] & ya2 in rng fa & yb2 in rng fb;

    Z5: b = ya1*yb1*B & y1 = ya1*yb1 by Z2,XTUPLE_0:1;
    Z6: b = ya2*yb2*B & y2 = ya2*yb2 by Z4,XTUPLE_0:1;
    
    b in dom x by Z3, XTUPLE_0:def 12; then
    b in Left_Cosets B by A5a;
    then reconsider b as Element of Left_Cosets B;

    Z7: ya1 = ya2
    proof
      Y1: yb1 in A by Z2,Th84;
      Y2: b*A = ya1*yb1*A by A1,Z5,Th77b
             .= ya1*(yb1*A) by GROUP_2:105
             .= ya1*(carr A) by Y1, GROUP_2:113
             .= ya1*A by GROUP_2:def 13;
      Y3: yb2 in A by Z4,Th84;
      b*A = ya2*yb2*A by A1,Z6,Th77b
         .= ya2*(yb2*A) by GROUP_2:105
         .= ya2*(carr A) by Y3, GROUP_2:113
         .= ya2*A by GROUP_2:def 13;
      then Y4: ya1*A = ya2*A by Y2;
      dom fa = Left_Cosets A by FUNCT_2:def 1; then
      Y5: ya1*A in dom fa & ya2*A in dom fa by Y1,Y3,GROUP_2:def 15;
      thus ya1 = fa.(ya1*A) by A3,Z2,Th83
              .= fa.(ya2*A) by Y4,Y5
              .= ya2 by A3,Z4,Th83;
    end;

    Z8: yb1 = yb2
    proof
      ya1*yb1*B = ya1*yb2*B by Z5,Z6,Z7;
      then Y1: yb1*B = yb2*B by Th82;
      Y4: yb1 in A & yb2 in A by Z2,Z4,Th84; then
      reconsider bb1=yb1,bb2=yb2 as Element of A by GROUP_2:42;
      Y2: bb1*B1 = yb1*B by A1,A2,Th81
                .= yb2*B by Y1
                .= bb2*B1 by A1,A2,Th81;
      dom fb = Left_Cosets B1 by FUNCT_2:def 1; then
      Y3: bb1*B1 in dom fb & bb2*B1 in dom fb by Y4,GROUP_2:def 15;
      bb1 = fb.(bb1*B1) by A4,Z2,Th83
         .= fb.(bb2*B1) by Y2,Y3
         .= bb2 by A4,Z4,Th83;
      hence thesis;
    end;

    thus y1 = ya1*yb1 by Z5
           .= ya2*yb1 by Z7
           .= ya2*yb2 by Z8
           .= y2 by Z6;
  end;

  then A6: x is Function-like by FUNCT_1:def 1; then
  reconsider x as Function of Left_Cosets B,the carrier of G
  by A5, FUNCT_2:def 1, FUNCT_1:def 1;
  take x;

  :: OKAY, now that we've proven the existence of *some* function,
  :: let us prove we have *the* function we're hunting for...
  thus (for b being Element of Left_Cosets B holds b = (x/.b)*B)
  proof 
    let b be Element of Left_Cosets B;
    consider g being Element of G such that
    C1: b = g*B by GROUP_2:def 15;
    C2: b in dom x by A5a;
    then [b, x.b] in x by FUNCT_1:def 2;
    then consider a1,b1 being Element of G such that
    Z2: [b, x.b] = [a1*b1*B, a1*b1] & a1 in rng fa & b1 in rng fb;
    Z3: b = a1*b1*B & x.b = a1*b1 by Z2,XTUPLE_0:1;
    thus b = (x/.b)*B by Z3, C2, PARTFUN1:def 6;
  end;
  thus for rngB being Subset of G st rngB = rng fb
  holds rng(x) = (rng(fa))*rngB;
::>                           *4

  thus for g,y being Element of G st g in A
  for a being Element of A st a = g & y = fb.(a * B1)
  holds x.(g*B) = (fa/.(carr A))*y
  proof
    let g,y be Element of G;
    assume Z1: g in A;
    let a be Element of A;
    assume Z2: a = g;
    assume Z3: y = fb.(a * B1);
    Z6: g*B in Left_Cosets B by GROUP_2:def 15;
    then g*B in dom x by A5a;
    then [g*B, x.(g*B)] in x by FUNCT_1:def 2;
    then consider a1,b1 being Element of G such that
    Z4: [g*B, x.(g*B)] = [a1*b1*B, a1*b1] & a1 in rng fa & b1 in rng fb;
    Z5: g*B = a1*b1*B & x.(g*B) = a1*b1 by Z4,XTUPLE_0:1;

    :: ((a1*b1)")*g in B;

    reconsider gB=g*B as Element of Left_Cosets B;
::>                                             *4
    gB * A = g * A by A1,Th77b;
    
    thus x.(g*B) = (fa/.(carr A))*y;
::>                               *4
  end;
end;







theorem Th78b:
  for G being Group
  for H being Subgroup of G
  for a,b being Element of G
  st a in b*H holds a*H = b*H;
::>                         *4

theorem Th79b:
  for G being Group
  for H being Subgroup of G
  for N being Subgroup of G
  for N0 being Subgroup of H st the carrier of N = the carrier of N0
  for q being Element of G st q in H
  for q0 being Element of H st q0 = q
  holds q*N = q0*N0;
::>               *4

definition
  let G be Group;
  let B be Subgroup of G;
  let M be normal Subgroup of G;
  redefine func (B,M) `*` -> strict normal Subgroup of B;
  correctness by LmDef5c, GROUP_6:def 1;
end;

theorem Th79a:
  for G being Group
  for H being Subgroup of G
  for N being normal Subgroup of G st N is Subgroup of H
  for q being Element of G st q in H
  for q0 being Element of H st q0 = q
  holds q*N = q0*((H,N)`*`)
proof
  let G be Group;
  let H be Subgroup of G;
  let N be normal Subgroup of G;
  assume A1: N is Subgroup of H;
  let q be Element of G;
  assume A2: q in H;
  let q0 be Element of H;
  assume A3: q0 = q;
  the multMagma of N is strict Subgroup of H by A1,LmDef5a;
  then A4: (H,N)`*` is strict normal Subgroup of H
  & (H,N)`*` = the multMagma of N by GROUP_6:def 1;
  for x being object
  holds x in q*N iff x in q0*((H,N)`*`)
  proof
    let x be object;
    hereby 
      assume Z1: x in q*N;
      then consider g being Element of G such that
      Z2: x = q*g & g in N by GROUP_2:103;
      g in the carrier of N by Z2; then
      g in the carrier of ((H,N)`*`) by A4; then 
      Z3: g in ((H,N)`*`);
      then g in H by GROUP_2:40;
      then reconsider g0=g as Element of H by GROUP_2:42;
      q = q0 & g = g0 by A3;
      then q0*g0 = q * g by GROUP_2:43
                .= x by Z2;
      hence x in q0*((H,N)`*`) by Z3,GROUP_2:103;
    end;
    assume Z1: x in q0*((H,N)`*`);
    then consider g0 being Element of H such that
    Z2: x = q0 * g0 & g0 in ((H,N)`*`) by GROUP_2:103;
    g0 in the carrier of ((H,N)`*`) by Z2;
    then g0 in the carrier of N by A4; then
    Z3: g0 in N by Z2, GROUP_6:def 1;
    then g0 in G by GROUP_2:40;
    then reconsider g=g0 as Element of G by GROUP_2:42;
    q = q0 & g = g0 by A3; then
    q * g = q0 * g0 by GROUP_2:43
         .= x by Z2;
    hence x in q*N by Z3,GROUP_2:103;
  end;

  hence q*N = q0*((H,N)`*`) by TARSKI:2;
end;


theorem Th79:
  for G being Group
  for P being Subgroup of G
  for V being normal Subgroup of G st V is Subgroup of P
  for Q being Subgroup of P
  st Q,P./.((P,V)`*`) are_isomorphic & Q /\ V=(1).G
  & V * Q = the carrier of P
  ex x being Function of G./.V,G
  st (for a being Element of G./.V holds (x.a)*V = a)
  & (for q being Element of G st q in Q holds x.(q*V) = q)
proof
  let G be Group;
  let P be Subgroup of G;
  let V be normal Subgroup of G;
  assume V is Subgroup of P;
  then A1: V is normal Subgroup of P by GROUP_6:8;
  let Q be Subgroup of P;
  set J = ((P,V)`*`);
  assume A2: Q, P./.J are_isomorphic;
  assume A3: Q /\ V = (1).G;
  assume A3b: V * Q = the carrier of P;
  Q /\ V = (1).P by A3, GROUP_2:63;
  then 
  Q,V are_complements_in P by A1,A3,A3b,ThDef7Sub3, GROUP_2:63; then
  Q,J are_complements_in P by A1,ThDef7Sub6; then
  consider phi being Homomorphism of Q,P./.J such that
  A4: (for h being Element of Q
      for g being Element of P st g = h
      holds phi.h = g*J) &
      phi is bijective by Th28b;

  :: defpred P[Element of G./.V, Element of G] means
  :: (ex a0 being Element of P./.J st a0 = $1 & $2 = phi.a0)
  :: or (not (ex a0 being Element of P./.J st a0 = $1) & $1 = $2*V);

  defpred P[Element of G./.V, Element of G] means
  ($1 in P./.J & (ex a0 being Element of P./.J st a0 = $1 & $2 = phi.a0)
   & $2 in Q & $1 = $2*V)
  or (not ($1 in P./.J) & $1 = $2*V);

  A5: for x being Element of G./.V
  ex y being Element of G
  st P[x, y]
  proof
    let x be Element of G./.V;
    per cases;
    suppose B1: x in P./.J;
      then reconsider a0=x as Element of P./.J;
      (phi") is Homomorphism of (P./.J),Q by A4,GROUP_6:62; then
      B2: (phi").a0 in Q by B1,FUNCT_2:5;
      then reconsider y=(phi").a0 as Element of G by GROUP_2:42;
      take y;
      reconsider h=(phi").a0 as Element of Q by B2,GROUP_2:42;
      (phi") is Homomorphism of (P./.J),Q by A4,GROUP_6:62; then
      B2d: (phi") is Function of the carrier of (P./.J),the carrier of Q
      & the carrier of Q <> {}
      & a0 in the carrier of (P./.J);

      (phi") is Homomorphism of (P./.J),Q by A4,GROUP_6:62; then
      B2e: (phi") is Function of the carrier of (P./.J),the carrier of Q
      & the carrier of Q <> {} & rng phi = the carrier of (P./.J)
      & phi is one-to-one by A4, FUNCT_2:def 3;
      then phi * (phi") = id (the carrier of (P./.J)) by FUNCT_2:29;
      reconsider y0=y as Element of P by B2, GROUP_2:42;
      h in Q & y0 in P & y0 = h;
      B3f: h = y;
      B4: phi.h = phi.((phi").a0)
      .= (phi * (phi")).a0 by A4, B2d, FUNCT_2:15, FUNCT_2:def 3
      .= (id (the carrier of (P./.J))).a0 by A4, B2e, FUNCT_2:29, FUNCT_2:def 3
      .= a0
      .= x;
      then B3: x = y0*J by A4,B3f;
      x in (P./.J) by B1;
      phi.h = phi.y by B3f; :: then
      B5: (ex a1 being Element of P./.J st a1 = x & y = phi.a1)
      proof
        take a1=a0;
        thus a1 = x;
        phi.a1 
::>           *321
        thus y = phi.a1;
      end;
::>     *70
:: by B3f,B4;
      y in Q by B2;
      V is Subgroup of P & y in P by A1,B2,GROUP_2:40; then
      y*V = y0*J by A1, Th79a
         .= x by B3;
      hence thesis by B1, B2, B3, B5;
    end;
    suppose B2: not (x in P./.J);
      x in G./.V; then x in Left_Cosets V by GROUP_6:17;
      then
      consider y being Element of G such that
      B3: x = y * V by GROUP_2:def 15;
      take y;
      thus thesis by B2,B3;
    end;
  end;

  consider f being Function of G./.V,G such that
  A6: for x being Element of G./.V holds P[x, f.x]
  from FUNCT_2:sch 3(A5);

  take f;
  thus for a being Element of G./.V holds (f.a)*V = a by A6;

  thus for q being Element of G st q in Q holds f.(q*V) = q
  proof
    let q be Element of G;
    assume Z1: q in Q;
    q*V in Left_Cosets V by GROUP_2:def 15;
    then q*V in G./.V by GROUP_6:17; then
    reconsider a=q*V as Element of G./.V;

    q*V = (f.a)*V by A6;
    then Z2: (q")*(f.a) in V by GROUP_2:114;
    Z3: a in P./.J
    proof
      reconsider q0=q as Element of P by Z1, GROUP_2:42;
      q0*J in Left_Cosets J by GROUP_2:def 15;
      then C1: q0*J in P./.J by GROUP_6:17;
      q0 in P by Z1;
      q in P by Z1, GROUP_2:40;
      q = q0 & q in P & q0 in P & V is normal Subgroup of P
      by A1,Z1,GROUP_2:40;
      then q*V = q0*J by Th79a;
      hence thesis by C1;
    end;
    then f.a in Q by A6; then
    (q") in Q & f.a in Q by A6, Z1, GROUP_2:51;
    then (q")*(f.a) in Q by GROUP_2:50;
    then (q")*(f.a) in Q /\ V by Z2, GROUP_2:82;
    then (q")*(f.a) in (1).G by A3;
    then (q")*(f.a) = 1_G by GROUP_5:1;
    then q = q*((q")*(f.a)) by GROUP_1:def 4
          .= (q*(q"))*(f.a) by GROUP_1:def 3
          .= (1_G)*(f.a) by GROUP_1:def 5
          .= f.a by GROUP_1:def 4;
    hence thesis by A6;
  end;

end;
proof end;
::> *396



theorem Th80:
  for G being Group
  for P being Subgroup of G
  for V being normal Subgroup of G st V is Subgroup of P
  for Q being Subgroup of P
  st V*Q=P & Q*V=P
  ex x being Function of G./.V,G
  st (for a being Element of G./.V holds (x.a)*V = a)
  & (for q being Element of G st q in Q holds x.(q*V) = q)
proof
  let G be Group;
  let P be Subgroup of G;
  let V be normal Subgroup of G;
  assume V is Subgroup of P;
  then A1: V is normal Subgroup of P by GROUP_6:8;
  let Q be Subgroup of P;
  assume A2: V*Q=P;
  assume A3: Q*V=P;
  1_G in V by GROUP_2:46; then
  consider x being Function of Left_Cosets V,G such that
  A4: (for a being Element of Left_Cosets V holds a = (x/.a)*V) and
  A5: x.(carr V) = 1_G by Th74;
  the carrier of (G./.V) = Left_Cosets V by GROUP_6:17; then
  x is Function of the carrier of (G./.V),the carrier of G;
  then reconsider x1=x as Function of G./.V,G;

  A6: for a being Element of G./.V holds x.a=x1.a;

  take x1;
  thus (for a being Element of G./.V holds (x1.a)*V = a)
  proof 
    let a be Element of G./.V;
    a in the carrier of G./.V; then
    a in Left_Cosets V by GROUP_6:17; hence
    a = (x/.a)*V by A4,GROUP_6:17
     .= (x1/.a)*V by A6
     .= (x1.a)*V;
  end;
  let q be Element of G;
  assume Z4: q in Q;
  thus x1.(q*V) = q;
::>               *4
end;




begin :: Gaschutz's Theorem


for G being Group
for B being Subgroup of G
for M being normal Subgroup of G
holds (B,M) `*` is strict normal Subgroup of B;

reserve p for Nat;

theorem Th100:
  for p being Nat
  for G being Group
  holds (1).G is p-group
proof
  let p be Nat;
  let G be Group;
  ex r being Nat st card (1).G = p |^ r
  proof
    take 0;
    thus card (1).G = 1 by GROUP_2:69
                   .= p |^ 0 by NEWTON:4;
  end;
  hence (1).G is p-group by GROUP_10:def 17;
end;

registration
  let p be Nat;
  let G be Group;

  cluster commutative normal p-group for Subgroup of G;
  existence
  proof
    take H=(1).G;
    H is normal & H is p-group & H is commutative by Th100;
    hence thesis;
  end;
end;

theorem Th101:
  (1).A is commutative normal p-group Subgroup of A by Th100;

theorem Th102:
  for p being Prime
  for G being finite Group
  for V being commutative normal p-group Subgroup of G
  for P being Subgroup of G st P is_Sylow_p-subgroup_of_prime p
  holds V is Subgroup of P
proof
  let p be Prime;
  let G be finite Group;
  let V be commutative normal p-group Subgroup of G;
  let P be Subgroup of G;
  assume A1: P is_Sylow_p-subgroup_of_prime p;
  consider P1 being Subgroup of G such that
  A2: P1 is_Sylow_p-subgroup_of_prime p & V is Subgroup of P1
  by GROUP_10:12;
  P,P1 are_conjugated by A1,A2,GROUP_10:12;
  then consider g being Element of G such that
  A3: the multMagma of P = P1 |^ g
  by GROUP_3:def 11;
  for v being Element of G st v in V
  holds v in P
  proof
    let v be Element of G;
    assume v in V;
    then v |^ (g") in V by th23;
    then v |^ (g")  in P1 by A2, GROUP_2:40;
    then (v |^ (g")) |^ g in P1 |^ g by GROUP_3:58;
    then (v |^ (g")) |^ g in P by A3;
    hence v in P by GROUP_3:25;
  end;
  hence V is Subgroup of P by GROUP_2:58;
end;

theorem Th103:
  for G being Group
  for B being Subgroup of G
  for N being normal Subgroup of G
  holds N /\ B = B /\ N
proof
  let G be Group;
  let B be Subgroup of G;
  let N be normal Subgroup of G;
  the carrier of (B /\ N) = (carr N) /\ (carr B) by GROUP_2:def 10
                         .= the carrier of (N /\ B) by GROUP_2:def 10;
  hence B /\ N = N /\ B by GROUP_2:59;
end;

theorem Th105:
  for G being Group
  for N being normal Subgroup of G
  for a,b being Element of G
  holds (a*N) * (b*N) = (a*b)*N
proof
  let G be Group;
  let N be normal Subgroup of G;
  let a,b be Element of G;
  thus (a * N) * (b * N) = ((a * N) * b) * N by GROUP_3:9
                        .= (a * (N * b)) * N by GROUP_2:106
                        .= (a * (b * N)) * N by GROUP_3:117
                        .= ((a * b) * N) * N by GROUP_2:105
                        .= (a * b) * N by GROUP_6:5;
end;

theorem Th105b:
  for G being Group
  for N being normal Subgroup of G
  for g,h being Element of G
  for a,b being Element of (G./.N)
  st a = g*N & b = h*N
  holds a * b = (g * h)*N
proof
  let G be Group;
  let N be normal Subgroup of G;
  let g,h be Element of G;
  let a,b be Element of (G./.N);
  assume Z1: a = g*N;
  assume Z2: b = h*N;
  A1: a = (nat_hom N).g by Z1, GROUP_6:def 8;
  A2: b = (nat_hom N).h by Z2, GROUP_6:def 8;
  thus a * b = ((nat_hom N).g) * b by A1
  .= ((nat_hom N).g) * ((nat_hom N).h) by A2
  .= (nat_hom N).(g * h) by GROUP_6:def 6
  .= (g * h)*N by GROUP_6:def 8;
end;

theorem Th106:
  for G being Group
  for N being normal Subgroup of G
  for x being Function of G./.N,G
  st (for a being Element of G./.N holds a = (x.a)*N) 
  ex gamma being Function of [:the carrier of (G./.N),the carrier of (G./.N):],N
  st (for a,b being Element of G./.N
  for n,n1 being Element of N st n = x.(a * b)
  & n1 = gamma.(a,b)
  holds (x.a)*(x.b) = n * n1)
proof
  let G be Group;
  let N be normal Subgroup of G;
  let x be Function of G./.N,G;
  assume A1: (for a being Element of G./.N holds a = (x.a)*N);

  set UGN = the carrier of (G./.N);
  set UN = N;
  defpred P[Element of UGN, Element of UGN, Element of UN] means
  ex n being Element of G st n in N & (x.$1)*(x.$2) = (x.($1*$2))*n
  & n = $3;

  A2: for a,b being Element of UGN
  ex n being Element of UN
  st P[a, b, n]
  proof
    let a,b be Element of UGN;
    B1: a = (x.a) * N by A1;
    B2: b = (x.b) * N by A1;
    B3: x.(a * b) * N = a * b by A1
                     .= ((x.a) * (x.b)) * N by B1, B2, Th105b;
    then consider n being Element of G such that
    B4: n in N & ((x.a) * (x.b)) = (x.(a * b)) * n by Th30b;
    reconsider n0=n as Element of N by B4,GROUP_2:42;
    take n0;
    thus thesis by B4;
  end;

  consider gamma being Function of [:UGN,UGN:],UN such that
  A3: for x being Element of UGN
  for y being Element of UGN
  holds P[x, y, gamma.(x,y)]
  from BINOP_1:sch 3(A2);

  take gamma;
  let a,b be Element of G./.N;
  let n,n1 be Element of N;
  assume A4: n = x.(a * b);
  assume A5: n1 = gamma.(a,b);

  P[a,b,n1] by A1,A3,A5;
  then consider k being Element of G such that
  A6: k in N & (x.a)*(x.b) = (x.(a*b))*k & k = n1 by A3;

  thus thesis by A4,A5,A6, GROUP_2:43;
end;

theorem Th107:
  for G being Group
  for N being normal Subgroup of G
  for x being Function of G./.N,G
  st (for a being Element of G./.N holds a = (x.a)*N)
  ex gamma being Function of [:the carrier of (G./.N),the carrier of (G./.N):],G
  st (for a,b being Element of G./.N
  holds (x.a)*(x.b) = (x.(a * b)) * (gamma.(a,b))
  & gamma.(a,b) in N)
proof
  let G be Group;
  let N be normal Subgroup of G;
  let x be Function of G./.N,G;
  assume A1: (for a being Element of G./.N holds a = (x.a)*N);
  set UGN=the carrier of (G./.N);
  deffunc GAMMA(Element of UGN, Element of UGN)
  = ((x.($1 * $2))")*((x.($1))*(x.($2)));
  consider gamma being Function of [:UGN,UGN:],G
  such that
  A2: for a,b being Element of G./.N holds gamma.(a,b) = GAMMA(a,b)
  from BINOP_1:sch 4;
  take gamma;
  thus for a,b being Element of G./.N
  holds (x.a)*(x.b) = (x.(a * b))*(gamma.(a,b)) & gamma.(a,b) in N
  proof
    let a,b be Element of G./.N;
    thus B1: (x.(a * b))*(gamma.(a,b))
     = (x.(a * b))*(((x.(a * b))")*((x.a)*(x.b))) by A2
    .= ((x.(a * b)) * ((x.(a * b))")) * ((x.a) * (x.b)) by GROUP_1:def 3
    .= (1_G) * ((x.a) * (x.b)) by GROUP_1:def 5
    .= (x.a) * (x.b) by GROUP_1:def 4;

    B2: a = (x.a) * N by A1
    .= (nat_hom N).(x.a) by GROUP_6:def 8;
    B3: b = (x.b) * N by A1
    .= (nat_hom N).(x.b) by GROUP_6:def 8;

    (x.(a * b)) * N = a * b by A1
    .= ((nat_hom N).(x.a)) * ((nat_hom N).(x.b)) by B2,B3,GROUP_2:43
    .= (nat_hom N).((x.a) * (x.b)) by GROUP_6:def 6
    .= ((x.a) * (x.b))  * N by GROUP_6:def 8;
    then ((x.(a * b))") * ((x.a) * (x.b)) in N by GROUP_2:114;
    hence gamma.(a,b) in N by A2;
  end;
end;

theorem Th104:
  for G being Group
  for N being normal Subgroup of G
  for H being Subgroup of G st H /\ N = (1).G
  ex x being Function of G./.N,G
  st ((nat_hom N) * x = id (G./.N))
  & (for a being Element of G./.N holds a = (x.a)*N)
  & (for g being Element of G st g in H holds x.((nat_hom N).g) = g)
  :: & (for H being Subgroup of G st H /\ N = (1).G
  ::    for g being Element of G st g in H
  ::    holds x.((nat_hom N).g) = g)
proof
  let G be Group;
  let N be normal Subgroup of G;
  let H be Subgroup of G;
  assume A0: H /\ N = (1).G;
  deffunc F(Element of G./.N) = the Element of $1;

  defpred Px[Element of G./.N, Element of G] means
  $1 = $2*N;

  A1: for a being Element of G./.N
  ex g being Element of G
  st Px[a, g]
  proof
    let a be Element of G./.N;
    a in G./.N; then
    consider g being Element of G such that
    B1: a = g*N & a = N*g by GROUP_6:23;
    take g;
    thus Px[a, g] by B1;
  end;

  consider x being Function of G./.N,G such that
  A2: for a being Element of G./.N holds Px[a, x.a]
  from FUNCT_2:sch 3(A1);
  take x;

  for a being Element of G./.N
  holds ((nat_hom N) * x).a = (id (G./.N)).a
  proof
    let a be Element of G./.N;
    a = (x.a) * N by A2
     .= (nat_hom N).(x.a) by GROUP_6:def 8
     .= ((nat_hom N) * x).a by FUNCT_2:15;
    hence thesis;
  end;
  hence (nat_hom N) * x = id (G./.N) by FUNCT_2:def 7;
  thus (for a being Element of G./.N holds a = (x.a)*N) by A2;

  thus for g being Element of G st g in H holds x.((nat_hom N).g) = g
  proof
    let g be Element of G;
    assume Z1: g in H;
    thus x.((nat_hom N).g) = g;
::>                          *4
  end;
end;




:: theorem Th108:
::   for G1,G2 being Group
::   for H1 being Subgroup of G1
::   for phi being Homomorphism of G1,G2
::   ex H2 being Subgroup of G2
::   st ex f being Homomorphism of H1,H2
::   st the carrier of H2 = phi .: (the carrier of H1)
::   & f = phi | (the carrier of H1)
::   & f is onto;

LmUh2:
  for G1,G2 being Group
  for A being non empty Subset of G1
  for phi being Homomorphism of G1,G2
  holds phi|A is Function of A,G2
proof
  let G1,G2 be Group;
  let A be non empty Subset of G1;
  let phi be Homomorphism of G1,G2;
  thus phi|A is Function of A,G2 by FUNCT_2:32;
end;


:: definition
::   let G1,G2 be Group;
::   let phi be Homomorphism of G1,G2;
::   let A be non empty Subset of G1;
::   redefine func phi | A -> Function of A,G2;
::   coherence by LmUh2;
:: end;

:: theorem Th109:
::   for G1,G2 being Group
::   for H1 being Subgroup of G1
::   for H2 being Subgroup of G2
::   for phi being Homomorphism of G1,G2
::   for psi being Homomorphism of H1,H2
::   st psi = phi | (the carrier of H1)
::   holds Ker psi = H1 /\ Ker phi;



for G being Group
for B being Subgroup of G
for M being normal Subgroup of G
holds (B,M) `*` is strict normal Subgroup of B;

:: Wolfgang Gaschtz, "Zur Erweiterungstheorie der endlichen Gruppen". J. reine angew. Math. 190 (1952) 93107.
::$N Gasch\"{u}tz's Theorem
theorem
  for p being Prime
  for G being finite Group
  for V being normal Subgroup of G
  st V is commutative p-group
  for P being Subgroup of G st P is_Sylow_p-subgroup_of_prime p
  holds G splits_over V iff P splits_over V
proof
  let p be Prime;
  let G be finite Group;
  let V be normal Subgroup of G;
  assume A1: V is commutative p-group;
  let P be Subgroup of G;
  assume A2: P is_Sylow_p-subgroup_of_prime p;
  
  then V is Subgroup of P by A1,Th102; then
  V is normal Subgroup of P by GROUP_6:8;
  then V is normal Subgroup of P
  & the multMagma of V is strict Subgroup of P by LmDef5;
  then A3: (P,V)`*` is strict normal Subgroup of P & V is normal Subgroup of P
  by GROUP_6:def 1;

  thus G splits_over V implies P splits_over V
  proof
    assume Z1: G splits_over V;
    then consider H being Subgroup of G such that
    Z2: H,V are_complements_in G by Def5;
    H*V = the carrier of G by Z2,Def7;
    carr P c= the carrier of G by GROUP_2:def 9; then
    carr P = (carr P) /\ (the carrier of G) by XBOOLE_1:28
          .= (carr P) /\ (V * H) by Z2,Th89
          .= (V * H) /\ (carr P)
          .= V * (H /\ P) by A3, DedekindModularProperty;
    then Z3: the carrier of P = V * (H /\ P) by GROUP_2:def 9;
    Z4: (H /\ P) /\ V = V /\ (H /\ P) by Th103
                     .= (V /\ H) /\ P by GROUP_2:84
                     .= (H /\ V) /\ P by Th103
                     .= (1).G /\ P by Z2,Def7
                     .= (1).G by GROUP_2:85
                     .= (1).P by GROUP_2:63;
    H /\ P is Subgroup of P by GROUP_2:88; then
    H /\ P, V are_complements_in P by A3,Z3,Z4,ThDef7Sub3;
    hence thesis by A3,Def5;
  end;

  thus P splits_over V implies G splits_over V
  proof
    assume B1: P splits_over V;
    then consider Q being Subgroup of P such that
    B2: Q,V are_complements_in P
    by A3, Def5;
    B3: Q * V = the carrier of P & Q /\ V = (1).P by A3,B2,ThDef7Sub2;

    Q,P./.((P,V)`*`) are_isomorphic
    proof
      Q,V are_complements_in P & V is Subgroup of P by A3,B2; then
      Z1: Q,((P,V)`*`) are_complements_in P by ThDef7Sub6;
      ((P,V)`*`) is strict normal Subgroup of P by A3;
      hence thesis by Z1, Th28;
    end;
    then
    V is Subgroup of P & Q,P./.((P,V)`*`) are_isomorphic & Q /\ V=(1).G
    by A3, B3, GROUP_2:63;
    then consider x being Function of G./.V,G such that
    :: B4: ((nat_hom V) * x = id (G./.V)) and
    B4: (for a being Element of G./.V holds a = (x.a)*V) and
    B5: (for q being Element of G st q in Q holds x.(q*V) = q)
    :: & (for H being Subgroup of G st H /\ V = (1).G
    :: for g being Element of G st g in G
    :: holds x.((nat_hom N).g) = g)
    by A3,B3,Th79;
::>  *4
    
    consider gamma
    being Function of [:the carrier of (G./.V),the carrier of (G./.V):],G
    such that
    B6: (for a,b being Element of G./.V
         holds (x.a)*(x.b) = (x.(a * b)) * (gamma.(a,b))
         & gamma.(a,b) in V) by B4,Th107;
    
    Eq1: for a,b being Element of (G./.V)
    holds (x.a) * (x.b) = (x.(a * b)) * (gamma.(a,b))
    by B6;
    
    :: Eq2 follows from Eq1 and associativity
    Eq2: for a,b,c being Element of (G./.V)
    holds (gamma.(a*b,c)) * ((gamma.(a,b)) |^ (x.c))
    = (gamma.(a,b*c))*(gamma.(b,c))
    proof
      let a,b,c be Element of (G./.V);
      LHS: ((x.a) * (x.b)) * (x.c) = ((x.(a * b)) * (gamma.(a,b))) * (x.c) by Eq1
      .= (x.(a * b)) * (gamma.(a,b)) * (x.c) by GROUP_1:def 3
      .= (x.(a * b)) * (1_G) * (gamma.(a,b)) * (x.c) by GROUP_1:def 4
      .= (x.(a * b)) * ((x.c) * ((x.c)")) * (gamma.(a,b)) * (x.c) by GROUP_1:def 5
      .= (x.(a * b)) * (x.c) * ((x.c)") * (gamma.(a,b)) * (x.c) by GROUP_1:def 3
      .= (x.(a * b)) * (x.c) * (((x.c)") * (gamma.(a,b))) * (x.c) by GROUP_1:def 3
      .= (x.(a * b)) * (x.c) * ((((x.c)") * (gamma.(a,b))) * (x.c)) by GROUP_1:def 3
      .= (x.(a * b)) * (x.c) * ((gamma.(a,b)) |^ (x.c)) by GROUP_3:def 2
      .= ((x.(a * b)) * (x.c)) * ((gamma.(a,b)) |^ (x.c)) by GROUP_1:def 3
      .= (x.((a * b) * c)) * (gamma.(a*b,c)) * ((gamma.(a,b)) |^ (x.c)) by Eq1
      .= (x.(a * b * c)) * (gamma.(a*b,c)) * ((gamma.(a,b)) |^ (x.c)) by GROUP_1:def 3;

      RHS: (x.a) * ((x.b) * (x.c))
      = (x.a) * ((x.(b * c)) * (gamma.(b,c))) by Eq1
      .= (x.a) * (x.(b * c)) * (gamma.(b,c)) by GROUP_1:def 3
      .= x.(a * (b * c)) * (gamma.(a, b * c)) * (gamma.(b,c)) by Eq1
      .= x.(a * b * c) * (gamma.(a, b * c)) * (gamma.(b,c)) by GROUP_1:def 3;

      (x.(a * b * c)) * ((gamma.(a*b,c)) * ((gamma.(a,b)) |^ (x.c)))
      = (x.(a * b * c)) * (gamma.(a*b,c)) * ((gamma.(a,b)) |^ (x.c)) by GROUP_1:def 3
      .= ((x.a) * (x.b)) * (x.c) by LHS
      .= (x.a) * ((x.b) * (x.c)) by GROUP_1:def 3
      .= (x.(a * b * c)) * (gamma.(a,b*c))*(gamma.(b,c)) by RHS
      .= (x.(a * b * c)) * ((gamma.(a,b*c))*(gamma.(b,c))) by GROUP_1:def 3;
      
      hence (gamma.(a*b,c)) * ((gamma.(a,b)) |^ (x.c))
      = (gamma.(a,b*c))*(gamma.(b,c)) by GROUP_1:6;
    end;

    Eq3: for g being Element of G st g in Q
    for a being Element of (G./.V)
    holds x.((nat_hom V).g) = g & gamma.((nat_hom V).g, a) = 1_G
    proof
      let g be Element of G;
      assume Z1: g in Q;
      let a be Element of (G./.V);
      thus Z2: x.((nat_hom V).g) = x.(g * V) by A3, GROUP_6:def 8
                                .= g by B5,Z1;
                                
      (x.(g*a))*(gamma.(g*V,a)) = (x.(g * V))*(x.a) by Eq1
::>        *103                              *103
      .= (x.((nat_hom V).g))*(x.a) by A3,GROUP_6:def 8
      .= g*(x.a) by Z2;
      (x.(g * V))*(x.a) = (x.((nat_hom V).g))*(x.a) by A3,GROUP_6:def 8
::>              *103
      .= g*(x.a) by Z2;
      ((nat_hom V).g) * a = ((nat_hom V).g) * ((nat_hom V).(x.a))
::>                                                             *4
      .= (nat_hom).(g * (x.a)) by GROUP_6:def 6;
::>             *165
      :: ((x.g)*V)*((x.a)*V) =
      (x.(a * ((nat_hom V).g))) * (gamma.(a, (nat_hom V).g))
      = (x.a) * (x.((nat_hom V).g)) by Eq1
      .= (x.a) * g by Z2;
      thus gamma.((nat_hom V).g, a) = 1_G;
::>                                     *4
    end;


    Eq4: for b,c being Element of (G./.V)
    for g being Element of G st g in Q
    holds gamma.(((nat_hom V).g)*b,c) = gamma.(b,c)
    proof
      let b,c be Element of (G./.V);
      let g be Element of G;
      assume Z1: g in Q;
      set a = ((nat_hom V).g);
      thus gamma.(a*b,c) = gamma.(a*b,c) * (1_G) by GROUP_1:def 4
                        .= gamma.(a*b,c) * ((1_G) |^ (x.c)) by GROUP_3:17
                        .= gamma.(a*b,c) * ((gamma.(a,b)) |^ (x.c)) by Z1,Eq3
                        .= (gamma.(a,b*c))*(gamma.(b,c)) by Eq2
                        .= (1_G)*(gamma.(b,c)) by Z1,Eq3
                        .= gamma.(b,c) by GROUP_1:def 4;
    end;

    set X = rng x;
    consider Y being Subset of G such that
    Eq51: X = Q * Y;
::>               *4

    the carrier of G is finite set;
    consider beta being Function of (G./.V),G such that
    Eq5: for c being Element of (G./.V)
    ex setfactors being finite Subset of the carrier of G,
       factors being FinSequence of the carrier of G
    st (setfactors = the set of all gamma.(y,c)
                     where y is Element of ((nat_hom V) .: Y))
    & factors = canFS setfactors
    & beta.c = Product factors
    & beta.c in V;
::>             *4

    set m = Index P;
    Eq6: for b,c being Element of (G./.V)
    holds (beta.c)*((beta.b) |^ (x.c)) = (beta.(b * c))*((gamma.(b,c)) |^ m);
::>                                                                        *4


    consider n being Nat such that
    Eq61: n < card V & (m * n) mod (card V) = 1;
::>                                           *4
    
    deffunc ALPHA(Element of G./.V) = ((beta.$1)") |^ n;
    consider alpha being Function of (G./.V),G such that
    Eq62: for c being Element of (G./.V) holds alpha.c = ALPHA(c)
    from FUNCT_2:sch 4;

    for c being Element of (G./.V) holds alpha.c in V by Eq5,Eq62;
::>                                                    *4
    
    Eq7: for b,c being Element of (G./.V)
    holds (alpha.c)*((alpha.b) |^ (x.c)) = (alpha.(b*c))*((gamma.(b,c))");
::>                                                                     *4


    deffunc Fy(Element of (G./.V)) = (x.$1)*(alpha.$1);
    consider y being Function of (G./.V),G such that
    Eq701: for c being Element of (G./.V) holds y.c = Fy(c)
    from FUNCT_2:sch 4;
    
    
    set UH = rng y; :: the set of all y.a where a is Element of (G./.V);
    reconsider UH as Subset of G;
    UH <> {} & (for g1,g2 being Element of G st g1 in UH & g2 in UH
    holds g1 * g2 in UH) & (for g being Element of G st g in UH
    holds g" in UH);
::>               *4,4
    then consider H being strict Subgroup of G such that
    Eq71: the carrier of H = UH
    by GROUP_2:52;
    H * V = the carrier of G & H /\ V = (1).G;
::>                                         *4,4
    then H,V are_complements_in G by Def7;
    hence G splits_over V by Def5;
  end;
end;
::> 4: This inference is not accepted
::> 70: Something remains to be proved
::> 103: Unknown functor
::> 165: Unknown functor format
::> 321: Predicate symbol or "is" expected
::> 396: Formula expected
