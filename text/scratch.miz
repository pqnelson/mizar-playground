environ

 vocabularies NUMBERS, NEWTON, ARYTM_3, RELAT_1, NAT_1, XXREAL_0,
   ARYTM_1, SUBSET_1, CARD_1, TARSKI, FUNCT_1, PRE_POLY, XCMPLX_0,
   NAT_3, SCRATCH;

 notations TARSKI, XBOOLE_0, SUBSET_1, ORDINAL1, CARD_1, NUMBERS,
   XCMPLX_0, XXREAL_0, XREAL_0, REAL_1, NAT_D, INT_2, RELAT_1, FUNCT_1,
   FUNCT_2, FINSEQ_1, FINSEQ_2, VALUED_0, PBOOLE, RVSUM_1, NEWTON,
   TREES_4, FUNCOP_1, XXREAL_2, PRE_POLY, NAT_3;

 constructors BINOP_1, SETWISEO, NAT_D, FINSEQOP, FINSOP_1, NEWTON, WSIERP_1,
   BINOP_2, XXREAL_2, RELSET_1, PRE_POLY, REAL_1, NAT_3;
 registrations XBOOLE_0, RELAT_1, FUNCT_1, FINSET_1, NUMBERS, XCMPLX_0,
   XXREAL_0, NAT_1, INT_1, BINOP_2, MEMBERED, NEWTON, VALUED_0, FINSEQ_1,
   XXREAL_2, CARD_1, FUNCT_2, RELSET_1, ZFMISC_1, FINSEQ_2, PRE_POLY,
   XREAL_0, RVSUM_1, NAT_3;
 requirements NUMERALS, SUBSET, ARITHM, REAL, BOOLE;
 definitions TARSKI, XBOOLE_0, INT_2, NAT_D, FINSEQ_1, VALUED_0,
   PRE_POLY, NAT_3;
 equalities FINSEQ_1, FINSEQ_2, PBOOLE, ORDINAL1, NAT_3;
 expansions TARSKI, XBOOLE_0, INT_2, NAT_D, FINSEQ_1, VALUED_0, PBOOLE,
   NAT_3;
 theorems NEWTON, NAT_1, INT_2, XREAL_1, NAT_D, PRE_POLY, NAT_3,
   MOEBIUS1, TARSKI, PREPOWER;

begin :: preparatory work

Lm2:
  for k,m being non zero Nat
  st k*m <= m
  holds k <= 1
proof
  let k,m be non zero Nat;
  assume k*m <= m;
  then k*m - m <= 0 by XREAL_1:47;
  then A1: (k - 1)*m <= 0;
  k > 1 implies contradiction
  proof
    assume k > 1;
    then 0 < (k - 1) by XREAL_1:50;
    hence contradiction by A1;
  end;
  hence k <= 1;
end;


theorem Lm3:
  for a,b,c being Nat
  st 1 < b & b |^ a <= b |^ c
  holds b |-count (b |^ a) <= b |-count (b |^ c)
proof
  let a,b,c be Nat;
  assume A1: 1 < b;
  assume A2: b |^ a <= b |^ c;
  A3: b |-count (b |^ a) = a & b |-count (b |^ c) = c by A1, NAT_3:25;
  a > c implies contradiction
  proof
    assume A4: a > c;
    then consider t being Nat such that
    A5: a = c + t by NAT_1:10;
    t = a - c by A5;
    then t > 0 by A4,XREAL_1:50;
    then A6: t >= 1 by NAT_1:25;
    1 |^ t = 1;
    then A7: b |^ t > 1 by A1, A6, PREPOWER:10;
    b |^ a = (b |^ c) * (b |^ t) by A5, NEWTON:8;
    hence contradiction by A1,A2,A7,PREPOWER:11, XREAL_1:155;
  end;

  hence b |-count (b |^ a) <= b |-count (b |^ c) by A3;
end;


:: converse to NAT_3:30
theorem Th1:
  for m,n being non zero Nat
  st (for p being Prime holds (ppf m).p <= (ppf n).p)
  holds m divides n
proof
  let m,n be non zero Nat;
  assume A1: for p being Prime holds (ppf m).p <= (ppf n).p;
  for p being Prime holds p |-count m <= p |-count n
  proof
    let p be Prime;
    (ppf m).p <= (ppf n).p by A1;
    per cases;
    suppose B1: p in support (ppf n) & p in support (ppf m);
      then p in support (pfexp n) by NAT_3:def 9;
      then (ppf n).p = p |^ (p |-count n) by NAT_3:def 9;
      then A3: (ppf m).p <= p |^ (p |-count n) by A1;
      p in support (pfexp m) by B1,NAT_3:def 9;
      then A4: (p |^ (p |-count m)) <= (p |^ (p |-count n))
      by A3, NAT_3:def 9;
      not (p |-count m <= p |-count n) implies contradiction
      proof
        assume B2: not (p |-count m <= p |-count n);
        then consider k being Nat such that
        B3: (p |-count m) = (p |-count n) + k by NAT_1:10;
        p |^ (p |-count m) = (p |^ (p |-count n)) * (p |^ k) by B3, NEWTON:8;
        then p |^ k <= 1 by A4,Lm2;
        then p |^ k <= p |^ 0 by NEWTON:4;
        then B4: p |-count (p |^ k) <= p |-count (p |^ 0) by Lm3, INT_2:def 4;
        p |-count (p |^ 0) = 0 & p |-count (p |^ k) = k
        by INT_2:def 4, NAT_3:25;
        then k = 0 by B4;
        hence contradiction by B2,B3;
      end;
      hence thesis;
    end;
    suppose p in support (ppf n) & not (p in support (ppf m));
      then not (p in support (pfexp m)) by NAT_3:def 9;
      then (pfexp m).p = 0 by PRE_POLY:def 7;
      hence thesis by NAT_3:def 8;
    end;
    suppose not (p in support (ppf n));
      then not (p in support (pfexp n)) by NAT_3:def 9;
      then (pfexp n).p = 0 by PRE_POLY:def 7;
      then p |-count n = 0 by NAT_3:def 8;
      then (ppf n).p = 0 by NAT_3:55;
      then (ppf m).p = 0 by A1;
      then not (p in support (ppf m)) by PRE_POLY:def 7;
      then not (p in support (pfexp m)) by NAT_3:def 9;
      then (pfexp m).p = 0 by PRE_POLY:def 7;
      hence thesis by NAT_3:def 8;
    end;
  end;
  hence m divides n by MOEBIUS1:19;
end;

theorem
  for m,n being non zero Nat
  st (for p being Prime holds (ppf m).p <= (ppf n).p)
  holds m divides n by Th1;

reserve Pi for Subset of SetPrimes;

definition
  let Pi be Subset of SetPrimes;
  let IT be Nat;
  attr IT is Pi-number means :Def1:
  for p being Prime st p divides IT holds p in Pi;
end;

theorem
  for n being non zero Nat
  holds (for p being Prime st p divides n holds p in Pi)
        iff n is Pi-number;

theorem
  for Pi1, Pi2 being Subset of SetPrimes
  st Pi1 c= Pi2
  for n being non zero Nat
  st n is Pi1-number
  holds n is Pi2-number;

theorem
  for n being non zero Nat
  holds support (ppf n) c= Pi iff n is Pi-number
proof
  let n be non zero Nat;
  hereby 
    assume A1: support (ppf n) c= Pi;
    for p being Prime st p divides n holds p in Pi
    proof
      let p be Prime;
      assume A2: p divides n;
      then p in support (pfexp n) by NAT_3:37;
      then p in support (ppf n) by NAT_3:def 9;
      hence p in Pi by A1;
    end;
    hence n is Pi-number;
  end;
  assume A3: n is Pi-number;
  for p being Nat st p in support (ppf n) holds p in Pi
  proof
    let p be Nat;
    assume p in support (ppf n);
    then A4: p in support (pfexp n) by NAT_3:def 9;
    then p is Prime by NAT_3:34;
    hence p in Pi by A3, A4, NAT_3:36;
  end;
  hence support (ppf n) c= Pi;
end;





