:: Characteristic Subgroups.
::  by Alex Nelson
::
:: This code can be distributed under the GNU General Public Licence
:: version 3.0 or later, or the Creative Commons Attribution-ShareAlike
:: License version 3.0 or later, subject to the binding interpretation
:: detailed in file COPYING.interpretation.
:: See COPYING.GPL and COPYING.CC-BY-SA for the full text of these
:: licenses, or see http://www.gnu.org/licenses/gpl.html and
:: http://creativecommons.org/licenses/by-sa/3.0/.


environ

 vocabularies MOD_4, CHAR, CARD_3, RLVECT_1, UPROOTS, QC_LANG1, RLSUB_1,
   RELAT_1, TARSKI, FUNCT_1, ZFMISC_1, XXREAL_0, XXREAL_1, FINSEQ_1,
   FINSET_1, NUMBERS, WELLORD1, SUBSET_1, XBOOLE_0, PARTFUN1,
   FUNCT_2, CARD_1, ARYTM_3, CQC_SIM1, ORDINAL1, PBOOLE, EQREL_1,
   STRUCT_0, GROUP_1, GROUP_2, GROUP_3, GROUP_4, GROUP_5,
   GROUP_6, GROUP_7, BINOP_1, BINOP_2, ALGSTR_0, REALSET1, AUTGROUP,
   GR_CY_1, NATTRA_1, INT_1,
   NAT_1, INT_2, SETFAM_1, NEWTON, PRE_TOPC, GROUP_10, WEDDWITT;
 constructors TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, RELAT_1, FUNCT_1,
              RELSET_1, PARTFUN1, FUNCT_2, FUNCOP_1, FINSEQ_1, FINSEQ_2, FINSOP_1,
   CARD_1, CARD_3, NUMBERS, REAL_1, SETWISEO,
   ARYTM_2, ARYTM_3, ORDINAL2, SQUARE_1,
   SETFAM_1, ORDINAL1,INT_1, INT_2, PBOOLE,
   XXREAL_2, XCMPLX_0, XXREAL_0, XREAL_0,  NAT_1, NAT_D,
   BINOP_1, BINOP_2, FINSET_1, STRUCT_0, ALGSTR_0, REALSET1, MONOID_0,
   GROUP_1, GROUP_2, GROUP_3, GROUP_4, GROUP_5, PRALG_1, GROUP_7, GRSOLV_1,
   AUTGROUP, GROUP_9, GROUP_10, GR_CY_1, NEWTON, GROUP_6;
 notations TARSKI, XBOOLE_0, SUBSET_1, XCMPLX_0, ORDINAL1, RELAT_1,
   FUNCT_1, RELSET_1, FUNCT_2, FUNCOP_1, SETWISEO, PARTFUN1,
   ZFMISC_1, CARD_1, CARD_3, INT_1, NAT_1, ARYTM_2, ARYTM_3, INT_2,
   FINSEQ_2, REAL_1, SETFAM_1, NUMBERS, MEMBERED, PBOOLE, BINOP_1,
   BINOP_2, FINSET_1, STRUCT_0, ALGSTR_0, XXREAL_0, FINSEQ_1, GROUP_1, GROUP_2,
   GROUP_3, GROUP_4, GROUP_5, REALSET1, NAT_D, GRSOLV_1,
   AUTGROUP, GROUP_9, GROUP_10, GR_CY_1, NEWTON, PRALG_1, GROUP_7, GROUP_6;
 registrations XBOOLE_0, RELAT_1, FUNCT_1, PARTFUN1, RELSET_1, FUNCT_2,
   ORDINAL1, FINSET_1, FINSEQ_1, NUMBERS, NAT_1, INT_1, INT_2, XCMPLX_0,
   ARYTM_3, XREAL_0, ARYTM_2, SETWISEO, CARD_1, NEWTON, FINSEQ_2,
   STRUCT_0, BINOP_1, GROUP_1, GROUP_2, GROUP_3, GROUP_6, GR_CY_1, GROUP_7;
 requirements BOOLE, SUBSET, NUMERALS, ARITHM, REAL;
 definitions TARSKI, SUBSET_1, FUNCT_1, FUNCT_2, ARYTM_2, FINSEQ_1, INT_1,
   PRALG_1, GROUP_1, GROUP_3, GROUP_4, GROUP_5, GROUP_6, AUTGROUP, NEWTON,
   XXREAL_0, GROUP_7;
 equalities PARTFUN1, FINSET_1, BINOP_1, REALSET1, STRUCT_0, GROUP_2,
   GROUP_3, GROUP_4, GROUP_5, GROUP_6, GR_CY_1,
   ALGSTR_0, NEWTON, PRALG_1, GROUP_7;
 expansions TARSKI, FINSET_1, GROUP_1, GROUP_2, GROUP_6, STRUCT_0, BINOP_1,
   FUNCT_2, NEWTON, PRALG_1, GROUP_7;
 theorems
   TARSKI, SUBSET_1, RELSET_1, FUNCT_1, FUNCT_2, XBOOLE_0, CARD_1, RELAT_1, INT_2,
   SETFAM_1, FINSEQ_3, PARTFUN1, ORDINAL1, CARD_3, PRALG_1, FINSEQ_1, FINSET_1,
   ZFMISC_1, NAT_D, INT_1,
   GROUP_1, GROUP_2, GROUP_3, GROUP_4, GROUP_5, GROUP_6, STRUCT_0, GRSOLV_1,
   AUTGROUP, GROUP_9, GROUP_10, GR_CY_1, XCMPLX_1, GROUP_7, WELLORD2;
 schemes FUNCT_2, GROUP_4, FINSEQ_1, FRAENKEL;

begin :: Preparatory Work
reserve X for set;

registration
  let X;
  cluster id X -> onto;
  coherence;
end;

registration
  let X;
  cluster id X -> bijective;
  coherence;
end;

LmSubgroupEltTrans:
  for G being Group
  for H1,H2 being Subgroup of G
  for g being Element of G
  st g in H1 & H1 is Subgroup of H2
  holds g in H2 by GROUP_2:40;

theorem Th2:
  for G1,G2 being Group
  for H being Subgroup of G1
  for f being Homomorphism of G1,G2
  for h being Element of G1
  st h in H
  holds (f|H).h = f.h
proof
  let G1,G2 be Group;
  let H be Subgroup of G1;
  let f be Homomorphism of G1,G2;
  let h be Element of G1;
  assume h in H;
  then (f|(the carrier of H)).h = f.h by FUNCT_1:49;
  hence (f|H).h = f.h by GRSOLV_1:def 2;
end;

theorem Th4:
  for G being Group
  for H being Subgroup of G
  st (for a being Element of G holds H |^ a = H)
  holds H is normal Subgroup of G
proof
  let G be Group;
  let H be Subgroup of G;
  assume for a being Element of G holds H |^ a = H;
  then for a being Element of G holds the carrier of H |^ a = the carrier of H;
  then for a being Element of G holds the multMagma of H = H |^ a by GROUP_2:59;
  hence H is normal Subgroup of G by GROUP_3:def 13;
end;

theorem Th5:
  for X,Y being non empty set
  for f being Function of X,Y
  st f is bijective
  holds (f")" = f
proof
  let X,Y be non empty set;
  let f be Function of X,Y;
  assume A1: f is bijective;
  then A2: dom f = X & rng f = Y & f is one-to-one by FUNCT_2:def 3,def 1;
  reconsider g = f" as Function of Y,X by A2,FUNCT_2:25;
  A3: g is bijective by A1,GROUP_6:63;
  g is one-to-one & rng g = X implies g" is Function of X,Y
  by FUNCT_2:25;
  then reconsider h = g" as Function of X,Y by A3,FUNCT_2:def 3;

  for x being object st x in X holds h.x = f.x
  proof
    let x be object;
    assume x in X;
    then reconsider x as Element of X;
    consider y being object such that
    Z1: y = f.x;
    x = g.y by A1,Z1,FUNCT_2:26;
    then h.x = y by A1,Z1,FUNCT_2:26
            .= f.x by Z1;
    hence thesis;
  end;
  then h = f;
  hence (f")" = f;
end;

theorem Th6:
  for X,Y being non empty set
  for f being Function of X,Y
  st f is bijective
  for y being Element of Y
  holds f.((f").y) = y
proof
  let X,Y be non empty set;
  let f be Function of X,Y;
  assume A1: f is bijective;
  let y be Element of Y;
  f is onto by A1;
  then reconsider g = f" as Function of Y,X by A1,FUNCT_2:25;
  y = (g").(g.y) by A1,FUNCT_2:26
   .= f.((f").y) by Th5,A1;
  hence thesis;
end;

theorem Th7:
  for X,Y being non empty set
  for A being non empty Subset of X
  for x being Element of X
  st not x in A
  for f being Function of X,Y
  st f is one-to-one
  holds not f.x in (f .: A)
proof
  let X,Y be non empty set;
  let A be non empty Subset of X;
  let x be Element of X;
  assume A1: not x in A;
  let f be Function of X,Y;
  assume f is one-to-one;
  then A2: not ex a being object st x in dom f & a in dom f & a in A & f.a = f.x
  by A1, FUNCT_1:def 4;
  dom f = X by FUNCT_2:def 1;
  then A3: x in dom f;
  f.x in (f .: A) iff ex a being object st a in dom f & a in A & f.x = f.a
  by FUNCT_1:def 6;
  then f.x in (f .: A) implies contradiction by A2,A3;
  hence not (f.x in (f .: A));
end;

begin :: Nontrivial Groups and Subgroups
registration
  cluster non trivial for Group;
  existence
  proof
    reconsider G = multMagma (# REAL, addreal #) as Group by GROUP_1:3;
    take G;
    thus not (G is trivial);
  end;
end;
registration
  let G be Group;
  cluster trivial for Subgroup of G;
  existence
  proof
    take (1).G;
    thus thesis;
  end;
  let H be Subgroup of G;
  cluster trivial for Subgroup of H;
  existence
  proof
    take (1).H;
    thus thesis;
  end;
end;
registration
  let G be non trivial Group;
  cluster non trivial for Subgroup of G;
  existence
  proof
    reconsider H=G as Subgroup of G by GROUP_2:54;
    the carrier of H <> {1_G};
    hence thesis;
  end;

  cluster strict non trivial for Subgroup of G;
  existence
  proof
    set H = multMagma (#the carrier of G, the multF of G#);
    reconsider H as Group-like non empty multMagma;
    the multF of H = (the multF of G)||the carrier of H;
    then H is strict Subgroup of G & H is non trivial by GROUP_2:def 5;
    hence thesis;
  end;
end;

theorem Th8:
  for G being Group
  holds G is trivial iff the multMagma of G = (1).G
proof
  let G be Group;
  thus G is trivial implies the multMagma of G = (1).G
  proof
    assume G is trivial;
    then consider x being object such that
    A1: the carrier of G = {x};
    x = 1_G by A1, TARSKI:def 1;
    then the carrier of G = {1_G} by A1;
    then the carrier of G = the carrier of (1).G by GROUP_2:def 7;
    hence the multMagma of G = (1).G by GROUP_2:61;
  end;
  thus the multMagma of G = (1).G implies G is trivial;
  thus thesis;
end;

LmFiniteNontrivial:
  not INT.Group(2) is trivial
proof
  set G = INT.Group(2);
  A1: the carrier of G = Segm 2
                      .= 2 by ORDINAL1:def 17;
  the carrier of (1).G = {1_G} by GROUP_2:def 7
                      .= {0} by GR_CY_1:14
                      .= {} \/ {0}
                      .= succ 0 by ORDINAL1:def 1
                      .= 1;
  then the carrier of (1).G <> the carrier of G by A1;
  hence not INT.Group(2) is trivial by Th8;
end;

registration
  cluster non trivial for finite Group;
  existence by LmFiniteNontrivial;
end;


theorem Th9:
  for G being Group
  for H being Subgroup of G
  st H is trivial
  holds the multMagma of H = (1).G
proof
  let G be Group;
  let H be Subgroup of G;
  assume H is trivial;
  then the multMagma of H = (1).H by Th8
                         .= (1).G by GROUP_2:63;
  hence thesis;
end;


theorem Th10:
  for G being Group
  for H being trivial Subgroup of G
  for K being trivial Subgroup of G
  holds the multMagma of H = the multMagma of K
proof
  let G be Group;
  let H be trivial Subgroup of G;
  let K be trivial Subgroup of G;
  the multMagma of H = (1).G by Th9
                    .= the multMagma of K by Th9;
  hence thesis;
end;

theorem Th11:
  for G being Group
  for K being trivial Subgroup of G
  for H being Subgroup of G
  st H is Subgroup of K
  holds H is trivial Subgroup of G
proof
  let G be Group;
  let K be trivial Subgroup of G;
  let H be Subgroup of G;
  assume A1: H is Subgroup of K;
  the carrier of H = {1_G}
  proof
    the multMagma of K = (1).G by Th9;
    then the carrier of K = {1_G} by GROUP_2:def 7;
    then B1: the carrier of H c= {1_G} by A1,GROUP_2:def 5;
    (1).G is Subgroup of H by GROUP_2:65;
    then the carrier of (1).G c= the carrier of H by GROUP_2:def 5;
    then {1_G} c= the carrier of H by GROUP_2:def 7;
    hence the carrier of H = {1_G} by B1,XBOOLE_0:def 10;
  end;
  then ex x being object st the carrier of H = {x};
  hence H is trivial Subgroup of G;
end;

begin :: Proper Subgroups
definition
  let G be Group;
  let IT be Subgroup of G;
  attr IT is proper means
  :Def1:
  the multMagma of IT <> the multMagma of G;
end;

reserve G for Group;
reserve H for Subgroup of G;
theorem Th12:
  H is proper iff the carrier of H <> the carrier of G
proof
  (the carrier of H c= the carrier of G) & (the multF of H =
  (the multF of G)||(the carrier of H)) by GROUP_2:def 5;
  hence thesis;
end;

reserve h,x,y for object;

theorem Th13:
  H is proper iff (the carrier of G) \ (the carrier of H) is non empty set
proof
  set UG = the carrier of G;
  set UH = the carrier of H;
  thus H is proper implies (the carrier of G) \ (the carrier of H) is non empty set
  proof
    assume A1: H is proper;
    UH c= UG & UH <> UG by A1,Th12, GROUP_2:def 5;
    then (for x holds x in UH implies x in UG) &
    the carrier of H <> the carrier of G;
    then (for x holds x in UH implies x in UG) &
    not (for x holds x in UH iff x in UG) by TARSKI:2;
    hence (the carrier of G) \ (the carrier of H) is non empty set
    by XBOOLE_0:def 5;
  end;
  thus (the carrier of G) \ (the carrier of H) is non empty set implies H is proper
  proof
    assume A1: (the carrier of G) \ (the carrier of H) is non empty set;
    set GH = UG \ UH;
    not (GH is empty) by A1;
    then ex x st x in GH by XBOOLE_0:def 1;
    then consider y such that
    A2: y in GH;
    y in UG & not y in UH by A2, XBOOLE_0:def 5;
    then UG <> UH;
    then the multMagma of G <> the multMagma of H;
    hence H is proper;
  end;
  thus thesis;
end;

registration
  let G be non trivial Group;
  cluster proper for Subgroup of G;
  existence
  proof
    take (1).G;
    thus (1).G is proper;
  end;
  cluster proper for normal Subgroup of G;
  existence
  proof
    take (1).G;
    thus (1).G is proper;
  end;
  cluster strict proper for normal Subgroup of G;
  existence
  proof
    take (1).G;
    thus (1).G is strict proper;
  end;
end;
registration
  let G be non trivial Group;
  cluster proper for Subgroup of G;
  existence
  proof
    take (1).G;
    thus (1).G is proper;
  end;
end;
registration
  let G be non trivial Group;
  let H be non trivial Subgroup of G;
  cluster proper for Subgroup of H;
  existence
  proof
    take (1).H;
    thus (1).H is proper;
  end;
end;

Lm1:
  for G being Group
  for H being Subgroup of G
  st H is maximal
  holds H is proper by GROUP_4:def 6;

registration
  let G be non trivial Group;
  cluster maximal -> proper for Subgroup of G;
  coherence by Lm1;
end;

theorem Th14:
  for G being non trivial Group
  for H being proper Subgroup of G
  for K being Subgroup of G
  st H is Subgroup of K & the multMagma of H <> the multMagma of K
  holds K is non trivial Subgroup of G
proof
  let G be non trivial Group;
  let H be proper Subgroup of G;
  let K be Subgroup of G;
  assume A1: H is Subgroup of K;
  assume A2: the multMagma of H <> the multMagma of K;
  not (K is non trivial Subgroup of G) implies contradiction
  proof
    assume B1: not K is non trivial Subgroup of G;
    then K is trivial Subgroup of G;
    then H is trivial Subgroup of G by A1,Th11;
    then the multMagma of H = the multMagma of K by B1,Th10;
    hence contradiction by A2;
  end;
  hence K is non trivial Subgroup of G;
end;

begin :: Automorphisms
definition :: Def2
  let G;
  mode Endomorphism of G is Homomorphism of G,G;
end;

reserve f for Endomorphism of G;
registration
  let G;
  cluster bijective for Endomorphism of G;
  existence
  proof
    reconsider i = id the carrier of G as Homomorphism of G,G by GROUP_6:38;
    i is bijective;
    hence thesis;
  end;
end;

definition :: Def3
  let G;
  mode Automorphism of G is bijective Endomorphism of G;
end;
reserve phi for Automorphism of G;

theorem Th15:
  Image (f|(1).G) = (1).G
proof
  Image(f|(1).G) = f .: ((1).G) by GRSOLV_1:def 3
                .= (1).G by GRSOLV_1:11;
  hence thesis;
end;

:: In particular, the trivial proper subgroup (1).G of G is invariant
:: under inner automorphisms, and thus is a characteristic subgroup.
theorem Th16:
  Image(phi|(1).G) is Subgroup of (1).G
proof
  (1).G is Subgroup of (1).G by GROUP_2:54;
  hence Image(phi|(1).G) is Subgroup of (1).G by Th15;
end;

theorem Th23:
  for G1,G2 being Group
  for f being Homomorphism of G1,G2
  for H being Subgroup of G1
  holds Ker(f|H) is Subgroup of Ker(f)
proof
  let G1,G2 be Group;
  let f be Homomorphism of G1,G2;
  let H be Subgroup of G1;
  A1: Ker(f|H) is Subgroup of G1 by GROUP_2:56;
  for g being Element of G1
  st g in Ker(f|H)
  holds g in Ker(f)
  proof
    let g be Element of G1;
    assume A2: g in Ker(f|H);
    Ker(f|H) is Subgroup of H;
    then A3: g in H by A2, LmSubgroupEltTrans;
    then A4: (f|H).g = f.g by Th2;
    g is Element of H by A3;
    then 1_G2 = f.g by A2,A4,GROUP_6:41;
    hence g in Ker(f) by GROUP_6:41;
  end;
  then Ker(f|H) is Subgroup of Ker(f) by A1,GROUP_2:58;
  hence thesis;
end;

Lm2: H is Subgroup of (1).G implies the multMagma of H = the multMagma of (1).G
proof
  assume H is Subgroup of (1).G;
  then H is Subgroup of (1).G & (1).G is Subgroup of H by GROUP_2:65;
  hence thesis by GROUP_2:55;
end;
Lm3:
(phi|H) is Homomorphism of H,Image(phi|H) & (phi|H) is one-to-one
proof
  thus phi|H is Homomorphism of H,Image(phi|H) by GROUP_6:49;
  Ker(phi) = (1).G by GROUP_6:56;
  then Ker(phi|H) is Subgroup of (1).G by Th23;
  then Ker(phi|H) = (1).G by Lm2
                 .= (1).H by GROUP_2:63;
  hence (phi|H) is one-to-one by GROUP_6:56;
end;

theorem Th17:
  (for f being Automorphism of G holds Image(f|H) is Subgroup of H) implies
  ex psi being Automorphism of G
  st psi = phi" & Image(phi|Image(psi|H)) is Subgroup of Image(phi|H)
proof
  assume A1: for f being Automorphism of G holds Image(f|H) is Subgroup of H;
  reconsider psi = phi" as Automorphism of G by GROUP_6:62;
  take psi;
  thus psi = phi";
  Image(psi|H) is Subgroup of H by A1;
  then phi .: Image(psi|H) is Subgroup of phi .: H by GRSOLV_1:12;
  then Image(phi|Image(psi|H)) is Subgroup of phi .: H by GRSOLV_1:def 3;
  hence Image(phi|Image(psi|H)) is Subgroup of Image(phi|H) by GRSOLV_1:def 3;
end;

theorem Th18:
  ex psi being Automorphism of G
  st psi = phi" & the multMagma of Image(phi|Image(psi|H)) = the multMagma of H
proof
  reconsider psi = phi" as Automorphism of G by GROUP_6:62;
  take psi;
  thus psi = phi";
  for g being Element of G holds g in Image(phi|Image(psi|H)) iff g in H
  proof
    let g be Element of G;
    thus g in Image(phi|Image(psi|H)) implies g in H
    proof
      assume g in Image(phi|Image(psi|H));
      then consider a being Element of Image(psi|H) such that
      B1: g = (phi|Image(psi|H)).a by GROUP_6:45;
      M1: a in Image(psi|H) & a is Element of G by GROUP_2:42;
      then B2: phi.a = g by B1, Th2;
      consider b being Element of H such that
      B3: a = (psi|H).b
      by M1,GROUP_6:45;
      M2: b in H & b is Element of G by GROUP_2:42;
      then b = phi.(psi.b) by Th6
            .= phi.((psi|H).b) by M2,Th2
            .= phi.a by B3
            .= g by B2;
      hence g in H;
    end;

    thus g in H implies g in Image(phi|Image(psi|H))
    proof
      assume B1: g in H;
      set a = (psi|H).g;
      B2: a in Image(psi|H)
      proof
        dom(psi|H) = the carrier of H by FUNCT_2:def 1;
        then g in dom(psi|H) by B1;
        then (psi|H).g in (psi|H) .: (the carrier of H) by FUNCT_1:def 6;
        then (psi|H).g in the carrier of Image(psi|H) by GROUP_6:def 10;
        then (psi|H).g in Image(psi|H);
        hence a in Image(psi|H);
      end;

      set K = Image(psi|H);
      set b = (phi|Image(psi|H)).a;

      B3: b in Image(phi|Image(psi|H))
      proof
        dom(phi|K) = the carrier of K by FUNCT_2:def 1;
        then a in dom(phi|K) by B2;
        then (phi|K).a in (phi|K) .: (the carrier of K) by FUNCT_1:def 6;
        then (phi|K).a in the carrier of Image(phi|K) by GROUP_6:def 10;
        then (phi|K).a in Image(phi|K);
        hence b in Image(phi|K);
      end;
      thus g in Image(phi|K)
      proof
        H is Subgroup of G & g in H & g is Element of G by B1;
        then B4: psi.g = (psi|H).g by Th2
                      .= a;
        B5: a is Element of K & K is Subgroup of G & a in K & phi is
        Homomorphism of G,G by B2;
        then a is Element of G by GROUP_2:42;
        then B6: (phi|K).a =  phi.a  by B5,Th2
                          .= phi.(psi.g) by B4
                          .= phi.((phi").g)
                          .= g by Th6;
        b = (phi|K).((psi|H).g)
         .= g by B6;
        hence thesis by B3;
      end;
    end;
    thus thesis;
  end;
  hence the multMagma of Image(phi|Image(psi|H)) = the multMagma of H by GROUP_2:60;
end;

theorem Th19:
  for H being strict Subgroup of G
  for K being Subgroup of G
  st Image(phi|H) is Subgroup of K
  holds ex psi being Automorphism of G
  st psi = phi" & H is Subgroup of Image(psi|K)
proof
  let H be strict Subgroup of G;
  let K be Subgroup of G;
  assume A1: Image(phi|H) is Subgroup of K;
  reconsider psi = phi" as Automorphism of G by GROUP_6:62;
  take psi;
  thus psi = phi";
  A2: psi" = phi by Th5;
  consider phi0 being Automorphism of G such that
  A3: phi0 = psi" and
  A4: the multMagma of Image(psi|Image(phi0|H)) = the multMagma of H
  by Th18;
  phi = phi0 by A2,A3;
  then A5: Image(psi|Image(phi|H)) = H by A4;
  psi .: Image(phi|H) is Subgroup of psi .: K by A1,GRSOLV_1:12;
  then Image(psi|Image(phi|H)) is Subgroup of psi .: K by GRSOLV_1:def 3;
  then Image(psi|Image(phi|H)) is Subgroup of Image(psi|K) by GRSOLV_1:def 3;
  hence H is Subgroup of Image(psi|K) by A5;
end;

theorem Th20:
  H,phi .: H are_isomorphic
proof
  reconsider H2 = phi .: H as Subgroup of G;
  phi|H is one-to-one by Lm3;
  then H,Image(phi|H) are_isomorphic by GROUP_6:68;
  hence H,phi .: H are_isomorphic by GRSOLV_1:def 3;
end;

theorem Th21:
  for G being finite Group
  for H1,H2 being strict Subgroup of G
  st H1,H2 are_isomorphic
  holds index H1 = index H2
proof
  let G be finite Group;
  let H1,H2 be strict Subgroup of G;
  assume H1,H2 are_isomorphic;
  then A1: card H1 = card H2 by GROUP_6:73;
  card H1 * index H1 = card G by GROUP_2:147
                    .= card H2 * index H2 by GROUP_2:147;
  then index H1 * card H1 = index H2 * card H1 by A1;
  hence index H1 = index H2 by XCMPLX_1:5;
end;

theorem Th22:
  G is finite implies
  for p being prime Nat
  for P being strict Subgroup of G
  st P is_Sylow_p-subgroup_of_prime p
  holds Image(phi|P) is_Sylow_p-subgroup_of_prime p
proof
  assume A0: G is finite;
  let p be prime Nat;
  let P be strict Subgroup of G;
  assume A1: P is_Sylow_p-subgroup_of_prime p;
  then A2: P is p-group by GROUP_10:def 18;
  set Q = (phi .: P);
  A3: P,Q are_isomorphic by Th20;
  then A4: card P = card Q by GROUP_6:75;
  consider r being Nat such that
  A5: card P = p |^ r
  by A2,GROUP_10:def 17;
  card Q = p |^ r by A4,A5;
  then A6: Q is p-group by GROUP_10:def 17;
  A7: index P = index Q by A0,A3,Th21;
  A8: Q = Image(phi|P) by GRSOLV_1:def 3;
  not p divides index P by A1, GROUP_10:def 18;
  then not p divides index Q by A7;
  hence Image(phi|P) is_Sylow_p-subgroup_of_prime p by A6, A8, GROUP_10:def 18;
end;

theorem Th24:
  for f being Automorphism of G
  st Image(f|H) = H
  holds f|H is Automorphism of H
proof
  let f be Automorphism of G;
  assume A1: Image(f|H) = H;
  then C1: f|H is Homomorphism of H,H by GROUP_6:49;
  C2: f|H is one-to-one by Lm3;

  f|H is onto Endomorphism of H
  proof
    set UH = the carrier of H;
    f|H is Function of UH,UH & rng(f|H) = the carrier of H by C1, GROUP_6:44, A1;
    then f|H is onto Function of UH,UH by FUNCT_2:def 3;
    then f|H is onto Homomorphism of H,H by C1;
    hence thesis;
  end;
  then f|H is bijective Homomorphism of H,H by C2;
  then f|H is Automorphism of H;
  hence thesis;
end;

theorem Th25:
  for G being non trivial Group
  for H being Subgroup of G
  for phi being Automorphism of G
  st H is proper Subgroup of G
  holds Image(phi|H) is proper Subgroup of G
proof
  let G be non trivial Group;
  let H be Subgroup of G;
  let phi be Automorphism of G;
  set UH = the carrier of H;
  set UG = the carrier of G;
  A1: phi is one-to-one & phi is onto & UH is non empty Subset of UG &
      phi is Function of UG,UG by GROUP_2:def 5;
  assume H is proper Subgroup of G;
  then UG \ UH is non empty by Th13;
  then consider x such that
  A2: x in UG \ UH by XBOOLE_0:def 1;
  A3: x in G & not x in H by A2,XBOOLE_0:def 5;
  A4: not (phi.x in phi .: H)
      proof
        not (phi.x in (phi .: UH)) by A1, A3, Th7;
        hence not (phi.x in (phi .: H)) by GRSOLV_1:8;
      end;
  phi.x is Element of G
  proof
    dom phi = UG & rng phi = UG by A1, FUNCT_2:def 1;
    then phi.x in UG by A3, FUNCT_1:def 3;
    hence phi.x is Element of G;
  end;
  then ex y being Element of G st not y in phi .: H by A4;
  then phi .: H is proper;
  hence Image(phi|H) is proper Subgroup of G by GRSOLV_1:def 3;
end;

theorem Th26:
  for G being non trivial Group
  for H being strict Subgroup of G
  for phi being Automorphism of G
  st H is maximal
  holds Image(phi|H) is maximal
proof
  let G be non trivial Group;
  let H be strict Subgroup of G;
  let phi be Automorphism of G;
  assume A1: H is maximal;
  A2: Image(phi|H) is proper Subgroup of G by A1,Th25;
  then P1: the multMagma of Image(phi|H) <> the multMagma of G by Def1;
  set UG = the carrier of G;
  set UH = the carrier of H;
  P2: for K being strict Subgroup of G
  st Image(phi|H) <> K & Image(phi|H) is Subgroup of K
  holds K = the multMagma of G
  proof
    let K be strict Subgroup of G;
    assume B1: Image(phi|H) <> K;
    assume B2: Image(phi|H) is Subgroup of K;
    then consider psi being Automorphism of G such that
    B3: psi = phi" and
    B4: H is Subgroup of Image(psi|K)
    by Th19;
    set UK = the carrier of K;
    reconsider K as non trivial strict Subgroup of G by A2,B1,B2,Th14;
    UK \ (the carrier of Image(phi|H)) is non empty by B1,B2,Def1,Th13;
    then consider k being object such that
    B6: k in UK \ (the carrier of Image(phi|H))
    by XBOOLE_0:def 1;
    reconsider k as Element of K by B6;
    set L = Image(psi|K);
    B8: psi.k in L
    proof
      C1: k in G by GROUP_2:41;
      consider l being object such that
      C2: l = psi.k;
      dom psi = the carrier of G by FUNCT_2:def 1;
      then l in psi .: (the carrier of K) by C1,C2,FUNCT_1:def 6;
      then l in the carrier of (psi .: K) by GRSOLV_1:8;
      hence psi.k in Image(psi|K) by C2,GRSOLV_1:def 3;
    end;
    B9: the multMagma of H <> L
    proof
      set UPH = the carrier of Image(phi|H);
      C1: phi is one-to-one & phi is onto & UPH is non empty Subset of UG &
      phi is Function of UG,UG by GROUP_2:def 5;
      k in G & not k in Image(phi|H) by B6, XBOOLE_0:def 5, GROUP_2:41;
      then C2: not psi.k in (psi .: UPH) by C1,Th7;
      consider phi2 being Automorphism of G such that
      C3: phi2 = psi" and
      C4: the multMagma of Image(psi|Image(phi2|H)) = the multMagma of H
      by Th18;
      phi2=phi by C3,B3, Th5;
      then C5: the multMagma of Image(psi|Image(phi|H)) = the multMagma of H by C4;
      set UPH = the carrier of Image(phi|H);
      psi .: UPH = the carrier of (psi .: Image(phi|H)) by GRSOLV_1:8
                .= the carrier of Image(psi|Image(phi|H)) by GRSOLV_1:def 3;
      then the carrier of H <> the carrier of L by B8,C2,C5;
      hence thesis;
    end;
    B10: Image(phi|L) = the multMagma of G
    proof
      L = the multMagma of G by A1,B4,B9,GROUP_4:def 6;
      then phi is Function of UG,UG & phi is onto & the carrier of L = UG;
      then phi .: the carrier of L = phi .: UG
                                  .= rng phi by RELSET_1:22
                                  .= UG by FUNCT_2:def 3;
      then UG = phi .: (the carrier of L)
             .= the carrier of (phi .: L) by GRSOLV_1:8
             .= the carrier of Image(phi|L) by GRSOLV_1:def 3;
      then the carrier of Image(phi|L) = the carrier of G;
      then the multMagma of Image(phi|L) = the multMagma of G by GROUP_2:61;
      hence thesis;
    end;
    Image(phi|L) = K
    proof
      consider psi2 being Automorphism of G such that
      C1: psi2 = phi" and
      C2: the multMagma of Image(phi|Image(psi2|K)) = the multMagma of K
      by Th18;
      thus Image(phi|Image(psi|K)) = K by B3,C1,C2;
    end;
    hence thesis by B10;
  end;
  thus Image(phi|H) is maximal by P1,P2,GROUP_4:def 6;
end;

begin :: Inner Automorphisms
definition :: Def4
  let G;
  let IT be Automorphism of G;
  attr IT is inner means
  :Def4:
  ex a being Element of G st
  for x being Element of G holds IT.x = x |^ a;
end;
notation
  let G be Group, f be Automorphism of G;
  antonym f is outer for f is inner;
end;

theorem Th27:
  for x being Element of G holds (id the carrier of G).x = x |^ 1_G by GROUP_3:19;

registration
  let G;
  cluster inner for Automorphism of G;
  existence
  proof
    reconsider i = id (the carrier of G) as Automorphism of G by GROUP_6:38;
    take i;
    take 1_G;
    thus thesis by Th27;
  end;
end;

theorem Th28:
  for G being strict Group, f being object
  holds (f in Aut G) iff (f is Automorphism of G)
proof
  let G be strict Group;
  let f be object;
  thus f in Aut G implies f is Automorphism of G
  proof
    assume A1: f in Aut G;
    then reconsider f as Endomorphism of G by AUTGROUP:def 1;
    f is bijective by A1,AUTGROUP:def 1;
    hence thesis;
  end;
  thus f is Automorphism of G implies f in Aut G by AUTGROUP:def 1;
  thus thesis;
end;

Lm6:
  for G being strict Group
  for f being Element of InnAut G
  holds f is Automorphism of G
proof
  let G be strict Group;
  let f be Element of InnAut G;
  f is Element of Aut G by AUTGROUP:12;
  hence f is Automorphism of G by Th28;
end;

theorem Th29:
  for G being strict Group
  for f being object
  holds (f in InnAut G) iff (f is inner Automorphism of G)
proof
  let G be strict Group;
  let f be object;
  A1: f is Automorphism of G implies
      f is Element of Funcs (the carrier of G, the carrier of G) by FUNCT_2:9;
  thus (f in InnAut G) implies (f is inner Automorphism of G)
  proof
    assume B1: f in InnAut G;
    then reconsider f as Automorphism of G by Lm6;
    consider a being Element of G such that
    B2: for x being Element of G holds f.x = x |^ a
    by A1,B1,AUTGROUP:def 4;
    thus thesis by Def4,B2;
  end;
  thus (f is inner Automorphism of G) implies (f in InnAut G)
  proof
    assume f is inner Automorphism of G;
    then reconsider f as inner Automorphism of G;
    consider a being Element of G such that
    B1: for x being Element of G holds f.x = x |^ a
    by Def4;
    thus thesis by A1,B1,AUTGROUP:def 4;
  end;
  thus thesis;
end;

theorem Th30:
  for a being Element of G
  for f being inner Automorphism of G
  st (for x being Element of G holds f.x = x |^ a)
  holds Image(f|H) = H |^ a
proof
  let a be Element of G,
      f be inner Automorphism of G;
  assume
A1: for x being Element of G holds f.x = x |^ a;
C1: for h being Element of G st h in H holds (f|H).h = h |^ a
  proof
    let h be Element of G;
    assume h in H;
    hence (f|H).h = f.h by Th2
                 .= h |^ a by A1;
  end;

C2: for y being Element of G st y in Image(f|H) holds y in H |^ a
  proof
    let y be Element of G;
    assume y in Image(f|H);
    then consider h being Element of H such that
    B1: (f|H).h = y by GROUP_6:45;
    reconsider h as Element of G by GROUP_2:42;
    B2: h in H;
    then h |^ a = (f|H).h by C1
               .= y by B1;
    hence y in H |^ a by B2,GROUP_3:58;
  end;
C3: for y being Element of G st y in H |^ a holds y in Image(f|H)
  proof
    let y be Element of G;
    assume y in H |^ a;
    then consider g being Element of G such that
    B1:    y=g|^a and
    B2:    g in H
    by GROUP_3:58;

    B3: (f|H).g = f.g by Th2,B2
               .= g |^ a by A1
               .= y by B1;
    thus y in Image(f|H) by B2,B3,GROUP_6:45;
  end;
  thus (H |^ a) = Image(f|H) by C2,C3;
end;

theorem Th31:
  for a being Element of G
  for f being Endomorphism of G
  st (for x being Element of G holds f.x = x |^ a)
  holds Ker f = (1).G
proof
  let a be Element of G;
  let f be Endomorphism of G;
  assume A1: for x being Element of G holds f.x = x |^ a;
  for x being Element of G holds x in Ker f implies x in (1).G
  proof
    let x be Element of G;
    assume x in Ker f;
    then 1_G = f.x by GROUP_6:41
            .= x |^ a by A1;
    then x = 1_G by GROUP_3:18;
    hence x in (1).G by GROUP_2:46;
  end;
  then A2: Ker f is Subgroup of (1).G by GROUP_2:58;

  A3: (1).G is Subgroup of Ker f by GROUP_2:65;
  thus Ker f = (1).G by A2,A3,GROUP_2:55;
end;

theorem Th32:
  for a being Element of G
  for f being Endomorphism of G
  st (for x being Element of G holds f.x = x |^ a)
  holds f is Automorphism of G
proof
  let a be Element of G;
  let f be Endomorphism of G;
  assume A1: for x being Element of G holds f.x = x |^ a;
  then Ker f = (1).G by Th31;
  then A2: f is one-to-one by GROUP_6:56;
  ex fInv being Endomorphism of G st f*fInv = id (the carrier of G)
  proof
    deffunc F(Element of G) = ($1) |^ a";
    consider fInv be Function of the carrier of G, the carrier of G such that
    A3: for g being Element of G holds fInv.g = F(g) from FUNCT_2:sch 4;
    for x1,x2 being Element of G holds fInv.(x1 * x2) = fInv.x1 * fInv.x2
    proof
      let x1,x2 be Element of G;
      A4: fInv.x1 = x1 |^ a" & fInv.x2 = x2 |^ a" by A3;
      fInv.(x1 * x2) = (x1 * x2) |^ a" by A3
                    .= (x1 |^ a") * (x2 |^ a") by GROUP_3:23
                    .= fInv.x1 * fInv.x2 by A4;
      hence thesis;
    end;
    then reconsider fInv as Endomorphism of G by GROUP_6:def 6;
    for x being Element of G holds (f*fInv).x = (id the carrier of G).x
    proof
      let x be Element of G;
      (f * fInv).x = f.(fInv.x) by FUNCT_2:15
                  .= f.(x |^ a") by A3
                  .= (x |^ a") |^ a by A1
                  .= x |^ (a" * a) by GROUP_3:24
                  .= x |^ 1_G by GROUP_1:def 5
                  .= x by GROUP_3:19
                  .= (id the carrier of G).x;
      hence thesis;
    end;
    then f*fInv = id the carrier of G;
    hence thesis;
  end;

  then f is onto by FUNCT_2:18;
  hence f is Automorphism of G by A2;
end;

theorem Th33:
  for a being Element of G
  for f being Endomorphism of G
  st (for x being Element of G holds f.x = x |^ a)
  holds f is inner Automorphism of G by Th32,Def4;

theorem Th34:
  for a being Element of G
  holds ex f being inner Automorphism of G st (for x being Element of G
  holds f.x = x |^ a)
proof
  let a be Element of G;
  deffunc F(Element of G) = ($1) |^ a;
  consider f be Function of the carrier of G, the carrier of G such that
A1: for g being Element of G holds f.g = F(g) from FUNCT_2:sch 4;
  for x1,x2 being Element of G holds f.(x1 * x2) = f.x1 * f.x2
  proof
    let x1,x2 be Element of G;
    A2: f.x1 = x1 |^ a & f.x2 = x2 |^ a by A1;
    f.(x1 * x2) = (x1 * x2) |^ a by A1
               .= (x1 |^ a) * (x2 |^ a) by GROUP_3:23
               .= f.x1 * f.x2 by A2;
    hence thesis;
  end;
  then reconsider f as Endomorphism of G by GROUP_6:def 6;
  for x being Element of G holds f.x = x |^ a & f is inner Automorphism of G
  by A1,Th33;
  hence thesis;
end;

theorem Th35:
  for H being strict Subgroup of G
  holds (H is normal) iff (for f being inner Automorphism of G holds Image(f|H)=H)
proof
  let H be strict Subgroup of G;
  A1: (H is normal) implies (for f being inner Automorphism of G holds Image(f|H)=H)
  proof
    assume B1: H is normal;
    let f be inner Automorphism of G;
    consider a being Element of G such that
    B2: for x being Element of G holds f.x = x |^ a
    by Def4;
    Image(f|H) = H |^ a by B2,Th30
              .= the multMagma of H by B1,GROUP_3:def 13
              .= H;
    hence Image(f|H)=H;
  end;
  A2: not ((for f being inner Automorphism of G holds Image(f|H)=H) implies H is normal)
  implies contradiction
  proof
    assume B1: for f being inner Automorphism of G holds Image(f|H)=H;
    assume not H is normal;
    then consider a being Element of G such that
    B2: H |^ a <> the multMagma of H
    by GROUP_3:def 13;
    consider f being inner Automorphism of G such that
    B3: (for x being Element of G holds f.x= x |^ a)
    by Th34;
    Image(f|H) = H |^ a by B3, Th30;
    hence contradiction by B1,B2;
  end;
  thus thesis by A1,A2;
end;

begin :: Characteristic Subgroups
:: Dummit and Foote, Abstract Algebra, ch.4 section 4
definition
  let G;
  let IT be Subgroup of G;
  attr IT is characteristic means
  :Def6:
  for f being Automorphism of G
  holds Image (f|IT) = IT;
end;

Lm8: H is Subgroup of (1).G implies the multMagma of H = the multMagma of (1).G
proof
  assume H is Subgroup of (1).G;
  then reconsider H as Subgroup of (1).G;
  H is Subgroup of (1).G & (1).G is Subgroup of H by GROUP_2:65;
  hence thesis by GROUP_2:55;
end;

Lm7: (1).G is characteristic
proof
  for f being Automorphism of G holds Image(f|(1).G) = (1).G
  proof
    let f be Automorphism of G;
    reconsider I = Image(f|(1).G) as Subgroup of (1).G by Th16;
    (1).G = I by Lm8;
    hence Image(f|(1).G) = (1).G;
  end;
  hence (1).G is characteristic;
end;


theorem Th36:
  ex H st H is characteristic
proof
  take H = (1).G;
  thus H is characteristic by Lm7;
end;

registration
  let G;
  cluster characteristic for Subgroup of G;
  existence by Th36;
end;
reserve K for characteristic Subgroup of G;
registration
  let G be Group;
  cluster strict characteristic for Subgroup of G;
  existence
  proof
    take (1).G;
    thus thesis by Lm7;
  end;
end;

theorem Th37:
  K is normal Subgroup of G
proof
A1:  for a being Element of G holds K |^ a = K
  proof
    let a be Element of G;
    consider f being inner Automorphism of G such that
  A2: for x being Element of G holds f.x = x |^ a
    by Th34;
    K = Image(f|K) by Def6
     .= K |^ a by A2,Th30;
    hence thesis;
  end;
  for a being Element of G holds the carrier of K |^ a = the carrier of K by A1;
  then for a being Element of G holds the multMagma of K = K |^ a by GROUP_2:59;
  hence K is normal Subgroup of G by GROUP_3:def 13;
end;

registration
  let G be Group;
  cluster characteristic -> normal for Subgroup of G;
  coherence by Th37;
end;

theorem Th38:
  for H being strict Subgroup of G
  st (for K being strict Subgroup of G
      st card K = card H
      holds H = K)
  holds H is characteristic
proof
  let H be strict Subgroup of G;
  assume A1: for K being strict Subgroup of G st card K = card H holds H = K;
  H is characteristic
  proof
    let phi be Automorphism of G;
    Image(phi|H) = phi .: H by GRSOLV_1:def 3;
    then card H = card Image(phi|H) by Th20,GROUP_6:73;
    hence Image(phi|H) = H by A1;
  end;
  hence thesis;
end;

theorem Th40:
  for G1,G2 being Group
  for H1 being Subgroup of G1
  for K being Subgroup of H1
  for H2 being Subgroup of G2
  for f being Homomorphism of G1,G2
  for g being Homomorphism of H1,H2
  st (for k being Element of G1 st k in K holds f.k=g.k)
  holds Image(f|K) = Image(g|K)
proof
  let G1,G2 be Group;
  let H1 be Subgroup of G1;
  let K be Subgroup of H1;
  let H2 be Subgroup of G2;
  let f be Homomorphism of G1,G2;
  let g be Homomorphism of H1,H2;
  assume A1: for k being Element of G1 st k in K holds f.k=g.k;
  A2: Image(f|K) is strict Subgroup of G2 &
      Image(g|K) is strict Subgroup of G2 by GROUP_2:56;
  for y being object
  holds y in the carrier of Image(f|K) iff y in the carrier of Image(g|K)
  proof
    let y be object;
    thus y in the carrier of Image(f|K) implies y in the carrier of Image(g|K)
    proof
      assume y in the carrier of Image(f|K);
      then consider h being Element of K such that
      B1: (f|K).h = y
      by STRUCT_0:def 5,GROUP_6:45;
      B2: h is Element of G1 & h is Element of H1 & h in K by GROUP_2:42;
      f.h = g.h by A1,B2
         .= (g|K).h by B2, Th2;
      then (g|K).h = f.h
                  .= (f|K).h by B2,Th2
                  .= y by B1;
      hence y in the carrier of Image(g|K) by STRUCT_0:def 5,GROUP_6:45;
    end;
    thus y in the carrier of Image(g|K) implies y in the carrier of Image(f|K)
    proof
      assume y in the carrier of Image(g|K);
      then consider h being Element of K such that
      C1: (g|K).h = y
      by STRUCT_0:def 5,GROUP_6:45;
      C2: h is Element of H1 & h is Element of G1 & h in K by GROUP_2:42;
      g.h = f.h by A1,C2
         .= (f|K).h by C2,Th2;
      then (f|K).h = g.h
                  .= (g|K).h by C2,Th2
                  .= y by C1;
      hence y in the carrier of Image(f|K) by STRUCT_0:def 5,GROUP_6:45;
    end;
    thus thesis;
  end;
  hence Image(f|K) = Image(g|K) by A2,GROUP_2:59,TARSKI:2;
end;

theorem Th39:
  for N being strict normal Subgroup of G
  for K being characteristic Subgroup of N
  holds K is normal Subgroup of G
proof
  let N be strict normal Subgroup of G;
  let K be characteristic Subgroup of N;
  for a being Element of G holds K |^ a = K
  proof
    let a be Element of G;
    consider g being inner Automorphism of G such that
    A1: for x being Element of G holds g.x = x |^ a
    by Th34;

    Image(g|N) = N by Th35;
    then reconsider f = g|N as Automorphism of N by Th24;
    A2: Image(f|K) = K by Def6;

    for k being Element of G st k in K holds f.k = g.k by Th2,GROUP_2:40;
    then Image(f|K) = Image(g|K) by Th40;
    then Image(g|K) = K & Image(g|K) = K |^ a by A1,A2,Th30;
    then K = K |^ a;
    hence thesis;
  end;
  hence K is normal Subgroup of G by Th4;
end;

theorem Th41:
  for N being characteristic Subgroup of G
  for K being characteristic Subgroup of N
  holds K is characteristic Subgroup of G
proof
  let N be characteristic Subgroup of G;
  let K be characteristic Subgroup of N;
  for g being Automorphism of G holds Image(g|K) = K
  proof
    let g be Automorphism of G;
    Image(g|N) = N by Def6;
    then reconsider f = g|N as Automorphism of N by Th24;
    A1: Image(f|K) = K by Def6;

    for k being Element of G st k in K holds f.k = g.k by Th2, GROUP_2:40;
    then Image(g|K) = Image(f|K) by Th40
                   .= K by A1;
    hence Image(g|K) = K;
  end;
  hence K is characteristic Subgroup of G by Def6;
end;

theorem Th42:
  for G being Group
  for H being strict Subgroup of G
  holds H is characteristic Subgroup of G iff
  (for phi being Automorphism of G holds Image(phi|H) is Subgroup of H)
proof
  let G be Group;
  let H be strict Subgroup of G;
  thus H is characteristic Subgroup of G implies
       (for phi being Automorphism of G holds Image(phi|H) is Subgroup of H)
  proof
    assume B1: H is characteristic Subgroup of G;
    let phi be Automorphism of G;
    Image(phi|H) = H & H is Subgroup of H by B1,GROUP_2:54,Def6;
    hence Image(phi|H) is Subgroup of H;
  end;

  thus (for phi being Automorphism of G holds Image(phi|H) is Subgroup of H)
       implies H is characteristic Subgroup of G
  proof
    assume A1: for phi being Automorphism of G holds Image(phi|H) is Subgroup of H;
    A2: for phi being Automorphism of G holds H is Subgroup of Image(phi|H)
    proof
      let phi be Automorphism of G;
      consider psi being Automorphism of G such that
      B1: psi = phi" and
      B2: Image(phi|Image(psi|H)) is Subgroup of Image(phi|H) by A1,Th17;
      consider psi2 being Automorphism of G such that
      B3: psi2 = phi" and
      B4: the multMagma of H = the multMagma of Image(phi|Image(psi2|H)) by Th18;
      thus H is Subgroup of Image(phi|H) by B1,B2,B3,B4;
    end;
    for phi being Automorphism of G holds H = Image(phi|H)
    proof
      let phi be Automorphism of G;
      H is Subgroup of Image(phi|H) & Image(phi|H) is Subgroup of H
      by A1,A2;
      hence H = Image(phi|H) by GROUP_2:55;
    end;
    hence H is characteristic Subgroup of G by Def6;
  end;

  thus thesis;
end;


theorem Th43:
  center G is characteristic Subgroup of G
proof
  set Z = center G;
  A1: for y,z being Element of G
  st z in Z
  holds (phi.z)*y = y*phi.z
  proof
    let y,z be Element of G;
    assume B1: z in Z;
    set x = (phi").y;
    (phi.z)*y = (phi.z)*(phi.x) by Th6
             .= phi.(z*x) by GROUP_6:def 6
             .= phi.(x*z) by B1,GROUP_5:77
             .= (phi.x)*(phi.z) by GROUP_6:def 6
             .= y*(phi.z) by Th6;
    hence thesis;
  end;
  A2: for z being Element of G
  st z in Z
  holds (phi|Z).z in Z
  proof
    let z be Element of G;
    assume B1: z in Z;
    then for y being Element of G holds (phi.z)*y=y*(phi.z) by A1;
    then (phi.z) in Z by GROUP_5:77;
    hence ((phi|Z).z) in Z by B1,Th2;
  end;
  Image(phi|Z) is Subgroup of Z
  proof
    B1: the carrier of Z c= the carrier of G by GROUP_2:def 5;
    for w being Element of G st w in rng(phi|Z) holds w in Z
    proof
      let w be Element of G;
      assume w in rng(phi|Z);
      then consider z being object such that
      C1: z in dom(phi|Z) and
      C2: (phi|Z).z = w by FUNCT_1:def 3;
      reconsider z as Element of Z by C1;
      z is Element of G by GROUP_2:42;
      hence w in Z by C2,A2,STRUCT_0:def 5;
    end;
    then for w being Element of G st w in rng(phi|Z) holds w in the carrier of Z
    by STRUCT_0:def 5;
    then rng(phi|Z) c= the carrier of Z by B1,SUBSET_1:2;
    then the carrier of Image(phi|Z) c= the carrier of Z by GROUP_6:44;
    hence Image(phi|Z) is Subgroup of Z by GROUP_2:57;
  end;

  hence Z is characteristic Subgroup of G by Th42;
end;

reserve A1,A2 for set;

scheme
  CharMeet{G() -> Group, P[set]} :
  for phi being Automorphism of G()
  holds phi .: meet{A where A is Subset of G() : ex K being strict Subgroup
  of G() st A = the carrier of K & P[K]} = meet{A where A is Subset of G() :
  ex K being strict Subgroup of G() st A = the carrier of K & P[K]}
provided
A1: for phi being Automorphism of G()
    for H being strict Subgroup of G()
    st P[H]
    holds P[Image(phi|H)] and
A2: ex H being strict Subgroup of G() st P[H]
proof
  let phi be Automorphism of G();
  set UG = the carrier of G();
  set Fam = {A where A is Subset of G() : ex K being strict Subgroup
  of G() st A = the carrier of K & P[K]};
  consider H being strict Subgroup of G() such that
A3: P[H]
  by A2;
  A4: Fam <> {}
  proof
    consider A being set such that
    B1: A = the carrier of H;
    the carrier of H is Subset of G() by GROUP_2:def 5;
    then A in Fam by A3, B1;
    hence thesis;
  end;

  A5: for phi0 being Automorphism of G()
  for x being object st x in meet Fam
  holds phi0.x in meet Fam
  proof
    let phi0 be Automorphism of G();
    let x be object;
    assume x in meet Fam;
    then B1: for A1 holds A1 in Fam implies x in A1 by SETFAM_1:def 1;
    for A being set holds A in Fam implies phi0.x in A
    proof
      let A be set;
      assume C1: A in Fam;
      consider A0 being Subset of G() such that
      C2: A0=A & ex H being strict Subgroup of G() st A0 = the carrier of H & P[H]
      by C1;
      consider K being strict Subgroup of G() such that
      C3: A = the carrier of K & P[K]
      by C2;
      consider psi being Automorphism of G() such that
      C4: psi = phi0" & the multMagma of Image(phi0|Image(psi|K)) = the multMagma of K
      by Th18;
      x in K by C1,B1,C3;
      then C5: x in G() & dom phi0 = the carrier of G() by GROUP_2:40,FUNCT_2:def 1;
      P[Image(psi|K)] by C3,A1;
      then carr Image(psi|K) in Fam;
      then x in carr Image(psi|K) by B1;
      then phi0.x in phi0 .: (carr Image(psi|K)) by C5, FUNCT_1:def 6;
      then phi0.x in carr (phi0 .: Image(psi|K)) by GRSOLV_1:8;
      hence phi0.x in A by C3,C4,GRSOLV_1:def 3;
    end;
    hence thesis by A4, SETFAM_1:def 1;
  end;

  for x being object st x in meet Fam
  holds x in phi .: meet Fam
  proof
    let x be object;
    assume B1: x in meet Fam;
    then carr H in Fam & for A1 holds A1 in Fam implies x in A1 by A3,SETFAM_1:def 1;
    then B2: x in H;
    then B3: x is Element of G() by GROUP_2:42;
    reconsider psi = phi" as Automorphism of G() by GROUP_6:62;
    B4: dom phi = the carrier of G() by FUNCT_2:def 1;
    B5: psi.x in meet Fam by B1,A5;
    B6: phi.(psi.x) = x by B3,Th6;
    psi is bijective & x in G() by B2, GROUP_2:40;
    then psi.x in dom phi by B4, FUNCT_2:5;
    hence x in phi .: meet Fam by B6,B5,FUNCT_1:def 6;
  end;
  then P1: meet Fam c= phi .: meet Fam by TARSKI:def 3;

  for y being object st y in phi .: meet Fam
  holds y in meet Fam
  proof
    let y be object;
    assume y in phi .: meet Fam;
    then consider x being object such that
    B1: x in dom phi & x in meet Fam & y = phi.x
    by FUNCT_1:def 6;
    thus y in meet Fam by B1,A5;
  end;
  then P2: phi .: meet Fam c= meet Fam by TARSKI:def 3;

  thus thesis by P1,P2,XBOOLE_0:def 10;

end;

scheme
  MeetIsChar{G() -> Group, P[set]} :
  ex K being strict Subgroup of G() st
  the carrier of K = meet {A where A is Subset of G() :
  ex H being strict Subgroup of G() st A = the carrier of H & P[H]} &
  K is characteristic
provided
A1: for phi being Automorphism of G()
    for H being strict Subgroup of G()
    st P[H]
    holds P[Image(phi|H)] and
A2: ex H being strict Subgroup of G() st P[H]
proof
  set Fam = {A where A is Subset of G() :
             ex H being strict Subgroup of G()
             st A = the carrier of H & P[H]};

  A3: for phi being Automorphism of G()
  holds phi .: meet Fam = meet Fam from CharMeet(A1,A2);

  consider K being strict Subgroup of G() such that
  A4: the carrier of K = meet Fam
  from GROUP_4:sch 1(A2);
  take K;

  for phi being Automorphism of G()
  holds Image(phi|K) = K
  proof
    let phi be Automorphism of G();
    the carrier of Image(phi|K)
     = the carrier of phi .: K by GRSOLV_1:def 3
    .= phi .: (the carrier of K) by GRSOLV_1:8
    .= phi .: meet Fam by A4
    .= meet Fam by A3
    .= the carrier of K by A4;
    hence Image(phi|K) = K by GROUP_2:59;
  end;
  hence thesis by A4;
end;

theorem Th44:
  for G being non trivial Group
  holds (ex H being strict Subgroup of G st H is maximal) implies
  Phi(G) is characteristic Subgroup of G
proof
  let G be non trivial Group;
  defpred P[Subgroup of G] means $1 is maximal;
  assume A1: ex H being strict Subgroup of G st P[H];
  set MaxSubCarrs = {A where A is Subset of G :
                     ex H being strict Subgroup of G
                     st A = the carrier of H & P[H]};

  A2: for phi being Automorphism of G
  for H being strict Subgroup of G
  st P[H]
  holds P[Image(phi|H)] by Th26;

  consider K being strict Subgroup of G such that
  A3: the carrier of K = meet {A where A is Subset of G :
      ex H being strict Subgroup of G st A = the carrier of H & P[H]} and
  A4: K is characteristic
  from MeetIsChar(A2,A1);
  K = Phi(G) by A1,A3,GROUP_4:def 7;
  hence thesis by A4;
end;

theorem Th45:
  for phi being Automorphism of G
  holds phi .: commutators G = commutators G
proof
  let phi be Automorphism of G;
  for g being object
  st g in commutators G
  holds g in phi .: commutators G
  proof
    let g be object;
    assume B1: g in commutators G;
    then reconsider g as Element of G;
    consider a,b being Element of G such that
    B2: g = [.a,b.]
    by B1, GROUP_5:58;
    reconsider psi = phi" as Automorphism of G by GROUP_6:62;
    set x = psi.a;
    set y = psi.b;
    set h = [.x,y.];
    dom phi = the carrier of G by FUNCT_2:def 1;
    then B3: h in dom phi;
    psi.g = psi.([.a,b.]) by B2
         .= [.psi.a,psi.b.] by GROUP_6:34
         .= h;
    then B4: psi.g = h;
    B5: phi.((phi").g) = g by Th6;
    h in dom phi & h in commutators G by B3;
    then phi.h in phi .: commutators G by FUNCT_1:def 6;
    then phi.(psi.g) in phi .: commutators G by B4;
    then phi.((phi").g) in phi .: commutators G;
    then g in phi .: commutators G by B5;
    hence thesis;
  end;
  then P1: commutators G c= phi .: commutators G by TARSKI:def 3;
  for h being object
  st h in phi .: commutators G
  holds h in commutators G
  proof
    let h be object;
    assume B1: h in phi .: commutators G;
    consider g being object such that
        g in dom phi and
    B2: g in commutators G and
    B3: h = phi.g
    by B1,FUNCT_1:def 6;
    consider a,b be Element of G such that
    B4: g = [.a,b.]
    by B2,GROUP_5:58;
    h = phi.g by B3
     .= phi.([.a,b.]) by B4
     .= [.phi.a, phi.b.] by GROUP_6:34;
    hence h in commutators G;
  end;
  then phi .: commutators G c= commutators G by TARSKI:def 3;
  hence commutators G = phi .: commutators G by P1,XBOOLE_0:def 10;
end;

theorem Th46:
  for G being Group
  for phi being Automorphism of G
  for H being Subgroup of G
  st (for h being Element of H
      holds phi.h in H)
  holds Image(phi|H) is Subgroup of H
proof
  let G be Group;
  let phi be Automorphism of G;
  let H be Subgroup of G;
  assume A1: for h being Element of H holds phi.h in H;
  for y being object
  st y in rng(phi|H)
  holds y in the carrier of H
  proof
    let y be object;
    assume y in rng(phi|H);
    then consider x being object such that
    B1: x in dom(phi|H) and
    B2: y = (phi|H).x
    by FUNCT_1:def 3;
    B3: x in H by B1;
    then reconsider x as Element of H;
    x is Element of G by GROUP_2:42;
    then (phi|H).x = phi.x by B3,Th2;
    then B4: y = phi.x by B2;
    phi.x in H by A1;
    hence y in the carrier of H by B4;
  end;
  then rng(phi|H) c= the carrier of H by TARSKI:def 3;
  then the carrier of Image(phi|H) c= the carrier of H by GROUP_6:44;
  hence Image(phi|H) is Subgroup of H by GROUP_2:57;
end;

theorem Th47:
  for G being Group
  for A being non empty Subset of G
  st (for phi being Automorphism of G
      holds phi .: A = A)
  holds gr A is characteristic
proof
  let G be Group;
  let A be non empty Subset of G;
  assume A1: for phi being Automorphism of G holds phi .: A = A;
  A2: for phi being Automorphism of G
  for a being Element of A
  holds phi.a in A
  proof
    let phi be Automorphism of G;
    let a be Element of A;
    dom phi = the carrier of G by FUNCT_2:def 1;
    then a in dom phi;
    then phi.a in phi .: A by FUNCT_1:def 6;
    hence phi.a in A by A1;
  end;
  set H = gr A;
  A3: for phi being Automorphism of G holds Image(phi|H) is Subgroup of H
  proof
    let phi be Automorphism of G;
    B2: for h being Element of G
    st h in H
    holds phi.h in H
    proof
      let h be Element of G;
      assume h in H;
      then consider F1 being FinSequence of the carrier of G,
                    I being FinSequence of INT such that
      C2: len F1 = len I and
      C3: rng F1 c= A and
      C4: Product(F1 |^ I) = h
      by GROUP_4:28;
      deffunc F(Nat) = phi.(F1/.$1);
      consider F2 being FinSequence such that
      C5: len(F2) = len F1 and
      C6: for k being Nat st k in dom F2 holds F2.k = F(k)
      from FINSEQ_1:sch 2;
      C7: dom F2 = dom F1 by C5,FINSEQ_3:29;
      C8: F2 is FinSequence of the carrier of G & rng F2 c= A
      proof
        D1: for y being object st y in rng F2 holds y in A
        proof
          let y be object;
          assume y in rng F2;
          then consider k being object such that
          E2: k in dom F2 and
          E3: y = F2.k by FUNCT_1:def 3;
          reconsider k as Element of NAT by E2;
          set x = F1.k;
          x in rng F1 by FUNCT_1:def 3, E2, C7;
          then reconsider x as Element of A by C3;
          E4: x = F1/.k by E2,C7,PARTFUN1:def 6;
          y = F(k) by E2,E3,C6
           .= phi.(F1/.k)
           .= phi.(x) by E4;
          then y in A by A2;
          then E5: y is Element of A;
          thus thesis by E5;
        end;
        D2: rng F2 c= A by D1, TARSKI:def 3;
        for y being object st y in rng F2 holds y in the carrier of G
        proof
          let y be object;
          assume y in rng F2;
          then y in A by D1;
          hence y in the carrier of G;
        end;
        hence rng F2 c= the carrier of G by TARSKI:def 3;
        thus rng F2 c= A by D2;
      end;
      then reconsider F2 as FinSequence of the carrier of G;
      set h2 = Product(F2 |^ I);
      C9: (for k being Nat st k in dom F1
      holds F2.k = phi.(F1.k)) & len F1 = len I & len F2 = len I
      proof
        thus (for k being Nat st k in dom F1
        holds F2.k = phi.(F1.k))
        proof
          let k be Nat;
          assume D1: k in dom F1;
          then k in dom F2 by C7;
          then F2.k = F(k) by C6
                   .= phi.(F1/.k);
          hence F2.k = phi.(F1.k) by D1,PARTFUN1:def 6;
        end;
        thus len F1 = len I by C2;
        thus len F2 = len I by C2,C5;
      end;
      then phi.(Product(F1 |^ I)) = Product(F2 |^ I) by GROUP_9:125;
      then len F2 = len I & rng F2 c= A & Product(F2 |^ I) = phi.h
      by C4,C9,C8;
      hence phi.h in H by GROUP_4:28;
    end;
    for h being Element of H
    holds phi.h in H
    proof
      let h be Element of H;
      C1: h in H;
      h is Element of G by GROUP_2:42;
      then phi.h in H by B2,C1;
      hence thesis;
    end;
    hence Image(phi|H) is Subgroup of H by Th46;
  end;
  thus gr A is characteristic by A3,Th42;
end;

theorem Th48:
  G` is characteristic
proof
  [.1_G,1_G.] in commutators G;
  then A1: commutators G <> {};
  for phi being Automorphism of G holds phi .: commutators G =
  commutators G by Th45;
  then gr commutators G is characteristic by A1,Th47;
  hence thesis;
end;

theorem Th49:
  for G1,G2 being Group
  for H being Subgroup of G1
  for a being Element of G1
  for f being Homomorphism of G1,G2
  holds f.:(a * H) = (f.a) * (f .: H)
proof
  let G1,G2 be Group;
  let H be Subgroup of G1;
  let a be Element of G1;
  let f be Homomorphism of G1,G2;
  A1: dom f = the carrier of G1 by FUNCT_2:def 1;
  for y being object st y in f.:(a * H) holds y in (f.a)*(f.:H)
  proof
    let y be object;
    assume B1: y in f .: (a * H);
    then consider x being object such that
    B2: x in the carrier of G1 & x in (a * H) and
    B3: y = f.x
    by A1,FUNCT_1:def 6;
    consider h being Element of G1 such that
    B4: x = a*h & h in H
    by B2,GROUP_2:103;
    B5: y = f.(a*h) by B3,B4
         .= (f.a)*(f.h) by GROUP_6:def 6;
    dom f = the carrier of G1 & h in H & h in G1 by A1, B4, GROUP_2:40;
    then f.h in f.:(the carrier of H) by B4,FUNCT_1:def 6;
    then f.h in the carrier of (f.:H) by GRSOLV_1:8;
    then f.h in f.:H;
    then (f.a)*(f.h) in (f.a)*(f.:H) by GROUP_2:103;
    hence y in (f.a) * (f .: H) by B5;
  end;
  then A1: f.:(a * H) c= (f.a) * (f .: H) by TARSKI:def 3;
  for y being object st y in (f.a)*(f.:H) holds y in f.:(a * H)
  proof
    let y be object;
    assume B1: y in (f.a)*(f.:H);
    then consider g being Element of G2 such that
    B2: y = (f.a)*g and
    B3: g in (f.:H)
    by GROUP_2:103;
    g in Image(f|H) by B3,GRSOLV_1:def 2,def 3;
    then consider x being Element of H such that
    B4: g = (f|H).x
    by GROUP_6:45;
    B5: x in H & x is Element of G1 by GROUP_2:42;
    reconsider x as Element of G1 by GROUP_2:42;
    (f|H).x = f.x by B5,Th2;
    then B6: g = f.x by B4;
    B7: y = (f.a)*g by B2
         .= (f.a)*(f.x) by B6
         .= f.(a*x) by GROUP_6:def 6;
    B8: a*x in a*H by B5,GROUP_2:103;
    dom f = the carrier of G1 by FUNCT_2:def 1;
    then a*x in the carrier of G1 & dom f = the carrier of G1 by A1,B8;
    then (a*x) in dom f & (a*x) in a*H & y=f.(a*x) by A1,B7,B8;
    hence y in f.:(a * H) by A1,B7,B8,FUNCT_1:def 6;
  end;
  then A2: (f.a) * (f .: H) c= f.:(a * H) by TARSKI:def 3;
  thus f.:(a * H) = (f.a) * (f .: H) by A1,A2,XBOOLE_0:def 10;
end;

theorem Th50:
  for G1,G2 being Group
  for H being Subgroup of G1
  for a being Element of G1
  for f being Homomorphism of G1,G2
  holds f.:(H * a) = (f .: H) * (f.a)
proof
  let G1,G2 be Group;
  let H be Subgroup of G1;
  let a be Element of G1;
  let f be Homomorphism of G1,G2;
  A1: dom f = the carrier of G1 by FUNCT_2:def 1;
  for y being object st y in f.:(H * a) holds y in (f.:H)*(f.a)
  proof
    let y be object;
    assume B1: y in f .: (H * a);
    then consider x being object such that
    B2: x in the carrier of G1 & x in (H * a) and
    B3: y = f.x
    by A1,FUNCT_1:def 6;
    consider h being Element of G1 such that
    B4: x = h*a & h in H
    by B2,GROUP_2:104;
    B5: y = f.(h*a) by B3,B4
         .= (f.h)*(f.a) by GROUP_6:def 6;
    dom f = the carrier of G1 & h in H & h in G1 by A1, B4, GROUP_2:40;
    then f.h in f.:(the carrier of H) by B4,FUNCT_1:def 6;
    then f.h in the carrier of (f.:H) by GRSOLV_1:8;
    then f.h in f.:H;
    then (f.h)*(f.a) in (f.:H)*(f.a) by GROUP_2:104;
    hence thesis by B5;
  end;
  then A1: f.:(H * a) c= (f .: H) * (f.a) by TARSKI:def 3;
  for y being object st y in (f.:H)*(f.a) holds y in f.:(H * a)
  proof
    let y be object;
    assume B1: y in (f.:H)*(f.a);
    then consider g being Element of G2 such that
    B2: y = g*(f.a) and
    B3: g in (f.:H)
    by GROUP_2:104;
    g in Image(f|H) by B3,GRSOLV_1:def 2,def 3;
    then consider x being Element of H such that
    B4: g = (f|H).x
    by GROUP_6:45;
    B5: x in H & x is Element of G1 by GROUP_2:42;
    reconsider x as Element of G1 by GROUP_2:42;
    (f|H).x = f.x by B5,Th2;
    then B6: g = f.x by B4;
    B7: y = g*(f.a) by B2
         .= (f.x)*(f.a) by B6
         .= f.(x*a) by GROUP_6:def 6;
    B8: x*a in H*a by B5,GROUP_2:104;
    dom f = the carrier of G1 by FUNCT_2:def 1;
    then x*a in the carrier of G1 & dom f = the carrier of G1 by A1,B8;
    then (x*a) in dom f & (x*a) in H*a & y=f.(x*a) by A1,B7,B8;
    hence y in f.:(H * a) by A1,B7,B8,FUNCT_1:def 6;
  end;
  then A2: (f .: H) * (f.a) c= f.:(H * a) by TARSKI:def 3;
  thus f.:(H * a) = (f .: H)*(f.a) by A1,A2,XBOOLE_0:def 10;
end;

theorem Th51:
  for G being Group
  for N being strict normal Subgroup of G
  for phi being Automorphism of G
  holds Image(phi|N) is normal Subgroup of G
proof
  let G be Group;
  let N be strict normal Subgroup of G;
  let phi be Automorphism of G;
  set H = Image(phi|N);
  A1: H = phi .: N by GRSOLV_1:def 3;
  for g being Element of G
  holds g * H = H * g
  proof
    let g be Element of G;
    set f = (phi").g;
    f is Element of G;
    B1: phi.f = g by Th6;
    B2: f * N = N * f by GROUP_3:117;
    B3: phi .: (f * N) = (phi.f) * (phi .: N) by Th49
                      .= g * H by B1,A1;
    B4: phi .: (N * f) = (phi .: N)*(phi.f) by Th50
                      .= (phi .: N)*g by B1
                      .= H*g by A1;
    hence g * H = H * g by B2,B3,B4;
  end;
  hence H is normal Subgroup of G by GROUP_3:117;
end;

theorem Th52:
  for G being Group
  for H being strict Subgroup of G
  holds H is characteristic iff
        (for phi being Automorphism of G
         for x being Element of G
         st x in H
         holds phi.x in H)
proof
  let G be Group;
  let H be strict Subgroup of G;
  thus H is characteristic implies (for phi being Automorphism of G
                                    for x being Element of G
                                    st x in H
                                    holds phi.x in H)
  proof
    assume B1: H is characteristic;
    let phi be Automorphism of G;
    let x be Element of G;
    assume B2: x in H;
    B3: H = Image(phi|H) by B1, Def6
         .= phi .: H by GRSOLV_1:def 3;
    dom phi = the carrier of G by FUNCT_2:def 1;
    then x in dom phi;
    then phi.x in phi .: (the carrier of H) by B2,B3,FUNCT_1:def 6;
    then phi.x in the carrier of (phi .: H) by GRSOLV_1:8;
    then phi.x in the carrier of H by B3;
    hence thesis;
  end;
  thus (for phi being Automorphism of G
        for x being Element of G
        st x in H
        holds phi.x in H)
       implies H is characteristic
  proof
    assume B1: for phi being Automorphism of G for x being Element of G st x in H holds phi.x in H;
    for phi being Automorphism of G holds Image(phi|H) is Subgroup of H
    proof
      let phi be Automorphism of G;
      for x being Element of H holds phi.x in H
      proof
        let x be Element of H;
        C1: x in H;
        reconsider g=x as Element of G by GROUP_2:42;
        g in H by C1;
        then phi.g in H by B1;
        hence phi.x in H;
      end;
      hence Image(phi|H) is Subgroup of H by Th46;
    end;
    hence H is characteristic by Th42;
  end;
end;

theorem Th53:
  for G being Group
  for H,K being strict characteristic Subgroup of G
  holds H /\ K is characteristic Subgroup of G
proof
  let G be Group;
  let H,K be strict characteristic Subgroup of G;
  for phi being Automorphism of G
  for x being Element of G
  st x in H /\ K
  holds phi.x in H /\ K
  proof
    let phi be Automorphism of G;
    let x be Element of G;
    assume B1: x in H /\ K;
    then B2: x in H & x in K by GROUP_2:82;
    then B3: phi.x in H by Th52;
    phi.x in K by B2,Th52;
    hence phi.x in H /\ K by B3, GROUP_2:82;
  end;
  hence H /\ K is characteristic Subgroup of G by Th52;
end;

theorem Th54:
  for G being Group
  for H,K being strict characteristic Subgroup of G
  holds H "\/" K is characteristic Subgroup of G
proof
  let G be Group;
  let H,K be strict characteristic Subgroup of G;

  for phi being Automorphism of G
  for g being Element of G
  st g in H "\/" K
  holds phi.g in H "\/" K
  proof
    let phi be Automorphism of G;
    let g be Element of G;
    assume B1: g in H "\/" K;
    then g in H*K by GROUP_4:53;
    then g in carr H * carr K by GROUP_4:def 8;
    then consider h, k being Element of G such that
    B2: g = h*k and
    B3: h in carr H and
    B4: k in carr K by GROUP_2:8;
    h in H by B3;
    then B5: phi.h in H by Th52;
    k in K by B4;
    then B6: phi.k in K by Th52;
    then phi.k in carr K & phi.h in carr H by B5;
    then phi.h * phi.k in carr H*carr K by GROUP_2:def 2, GROUP_4:def 8;
    then phi.(h*k) in carr H * carr K by GROUP_6:def 6;
    then phi.g in carr H * carr K by B2;
    then phi.g in H*K by GROUP_4:def 8;
    hence phi.g in H "\/" K by GROUP_4:53;
  end;

  hence H "\/" K is characteristic Subgroup of G by Th52;
end;

theorem Th55:
  for G being Group
  for H,K being strict characteristic Subgroup of G
  for phi being Automorphism of G
  holds phi .: commutators(H,K) = commutators(H,K)
proof
  let G be Group;
  let H,K be strict characteristic Subgroup of G;
  let phi be Automorphism of G;
  A1: dom phi = the carrier of G by FUNCT_2:def 1;

  for x being object
  st x in commutators(H,K)
  holds x in phi .: commutators(H,K)
  proof
    let x be object;
    assume B0: x in commutators(H,K);
    then reconsider g=x as Element of G;
    consider h,k being Element of G such that
    B1: x = [.h,k.] and
    B2: h in H and
    B3: k in K by B0,GROUP_5:52;
    phi.k in K & phi.h in H by B2,B3,Th52;
    reconsider psi = phi" as Automorphism of G by GROUP_6:62;
    set a = psi.h;
    set b = psi.k;
    B4: a in H by B2,Th52;
    B5: b in K by B3,Th52;
    B6: psi.x = psi.([.h,k.]) by B1
    .= [.psi.h,psi.k.] by GROUP_6:34
    .= [.a,b.];
    phi.([. a,b .]) = phi.(psi.x) by B6
    .= g by Th6;
    then B7: x = phi.([. a,b .]);
    B8: [. a, b .] in commutators(H,K) by B4,B5,GROUP_5:52;
    then [. a,b .] in the carrier of G;
    then phi.([. a,b .]) in phi .: commutators(H,K) by A1,B8,FUNCT_1:def 6;
    hence x in phi .: commutators(H,K) by B7;
  end;
  then A2: commutators(H,K) c= phi .: commutators(H,K);

  for y being object
  st y in phi .: commutators(H,K)
  holds y in commutators(H,K)
  proof
    let y be object;
    assume B1: y in phi .: commutators(H,K);
    then consider x being object such that
    B2: x in dom phi & x in commutators(H,K) & y = phi.x
    by FUNCT_1:def 6;
    consider h,k being Element of G such that
    B3: x = [.h,k.] and
    B4: h in H and
    B5: k in K by B2,GROUP_5:52;
    set a = phi.h;
    B6: a in H by B4,Th52;
    set b = phi.k;
    B7: b in K by B5,Th52;
    phi.x = phi.([. h,k .]) by B3
    .= [. phi.h, phi.k .] by GROUP_6:34;
    hence y in commutators(H,K) by B2,B6,B7,GROUP_5:52;
  end;
  then phi .: commutators(H,K) c= commutators(H,K);
  hence phi .: commutators(H,K) = commutators(H,K) by A2,XBOOLE_0:def 10;
end;

theorem Th56:
  for G being Group
  for H,K being strict characteristic Subgroup of G
  holds [.H,K.] is characteristic Subgroup of G
proof
  let G be Group;
  let H,K be strict characteristic Subgroup of G;
  set A = commutators(H,K);
  A1: A is non empty Subset of G by GROUP_5:53;
  then reconsider A as non empty Subset of G;
  for phi being Automorphism of G
  holds phi .: A = A by Th55;
  then gr A is characteristic by A1, Th47;
  hence [.H,K.] is characteristic Subgroup of G by GROUP_5:63;
end;

begin :: Results concerning meets
scheme
  MeetIsMinimal{G() -> Group, P[set]} :
  ex H being strict Subgroup of G() st
  the carrier of H = meet {A where A is Subset of G() :
                           ex K being strict Subgroup of G()
                           st A = the carrier of K & P[K]} &
  (for K being strict Subgroup of G() st P[K] holds H is Subgroup of K)
provided
A1: ex H being strict Subgroup of G() st P[H]
proof
  set Fam = {A where A is Subset of G() : ex H being strict Subgroup of G()
                                          st A = the carrier of H & P[H]};
  consider H being strict Subgroup of G() such that
A2: the carrier of H = meet {A where A is Subset of G() :
                             ex K being strict Subgroup of G()
                             st A = the carrier of K & P[K]}
  from GROUP_4:sch 1(A1);
  take H;
  for K being strict Subgroup of G() st P[K] holds H is Subgroup of K
  proof
    let K be strict Subgroup of G();
    assume P[K];
    then carr K in Fam;
    then meet Fam c= carr K by SETFAM_1:3;
    then carr H c= carr K by A2;
    then H is Subgroup of K by GROUP_2:57;
    hence thesis;
  end;
  hence thesis by A2;
end;

theorem Th57:
  for G being Group
  for H1,H2 being Subgroup of G
  st H1 is Subgroup of H2
  for a being Element of G
  holds H1 |^ a is Subgroup of H2 |^ a
proof
  let G be Group;
  let H1,H2 be Subgroup of G;
  assume A1: H1 is Subgroup of H2;
  let a be Element of G;
  for h being Element of G
  st h in H1 |^ a
  holds h in H2 |^ a
  proof
    let h be Element of G;
    assume B1: h in H1 |^ a;
    then consider g being Element of G such that
    B2: h = g |^ a and
    B3: g in H1
    by GROUP_3:58;
    g in H2 by A1,B3,GROUP_2:40;
    then g |^ a in H2 |^ a by GROUP_3:58;
    hence thesis by B2;
  end;
  hence H1 |^ a is Subgroup of H2 |^ a by GROUP_2:58;
end;

scheme
  MeetOfNormsIsNormal{G() -> Group, P[set]} :
  for H being strict Subgroup of G()
  st the carrier of H = meet {A where A is Subset of G() :
                              ex N being strict Subgroup of G()
                              st A = the carrier of N & N is normal & P[N]}
  holds H is strict normal Subgroup of G()
provided
A1: ex H being strict normal Subgroup of G() st P[H]
proof
  defpred IsNorm[Subgroup of G()] means $1 is normal Subgroup of G();
  set Fam = {A where A is Subset of G() : ex N being strict Subgroup of G()
                                          st A = the carrier of N &
                                             N is normal & P[N]};
  let H be strict Subgroup of G();
  assume A2: the carrier of H = meet Fam;
  A3: Fam <> {}
  proof
    consider N being strict normal Subgroup of G() such that
    B1: P[N]
    by A1;
    carr N in Fam by B1;
    hence thesis;
  end;
  A4: for N being strict normal Subgroup of G() st P[N]
      holds H is Subgroup of N
  proof
    let N be strict normal Subgroup of G();
    assume P[N];
    then carr N in Fam;
    then meet Fam c= carr N by SETFAM_1:3;
    then the carrier of H c= carr N by A2;
    then H is Subgroup of N by GROUP_2:57;
    hence thesis;
  end;
  A5: for a being Element of G()
  for N being strict normal Subgroup of G() st P[N]
  holds H |^ a is Subgroup of N
  proof
    let a be Element of G();
    let N be strict normal Subgroup of G();
    assume B1: P[N];
    then H is Subgroup of N by A4;
    then H |^ a is Subgroup of N |^ a by Th57;
    hence H |^ a is Subgroup of N by GROUP_3:def 13;
  end;
  A6: for N being strict normal Subgroup of G() st carr N in Fam
  holds P[N]
  proof
    let N be strict normal Subgroup of G();
    assume B1: carr N in Fam;
    consider A being Subset of G() such that
    B2: A = carr N;
    A in Fam by B1,B2;
    then consider A0 being Subset of G() such that
    B3: A = A0 and
    B4: ex H0 being strict Subgroup of G()
        st A0 = the carrier of H0 & H0 is normal & P[H0];
    consider H0 being strict Subgroup of G() such that
    B5: A0 = the carrier of H0 & H0 is normal & P[H0]
    by B4;
    carr H0 = carr N by B2,B3,B5;
    then H0 = N by GROUP_2:59;
    hence P[N] by B5;
  end;
  A7: for a being Element of G()
  for N being strict normal Subgroup of G() st carr N in Fam
  holds H |^ a is Subgroup of N
  proof
    let a be Element of G();
    let N be strict normal Subgroup of G();
    assume B1: carr N in Fam;
    then P[N] by A6;
    then H is Subgroup of N by A4;
    then H |^ a is Subgroup of N |^ a by Th57;
    hence H |^ a is Subgroup of N by GROUP_3:def 13;
  end;
  A8: for a being Element of G()
  for N being strict normal Subgroup of G() st carr N in Fam
  holds carr(H |^ a) c= carr N
  proof
    let a be Element of G();
    let N be strict normal Subgroup of G();
    assume B1: carr N in Fam;
    then H |^ a is Subgroup of N by A7;
    hence carr(H |^ a) c= carr N by GROUP_2:def 5;
  end;

  for a being Element of G()
  holds H |^ a is Subgroup of H
  proof
    let a be Element of G();
    B1: Fam <> {} by A3;
    B2: (for N being strict normal Subgroup of G() st carr N in Fam
    holds carr(H |^ a) c= carr N) by A3,A8,GROUP_2:def 5;
    B3: for A being Subset of G() st A in Fam holds carr(H |^ a) c= A
    proof
      let A be Subset of G();
      assume C0: A in Fam;
      then consider A0 being Subset of G() such that
      C1: A = A0 and
      C2: ex H0 being strict Subgroup of G()
          st A0 = the carrier of H0 & H0 is normal & P[H0];
      consider H0 being strict Subgroup of G() such that
      C3: A0 = the carrier of H0 & H0 is normal & P[H0]
      by C2;
      reconsider H0 as strict normal Subgroup of G() by C3;
      carr H0 in Fam by C0,C1,C3;
      then carr(H |^ a) c= carr H0 by B1,B2;
      hence carr(H |^ a) c= A by C1,C3;
    end;
    for x being object st x in carr (H |^ a) holds x in meet Fam
    proof
      let x be object;
      assume C1: x in carr(H |^ a);
      for A being set st A in Fam holds x in A
      proof
        let A be set;
        assume C2: A in Fam;
        then consider A0 being Subset of G() such that
        C3: A0 = A and
        C4: ex H0 being strict Subgroup of G()
            st A0 = the carrier of H0 & H0 is normal & P[H0];
        carr(H |^ a) c= A0 by C2,C3,B3;
        hence thesis by C1,C3;
      end;
      hence thesis by B1,SETFAM_1:def 1;
    end;
    then carr (H |^ a) c= meet Fam by B1,SETFAM_1:def 1;
    then carr (H |^ a) c= carr H by A2;
    hence thesis by GROUP_2:57;
  end;
  then H is normal by GROUP_3:122;
  hence thesis;
end;


theorem Th58:
  for G being Group
  for X being finite set
  st X <> {} & (for A being Element of X
                ex N being strict normal Subgroup of G
                st A = the carrier of N)
  ex N being strict normal Subgroup of G
  st the carrier of N = meet X
proof
  let G be Group;
  let X be finite set;
  assume A1: X <> {};
  assume A2: for A being Element of X
             ex N being strict normal Subgroup of G
             st A = the carrier of N;
  defpred P[Group] means $1 is normal Subgroup of G & the carrier of $1 in X;
  set Fam = {A where A is Subset of G : ex N being strict Subgroup of G
                                        st A = the carrier of N & P[N]};
  set Fam2 = {A where A is Subset of G : ex N being strict Subgroup of G
                                         st A = the carrier of N &
                                         N is normal & P[N]};
  A3: ex H being strict Subgroup of G st P[H]
  proof
    X <> {} by A1;
    then X is non empty;
    then ex A being object st A in X by XBOOLE_0:def 1;
    then consider A being object such that
    B1: A in X;
    reconsider A as Element of X by B1;
    consider H being strict normal Subgroup of G such that
    B2: A = the carrier of H
    by A2;
    take H;
    the carrier of H in X by B1,B2;
    hence P[H];
  end;

  consider N being strict Subgroup of G such that
  A4: the carrier of N = meet Fam
  from GROUP_4:sch 1(A3);
  for A being object
  holds A in Fam iff A in Fam2
  proof
    let A be object;
    thus A in Fam implies A in Fam2
    proof
      assume B1: A in Fam;
      then consider A0 being Subset of G such that
      B2: A = A0 and
      B3: ex N being strict Subgroup of G st A0 = the carrier of N & P[N];

      consider N being strict Subgroup of G such that
      B4: A0 = the carrier of N & P[N]
      by B3;
      B5: N is normal by B4;
      hence A in Fam2 by B2,B3,B4,B5;
    end;
    thus A in Fam2 implies A in Fam
    proof
      assume B1: A in Fam2;
      then consider A0 being Subset of G such that
      B2: A = A0 and
      B3: ex N being strict Subgroup of G
          st A0 = the carrier of N & N is normal & P[N];
      thus A in Fam by B2,B3;
    end;
  end;
  then A5: Fam = Fam2 by TARSKI:2;
  B3: ex H being strict normal Subgroup of G st P[H]
  proof
    consider H being strict Subgroup of G such that
    B1: P[H]
    by A3;
    H is strict normal Subgroup of G & the carrier of H in X by B1;
    then reconsider H as strict normal Subgroup of G;
    take H;
    thus thesis by B1;
  end;
  A6: for H being strict Subgroup of G
  st the carrier of H = meet Fam2
  holds H is strict normal Subgroup of G
  from MeetOfNormsIsNormal(B3);
  then N is normal by A4,A5;
  then reconsider N as strict normal Subgroup of G;
  take N;

  for A being object
  holds A in Fam iff A in X
  proof
    let A be object;
    thus A in Fam implies A in X
    proof
      assume B1: A in Fam;
      then consider A0 being Subset of G such that
      B3: A = A0 and
      B2: ex N being strict Subgroup of G st A0 = the carrier of N & P[N];

      consider N being strict Subgroup of G such that
      B4: A0 = the carrier of N & P[N]
      by B2;
      reconsider N as strict normal Subgroup of G by B4;
      the carrier of N in X by B4;
      hence thesis by B2,B3,B4;
    end;
    thus A in X implies A in Fam
    proof
      assume B1: A in X;
      then consider N being strict normal Subgroup of G such that
      B2: A = the carrier of N
      by A2;
      B3: P[N] by B1,B2;
      A is Subset of G by B2,GROUP_2:def 5;
      hence A in Fam by B2,B3;
    end;
  end;
  then Fam = X by TARSKI:2;
  hence the carrier of N = meet X by A4,A5,TARSKI:2;
end;



begin :: Centralizer of Characteristic Subgroups is Characteristic
definition
  let G be Group;
  let A be Subset of G;
  func Centralizer A -> strict Subgroup of G means
  :Def8:
  the carrier of it = { b where b is Element of G :
                        for a being Element of G st a in A holds a*b = b*a };
  existence
  proof
    set Car = {b where b is Element of G :
               for a being Element of G st a in A holds a*b = b*a };
    C1: 1_G in Car
    proof
      for a being Element of G st a in A holds 1_G*a = a*1_G
      proof
        let a be Element of G;
        assume a in A;
        then 1_G*a = a by GROUP_1:def 4
                  .= a*1_G by GROUP_1:def 4;
        hence thesis;
      end;
      hence thesis;
    end;

    for x being object st x in Car holds x in the carrier of G
    proof
      let x be object;
      assume x in Car;
      then ex g being Element of G
           st (x = g) & (for a being Element of G st a in A holds a*g = g*a);
      hence thesis;
    end;

    then C2: Car is Subset of G by TARSKI:def 3;

    C3: for g1,g2 being Element of G st g1 in Car & g2 in Car
    holds g1*g2 in Car
    proof
      let g1,g2 be Element of G;
      assume B1: g1 in Car;
      assume B2: g2 in Car;
      B3: ex z1 being Element of G st (z1 = g1) & (for a being Element of G
      st a in A holds a*z1 = z1*a) by B1;
      B4: ex z2 being Element of G st (z2 = g2) & (for a being Element of G
      st a in A holds a*z2 = z2*a) by B2;
      for a being Element of G st a in A holds a*(g1*g2)=(g1*g2)*a
      proof
        let a be Element of G;
        assume Z1: a in A;
        a*(g1*g2) = (a*g1)*g2 by GROUP_1:def 3
                 .= (g1*a)*g2 by B1,Z1,B3
                 .= g1*(a*g2) by GROUP_1:def 3
                 .= g1*(g2*a) by B2,Z1,B4
                 .= g1*g2*a by GROUP_1:def 3;
        hence thesis;
      end;
      hence thesis;
    end;

    C4: for g being Element of G st g in Car holds g" in Car
    proof
      let g be Element of G;
      assume Z1: g in Car;
      then Z2: ex z1 being Element of G st (z1 = g) & (for a being
      Element of G st a in A holds z1*a=a*z1);
      for a being Element of G st a in A holds g" * a = a * g"
      proof
        let a be Element of G;
        assume Z3: a in A;
        g" * (g * a) = a by GROUP_3:1;
        then g" * ((a*g) * g") = g" * ((g*a) * g") by Z3,Z2
                              .= (g" * (g * a)) * g" by GROUP_1:def 3
                              .= ((g" * g) * a) * g"  by GROUP_1:def 3
                              .= (1_G * a) * g"  by GROUP_1:def 5
                              .= a * g"  by GROUP_1:def 4;
        then g" * a = a * g" by GROUP_3:1;
        hence thesis;
      end;
      hence thesis;
    end;

    thus thesis by C1,C2,C3,C4,GROUP_2:52;
  end;
  uniqueness
  proof
    let H1,H2 be strict Subgroup of G such that
A1: the carrier of H1 = {b where b is Element of G : for a being Element of G
                                                     st a in A
                                                     holds a*b = b*a } and
A2: the carrier of H2 = {b where b is Element of G : for a being Element of G
                                                     st a in A
                                                     holds a*b = b*a };
    for g being Element of G holds g in H1 iff g in H2 by A1,A2;
    hence thesis;
  end;
end;

theorem Th59:
  for G being Group
  for A being Subset of G
  for g being Element of G
  holds (for a being Element of G st a in A holds g*a = a*g) iff
        g is Element of Centralizer A
proof
  let G be Group;
  let A be Subset of G;
  let g be Element of G;
A1: the carrier of Centralizer A = {b where b is Element of G : for a
  being Element of G st a in A holds b*a=a*b} by Def8;
  hereby
    assume for a being Element of G st a in A holds g*a = a*g;
    then g in the carrier of Centralizer A by A1;
    hence g is Element of Centralizer A;
  end;
  assume g is Element of Centralizer A;
  then g in the carrier of Centralizer A;
  then ex b being Element of G st (b = g) & (for a being Element of G st a in A
  holds b*a = a*b) by A1;
  hence thesis;
end;

theorem Th60:
  for G being Group
  for A,B being Subset of G
  st A c= B
  holds Centralizer B is Subgroup of Centralizer A
proof
  let G be Group;
  let A,B be Subset of G;
  assume A1: A c= B;
  for g being Element of G st g in Centralizer B
  holds g in Centralizer A
  proof
    let g be Element of G;
    assume B1: g in Centralizer B;
    for a being Element of G st a in A
    holds g*a = a*g
    proof
      let a be Element of G;
      assume a in A;
      then a in B by A1;
      hence g*a = a*g by B1,Th59;
    end;
    then g is Element of Centralizer A by Th59;
    hence thesis;
  end;
  hence Centralizer B is Subgroup of Centralizer A by GROUP_2:58;
end;

definition
  let G be Group;
  let H be Subgroup of G;
  func Centralizer H -> strict Subgroup of G means
  :Def9:
  it = Centralizer carr H;
  correctness;
end;

theorem Th61:
  for G being Group
  for H being Subgroup of G
  holds the carrier of Centralizer H = {b where b is Element of G : for a
  being Element of G st a in H holds b*a=a*b}
proof
  let G be Group;
  let H be Subgroup of G;
  set A = carr H;
  set Car = {b where b is Element of G : for a being Element of G st a in H
                                         holds b*a=a*b};

  A1: the carrier of Centralizer A = {b where b is Element of G : for a
  being Element of G st a in A holds b*a=a*b} by Def8;
  A2: the carrier of Centralizer A = the carrier of Centralizer carr H by Def9;

  for x being object st x in Car holds x in the carrier of Centralizer H
  proof
    let x be object;
    assume B1: x in Car;
    ex b being Element of G
    st (x = b) & (for a being Element of G st a in carr(H) holds b*a=a*b)
    proof
      consider b being Element of G such that
      B2: x = b and
      B3: for a being Element of G st a in H holds b*a=a*b
      by B1;
      B4: for a being Element of G st a in carr H holds b*a=a*b
      proof
        let a be Element of G;
        assume Z1: a in carr H;
        then a in the carrier of H;
        then a in H;
        hence b*a=a*b by B3;
      end;
      hence thesis by B2;
    end;
    then x in the carrier of Centralizer carr H by A1,A2;
    hence thesis by Def9;
  end;
  then A3: Car c= the carrier of Centralizer H by TARSKI:def 3;

  for x being object st x in the carrier of Centralizer H holds x in Car
  proof
    let x be object;
    assume B1: x in the carrier of Centralizer H;
    then B2: x in the carrier of Centralizer carr H by Def9;
    then B3: ex b being Element of G
    st (x = b) & (for a being Element of G st a in carr(H) holds b*a=a*b)
    by A1,A2;
    ex b being Element of G
    st (x=b) & (for a being Element of G st a in H holds b*a=a*b)
    proof
      consider b being Element of G such that
      Z1: x = b  & (for a being Element of G st a in carr H holds b*a=a*b)
      by B3;
      for a being Element of G st a in H holds b*a=a*b by Z1;
      hence thesis by Z1;
    end;
    then x in Car;
    hence thesis;
  end;
  then the carrier of Centralizer H c= Car by TARSKI:def 3;
  hence thesis by A3,XBOOLE_0:def 10;
end;


theorem Th62:
  for G being Group
  for H being Subgroup of G
  for g being Element of G
  holds (for a being Element of G st a in H holds g*a = a*g) iff
        g is Element of Centralizer H
proof
  let G be Group;
  let H be Subgroup of G;
  let g be Element of G;
A1: the carrier of Centralizer H = {b where b is Element of G : for a
  being Element of G st a in H holds b*a=a*b} by Th61;
  hereby
    assume B1: for a being Element of G st a in H holds g*a = a*g;
    then g in the carrier of Centralizer H by A1;
    hence g is Element of Centralizer H;
  end;
  assume A2: g is Element of Centralizer H;
  then g in the carrier of Centralizer H;
  then ex b being Element of G st (b = g) & (for a being Element of G st a in H
  holds b*a = a*b) by A1;
  hence thesis;
end;

theorem Th63:
  for G being Group
  for A being Subset of G
  holds A is Subset of Centralizer (Centralizer A)
proof
  let G be Group;
  let A be Subset of G;
  set H = Centralizer A;
  for g being object
  st g in A
  holds g in the carrier of Centralizer H
  proof
    let g be object;
    assume B1: g in A;
    then reconsider g as Element of G by GROUP_2:40;
    for h being Element of G st h in H
    holds g*h = h*g by B1,Th59;
    then g is Element of Centralizer H by Th62;
    hence thesis;
  end;
  then A c= the carrier of Centralizer H by TARSKI:def 3;
  hence A is Subset of Centralizer H;
end;

theorem Th64:
  for G being Group
  for K being strict characteristic Subgroup of G
  holds (Centralizer K) is characteristic Subgroup of G
proof
  let G be Group;
  let K be strict characteristic Subgroup of G;
  for phi being Automorphism of G
  for x being Element of G
  st x in Centralizer K
  holds phi.x in Centralizer K
  proof
    let phi be Automorphism of G;
    let x be Element of G;
    assume B1: x in Centralizer K;
    set y = phi.x;
    reconsider psi = phi" as Automorphism of G by GROUP_6:62;
    for k being Element of G st k in K holds y*k = k*y
    proof
      let k be Element of G;
      assume C1: k in K;
      then C2: psi.k in K by Th52;
      set j = psi.k;
      C3: j in K by C2;
      then x*j = j*x by B1,Th62;
      then phi.(x*j) = phi.(j*x) by C3
                    .= phi.j * phi.x by GROUP_6:def 6;
      then phi.x * phi.j = phi.j * phi.x by GROUP_6:def 6;
      then y * phi.(psi.k) = phi.(psi.k) * y
                          .= k * y by Th6;
      hence y * k = k * y by Th6;
    end;
    then y is Element of Centralizer K by Th62;
    hence thesis;
  end;
  hence Centralizer K is characteristic Subgroup of G by Th52;
end;

definition
  let G be Group;
  let a be Element of G;
  redefine func {a} -> Subset of G;
  coherence
  proof
    for x being object st x in {a}
    holds x in the carrier of G
    proof
      let x be object;
      assume Z3: x in {a};
      then x = a by TARSKI:def 1;
      hence thesis;
    end;
    then {a} c= the carrier of G by TARSKI:def 3;
    hence {a} is Subset of G;
  end;
end;

theorem Th65:
  for x,y being object
  holds {x} = {y} iff x = y
proof
  let x,y be object;
  thus {x} = {y} implies x = y
  proof
    assume {x} = {y};
    then {x} c= {y} by XBOOLE_0:def 10;
    hence x = y by ZFMISC_1:3;
  end;
  thus x = y implies {x} = {y};
end;

definition
  let G be Group;
  let a be Element of G;
  func Normalizer a -> strict Subgroup of G equals
  Normalizer{a};
  correctness;
end;

theorem Th66:
  for G being Group
  for a,x being Element of G
  holds x in Normalizer a iff ex h being Element of G st x = h & a |^ h = a
proof
  let G be Group;
  let a,x be Element of G;
  A1: x in Normalizer{a} iff ex h being Element of G st x = h & {a} |^ h = {a}
  by GROUP_3:129;
  then A2: x in Normalizer{a} iff {a} |^ x = {a};
  A3: {a} |^ x = {a} |^ {x} by GROUP_3:def 3
              .= {a |^ x} by GROUP_3:37;
  then {a |^ x} = {a} iff a |^ x = a by Th65;
  then x in Normalizer{a} iff a |^ x = a by A2,A3;
  hence thesis by A1;
end;

theorem Th67:
  for G being finite Group
  for H1,H2 being strict Subgroup of G
  st card(H1 /\ H2) = card H1 & card(H1 /\ H2) = card H2
  holds H1 = H2
proof
  let G be finite Group;
  let H1,H2 be strict Subgroup of G;
  assume A1: card(H1 /\ H2) = card H1;
  assume A2: card(H1 /\ H2) = card H2;
  A3: H1 /\ H2 = H1
  proof
    reconsider H12 = H1 /\ H2 as strict Subgroup of H1 by GROUP_2:88;
    multMagma(# the carrier of H12, the multF of H12 #)
    = multMagma(# the carrier of H1, the multF of H1 #) by A1,GROUP_2:73;
    hence thesis;
  end;
  H1 /\ H2 = H2
  proof
    reconsider H12 = H1 /\ H2 as strict Subgroup of H2 by GROUP_2:88;
    multMagma(# the carrier of H12, the multF of H12 #)
    = multMagma(# the carrier of H2, the multF of H2 #) by A2,GROUP_2:73;
    hence thesis;
  end;
  hence thesis by A3;
end;

:: int_2 defines `a divides b` means `ex q st b = a*q`
theorem Th68:
  for a,b,c being Nat
  st c <> 0 & c*a divides c*b
  holds a divides b
proof
  let a,b,c be Nat;
  assume A1: c <> 0;
  assume A2: c*a divides c*b;
  then consider q being Integer such that
  A3: c*b = c*a*q by INT_1:def 3;
  b*c = a*q*c by A3;
  then A4: b = a*q by A1,XCMPLX_1:5;
  take q;
  thus thesis by A4, INT_1:def 3;
end;

theorem Th69:
  for a,b,c being Nat
  st b<>0 & b divides c & a*b,c are_coprime
  holds b=1
proof
  let a,b,c be Nat;
  assume A1: b<>0;
  assume A2: b divides c;
  assume A3: a*b,c are_coprime;
  b divides a*b by INT_1:def 3;
  then b divides (a*b gcd c) by A2,INT_2:22;
  then b divides 1 by A3, INT_2:def 3;
  hence b=1 by INT_2:13;
end;

theorem Th70:
  for G1,G2 being finite Group
  for N1 being normal Subgroup of G1
  for N2 being normal Subgroup of G2
  st G1./.N1, G2./.N2 are_isomorphic
  holds card(N2)*card(G1)=card(N1)*card(G2)
proof
  let G1,G2 be finite Group;
  let N1 be normal Subgroup of G1;
  let N2 be normal Subgroup of G2;
  assume A1: G1./.N1, G2./.N2 are_isomorphic;
  then card(G1./.N1) = card(G2./.N2) by GROUP_6:73
                    .= index N2 by GROUP_2:134,GROUP_6:27;
  then A2: index N1 = index N2 by GROUP_2:134,GROUP_6:27;
  set k = index N1;
  A3: k = index N2 by A2;
  A4: card(G1) = card(N1) * index(N1) by GROUP_2:147
              .= card(N1) * k;
  A5: card(G2) = card(N2) * index(N2) by GROUP_2:147
              .= card(N2) * k by A3;
  then card(N1)*card(G2) = card(N1)*card(N2)*k
                        .= card(N2)*card(N1)*k
                        .= card(N2)*(card(N1)*k)
                        .= card(N2)*card(G1) by A4;
  hence thesis;
end;

theorem Th71:
  for G being finite Group
  for K,N being strict normal Subgroup of G
  for m,d being Nat
  st m = card N & m = card K & d = card(K /\ N)
  holds d*card(N "\/" K) = m*m
proof
  let G be finite Group;
  let K,N be strict normal Subgroup of G;
  let m,d be Nat;
  assume A1: m = card N;
  assume A2: m = card K;
  assume A3: d = card(K /\ N);
  reconsider B=K as Subgroup of G;
  A4: card(B) = m by A2;

  N is Subgroup of B "\/" N by GROUP_4:60;
  then A5: (B "\/" N,N)`*` = the multMagma of N by GROUP_6:def 1;

  (B "\/" N)./.(B "\/" N,N)`*`, B./.(B /\ N) are_isomorphic by GROUP_6:81;
  then card(B /\ N)*card(B "\/" N) = card(B) * card((B "\/" N,N)`*`) by Th70;
  then d*card(B "\/" N) = card(B) * card((B "\/" N,N)`*`) by A3
                       .= card(B)*card(N) by A5
                       .= card(B)*m by A1
                       .= m*m by A4;
  hence d*card(N "\/" K) = m*m;
end;

:: Gorenstein, Finite Groups, Theorem 2.1.3
theorem Th72:
  for G being finite Group
  for N being strict normal Subgroup of G
  st card N, index N are_coprime
  holds N is characteristic Subgroup of G
proof
  let G be finite Group;
  let N be strict normal Subgroup of G;
  assume A1: card N, index N are_coprime;
  then card N is Nat;
  consider m being Nat such that
  A2: m = card N;
  A6: m <> 0 by A2;
  consider n being Nat such that
  A3: n = index N;
  m,n are_coprime by A1,A2,A3;
  then m gcd n = 1 by INT_2:def 3;
  A4: card G = m*n by A2,A3,GROUP_2:147;
  A5: for phi being Automorphism of G holds Image(phi|N) = N
  proof
    let phi be Automorphism of G;
    set K = Image(phi|N);
    K is normal Subgroup of G by Th51;
    then reconsider K as strict normal Subgroup of G;
    K = phi .: N by GRSOLV_1:def 3;
    then N,K are_isomorphic by Th20;
    then B1: card K = card N by GROUP_6:73;
    set d = card(N /\ K);
    B10: d <> 0;
    B11: d = card(N /\ K);
    B2: d divides m
    proof
      N /\ K is Subgroup of N by GROUP_2:88;
      then card(N /\ K) divides card N by GROUP_2:148;
      hence thesis by A2;
    end;
    then consider q being Nat such that
    B21: m = d*q by NAT_D:def 3;
    B22: q<>0 by B21,B10,A6;
    B3: card(N "\/" K) = m*q
    proof
      K /\ N = N /\ K
      proof
        carr(K /\ N) = (carr K) /\ (carr N) by GROUP_2:81,GROUP_2:def 10
        .= carr(N) /\ carr(K)
        .= carr(N /\ K) by GROUP_2:def 10;
        then carr(K /\ N) = carr(N /\ K);
        hence thesis by GROUP_2:59;
      end;
      then m = card N & d = card(K /\ N) by A2,B11;
      then d*card(N "\/" K) = m*m by B1,Th71;
      then d*card(N "\/" K) = m*(d*q) by B21
      .= m*d*q
      .= d*m*q
      .= d*(m*q);
      then card(N "\/" K) = m*q by XCMPLX_1:5;
      hence thesis;
    end;
    B4: m*q divides m*n
    proof
      N "\/" K is Subgroup of G;
      then card(N "\/" K) divides card G by GROUP_2:148;
      then m*q divides card G by B3;
      hence thesis by A4;
    end;
    then B41: q divides n by A6,Th68;
    d*q, n are_coprime by A1,A2,A3,B21;
    then q=1 by B22,B41,Th69;
    then B5: m = d by B21;
    card(K) = card(N /\ K) & card(N) = card(N /\ K)
    proof
      card(N) = m by A2
      .= d by B5
      .= card(N /\ K) by B1;
      then C1: card(N) = card(N /\ K);
      thus card(K) = card(N /\ K) by B1,C1;
      thus card(N) = card(N /\ K) by C1;
    end;
    then N=K by Th67;
    hence Image(phi|N) = N;
  end;
  thus N is characteristic Subgroup of G by A5,Def6;
end;

theorem Th73a:
  for G1,G2,G3 being Group
  for f1 being Homomorphism of G1,G2
  for f2 being Homomorphism of G2,G3
  for A being Subgroup of G1
  holds the multMagma of f2 .: (f1 .: A) = the multMagma of ((f2 * f1) .: A)
proof
  let G1,G2,G3 be Group;
  let f1 be Homomorphism of G1,G2;
  let f2 be Homomorphism of G2,G3;
  let A be Subgroup of G1;
  for z being Element of G3
  holds z in f2 .: (f1 .: A) iff z in (f2 * f1) .: A
  proof
    let z be Element of G3;
    thus z in f2 .: (f1 .: A) implies z in (f2 * f1) .: A
    proof
      assume A1: z in f2 .: (f1 .: A);
      then z in the carrier of f2 .: (f1 .: A);
      then z in f2 .: (the carrier of f1 .: A) by GRSOLV_1:8;
      then consider y being object such that
      A2: y in dom f2 and
      A3: y in the carrier of (f1 .: A) and
      A4: z = f2.y by FUNCT_1:def 6;
      y in f1 .: (the carrier of A) by A3,GRSOLV_1:8;
      then consider x being object such that
      A5: x in dom f1 & x in the carrier of A & y = f1.x by FUNCT_1:def 6;
      A6: x in dom(f2 * f1) by A2,A5,FUNCT_1:11;
      z = f2.y by A4;
      then z = f2.(f1.x) by A5;
      then A7: z = (f2 * f1).x by A3,A6,FUNCT_1:12;
      x in dom(f2 * f1) & x in the carrier of A & z = (f2 * f1).x by A5,A6,A7;
      then z in (f2 * f1) .: the carrier of A by FUNCT_1:def 6;
      hence thesis by GRSOLV_1:8;
    end;

    thus z in (f2 * f1) .: A implies z in f2 .: (f1 .: A)
    proof
      assume A1: z in (f2 * f1) .: A;
      then z in the carrier of (f2 * f1) .: A;
      then z in (f2 * f1) .: the carrier of A by GRSOLV_1:8;
      then consider x being object such that
      A2: x in dom (f2 * f1) & x in the carrier of A & z = (f2 * f1).x
      by FUNCT_1:def 6;
      A3: x in dom f1 & f1.x in dom f2 by A2,FUNCT_1:11;
      A4: (f2 * f1).x = f2.(f1.x) by A2,FUNCT_1:12;

      set y = f1.x;
      x in dom f1 & x in the carrier of A & y = f1.x by A2,A3;
      then A5: y in f1 .: (the carrier of A) by FUNCT_1:def 6;
      y in dom f2 by A3;
      z = (f2 * f1).x by A2
      .= f2.(f1.x) by A4
      .= f2.y;
      then y in dom f2 & y in f1.:(the carrier of A) & z = f2.y by A3,A4,A5;
      then z in f2 .: (f1 .: (the carrier of A)) by FUNCT_1:def 6;
      then z in f2 .: (the carrier of (f1 .: A)) by GRSOLV_1:8;
      then z in the carrier of (f2 .: (f1 .: A)) by GRSOLV_1:8;
      hence z in f2 .: (f1 .: A);
    end;

  end;

  hence the multMagma of f2 .: (f1 .: A) = the multMagma of ((f2 * f1) .: A)
  by GROUP_2:60;
end;


theorem Th73:
  for G being Group
  for N being strict normal Subgroup of G
  for phi being Automorphism of G
  st Image(phi|N) = N
  ex sigma being Automorphism of G./.N
  st (for x being Element of G holds sigma.(x*N) = (phi.x)*N)
proof
  let G be Group;
  let N be strict normal Subgroup of G;
  let phi be Automorphism of G;
  assume A1: Image(phi|N) = N;
  defpred P[set,set] means ex a being Element of G st $1 = a*N & $2 = (phi.a)*N;
  A2: for x being Element of G./.N ex y being Element of G./.N st P[x,y]
  proof
    let x be Element of G./.N;
    x in Cosets N;
    then consider a being Element of G such that
    B1: x = a*N by GROUP_2:def 15;
    (phi.a)*N in Cosets N by GROUP_2:def 15;
    then consider y being Element of G./.N such that
    B2: y = (phi.a)*N;
    take y;
    thus P[x,y] by B1,B2;
  end;

  consider sigma being Function of G./.N, G./.N such that
  A3: for x being Element of G./.N holds P[x, sigma.x]
  from FUNCT_2:sch 3(A2);

  A4: for a being Element of G holds sigma.(a*N) = (phi.a)*N
  proof
    let a be Element of G;
    B0: dom sigma = the carrier of G./.N by FUNCT_2:def 1;
    B1: a*N in Cosets N by GROUP_2:def 15;
    then consider x being Element of G./.N such that
    B2: x = a*N;

    P[x, sigma.x] by A3;
    then consider b being Element of G such that
    B3: x = b*N & sigma.x = (phi.b)*N;
    B4: a*N = b*N by B2,B3;
    then B6: b" * a in N by GROUP_2:114;
    consider n being Element of G such that
    B7: n = b" * a & n in N by B6;

    b*n = b*(b" * a) by B7
    .= (b * b") * a by GROUP_1:def 3
    .= 1_G * a by GROUP_1:def 5
    .= a by GROUP_1:def 4;
    then B8: b*n = a;

    dom phi = the carrier of G & n in N by B7, FUNCT_2:def 1;
    then phi.n in phi .: (the carrier of N) by FUNCT_1:def 6;
    then phi.n in the carrier of (phi .: N) by GRSOLV_1:8;
    then phi.n in Image(phi|N) by GRSOLV_1:def 3;
    then B9: phi.n in N by A1;
    B10: phi.n * N = carr N by B9, GROUP_2:113;
    phi.a * N = phi.(b * n) * N by B8
    .= (phi.b * phi.n) * N by GROUP_6:def 6
    .= phi.b * (phi.n * N) by GROUP_2:105
    .= phi.b * N by B10
    .= sigma.x by B3;
    hence thesis by B2;
  end;

  for x,y being Element of G./.N holds sigma.(x*y) = sigma.x * sigma.y
  proof
    let x,y be Element of G./.N;
    P[x, sigma.x] by A3;
    then consider a being Element of G such that
    B1: x = a*N & sigma.x = (phi.a)*N;
    P[y, sigma.y] by A3;
    then consider b being Element of G such that
    B2: y = b*N & sigma.y = (phi.b)*N;
    B3: for g1,g2 being Element of G holds (g1*N)*(g2*N) = g1*g2*N
    proof
      let g1,g2 be Element of G;
      (g1*N)*(g2*N) = (g1 * N) * (N * g2) by GROUP_3:117
                   .= g1 * N * N * g2 by GROUP_3:11
                   .= g1 * (N*N) * g2 by GROUP_4:45
                   .= g1 * N * g2 by GROUP_2:76
                   .= g1 * (N * g2) by GROUP_2:106
                   .= g1 * (g2 * N) by GROUP_3:117
                   .= g1 * g2 * N by GROUP_2:105;
      hence thesis;
    end;

    B4: x * y = @x * @y by GROUP_6:def 3
             .= (a * N) * (b * N) by B1,B2
             .= a * b * N by B3;

    B5: (sigma.x) * (sigma.y) = phi.(a*b) * N
    proof
      sigma.x * sigma.y = @(sigma.x) * @(sigma.y) by GROUP_6:def 3
                       .= ((phi.a)*N) * ((phi.b) * N) by B1,B2
                       .= (phi.a) * (phi.b) * N by B3
                       .= phi.(a*b) * N by GROUP_6:def 6;
      hence thesis;
    end;
    sigma.(x * y) = sigma.(a * b * N) by B1,B2,B4
                 .= phi.(a*b) * N by A4
                 .= sigma.x * sigma.y by B5;
    hence sigma.(x*y) = sigma.x * sigma.y;
  end;

  then reconsider sigma as Homomorphism of G./.N, G./.N by GROUP_6:def 6;
  sigma is bijective
  proof
     :: phi.n in N;
    BB1: for x being Element of G holds x*N in Ker sigma iff x in N
    proof
      let x be Element of G;
      x*N in Left_Cosets N by GROUP_2:def 15;
      then reconsider z = x*N as Element of G./.N by GROUP_6:def 4;
      B1: z = x*N;
      B2: dom phi = the carrier of G by FUNCT_2:def 1;
      B3: (phi").(phi.x) = x by FUNCT_2:26;
      thus x*N in Ker sigma implies x in N
      proof
        assume Z1: (x*N) in Ker sigma;
        then z in Ker sigma;
        then sigma.z = 1_(G./.N) by GROUP_6:41;
        then Z2: sigma.(x*N) = 1_(G./.N) by GROUP_6:41
                            .= carr N by GROUP_6:24;
        (phi.x)*N = sigma.(x*N) by A4,B1
                 .= carr N by Z2;
        then Z3: phi.x in N by GROUP_2:113;
        then phi.x in Image(phi|N) by A1;
        then Z33: phi.x in phi .: N by GRSOLV_1:def 3;
        Z4: phi" is Automorphism of G by GROUP_6:62;
        then Z5: dom (phi") = the carrier of G by FUNCT_2:def 1;
        then phi.x in dom (phi") by B2,Z3;
        then (phi").(phi.x) in phi" .: (the carrier of N) by Z3,Z4,B2,FUNCT_1:def 6;
        then Z6: x in phi" .: (the carrier of N) by B3;
        consider psi being Automorphism of G such that
        Z7: psi = phi" and
        Z8: the multMagma of Image(phi|Image(psi|N)) = the multMagma of N
        by Th18;
        reconsider i = id the carrier of G as Automorphism of G by GROUP_6:38;
        the carrier of G <> {} & phi is onto;
        then Z7a: psi * phi = id the carrier of G by Z7,FUNCT_2:29,FUNCT_2:60;
        dom psi = the carrier of G by FUNCT_2:def 1;
        then phi.x in dom psi by Z3,GROUP_2:39;
        then psi.(phi.x) in psi .: (the carrier of (phi .: N)) by Z33,FUNCT_1:def 6;
        then psi.(phi.x) in the carrier of (psi .: (phi .: N)) by GRSOLV_1:8;
        then psi.(phi.x) in psi .: (phi .: N);
        then psi.(phi.x) in (psi * phi) .: N by Th73a;
        then (phi").(phi.x) in (psi * phi) .: N by Z7;
        then x in (psi * phi) .: N by B3;
        then x in i .: N by Z7a;
        then x in the carrier of (i .: N);
        then x in i .: (the carrier of N) by GRSOLV_1:8;
        then Z8: x in (id the carrier of G) .: (the carrier of N);
        the carrier of N is Subset of the carrier of G by GROUP_2:def 5;
        then x in (the carrier of N) by Z8,FUNCT_1:92;
        hence x in N;
      end;
      thus x in N implies x*N in Ker sigma
      proof
        assume Z1: x in N;
        then x * N = carr N by GROUP_2:113;
        then Z2: x * N = 1_(G./.N) by GROUP_6:24;
        then sigma.(x*N) = 1_(G./.N) by GROUP_6:31;
        hence x*N in Ker sigma by Z2,GROUP_6:41;
      end;
    end;
    for x being Element of G./.N holds x in Ker sigma iff x in (1).(G./.N)
    proof
      let x be Element of G./.N;
      thus x in Ker sigma implies x in (1).(G./.N)
      proof
        assume Z1: x in Ker sigma;
        x in G./.N;
        then consider g being Element of G such that
        Z2: x = g*N by GROUP_2:def 15;
        g*N in Ker sigma by Z1,Z2;
        then g in N by BB1;
        then g*N = carr N by GROUP_2:113;
        then Z3: g*N = 1_(G./.N) by GROUP_6:24;
        then g*N in {1_(G./.N)} by TARSKI:def 1;
        then g*N in (1).(G./.N) by GROUP_2:def 7;
        hence x in (1).(G./.N) by Z2;
      end;
      thus x in (1).(G./.N) implies x in Ker sigma
      proof
        assume Z1: x in (1).(G./.N);
        then x in the carrier of (1).(G./.N);
        then x in {1_(G./.N)} by GROUP_2:def 7;
        then x = 1_(G./.N) by TARSKI:def 1;
        then sigma.x = 1_(G./.N) by GROUP_6:31;
        hence x in Ker sigma by GROUP_6:41;
      end;
    end;

    then Ker sigma = (1).(G./.N) by GROUP_2:61;
    hence sigma is one-to-one by GROUP_6:56;

    for y being Element of G./.N holds y in Image sigma
    proof
      let y be Element of G./.N;
      y in G./.N;
      then consider b being Element of G such that
      B2: y = b*N by GROUP_2:def 15;
      reconsider psi = phi" as Automorphism of G by GROUP_6:62;
      psi" = phi by Th5;
      consider a being Element of G such that
      B3: a = psi.b;
      a*N in Cosets N by GROUP_2:def 15;
      then a*N in G./.N;
      then consider x being Element of G./.N such that
      B4: x = a*N;
      a = (phi").b by B3;
      then phi.a = phi.((phi").b)
      .= b by Th6;
      then B5: phi.a = b;
      sigma.x = sigma.(a*N) by B4
      .= (phi.a)*N by A4
      .= b*N by B5
      .= y by B2;
      then B6: y = sigma.x;
      then y in Image sigma by GROUP_6:45;
      hence thesis;
    end;
    then Image sigma = G./.N by GROUP_2:62;
    hence sigma is onto by GROUP_6:57;
  end;
  then reconsider sigma as Automorphism of G./.N;
  take sigma;
  let x be Element of G;
  thus sigma.(x*N) = (phi.x)*N by A4;
end;


theorem Th74:
  for G being finite Group
  for H being strict characteristic Subgroup of G
  for K being strict Subgroup of G
  st H is Subgroup of K
  holds H is normal Subgroup of K
proof
  let G be finite Group;
  let H be strict characteristic Subgroup of G;
  let K be strict Subgroup of G;
  assume A1: H is Subgroup of K;
  H is normal Subgroup of G;
  then A2: Ker (nat_hom H) = H by GROUP_6:43;
  then A3: for g being Element of G
  holds g in Ker (nat_hom H) iff g in H by GROUP_2:60;

  reconsider R = Ker ((nat_hom H)|K) as strict Subgroup of K;

  A4: for k being Element of K
  holds k in H iff k in Ker ((nat_hom H)|K)
  proof
    let k be Element of K;
    reconsider g=k as Element of G by GROUP_2:42;
    B2: g = k;
    then B4: g in K;
    thus k in H implies k in Ker ((nat_hom H)|K)
    proof
      assume B1: k in H;
      B3: g in H by B1;
      then B4: g in K by A1;
      g is Element of G;
      then g in Ker (nat_hom H) iff g in H by A3;
      then (nat_hom H).g = 1_(G./.H) by B3,GROUP_6:41;
      then ((nat_hom H)|K).g = 1_(G./.H) by B4,Th2;
      then ((nat_hom H)|K).k = 1_(G./.H) by B1,GRSOLV_1:def 2,GROUP_6:41;
      then k in Ker ((nat_hom H)|K) by GROUP_6:41;
      hence thesis;
    end;
    thus k in Ker ((nat_hom H)|K) implies k in H
    proof
      assume Z1: k in Ker ((nat_hom H)|K);
      then Z2: g in Ker ((nat_hom H)|K) by B2;
      then ((nat_hom H)|K).g = (nat_hom H).g by B4,Th2;
      then (nat_hom H).g = 1_(G./.H) by Z2,GROUP_6:41;
      then g in Ker (nat_hom H) by GROUP_6:41;
      then g in H by GROUP_6:43;
      hence k in H by B2;
    end;

  end;
  reconsider H1=H as strict Subgroup of K by A1;
  for k being Element of K holds k in H1 iff k in R
  proof
    let k be Element of K;
    thus k in H1 implies k in R by A4;
    thus k in R implies k in H1 by A4;
  end;
  then the multMagma of R = the multMagma of H1 by GROUP_2:60;
  hence thesis;
end;

:: Gorenstein, Finite Groups, Theorem 2.1.2 (iv)
theorem
  for G being finite Group
  for H being strict characteristic Subgroup of G
  for K being strict Subgroup of G
  st H is Subgroup of K &
  K./.(K,H)`*` is characteristic Subgroup of G./.H
  holds K is characteristic Subgroup of G
proof
  let G be finite Group;
  let H be strict characteristic Subgroup of G;
  let K be strict Subgroup of G;
  assume A1: H is Subgroup of K;
  assume A2: K./.(K,H)`*` is characteristic Subgroup of G./.H;
  A3: (K,H)`*` = H by A1,GROUP_6:def 1;
  then A5: carr((K,H)`*`) = carr(H);
  A4: H is normal Subgroup of K by A1,Th74;
  for phi being Automorphism of G
  for k being Element of G st k in K
  holds phi.k in K
  proof
    let phi be Automorphism of G;
    let k be Element of G;
    assume B1: k in K;
    Image(phi|H) = H by Def6;
    then consider sigma being Automorphism of G./.H such that
    B2: for x being Element of G holds sigma.(x*H) = (phi.x)*H
    by Th73;
    consider J being strict characteristic Subgroup of G./.H such that
    B3: J = (K./.(K,H)`*`) by A2;
    B4: for j being Element of G./.H st j in J holds sigma.j in J by Th52;
    Image(sigma|J) = J by Def6;

    B5: for k1 being Element of G st k1*H in J holds k1 in K
    proof
      let k1 be Element of G;
      assume C1: k1*H in J;
      C2: the carrier of H = the carrier of ((K,H)`*`) by A1,GROUP_6:def 1;
      then k1*H = k1*(carr H) by GROUP_2:def 14
      .= k1*(K,H)`*` by C2;
      then C3: k1*H = k1*(K,H)`*`;
      set x = k1*(K,H)`*`;
      C3a: k1*H = x by C3;
      C4: x in J by C1,C3;
      then x in (K./.(K,H)`*`) by B3;
      then C5: x in Cosets (K,H)`*` by GROUP_6:def 4;
      then consider a being Element of K such that
      C6: x = a*(K,H)`*` by GROUP_2:def 15;

      reconsider a1 = a as Element of G by GROUP_2:42;
      C7: a1 in K;
      for j1 being object holds j1 in a*(K,H)`*` iff j1 in a1*H
      proof
        let j1 be object;
        thus j1 in a*(K,H)`*` implies j1 in a1*H
        proof
          assume Z1: j1 in a*(K,H)`*`;
          Z2: H = (K,H)`*` by A1,GROUP_6:def 1;
          consider g1 being Element of K such that
          Z3: j1 = a*g1 & g1 in (K,H)`*` by Z1,GROUP_2:103;
          reconsider g=g1 as Element of G by GROUP_2:42;
          g1=g & a1=a;
          then Z4a: a*g1 = a1*g by GROUP_2:43;
          then Z4: j1 = a1*g by Z3;
          g in H by Z2,Z3;
          then a1*g in a1*H by GROUP_2:103;
          hence j1 in a1*H by Z4;
        end;

        thus j1 in a1*H implies j1 in a*(K,H)`*`
        proof
          assume Z1: j1 in a1*H;
          then consider g1 being Element of G such that
          Z2: j1 = a1*g1 & g1 in H by GROUP_2:103;
          Z3: H = (K,H)`*` by A1, GROUP_6:def 1;
          then reconsider g=g1 as Element of K by Z2,GROUP_2:42;
          g in H by Z2;
          then Z3b: g in (K,H)`*` by Z3;
          then a=a1 & g=g1 & g in (K,H)`*`;
          then Z4a: a1*g1 = a*g by GROUP_2:43;
          then Z4: j1 = a*g by Z2;
          g in (K,H)`*` by Z3b;
          then a*g in a*(K,H)`*` by GROUP_2:103;
          hence j1 in a*(K,H)`*` by Z4;
        end;

      end;
      then a*(K,H)`*` = a1*H by TARSKI:2;
      then a1*H = x by C6
      .= k1*H by C3;
      then (a1") * k1 in H by GROUP_2:114;
      then (a1") * k1 in K by A4,GROUP_2:41;
      then C8: a1 * ((a1") * k1) in K by C7,GROUP_2:50;
      a1 * ((a1") * k1) = (a1 * a1") * k1 by GROUP_1:def 3
      .= 1_G * k1 by GROUP_1:def 5
      .= k1 by GROUP_1:def 4;
      hence k1 in K by C8;
    end;

    B6: for k1 being Element of G holds k1 in K iff k1*H in J
    proof
      let k1 be Element of G;
      thus k1 in K implies k1*H in J
      proof
        assume C1: k1 in K;
        then reconsider k2=k1 as Element of K;
        C2: k2*((K,H)`*`) in J by B3, GROUP_2:def 15;
        C3: k1 = k2;
        C4: k2*((K,H)`*`) = k2*carr(((K,H)`*`));
        for x being object holds x in k2*carr((K,H)`*`) iff x in k1*carr(H)
        proof
          let x be object;
          thus x in k2*carr((K,H)`*`) implies x in k1*carr(H)
          proof
            assume Z1: x in k2*carr((K,H)`*`);
            K is Group &
            k2 is Element of K & (K,H)`*` is normal Subgroup of K by GROUP_6:def 1;
            then x in k2*((K,H)`*`) iff
                 ex g being Element of K st (x = k2*g & g in (K,H)`*`)
            by GROUP_2:103;
            then consider huh being Element of K such that
            Z2: x = k2*huh & huh in (K,H)`*` by Z1; :: , GROUP_2:103, GROUP_6:def 1;
            huh in (K,H)`*` by Z2;
            then Z3: huh in H by A3;
            then reconsider huh2=huh as Element of G by GROUP_2:42;
            set x2 = k1*huh2;
            k1=k2 by C3;
            then k2*huh=k1*huh2 by GROUP_2:43;
            x = k1*huh2 by C3,Z2,Z3,GROUP_2:43;
            hence thesis by Z1,C3,Z3,GROUP_2:27;
          end;
          assume Z2: x in k1*carr(H);
          then consider h1 being Element of G such that
          Z3: x = k1*h1 & h1 in carr(H) by GROUP_2:27;
          the multMagma of ((K,H)`*`) = the multMagma of H by A1, GROUP_6:def 1;
          then Z4: the carrier of H = the carrier of ((K,H)`*`) by GROUP_6:def 1;
          h1 in carr H by Z3;
          then Z5: h1 in carr((K,H)`*`) by Z4,GROUP_6:def 1;
          then h1 in carr(H) by Z4,GROUP_6:def 1;
          Z6: h1 in H by Z3;
          then reconsider h2=h1 as Element of K by A1,GROUP_2:42;
          reconsider H1=H as normal Subgroup of K by A1,A4;
          Z7: k1*h1 = k2*h2 by GROUP_2:43;
          then Z8: x = k2*h2 by Z3;
          (K,H)`*` is normal Subgroup of K by GROUP_6:def 1;
          then the multMagma of ((K,H)`*`) = the multMagma of H by A1,GROUP_6:def 1;
          then Z11: the carrier of H = the carrier of ((K,H)`*`) by GROUP_6:def 1;
          Z9: carr H1 = carr H;
          then the carrier of H1 = the carrier of H;
          then the carrier of H1 = the carrier of ((K,H)`*`) by Z4;
          then carr H1 = carr((K,H)`*`);
          then Z10: carr H1 = carr((K,H)`*`) by Z11;
          then k2*carr(H1) = k2*carr((K,H)`*`);
          h2 in H1 by Z6,Z9;
          then k2*h2 in k2*carr(H1) by C3,Z5,Z7,GROUP_2:27;
          then x in k2*carr(H1) by Z8;
          then x in k2*carr((K,H)`*`) by Z9,Z11;
          then x in k2*carr((K,H)`*`);
          hence x in k2*carr((K,H)`*`);
        end;

        then k2*carr(((K,H)`*`)) = k1*carr(H) by TARSKI:2
                                .= k1*H;
        hence k1*H in J by A3,C2;
      end;
      thus k1*H in J implies k1 in K by B5;
    end;
    then B6: k*H in J by B1;
    then reconsider kH = k*H as Element of G./.H by GROUP_2:42;
    kH in J by B6;
    then sigma.(kH) in J by B4;
    then sigma.(k*H) in J & sigma.(k*H) = (phi.k)*H by B1,B2,B4;
    then (phi.k)*H in J by B4,B5;
    then phi.k in K by B5;
    hence thesis;
  end;

  hence K is characteristic Subgroup of G by Th52;
end;

theorem
  for G being Group
  for H being Subgroup of G
  holds H is Subgroup of Centralizer H iff H is commutative Group
proof
  let G be Group;
  let H be Subgroup of G;
  thus H is Subgroup of Centralizer H implies H is commutative Group
  proof
    assume A1: H is Subgroup of Centralizer H;
    A2: for g,h being Element of G st g in H & h in H holds g*h=h*g
    proof
      let g,h be Element of G;
      assume B1: g in H;
      assume B2: h in H;
      g in Centralizer H by B1,A1,GROUP_2:40;
      hence g*h=h*g by B2,Th62;
    end;
    for g,h being Element of H holds g*h=h*g
    proof
      let g,h be Element of H;
      B1: g in H & h in H;
      reconsider g1=g, h1=h as Element of G by GROUP_2:42;
      g1 in H & h1 in H by B1;
      then B2: g1*h1=h1*g1 by A2;
      g*h = g1*h1 by GROUP_2:43
         .= h1*g1 by B2
         .= h*g by GROUP_2:43;
      hence thesis;
    end;
    hence thesis by GROUP_1:def 12;
  end;

  thus H is commutative Group implies H is Subgroup of Centralizer H
  proof
    assume A1: H is commutative Group;
    A2: for g,h being Element of H holds g*h=h*g by A1,GROUP_1:def 12;
    A3: for g,h being Element of G st g in H & h in H holds g*h=h*g
    proof
      let g,h be Element of G;
      assume B1: g in H;
      assume B2: h in H;
      reconsider g1=g,h1=h as Element of H by B1,B2;
      g*h = g1*h1 by GROUP_2:43
         .= h1*g1 by A2
         .= h*g by GROUP_2:43;
      hence g*h=h*g;
    end;
    for g being Element of G st g in H holds g in Centralizer H
    proof
      let g be Element of G;
      assume B1: g in H;
      for a being Element of G st a in H holds g*a = a*g by B1,A3;
      then g is Element of Centralizer H by Th62;
      hence g in Centralizer H;
    end;
    hence thesis by GROUP_2:58;
  end;
end;

theorem
  for G being Group
  holds Centralizer (Omega).G = center G
proof
  let G be Group;
  A1: for g being Element of G holds g in Centralizer (Omega).G iff g in center G
  proof
    let g be Element of G;
    thus g in Centralizer (Omega).G implies g in center G
    proof
      assume B1: g in Centralizer (Omega).G;
      then B2: for a being Element of G st a in (Omega).G holds g*a = a*g by Th62;
      for a being Element of G holds g*a = a*g
      proof
        let a be Element of G;
        a in G;
        then a in (Omega).G by GROUP_2:def 8;
        hence g*a = a*g by B2;
      end;
      hence g in center G by GROUP_5:77;
    end;

    thus g in center G implies g in Centralizer (Omega).G
    proof
      assume B1: g in center G;
      then for b being Element of G holds g*b = b*g by GROUP_5:77;
      then for b being Element of G st b in (Omega).G holds g*b = b*g
      by GROUP_2:def 8;
      then g is Element of Centralizer (Omega).G by Th62;
      hence thesis;
    end;

  end;
  hence Centralizer (Omega).G = center G by GROUP_2:60;
end;

:: Th62:
:: for g being Element of G
:: holds (for a being Element of G st a in H holds g*a = a*g) iff
::        g is Element of Centralizer H
Lm8:
  for G being Group
  for H being Subgroup of G
  holds Centralizer H is strict Subgroup of Normalizer H
proof
  let G be Group;
  let H be Subgroup of G;
  set Z = Centralizer H;
  for g being Element of G st g in Centralizer H holds g in Normalizer H
  proof
    let g be Element of G;
    assume A1: g in Centralizer H;
    then g is Element of Centralizer H;
    then A2: for a being Element of G st a in H holds g*a = a*g by Th62;
    A3: for a being Element of G st a in H holds a = (g")*a*g
    proof
      let a be Element of G;
      assume B1: a in H;
      then B2: g*a = a*g by A2;
      then g" * (a * g) = g" * (g*a)
                       .= (g" * g)*a by GROUP_1:def 3
                       .= (1_G)*a by GROUP_1:def 5
                       .= a by GROUP_1:def 4;
      hence a = (g")*a*g by GROUP_1:def 3;
    end;
    for a being Element of G holds a in H iff a in (H |^ g)
    proof
      let a be Element of G;
      thus a in H implies a in H |^ g
      proof
        assume B1: a in H;
        then B2: a = a |^ g by A3, GROUP_3:def 2;
        hence a in H |^ g by B1, GROUP_3:58;
      end;
      thus a in H |^ g implies a in H
      proof
        assume B1: a in H |^ g;
        then consider h being Element of G such that
        B2: a = h |^ g & h in H
        by GROUP_3:58;
        (h |^ g) = a |^ g by B2,A3;
        then B3: h = a by GROUP_3:16;
        hence a in H by B2;
      end;
    end;
    then A4: the multMagma of H = the multMagma of (H |^ g) by GROUP_2:60;
    then A5: carr H = carr(H |^ g);
    the carrier of H |^ g = carr(H) |^ g by GROUP_3:def 6;
    then A6: carr H = carr(H) |^ g by A5;
    then ex h being Element of G st h=g & carr(H) |^ h = carr H;
    then g in Normalizer carr(H) by GROUP_3:129;
    hence g in Normalizer H;
  end;
  hence Centralizer H is strict Subgroup of Normalizer H by GROUP_2:58;
end;

theorem
  for G being Group
  for N being normal Subgroup of G
  holds Centralizer N is normal Subgroup of G
proof
  let G be Group;
  let N be normal Subgroup of G;
  A1: for x,n being Element of G st x in Centralizer N & n in N holds x*n = n*x
  proof
    let x,n be Element of G;
    assume B1: x in Centralizer N;
    assume B2: n in N;
    x is Element of Centralizer N by B1;
    hence x*n = n*x by B1,B2,Th62;
  end;

  A2: for g,n being Element of G st n in N holds n |^ g in N
  proof
    let g,n be Element of G;
    assume B1: n in N;
    N is normal Subgroup of G;
    then the multMagma of N = the multMagma of (N |^ g) by GROUP_3:def 13;
    then B2: the carrier of N = the carrier of (N |^ g) by GROUP_3:def 13;
    n |^ g in N |^ g by B1,GROUP_3:58;
    then n |^ g in the carrier of (N |^ g);
    then n |^ g in the carrier of N by B2;
    hence thesis;
  end;

  A3: for g,x,n being Element of G st x in Centralizer N & n in N
  holds (x |^ g)*n = n*(x |^ g)
  proof
    let g,x,n be Element of G;
    assume B1: x in Centralizer N;
    assume B2: n in N;
    consider n2 being Element of G such that
    B3: n2 = g * n * g";
    B4: n2 = n |^ g" by B3,GROUP_3:22;
    then B5: n2 in N by B2,A2;
    x is Element of Centralizer N by B1;
    then x * n2 = n2 * x by B5,Th62;
    then (x * n2) |^ g = (n2 * x) |^ g
                      .= (n2 |^ g) * (x |^ g) by GROUP_3:23;
    then (x |^ g) * (n2 |^ g) = (n2 |^ g) * (x |^ g) by GROUP_3:23
                             .= n * (x |^ g) by B4,GROUP_3:25;
    then (x |^ g) * n = n * (x |^ g) by B4,GROUP_3:25;
    hence thesis;
  end;

  A4: for g,z being Element of G st z in Centralizer N holds z |^ g in Centralizer N
  proof
    let g,z be Element of G;
    assume Z1: z in Centralizer N;
    then for n being Element of G st n in N holds
    (z |^ g)*n = n*(z |^ g) by A3;
    then (z |^ g) is Element of Centralizer N by Th62;
    hence z |^ g in Centralizer N;
  end;


  for g being Element of G holds (Centralizer N) |^ g = Centralizer N
  proof
    let g be Element of G;

    B1: for z being Element of G st z in Centralizer N
    holds z |^ g in Centralizer N by A4;

    for z being Element of G
    holds z |^ g in (Centralizer N) |^ g iff z |^ g in (Centralizer N)
    proof
      let z be Element of G;
      thus z |^ g in (Centralizer N) |^ g implies z |^ g in (Centralizer N)
      proof
        assume B3: z |^ g in (Centralizer N) |^ g;
        then consider g3 being Element of G such that
        B4: z |^ g = g3 |^ g and
        B5: g3 in Centralizer N
        by GROUP_3:58;
        z = g3 by B4,GROUP_3:16;
        then z in Centralizer N by B4,B5;
        hence z |^ g in (Centralizer N) by B1;
      end;
      assume Z4: z |^ g in (Centralizer N);
      then (z |^ g) |^ g" in (Centralizer N) by A4;
      then z in Centralizer N by GROUP_3:25;
      hence z |^ g in (Centralizer N) |^ g by GROUP_3:58;
    end;
    for z being Element of G
    holds z in (Centralizer N) |^ g iff z in (Centralizer N)
    proof
      let z be Element of G;
      hereby
        assume z in (Centralizer N) |^ g;
        then (z |^ g") in ((Centralizer N) |^ g) |^ g" by GROUP_3:58;
        then (z |^ g") in Centralizer N by GROUP_3:62;
        then (z |^ g") |^ g in Centralizer N by A4;
        hence z in (Centralizer N) by GROUP_3:25;
      end;
      assume z in (Centralizer N);
      then (z |^ g") in (Centralizer N) by A4;
      then (z |^ g") |^ g in (Centralizer N) |^ g by GROUP_3:58;
      hence z in (Centralizer N) |^ g by GROUP_3:25;
    end;

    hence (Centralizer N) |^ g = Centralizer N by GROUP_2:60;
  end;
  hence Centralizer N is normal Subgroup of G by GROUP_3:def 13;
end;

:: GROUP_3:134
:: for x being set
:: for G being Group
:: for H being strict Subgroup of G holds
:: ( x in Normalizer H iff ex h being Element of G st
:: ( x = h & H |^ h = H ) )

theorem Th81:
  for G being Group
  for H being Subgroup of G
  for h,n being Element of G
  st h in H & n in Normalizer H
  holds h |^ n in H
proof
  let G be Group;
  let H be Subgroup of G;
  let h,n be Element of G;
  assume A1: h in H;
  assume A2: n in Normalizer H;
  then A2a: n" in Normalizer H by GROUP_2:51;
  then n" in Normalizer (carr H);
  then consider g being Element of G such that
  A3: n" = g & (carr H) |^ g = carr H by GROUP_3:129;
  h in carr H by A1;
  then h in (carr H) |^ g by A3;
  then consider h1 being Element of G such that
  A4: h = h1 |^ g & h1 in carr H by GROUP_3:41;
  h |^ n = (h1 |^ (n")) |^ n by A3,A4
        .= h1 by GROUP_3:25;
  then h |^ n in carr H by A4;
  hence h |^ n in H;
end;


theorem Th82:
  for G being Group
  for H being Subgroup of G
  holds H is Subgroup of Normalizer H
proof
  let G be Group;
  let H be Subgroup of G;
  A1: for g being Element of G st g in H
  for x being Element of G st x in (carr H) |^ g holds x in carr H
  proof
    let g be Element of G;
    assume B1: g in H;
    let x be Element of G;
    assume B2: x in (carr H) |^ g;
    then consider h being Element of G such that
    B3: x = h |^ g & h in carr H by GROUP_3:41;
    B4: h in H by B3;
    g" in H by B1,GROUP_2:51;
    then g" * h in H by B4, GROUP_2:50;
    then (g" * h)*g in H by B1, GROUP_2:50;
    then g" * h * g in H by GROUP_1:def 3;
    then h |^ g in H by GROUP_3:def 2;
    then x in H by B3;
    hence x in carr H;
  end;

  for g being Element of G st g in H holds g in Normalizer H
  proof
    let g be Element of G;
    assume B1: g in H;
    B2: for x being Element of G st x in (carr H) |^ g holds x in carr H by A1,B1;
    for x being Element of G st x in carr H holds x in (carr H) |^ g
    proof
      let x be Element of G;
      thus x in carr H implies x in (carr H) |^ g
      proof
        assume B3: x in carr H;
        then B4: x in H;
        set h = x |^ g";
        B5: h = g * x * g" by GROUP_3:def 2;
        g" in H by B1,GROUP_2:51;
        then x * g" in H by B4,GROUP_2:50;
        then g * (x * g") in H by B1,GROUP_2:50;
        then g * x * g" in H by GROUP_1:def 3;
        then h in H by B5;
        then B4: h in (carr H);
        B5: h |^ g = (x |^ g") |^ g
                  .= x by GROUP_3:25;
        then h |^ g in (carr H) |^ g by B4, GROUP_3:41;
        hence x in (carr H) |^ g by B5,GROUP_3:41;
      end;
    end;
    then (carr H) |^ g c= carr H & carr H c= (carr H) |^ g by B2;
    then (carr H) |^ g = carr H by XBOOLE_0:def 10;
    then g in Normalizer carr H by GROUP_3:129;
    hence g in Normalizer H;
  end;

  hence H is Subgroup of Normalizer H by GROUP_2:58;
end;


:: Perhaps consider the group morphism sending n in Normalizer H to the associated inner Automorphism of H, which has the centralizer H as its kernel?
theorem
  for G being Group
  for H being Subgroup of G
  holds Centralizer H is strict normal Subgroup of Normalizer H
proof
  let G be Group;
  let H be Subgroup of G;
  A1: Centralizer H is strict Subgroup of Normalizer H by Lm8;
  then reconsider Z=Centralizer H as strict Subgroup of Normalizer H;

  (Centralizer H) is normal Subgroup of Normalizer H
  proof
    reconsider Z=Centralizer H as strict Subgroup of Normalizer H by Lm8;
    Z3: Centralizer H = Z;
    set N = Normalizer H;

    B62: for z being Element of N
    holds (for n being Element of N st n in H holds z*n = n*z) iff
          z is Element of Z
    proof
      let z be Element of N;
      C0: z in N;
      reconsider z1=z as Element of G by GROUP_2:42;
      C1: z1 in N by C0;

      C2: z is Element of Z implies (for n being Element of N st n in H holds z*n = n*z)
      proof
        assume Z1: z is Element of Z;
        let n be Element of N;
        assume Z2: n in H;
        reconsider n1=n as Element of G by GROUP_2:42;
        Z3: n1 in H by Z2;
        z in Z by Z1;
        then z in carr(Centralizer H);
        then z1 in Centralizer H;
        then z1 is Element of Centralizer H;
        then z1*n1 = n1*z1 by Z3,Th62
        .= n*z by GROUP_2:43;
        then z*n = n*z by GROUP_2:43;
        hence thesis;
      end;
      not (z is Element of Z) implies not (for n being Element of N st n in H holds z*n = n*z)
      proof
        assume Z1: not z is Element of Z;
        then not z in Z;
        then not z1 in Centralizer H;
        then not z1 is Element of Centralizer H;
        then consider g being Element of G such that
        Z2: g in H & g*z1 <> z1 * g by Th62;
        Z3: H is Subgroup of Normalizer H by Th82;
        g in H by Z2;
        then g in Normalizer H by Z3, GROUP_2:41;
        then reconsider n=g as Element of N;
        Z6: n in H by Z2;
        Z4: g*z1 = n*z by GROUP_2:43;
        z1*g = z*n by GROUP_2:43;
        then Z5: n*z <> z*n by Z2,Z4;
        take n;
        thus thesis by Z5,Z6;
      end;
      hence
      (for n being Element of N st n in H holds z*n = n*z) implies z is Element of Z;

      thus z is Element of Z implies (for n being Element of N st n in H holds z*n = n*z) by C2;
    end;


    B3: for z,n,h being Element of N
    st z in Z & n in N & h in H
    holds h |^ (z |^ n) = h
    proof
      let z,n,h be Element of N;
      assume C1: z in Z;
      assume C2: n in N;
      assume C3: h in H;
      C4: h |^ (z |^ n) = (z |^ n)" * h * (z |^ n) by GROUP_3:def 2
      .= (z" |^ n) * h * (z |^ n) by GROUP_3:26
      .= (n" * z" * n) * h * (n" * z * n) by GROUP_3:def 2
      .= ((n" * z") * n) * h * (n" * (z * n)) by GROUP_1:def 3
      .= ((n" * z") * n) * (h * (n" * (z * n))) by GROUP_1:def 3
      .= (n" * z") * (n * (h * (n" * (z * n)))) by GROUP_1:def 3
      .= (n" * z") * (n * h * (n" * (z * n))) by GROUP_1:def 3
      .= (n" * z") * ((n * h) * (n" * (z * n))) by GROUP_1:def 3
      .= (n" * z") * (((n * h) * n") * (z * n)) by GROUP_1:def 3
      .= (n" * z") * ((n * h) * n") * (z * n) by GROUP_1:def 3
      .= (n" * z") * (n * h * n") * (z * n) by GROUP_1:def 3;

      C4a: for a,b being Element of G
      holds a in N & b in H & b in N implies a*b*a" in H & a*b*a" in N
      proof
        let a,b be Element of G;
        assume D1: a in N;
        assume D2: b in H;
        assume D3: b in N;
        D4: a*b in N by D1,D3,GROUP_2:50;
        D5: a" in N by D1,GROUP_2:51;
        then D6: a*b*a" in N by D4,GROUP_2:50;
        a" in N & b in H by D2,D5;
        then b |^ a" in H by Th81;
        then a*b*a" in H by GROUP_3:def 2;
        hence a*b*a" in H;
        thus a*b*a" in N by D6;
      end;

      n * h * n" in H
      proof
        reconsider h1=h,n1=n as Element of G by GROUP_2:42;
        n1" = n" by GROUP_2:48;
        then h1*n1" = h*n" by GROUP_2:43;
        then n1*(h1*n1") = n*(h*n") by GROUP_2:43
                        .= n*h*n" by GROUP_1:def 3;
        then D1: n1*h1*n1" = n*h*n" by GROUP_1:def 3;
        h1 in N & n1 in N & h1 in H by C3;
        then n1*h1*n1" in H & n1*h1*n1" in N by C4a;
        hence thesis by D1;
      end;
      then consider h2 being Element of N such that
      C5: n * h * n" = h2 & h2 in H;

      z is Element of Z by C1;
      then for n0 being Element of N st n0 in H holds z*n0 = n0*z by B62;
      then z*h2 = h2*z by C5;
      then z"*(z * h2) = z" * (h2 * z);
      then (z" * z) * h2 = z" * (h2 * z) by GROUP_1:def 3
                        .= (z" * h2) * z by GROUP_1:def 3;
      then (z" * h2) * z = (z" * z) * h2
                        .= 1_N * h2 by GROUP_1:def 5
                        .= h2 by GROUP_1:def 4;
      then h2 * z" = ((z" * h2) * z) * z"
                  .= (z" * h2) * (z * z") by GROUP_1:def 3
                  .= (z" * h2) * 1_N by GROUP_1:def 5
                  .= z" * h2 by GROUP_1:def 4;
      then C6: z" * h2 = h2 * z";
      h |^ (z |^ n) = (n" * z") * (n * h * n") * (z * n) by C4
                   .= (n" * z") * h2 * (z * n) by C5
                   .= (n" * z") * (h2 * (z * n)) by GROUP_1:def 3
                   .= n" * (z" * (h2 * (z * n))) by GROUP_1:def 3
                   .= n" * ((z" * h2) * (z * n)) by GROUP_1:def 3
                   .= n" * ((h2 * z") * (z * n)) by C6
                   .= n" * (h2 * (z" * (z * n))) by GROUP_1:def 3
                   .= n" * (h2 * ((z" * z) * n)) by GROUP_1:def 3
                   .= n" * (h2 * (1_N * n)) by GROUP_1:def 5
                   .= n" * (h2 * n) by GROUP_1:def 4
                   .= n" * h2 * n by GROUP_1:def 3
                   .= h2 |^ n by GROUP_3:def 2
                   .= (n * h * n") |^ n by C5
                   .= (h |^ n") |^ n by GROUP_3:def 2
                   .= h by GROUP_3:25;
      hence h |^ (z |^ n) = h by C4;
    end;


    B4: for z,n,h being Element of N
    st z in Z & h in H
    holds (z |^ n)*h = h*(z |^ n)
    proof
      let z,n,h be Element of N;
      assume C1: z in Z;
      assume C2: h in H;
      n in N;
      then h |^ (z |^ n) = h by C1,C2,B3;
      hence (z |^ n)*h = h*(z |^ n) by GROUP_3:22;
    end;


    B5: for n being Element of N
    for z being Element of N st z in Z holds (z |^ n) in Z
    proof
      let n be Element of N;
      let z be Element of N;
      assume C1: z in Z;
      set g = z |^ n;
      for h being Element of N st h in H holds g*h = h*g by C1,B4;
      then g is Element of Z by B62;
      hence thesis;
    end;

    B6: for n being Element of Normalizer H
    holds Z is Subgroup of Z |^ n
    proof
      let n be Element of Normalizer H;
      for z being Element of N st z in Z holds z in Z |^ n
      proof
        let z be Element of N;
        assume C1: z in Z;
        n" in N;
        then (z |^ n") in Z by C1,B5;
        then (z |^ n") |^ n in (Z |^ n) by GROUP_3:58;
        hence z in Z |^ n by GROUP_3:25;
      end;

      hence Z is Subgroup of Z |^ n by GROUP_2:58;
    end;
    then Z is normal Subgroup of N by GROUP_3:121;
    hence thesis;
  end;
  hence thesis by Lm8;
end;
